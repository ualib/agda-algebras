---
layout: default
title : UALib.Varieties.FreeAlgebras module (Agda Universal Algebra Library)
date : 2021-03-01
author: William DeMeo
---

## <a id="free-algebras-and-birkhoffs-theorem">Free Algebras and Birkhoff's Theorem</a>

This section presents the [UALib.Varieties.FreeAlgebras][] module of the [Agda Universal Algebra Library][].

First we will define the relatively free algebra in a variety, which is the "freest" algebra among (universal for) those algebras that model all identities holding in the variety. Then we give a formal proof of Birkhoff's theorem which says that a variety is an equational class. In other terms, a class ğ’¦ of algebras is closed under the operators `H`, `S`, and `P` if and only if ğ’¦ is the class of algebras that satisfy some set of identities.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Algebras.Signatures using (Signature; ğ“; ğ“¥)
open import MGS-Subsingleton-Theorems using (global-dfunext)

module Varieties.FreeAlgebras {ğ‘† : Signature ğ“ ğ“¥}{gfe : global-dfunext} where

open import Varieties.Preservation {ğ‘† = ğ‘†}{gfe} public

\end{code}


#### <a id="the-free-algebra-in-theory">The free algebra in theory</a>

Recall, we proved in [the universal property](Terms.Basic.html#the-universal-property) section of the [Terms.Basic][] module that the term algebra `ğ‘» X` is the absolutely free algebra in the class of all `ğ‘†`-structures. In this section, we formalize, for a given class `ğ’¦` of `ğ‘†`-algebras, the (relatively) free algebra in `S(P ğ’¦)` over `X`.

We use the next definition to take a free algebra *for* a class `ğ’¦` and produce the free algebra *in* `ğ’¦`.

`Î˜(ğ’¦, ğ‘¨) := {Î¸ âˆˆ Con ğ‘¨ : ğ‘¨ / Î¸ âˆˆ (S ğ’¦)}` &nbsp; &nbsp; and &nbsp; &nbsp; `Ïˆ(ğ’¦, ğ‘¨) := â‹‚ Î˜(ğ’¦, ğ‘¨)`.

Notice that `Î˜(ğ’¦, ğ‘¨)` may be empty, in which case `Ïˆ(ğ’¦, ğ‘¨) = 1` and then `ğ‘¨ / Ïˆ(ğ’¦, ğ‘¨)` is trivial.

The free algebra is constructed by applying the above definitions to the special case in which `ğ‘¨` is the term algebra `ğ‘» X` of `ğ‘†`-terms over `X`.

Since `ğ‘» X` is free for (and in) the class of all `ğ‘†`-algebras, it follows that `ğ‘» X` is free for every class `ğ’¦` of `ğ‘†`-algebras. Of course, `ğ‘» X` is not necessarily a member of `ğ’¦`, but if we form the quotient of `ğ‘» X` modulo the congruence `Ïˆ(ğ’¦, ğ‘» X)`, which we denote by `ğ”‰ := (ğ‘» X) / Ïˆ(ğ’¦, ğ‘» X)`, then it's not hard to see that `ğ”‰` is a subdirect product of the algebras in `{(ğ‘» ğ‘‹) / Î¸}`, where `Î¸` ranges over `Î˜(ğ’¦, ğ‘» X)`, so `ğ”‰` belongs to `S(P ğ’¦)`, and it follows that `ğ”‰` satisfies all the identities satisfied by all members of `ğ’¦`.  Indeed, for each pair `p q : ğ‘» X`, if `ğ’¦ âŠ§ p â‰ˆ q`, then `p` and `q` must belong to the same `Ïˆ(ğ’¦, ğ‘» X)`-class, so `p` and `q` are identified in the quotient `ğ”‰`.

The `ğ”‰` that we have just defined is called the **free algebra over** `ğ’¦` **generated by** `X` and (because of what we just observed) we may say that `ğ”‰` is free *in* `S(P ğ’¦)`.<sup>[1](Varieties.FreeAlgebras.html#fn1)</sup>

#### <a id="the-free-algebra-in-agda">The free algebra in Agda</a>

Here we represent `ğ”‰` as a type in Agda by first constructing the congruence `Ïˆ(ğ’¦, ğ‘» ğ‘‹)` described above.

We assume two ambient universes `ğ“¤` and `ğ“§`, as well as a type `X : ğ“§ Ì‡`. As usual, this is accomplished with the `module` directive.

\begin{code}

module the-free-algebra {ğ“¤ ğ“§ : Universe}{X : ğ“§ Ì‡} where

 -- NOTATION (universe aliases for convenience and readability).
 ğ“¸ğ“¿ğ“¾ ğ“¸ğ“¿ğ“¾+ ğ“¸ğ“¿ğ“¾++ : Universe
 ğ“¸ğ“¿ğ“¾ = ov ğ“¤
 ğ“¸ğ“¿ğ“¾+ = ğ“¸ğ“¿ğ“¾ âº
 ğ“¸ğ“¿ğ“¾++ = ğ“¸ğ“¿ğ“¾ âº âº

\end{code}

We first construct the congruence relation `ÏˆCon`, modulo which `ğ‘» X` yields the relatively free algebra, `ğ”‰ ğ’¦ X := ğ‘» X â•± ÏˆCon`. We start by letting `Ïˆ` be the collection of identities `(p, q)` satisfied by all subalgebras of algebras in `ğ’¦`.

\begin{code}

 Ïˆ : (ğ’¦ : Pred (Algebra ğ“¤ ğ‘†) ğ“¸ğ“¿ğ“¾) â†’ Pred (âˆ£ ğ‘» X âˆ£ Ã— âˆ£ ğ‘» X âˆ£) (ğ“§ âŠ” ğ“¸ğ“¿ğ“¾)
 Ïˆ ğ’¦ (p , q) = âˆ€(ğ‘¨ : Algebra ğ“¤ ğ‘†)(sA : ğ‘¨ âˆˆ S{ğ“¤}{ğ“¤} ğ’¦)(h : X â†’ âˆ£ ğ‘¨ âˆ£ )

                  â†’  (free-lift ğ‘¨ h) p â‰¡ (free-lift ğ‘¨ h) q

\end{code}

We convert the predicate Ïˆ into a relation by [currying](https://en.wikipedia.org/wiki/Currying).

\begin{code}

 ÏˆRel : (ğ’¦ : Pred (Algebra ğ“¤ ğ‘†) ğ“¸ğ“¿ğ“¾) â†’ Rel âˆ£ ğ‘» X âˆ£ (ğ“§ âŠ” ğ“¸ğ“¿ğ“¾)
 ÏˆRel ğ’¦ p q = Ïˆ ğ’¦ (p , q)

\end{code}

To express `ÏˆRel` as a congruence of the term algebra `ğ‘» X`, we must prove that

1. `ÏˆRel` is compatible with the operations of `ğ‘» X` (which are jsut the terms themselves) and
2. `ÏˆRel` it is an equivalence relation.

\begin{code}

 Ïˆcompatible : (ğ’¦ : Pred (Algebra ğ“¤ ğ‘†) ğ“¸ğ“¿ğ“¾) â†’ compatible (ğ‘» X)(ÏˆRel ğ’¦)
 Ïˆcompatible ğ’¦ ğ‘“ {p} {q} Ïˆpq ğ‘¨ sA h = Î³
  where
   Ï• : hom (ğ‘» X) ğ‘¨
   Ï• = lift-hom ğ‘¨ h

   Î³ : âˆ£ Ï• âˆ£ ((ğ‘“ Ì‚ ğ‘» X) p) â‰¡ âˆ£ Ï• âˆ£ ((ğ‘“ Ì‚ ğ‘» X) q)

   Î³ = âˆ£ Ï• âˆ£ ((ğ‘“ Ì‚ ğ‘» X) p) â‰¡âŸ¨ âˆ¥ Ï• âˆ¥ ğ‘“ p âŸ©
       (ğ‘“ Ì‚ ğ‘¨) (âˆ£ Ï• âˆ£ âˆ˜ p) â‰¡âŸ¨ ap(ğ‘“ Ì‚ ğ‘¨)(gfe Î» x â†’ (Ïˆpq x) ğ‘¨ sA h) âŸ©
       (ğ‘“ Ì‚ ğ‘¨) (âˆ£ Ï• âˆ£ âˆ˜ q) â‰¡âŸ¨ (âˆ¥ Ï• âˆ¥ ğ‘“ q)â»Â¹ âŸ©
       âˆ£ Ï• âˆ£ ((ğ‘“ Ì‚ ğ‘» X) q) âˆ

 ÏˆRefl : {ğ’¦ : Pred (Algebra ğ“¤ ğ‘†) ğ“¸ğ“¿ğ“¾} â†’ reflexive (ÏˆRel ğ’¦)
 ÏˆRefl = Î» _ _ _ _ â†’ ğ“‡â„¯ğ’»ğ“

 ÏˆSymm : {ğ’¦ : Pred (Algebra ğ“¤ ğ‘†) ğ“¸ğ“¿ğ“¾} â†’ symmetric (ÏˆRel ğ’¦)
 ÏˆSymm _ _ pÏˆRelq ğ‘ª Ï• h = (pÏˆRelq ğ‘ª Ï• h)â»Â¹

 ÏˆTrans : {ğ’¦ : Pred (Algebra ğ“¤ ğ‘†) ğ“¸ğ“¿ğ“¾} â†’ transitive (ÏˆRel ğ’¦)
 ÏˆTrans _ _ _ pÏˆq qÏˆr ğ‘ª Ï• h = (pÏˆq ğ‘ª Ï• h) âˆ™ (qÏˆr ğ‘ª Ï• h)

 ÏˆIsEquivalence : {ğ’¦ : Pred (Algebra ğ“¤ ğ‘†) ğ“¸ğ“¿ğ“¾} â†’ IsEquivalence (ÏˆRel ğ’¦)
 ÏˆIsEquivalence = record { rfl = ÏˆRefl ; sym = ÏˆSymm ; trans = ÏˆTrans }

\end{code}

We have collected all the pieces necessary to express the collection of identities satisfied by all subalgebras of algebras in the class as a congruence relation of the term algebra. We call this congruence `ÏˆCon` and define it using the Congruence constructor `mkcon`.

\begin{code}

 ÏˆCon : (ğ’¦ : Pred (Algebra ğ“¤ ğ‘†) ğ“¸ğ“¿ğ“¾) â†’ Congruence (ğ‘» X)
 ÏˆCon ğ’¦ = mkcon (ÏˆRel ğ’¦) (Ïˆcompatible ğ’¦) ÏˆIsEquivalence

\end{code}


Finally, we are ready to define the type representing the relatively free algebra in Agda.  We denote this type by ğ”‰ and define it as the quotient `ğ‘» X â•± (ÏˆCon ğ’¦)`.

\begin{code}

module the-relatively-free-algebra
 {ğ“¤ ğ“§ : Universe} {X : ğ“§ Ì‡} {ğ’¦ : Pred (Algebra ğ“¤ ğ‘†) (ov ğ“¤)} where

 open the-free-algebra{ğ“¤}{ğ“§}{X}

 ğ“• : Universe -- (universe level of the relatively free algebra)
 ğ“• = (ğ“§ âŠ” ov ğ“¤) âº

 ğ”‰ : Algebra ğ“• ğ‘†
 ğ”‰ =  ğ‘» X â•± (ÏˆCon ğ’¦)

\end{code}

The domain of `ğ”‰` is `âˆ£ ğ”‰ âˆ£ = âˆ£ ğ‘» X âˆ£ / âŸ¨ ÏˆCon ğ’¦ âŸ©`, which is equal to

`Î£ C ê‰ _ ,  Î£ p ê‰ âˆ£ ğ‘» X âˆ£ ,  C â‰¡ ( [ p ] âŸ¨ ÏˆCon ğ’¦ âŸ© )`.

This Sigma type denotes the set `{ C : âˆƒ p âˆˆ âˆ£ ğ‘» X âˆ£ , C â‰¡ [ p ] âŸ¨ ÏˆCon ğ’¦ âŸ© }` of `âŸ¨ ÏˆCon ğ’¦ âŸ©`-classses of terms, as desired.

We left the type of `C` implicit (using the underscore symbol) for readability.  Since `C` denotes a particular class of the congruence relation `ÏˆCon ğ’¦` of `ğ‘» X`, and since congruence classes are subsets of the domain of the underlying algebra, the type of `C` is a predicate on the type of terms; specifically, `C : Pred âˆ£ ğ‘» X âˆ£ (ğ“§ âŠ” ğ“¸ğ“¿ğ“¾)`.





-----------------------------




#### <a id="hsp-theorem">HSP Theorem</a>

This section presents a formal proof of the Birkhoff HSP theorem.<sup>[2](Varieties.FreeAlgebras.html#fn2)</sup>

To complete the proof of Birkhoff's HSP theorem, it remains to show that every algebra ğ‘¨ that belongs to `Mod X (Th (V ğ’¦))`---i.e., every algebra that models the equations in `Th (V ğ’¦)`---belongs to `V ğ’¦`.  This will prove that `V ğ’¦` is an equational class.  (The converse, that every equational class is a variety was already proved; see the remarks at the end of this module.)

We accomplish this goal by constructing an algebra `ğ”½` with the following properties:

1. `ğ”½ âˆˆ V ğ’¦` and

2. Every `ğ‘¨ âˆˆ Mod X (Th (V ğ’¦))` is a homomorphic image of `ğ”½`.

(In earlier versions of the [Agda UALib][], the free algebra ğ”‰ developed in the [Birkhoff.FreeAlgebra][] section played the role of the algebra ğ”½ with properties 1 and 2.  However, we found a more direct path to the proof using the algebra `ğ”½ := (ğ‘» X) [ â„­ ]/ker homâ„­`.)

We denote by â„­ the product of all subalgebras of algebras in ğ’¦, and by `homâ„­` the homomorphism from `ğ‘» X` to `â„­` defined as follows:

`homâ„­ := â¨…-hom-co (ğ‘» X) ğ”„s homğ”„`.

Here, `â¨…-hom-co` (defined in [Homomorphisms.Basic](Homomorphisms.Basic.html#product-homomorphisms)) takes the term algebra `ğ‘» X`, a family `{ğ”„s : I â†’ Algebra ğ“¤ ğ‘†}` of ğ‘†-algebras, and a family `homğ”„ : âˆ€ i â†’ hom (ğ‘» X) (ğ”„s i)` of homomorphisms and constructs the natural homomorphism `homâ„­` from `ğ‘» X` to the product `â„­ := â¨… ğ”„`.  The homomorphism `homâ„­ : hom (ğ‘» X) (â¨… â„­)` is natural in the sense that the `i`-th component of the image of `ğ‘¡ : Term X` under `homâ„­` is the image `âˆ£ homğ”„ i âˆ£ ğ‘¡` of ğ‘¡ under the i-th homomorphism `homğ”„ i`.

In this module we fix `ğ“¤`, `X`, and `ğ’¦` in advance and assume ğ•, which supplies, for each algebra `ğ‘¨`, a surjective map âˆ£ ğ• ğ‘¨ âˆ£ from `X` onto ğ‘¨.

\begin{code}

--open import MGS-Powerset using (hfunext)

module HSPTheorem
-- {ğ‘† : Signature ğ“ ğ“¥}{gfe : global-dfunext}
 {ğ• : {ğ“¤ ğ“§ : Universe}{X : ğ“§ Ì‡ }(ğ‘¨ : Algebra ğ“¤ ğ‘†) â†’ X â†  ğ‘¨}
 {ğ“¤ : Universe} {X : ğ“¤ Ì‡}
 {ğ’¦ : Pred (Algebra ğ“¤ ğ‘†) (ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âº)}
 -- extensionality assumption:
    {fe : hfunext (ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âº)(ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âº)} where

-- open import Birkhoff.FreeAlgebra {ğ‘† = ğ‘†}{gfe} hiding (Pred; _âŠ”_; _âº; Algebra; _Ì‡ ) public
 open the-free-algebra {ğ“¤}{ğ“¤}{X}

\end{code}


#### <a id="F-in-classproduct">ğ”½ â‰¤  â¨… S(ğ’¦)</a>
Now we come to a step in the Agda formalization of Birkhoff's theorem that turns out to be surprisingly nontrivial. We must prove that the free algebra embeds in the product â„­ of all subalgebras of algebras in the class ğ’¦.  This is really the only stage in the proof of Birkhoff's theorem that requires the truncation assumption that â„­ be a set.

We begin by constructing â„­, using the techniques described in the section on <a href="https://ualib.gitlab.io/Varieties.Varieties.html#products-of-classes">products of classes</a>.

**Notation**. In this module, the type `â„‘s` will index the collection of all subalgebras of algebras in the class ğ’¦, and `ğ”„s : â„‘s â†’ Algebra ğ“¤ ğ‘†` will be a map from the index type to the subalgebras. 

\begin{code}

 open the-relatively-free-algebra {ğ“¤ = ğ“¤}{ğ“§ = ğ“¤}{X = X} {ğ’¦ = ğ’¦}

 -- NOTATION.
 â„‘s : ğ“¸ğ“¿ğ“¾ Ì‡
 â„‘s = â„‘{ğ“¤}{ğ“¤}{X} (S{ğ“¤}{ğ“¤} ğ’¦)
 ğ”„s : â„‘s â†’ Algebra ğ“¤ ğ‘†
 ğ”„s = Î» (i : â„‘s) â†’ âˆ£ i âˆ£

 SKğ”„ : (i : â„‘s) â†’ (ğ”„s i) âˆˆ S{ğ“¤}{ğ“¤} ğ’¦
 SKğ”„ = Î» (i : â„‘s) â†’ fst âˆ¥ i âˆ¥

 ğ”„h : (i : â„‘s) â†’ X â†’ âˆ£ ğ”„s i âˆ£
 ğ”„h = Î» (i : â„‘s) â†’ snd âˆ¥ i âˆ¥

 -- â„­ is the product of all subalgebras of algebras in ğ’¦.
 â„­ : Algebra ğ“¸ğ“¿ğ“¾ ğ‘†
 â„­ = â¨… ğ”„s

\end{code}

Observe that the inhabitants of â„­ are maps from â„‘s to {ğ”„s i : i âˆˆ â„‘s}.

\begin{code}

 homğ”„ : âˆ€ i â†’ hom (ğ‘» X) (ğ”„s i)
 homğ”„ i = lift-hom (ğ”„s i) (ğ”„h i)

 homâ„­ : hom (ğ‘» X) â„­
 homâ„­ = â¨…-hom-co (ğ‘» X) ğ”„s homğ”„

\end{code}


#### <a id="the-new-free-algebra">The new free algebra</a>

As mentioned above, the initial version of the [Agda UALib][] used the free algebra `ğ”‰` developed above.  However, our new, more direct proof uses the algebra `ğ”½`, which we now define, along with the natural epimorphism `epiğ”½ : epi (ğ‘» X) ğ”½` from `ğ‘» X` to `ğ”½`.<sup>[3](Varieties.FreeAlgebras.html#fn3)</sup>

\begin{code}

 ğ”½ : Algebra ğ“¸ğ“¿ğ“¾+ ğ‘†
 ğ”½ = (ğ‘» X) [ â„­ ]/ker homâ„­

 epiğ”½ : epi (ğ‘» X) ğ”½
 epiğ”½ = Ï€ker â„­ homâ„­

 homğ”½ : hom (ğ‘» X) ğ”½
 homğ”½ = epi-to-hom ğ”½ epiğ”½

 homğ”½-is-epic : Epic âˆ£ homğ”½ âˆ£
 homğ”½-is-epic = snd âˆ¥ epiğ”½ âˆ¥

\end{code}

We will need the following facts relating `homâ„­`, `homğ”½`, `and Ïˆ`.

\begin{code}

 Ïˆlemma0 : âˆ€ p q â†’ (âˆ£ homâ„­ âˆ£ p â‰¡ âˆ£ homâ„­ âˆ£ q) â†’ (p , q) âˆˆ Ïˆ ğ’¦
 Ïˆlemma0 p q phomâ„­q ğ‘¨ sA h = ap (Î» - â†’ - (ğ‘¨ , (sA , h))) phomâ„­q


 Ïˆlemma0-ap : {ğ‘¨ : Algebra ğ“¤ ğ‘†}{h : X â†’ âˆ£ ğ‘¨ âˆ£}
  â†’           ğ‘¨ âˆˆ S{ğ“¤}{ğ“¤} ğ’¦
              ---------------------------------------
  â†’           KER-pred âˆ£ homğ”½ âˆ£ âŠ† KER-pred (free-lift ğ‘¨ h)

 Ïˆlemma0-ap {ğ‘¨}{h} skA {p , q} x = Î³ where

   Î½ : âˆ£ homâ„­ âˆ£ p â‰¡ âˆ£ homâ„­ âˆ£ q
   Î½ = ker-in-con (ğ‘» X) (kercon â„­ homâ„­) p q x

   Î³ : (free-lift ğ‘¨ h) p â‰¡ (free-lift ğ‘¨ h) q
   Î³ = ((Ïˆlemma0 p q) Î½) ğ‘¨ skA h


\end{code}

We now use `Ïˆlemma0-ap` to prove that every map `h : X â†’ âˆ£ ğ‘¨ âˆ£`, from `X` to a subalgebra `ğ‘¨ âˆˆ S ğ’¦` of `ğ’¦`, lifts to a homomorphism from `ğ”½` to `ğ‘¨`.

\begin{code}

 ğ”½-lift-hom : (ğ‘¨ : Algebra ğ“¤ ğ‘†) â†’ ğ‘¨ âˆˆ S{ğ“¤}{ğ“¤} ğ’¦ â†’ (X â†’ âˆ£ ğ‘¨ âˆ£) â†’ hom ğ”½ ğ‘¨
 ğ”½-lift-hom ğ‘¨ skA h = fst(HomFactor (ğ‘» X){ğ‘¨}{ğ”½}(lift-hom ğ‘¨ h) homğ”½ homğ”½-is-epic (Ïˆlemma0-ap skA))

\end{code}


#### <a id="k-models-psi">ğ’¦ models Ïˆ</a>

The goal of this subsection is to prove that `ğ’¦` models `Ïˆ ğ’¦`. In other terms, for all pairs `(p , q) âˆˆ Term X Ã— Term X` of terms, if `(p , q) âˆˆ Ïˆ ğ’¦`, then `ğ’¦ âŠ§ p â‰‹ q`.

Next we define the lift of the natural embedding from `X` into ğ”½. We denote this homomorphism by `ğ”‘ : hom (ğ‘» X) ğ”½` and define it as follows.

\begin{code}

 Xâ†ªğ”½ : X â†’ âˆ£ ğ”½ âˆ£
 Xâ†ªğ”½ x = âŸ¦ â„Š x âŸ§

 ğ”‘ : hom (ğ‘» X) ğ”½
 ğ”‘ = lift-hom ğ”½ Xâ†ªğ”½

\end{code}

It turns out that the homomorphism so defined is equivalent to `homğ”½`.

\begin{code}

 homğ”½-is-lift-hom : âˆ€ p â†’ âˆ£ ğ”‘ âˆ£ p â‰¡ âˆ£ homğ”½ âˆ£ p
 homğ”½-is-lift-hom (â„Š x) = ğ“‡â„¯ğ’»ğ“
 homğ”½-is-lift-hom (node ğ‘“ ğ’•) = âˆ£ ğ”‘ âˆ£ (node ğ‘“ ğ’•)           â‰¡âŸ¨ âˆ¥ ğ”‘ âˆ¥ ğ‘“ ğ’• âŸ©
                           (ğ‘“ Ì‚ ğ”½)(Î» i â†’ âˆ£ ğ”‘ âˆ£(ğ’• i))  â‰¡âŸ¨ ap(ğ‘“ Ì‚ ğ”½)(gfe (Î» x â†’ homğ”½-is-lift-hom(ğ’• x))) âŸ©
                           (ğ‘“ Ì‚ ğ”½)(Î» i â†’ âˆ£ homğ”½ âˆ£ (ğ’• i))  â‰¡âŸ¨ (âˆ¥ homğ”½ âˆ¥ ğ‘“ ğ’•)â»Â¹ âŸ©
                           âˆ£ homğ”½ âˆ£ (node ğ‘“ ğ’•)           âˆ


\end{code}

We need a three more lemmas before we are ready to tackle our main goal.

\begin{code}

 Ïˆlemma1 : KER-pred âˆ£ ğ”‘ âˆ£ âŠ† Ïˆ ğ’¦
 Ïˆlemma1 {p , q} ğ”‘pq ğ‘¨ sA h = Î³
  where
   f : hom ğ”½ ğ‘¨
   f = ğ”½-lift-hom ğ‘¨ sA h

   h' Ï• : hom (ğ‘» X) ğ‘¨
   h' = HomComp (ğ‘» X) ğ‘¨ ğ”‘ f
   Ï• = lift-hom ğ‘¨ h

   fğ”‘â‰¡Ï• : (x : X) â†’ (âˆ£ f âˆ£ âˆ˜ âˆ£ ğ”‘ âˆ£) (â„Š x) â‰¡ âˆ£ Ï• âˆ£ (â„Š x)
   fğ”‘â‰¡Ï• x = ğ“‡â„¯ğ’»ğ“
   hâ‰¡Ï• : âˆ€ t â†’ (âˆ£ f âˆ£ âˆ˜ âˆ£ ğ”‘ âˆ£) t â‰¡ âˆ£ Ï• âˆ£ t
   hâ‰¡Ï• t = free-unique gfe ğ‘¨ h' Ï• fğ”‘â‰¡Ï• t

   Î³ : âˆ£ Ï• âˆ£ p â‰¡ âˆ£ Ï• âˆ£ q
   Î³ = âˆ£ Ï• âˆ£ p         â‰¡âŸ¨ (hâ‰¡Ï• p)â»Â¹ âŸ©
       âˆ£ f âˆ£ ( âˆ£ ğ”‘ âˆ£ p ) â‰¡âŸ¨ ap âˆ£ f âˆ£ ğ”‘pq âŸ©
       âˆ£ f âˆ£ ( âˆ£ ğ”‘ âˆ£ q ) â‰¡âŸ¨ hâ‰¡Ï• q âŸ©
       âˆ£ Ï• âˆ£ q âˆ


 Ïˆlemma2 : KER-pred âˆ£ homğ”½ âˆ£ âŠ† Ïˆ ğ’¦
 Ïˆlemma2 {p , q} hyp = Ïˆlemma1 {p , q} Î³
   where
    Î³ : (free-lift ğ”½ Xâ†ªğ”½) p â‰¡ (free-lift ğ”½ Xâ†ªğ”½) q
    Î³ = (homğ”½-is-lift-hom p) âˆ™ hyp âˆ™ (homğ”½-is-lift-hom q)â»Â¹


 Ïˆlemma3 : âˆ€ p q â†’ (p , q) âˆˆ Ïˆ ğ’¦ â†’ ğ’¦ âŠ§ p â‰‹ q
 Ïˆlemma3 p q pÏˆq {ğ‘¨} kA = Î³
  where
   skA : ğ‘¨ âˆˆ S ğ’¦
   skA = siso (sbase kA) (sym-â‰… lift-alg-â‰…)

   Î³ : (p Ì‡ ğ‘¨) â‰¡ (q Ì‡ ğ‘¨)
   Î³ = gfe Î» h â†’ (p Ì‡ ğ‘¨) h         â‰¡âŸ¨ free-lift-interp ğ‘¨ h p âŸ©
                 (free-lift ğ‘¨ h) p â‰¡âŸ¨ pÏˆq ğ‘¨ skA h âŸ©
                 (free-lift ğ‘¨ h) q â‰¡âŸ¨ (free-lift-interp ğ‘¨ h q)â»Â¹  âŸ©
                 (q Ì‡ ğ‘¨) h         âˆ

\end{code}

With these results in hand, it is now trivial to prove the main theorem of this subsection.

\begin{code}

 class-models-kernel : âˆ€ p q â†’ (p , q) âˆˆ KER-pred âˆ£ homğ”½ âˆ£ â†’ ğ’¦ âŠ§ p â‰‹ q
 class-models-kernel  p q hyp = Ïˆlemma3 p q (Ïˆlemma2 hyp)

\end{code}


#### <a id="the-homomorphic-images-of-F">The homomorphic images of ğ”½</a>

Finally we come to one of the main theorems of this module; it asserts that every algebra in `Mod X (Th ğ•ğ’¦)` is a homomorphic image of ğ”½.  We prove this below as the function (or proof object) `ğ”½-ModTh-epi`.  Before that, we prove two auxiliary lemmas.

\begin{code}

 kernel-in-theory : KER-pred âˆ£ homğ”½ âˆ£ âŠ† Th (V ğ’¦)
 kernel-in-theory {p , q} pKq = (class-ids-â‡’ p q (class-models-kernel p q pKq))

 open Congruence

 free-quot-subalg-â„­ : prop-ext (Term X) (ov ğ“¤) â†’ is-set âˆ£ â„­ âˆ£
  â†’                   (âˆ€ p q â†’ is-subsingleton (âŸ¨ kercon â„­ homâ„­ âŸ© p q))
  â†’                   (âˆ€ C â†’ is-subsingleton (ğ’{A = âˆ£ ğ‘» X âˆ£}{âŸ¨ kercon â„­ homâ„­ âŸ©} C))
                      -----------------------------------------------------------
  â†’                   ((ğ‘» X) [ â„­ ]/ker homâ„­) â‰¤ â„­

 free-quot-subalg-â„­ pe Cset ssR ssC = FirstHomCorollary (ğ‘» X) â„­ homâ„­ pe Cset ssR ssC


 module _ (Cset : is-set âˆ£ â„­ âˆ£)(pe : prop-ext (Term X) (ov ğ“¤))
          (ssR : âˆ€ p q â†’ is-subsingleton (âŸ¨ kercon â„­ homâ„­ âŸ© p q))
          (ssC : âˆ€ C â†’ is-subsingleton (ğ’{A = âˆ£ ğ‘» X âˆ£}{âŸ¨ kercon â„­ homâ„­ âŸ©} C)) where

  ğ”½â‰¤â„­ : ((ğ‘» X) [ â„­ ]/ker homâ„­) â‰¤ â„­
  ğ”½â‰¤â„­ = free-quot-subalg-â„­ pe Cset ssR ssC

  ğ•ğ’¦ : Pred (Algebra ğ“¸ğ“¿ğ“¾+ ğ‘†) ğ“¸ğ“¿ğ“¾++
  ğ•ğ’¦ = V{ğ“¤}{ğ“¸ğ“¿ğ“¾+} ğ’¦

  ğ”½-ModTh-epi : (ğ‘¨ : Algebra ğ“¸ğ“¿ğ“¾+ ğ‘†) â†’ ğ‘¨ âˆˆ Mod (Th ğ•ğ’¦) â†’ epi ğ”½ ğ‘¨
  ğ”½-ModTh-epi ğ‘¨ AinMTV = Î³
   where
    Ï• : hom (ğ‘» X) ğ‘¨
    Ï• = lift-hom ğ‘¨ (fst(ğ• ğ‘¨))

    Ï•E : Epic âˆ£ Ï• âˆ£
    Ï•E = lift-of-epi-is-epi (snd (ğ• ğ‘¨))

    pqlem2 : âˆ€ p q â†’ (p , q) âˆˆ KER-pred âˆ£ homğ”½ âˆ£ â†’ ğ‘¨ âŠ§ p â‰ˆ q
    pqlem2 p q hyp = AinMTV p q (kernel-in-theory hyp)

    kerincl : KER-pred âˆ£ homğ”½ âˆ£ âŠ† KER-pred âˆ£ Ï• âˆ£
    kerincl {p , q} x = Î³
     where
      Apq : ğ‘¨ âŠ§ p â‰ˆ q
      Apq = pqlem2 p q x
      Î³ : âˆ£ Ï• âˆ£ p â‰¡ âˆ£ Ï• âˆ£ q
      Î³ = âˆ£ Ï• âˆ£ p                    â‰¡âŸ¨ ğ“‡â„¯ğ’»ğ“ âŸ©
          free-lift ğ‘¨ (fst(ğ• ğ‘¨)) p â‰¡âŸ¨ (free-lift-interp ğ‘¨ (fst(ğ• ğ‘¨)) p)â»Â¹ âŸ©
          (p Ì‡ ğ‘¨) (fst(ğ• ğ‘¨))       â‰¡âŸ¨ extfun (pqlem2 p q x) (fst(ğ• ğ‘¨))  âŸ©
          (q Ì‡ ğ‘¨) (fst(ğ• ğ‘¨))       â‰¡âŸ¨ free-lift-interp ğ‘¨ (fst(ğ• ğ‘¨)) q âŸ©
          free-lift ğ‘¨ (fst(ğ• ğ‘¨)) q â‰¡âŸ¨ ğ“‡â„¯ğ’»ğ“ âŸ©
          âˆ£ Ï• âˆ£ q                  âˆ

    Î³ : epi ğ”½ ğ‘¨
    Î³ = fst (HomFactorEpi (ğ‘» X){ğ‘¨}{ğ”½} Ï• Ï•E homğ”½ homğ”½-is-epic  kerincl)


\end{code}

#### <a id="F-in-VK">ğ”½ âˆˆ V(ğ’¦)</a>

With this result in hand, along with what we proved earlier---namely, PS(ğ’¦) âŠ† SP(ğ’¦) âŠ† HSP(ğ’¦) â‰¡ V ğ’¦---it is not hard to show that ğ”½ belongs to V ğ’¦.

\begin{code}

  ğ”½âˆˆSP : ğ”½ âˆˆ (S{ğ“¸ğ“¿ğ“¾}{ğ“¸ğ“¿ğ“¾+} (P{ğ“¤}{ğ“¸ğ“¿ğ“¾} ğ’¦))
  ğ”½âˆˆSP = ssub (class-prod-s-âˆˆ-sp fe) ğ”½â‰¤â„­

  ğ”½âˆˆğ• : ğ”½ âˆˆ V ğ’¦
  ğ”½âˆˆğ• = SPâŠ†V' ğ”½âˆˆSP

\end{code}

#### <a id="the-hsp-theorem"> The HSP Theorem</a>

Now that we have all of the necessary ingredients, it is all but trivial to combine them to prove Birkhoff's HSP theorem.

\begin{code}

  birkhoff : Mod (Th (V ğ’¦)) âŠ† V ğ’¦

  birkhoff {ğ‘¨} Î± = Î³
   where
    Î³ : ğ‘¨ âˆˆ (V ğ’¦)
    Î³ = vhimg ğ”½âˆˆğ• ((ğ‘¨ , ğ”½-ModTh-epi ğ‘¨ Î± ) , refl-â‰…)

\end{code}

The converse inclusion, `V ğ’¦ âŠ† Mod X (Th (V ğ’¦))`, is a simple consequence of the fact that `Mod Th` is a closure operator. Nonetheless, completeness demands that we formalize this inclusion as well, however trivial the proof.

\begin{code}

  birkhoff' : V{ğ“¤}{ğ“¸ğ“¿ğ“¾} ğ’¦ âŠ† Mod {X = X}(Th (V ğ’¦))

  birkhoff' Î± p q pThq = pThq Î±

\end{code}

We have thus proved that every variety is an equational class.  Readers familiar with the classical formulation of the Birkhoff HSP theorem, as an "if and only if" result, might worry that we haven't completed the proof.  But recall that in the [Varieties.Preservation][] module we proved the following identity preservation lemmas:

* [(H-id1)](https://ualib.gitlab.io/UALib.Varieties.Preservation.html#964) ğ’¦ âŠ§ p â‰‹ q â†’ H ğ’¦ âŠ§ p â‰‹ q
* [(S-id1)](https://ualib.gitlab.io/UALib.Varieties.Preservation.html#2592) ğ’¦ âŠ§ p â‰‹ q â†’ S ğ’¦ âŠ§ p â‰‹ q
* [(P-id1)](https://ualib.gitlab.io/UALib.Varieties.Preservation.html#4111) ğ’¦ âŠ§ p â‰‹ q â†’ P ğ’¦ âŠ§ p â‰‹ q

From these it follows that every equational class is a variety. Thus, our formal proof of Birkhoff's theorem is complete.

----------------------------

<span class="footnote" id="fn1"><sup>1</sup>Since `X` is not a subset of `ğ”‰`, technically it doesn't make sense to say "`X` generates `ğ”‰`." But as long as ğ’¦ contains a nontrivial algebra, we will have `Ïˆ(ğ’¦, ğ‘» ğ‘‹) âˆ© XÂ² â‰  âˆ…`, and we can identify `X` with `X / Ïˆ(ğ’¦, ğ‘» X)` which does belong to ğ”‰.</span>

<span class="footnote" id="fn2"><sup>1</sup> In the previous version of the [UALib][] this section was part of a module called HSPTheorem module.</span>

<span class="footnote" id="fn3"><sup>3</sup> It might be an instructive exercise to prove that `ğ”½` is, in fact, isomorphic to the algebra `ğ”‰` that we defined earlier.</span>

<p></p>
<p></p>


[â† Varieties.Preservation](Varieties.Preservation.html)
<span style="float:right;">[Varieties â†‘](Varieties.html)</span>

{% include UALib.Links.md %}
