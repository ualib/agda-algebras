---
layout: default
title : UALib.Varieties.EquationalLogic module (The Agda Universal Algebra Library)
date : 2021-01-14
author: William DeMeo
---

### <a id="equational-logic-types">Equational Logic Types</a>

This section presents the [UALib.Varieties.EquationalLogic][] module of the [Agda Universal Algebra Library][].

We prove closure properties, or "invariance," of the models relation defined in [UALib.Varieties.ModelTheory][] module .  Proofs are given of the following facts (which are needed, for example, in the proof the Birkhoff HSP Theorem).

* [Algebraic invariance of âŠ§](#algebraic-invariance-of-âŠ§). The âŠ§ relation is an *algebraic invariant* (stable under isomorphism).

* [Product invariance of âŠ§](#product-invariance-of-âŠ§). Identities modeled by a class of algebras are also modeled by all products of algebras in the class.

* [Subalgebra invariance of âŠ§](#subalgebra-invariance-of-âŠ§). Identities modeled by a class of algebras are also modeled by all subalgebras of algebras in the class;

* [Homomorphism invariance of âŠ§](#homomorphism-invariance-of-âŠ§). Identities modeled by a class of algebras are also modeled by all homomorphic images (equivalently, all quotients) of algebras in the class;

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import UALib.Algebras using (Signature; ğ“; ğ“¥; Algebra; _â† _)
open import UALib.Prelude.Preliminaries using (global-dfunext; Universe; _Ì‡)


module UALib.Varieties.EquationalLogic
 {ğ‘† : Signature ğ“ ğ“¥}{gfe : global-dfunext}
 {ğ• : {ğ“¤ ğ“§ : Universe}{X : ğ“§ Ì‡ }(ğ‘¨ : Algebra ğ“¤ ğ‘†) â†’ X â†  ğ‘¨}
 where


open import UALib.Varieties.ModelTheory {ğ‘† = ğ‘†}{gfe}{ğ•} public
open import UALib.Prelude.Preliminaries using (âˆ˜-embedding; domain; embeddings-are-lc) public

\end{code}

-------------------------------------

#### <a id="computing-with-âŠ§">Computing with âŠ§</a>

We have formally defined ğ‘¨ âŠ§ ğ‘ â‰ˆ ğ‘, which represents the assertion that p â‰ˆ  q holds when this identity is interpreted in the algebra  ğ‘¨; syntactically,  ğ‘ Ì‡  ğ‘¨ â‰¡  ğ‘ Ì‡  ğ‘¨.  It should be emphasized that the expression  ğ‘ Ì‡  ğ‘¨ â‰¡  ğ‘ Ì‡  ğ‘¨ is interpreted computationally as an \emph{extensional equality}, by which we mean that for each *assignment function*  ğ’‚ :  X â†’ âˆ£ ğ‘¨ âˆ£, assigning values in the domain of  ğ‘¨ to the variable symbols in  X, we have (ğ‘ Ì‡  ğ‘¨)  ğ’‚ â‰¡ (ğ‘ Ì‡  ğ‘¨)  ğ’‚.

---------------------------------

#### <a id="algebraic-invariance-of-âŠ§I">Algebraic invariance of âŠ§</a>

The binary relation âŠ§ would be practically useless if it were not an *algebraic invariant* (i.e., invariant under isomorphism).

\begin{code}

âŠ§-I-invariance : {ğ“  ğ“¤ ğ“§ : Universe}{X : ğ“§ Ì‡}{ğ‘¨ : Algebra ğ“  ğ‘†}{ğ‘© : Algebra ğ“¤ ğ‘†}
                 (p q : Term{ğ“§}{X}) â†’ (ğ‘¨ âŠ§ p â‰ˆ q) â†’ (ğ‘¨ â‰… ğ‘©) â†’ ğ‘© âŠ§ p â‰ˆ q
âŠ§-I-invariance {ğ“ }{ğ“¤}{ğ“§}{X}{ğ‘¨}{ğ‘©} p q Apq (f , g , fâˆ¼g , gâˆ¼f) = Î³
 where
  Î³ : (p Ì‡ ğ‘©) â‰¡ (q Ì‡ ğ‘©)
  Î³ = gfe Î» x â†’
      (p Ì‡ ğ‘©) x â‰¡âŸ¨ ğ“‡â„¯ğ’»ğ“ âŸ©
      (p Ì‡ ğ‘©) (âˆ£ ğ’¾ğ’¹ ğ‘© âˆ£ âˆ˜ x) â‰¡âŸ¨ ap (Î» - â†’ (p Ì‡ ğ‘©) -) (gfe Î» i â†’ ((fâˆ¼g)(x i))â»Â¹)  âŸ©
      (p Ì‡ ğ‘©) ((âˆ£ f âˆ£ âˆ˜ âˆ£ g âˆ£) âˆ˜ x) â‰¡âŸ¨ (comm-hom-term gfe ğ‘¨ ğ‘© f p (âˆ£ g âˆ£ âˆ˜ x))â»Â¹ âŸ©
      âˆ£ f âˆ£ ((p Ì‡ ğ‘¨) (âˆ£ g âˆ£ âˆ˜ x)) â‰¡âŸ¨ ap (Î» - â†’ âˆ£ f âˆ£ (- (âˆ£ g âˆ£ âˆ˜ x))) Apq âŸ©
      âˆ£ f âˆ£ ((q Ì‡ ğ‘¨) (âˆ£ g âˆ£ âˆ˜ x)) â‰¡âŸ¨ comm-hom-term gfe ğ‘¨ ğ‘© f q (âˆ£ g âˆ£ âˆ˜ x) âŸ©
      (q Ì‡ ğ‘©) ((âˆ£ f âˆ£ âˆ˜ âˆ£ g âˆ£) âˆ˜  x) â‰¡âŸ¨ ap (Î» - â†’ (q Ì‡ ğ‘©) -) (gfe Î» i â†’ (fâˆ¼g) (x i)) âŸ©
      (q Ì‡ ğ‘©) x âˆ

\end{code}

As the proof makes clear, we show ğ‘© âŠ§ p â‰ˆ q by showing that p Ì‡ ğ‘© â‰¡ q Ì‡ ğ‘© holds *extensionally*, that is, `âˆ€ x, (p Ì‡ ğ‘©) x â‰¡ (q Ì‡ ğ‘©) x`.

--------------------------------------

#### <a id="Lift-invariance-of-âŠ§">Lift-invariance of âŠ§</a>

The âŠ§ relation is also invariant under the algebraic lift and lower operations.

\begin{code}

âŠ§-lift-alg-invariance : {ğ“¤ ğ“¦ ğ“§ : Universe}{X : ğ“§ Ì‡}
       (ğ‘¨ : Algebra ğ“¤ ğ‘†)(p q : Term{ğ“§}{X})
  â†’    ğ‘¨ âŠ§ p â‰ˆ q â†’ (lift-alg ğ‘¨ ğ“¦) âŠ§ p â‰ˆ q
âŠ§-lift-alg-invariance ğ‘¨ p q Apq = âŠ§-I-invariance p q Apq lift-alg-â‰…

âŠ§-lower-alg-invariance : {ğ“¤ ğ“¦ ğ“§ : Universe}{X : ğ“§ Ì‡}(ğ‘¨ : Algebra ğ“¤ ğ‘†)
                             (p q : Term{ğ“§}{X})
 â†’                           lift-alg ğ‘¨ ğ“¦ âŠ§ p â‰ˆ q â†’ ğ‘¨ âŠ§ p â‰ˆ q
âŠ§-lower-alg-invariance {ğ“¤}{ğ“¦}{ğ“§}{X} ğ‘¨ p q lApq = âŠ§-I-invariance p q lApq (sym-â‰… lift-alg-â‰…)

\end{code}

---------------------------------------------

#### <a id="product-invariance-of-âŠ§">Product-invariance of âŠ§</a>

Identities satisfied by all factors of a product are also satisfied by the product.

\begin{code}
âŠ§-P-invariance : {ğ“¤ ğ“§ : Universe}{X : ğ“§ Ì‡}(p q : Term{ğ“§}{X})
                 (I : ğ“¤ Ì‡ ) (ğ’œ : I â†’ Algebra ğ“¤ ğ‘†)
 â†’               ((i : I) â†’ (ğ’œ i) âŠ§ p â‰ˆ q)
                 --------------------------
 â†’               â¨… ğ’œ âŠ§ p â‰ˆ q

âŠ§-P-invariance p q I ğ’œ ğ’œpq = Î³
  where
   Î³ : (p Ì‡ â¨… ğ’œ) â‰¡ (q Ì‡ â¨… ğ’œ)
   Î³ = gfe Î» a â†’
    (p Ì‡ â¨… ğ’œ) a                           â‰¡âŸ¨ interp-prod gfe p ğ’œ a âŸ©
    (Î» i â†’ ((p Ì‡ (ğ’œ i)) (Î» x â†’ (a x) i))) â‰¡âŸ¨ gfe (Î» i â†’ cong-app (ğ’œpq i) (Î» x â†’ (a x) i)) âŸ©
    (Î» i â†’ ((q Ì‡ (ğ’œ i)) (Î» x â†’ (a x) i))) â‰¡âŸ¨ (interp-prod gfe q ğ’œ a)â»Â¹ âŸ©
    (q Ì‡ â¨… ğ’œ) a                           âˆ

âŠ§-P-lift-invariance : {ğ“¤ ğ“¦ ğ“§ : Universe}{X : ğ“§ Ì‡}(p q : Term{ğ“§}{X})
                      (I : ğ“¤ Ì‡ ) (ğ’œ : I â†’ Algebra ğ“¤ ğ‘†)
 â†’                    ((i : I) â†’ (lift-alg (ğ’œ i) ğ“¦) âŠ§ p â‰ˆ q)
                      ---------------------------------------
 â†’                    â¨… ğ’œ âŠ§ p â‰ˆ q

âŠ§-P-lift-invariance {ğ“¤}{ğ“¦} p q I ğ’œ lApq = âŠ§-P-invariance p q I ğ’œ Aipq
  where
   Aipq : (i : I) â†’ (ğ’œ i) âŠ§ p â‰ˆ q
   Aipq i = âŠ§-I-invariance p q (lApq i) (sym-â‰… lift-alg-â‰…)

âŠ§-class-P-invariance : {ğ“¤ ğ“§ : Universe}{X : ğ“§ Ì‡}
                       {ğ’¦ : Pred (Algebra ğ“¤ ğ‘†) (OV ğ“¤)}
                       (p q : Term{ğ“§}{X})
                       (I : ğ“¤ Ì‡)(ğ’œ : I â†’ Algebra ğ“¤ ğ‘†)
 â†’                     ğ’¦ âŠ§ p â‰‹ q  â†’  ((i : I) â†’ ğ’œ i âˆˆ ğ’¦)
                       ----------------------------------
 â†’                     â¨… ğ’œ âŠ§ p â‰ˆ q

âŠ§-class-P-invariance p q I ğ’œ Î± Kğ’œ = Î³
  where
   Î² : âˆ€ i â†’ (ğ’œ i) âŠ§ p â‰ˆ q
   Î² i = Î± (Kğ’œ i)

   Î³ : (p Ì‡ â¨… ğ’œ) â‰¡ (q Ì‡ â¨… ğ’œ)
   Î³ = âŠ§-P-invariance p q I ğ’œ Î²
\end{code}

--------------------------------------------

#### <a id="subalgebra-invariance-of-âŠ§">Subalgebra invariance of âŠ§</a>
We show that identities modeled by a class of algebras is also modeled by all subalgebras of the class.  In other terms, every term equation `p â‰ˆ q` that is satisfied by all `ğ‘¨ âˆˆ ğ’¦` is also satisfied by every subalgebra of a member of ğ’¦.

\begin{code}

âŠ§-S-invariance : {ğ“¤ ğ“  ğ“§ : Universe}{X : ğ“§ Ì‡}
                 {ğ’¦ : Pred (Algebra ğ“  ğ‘†) (OV ğ“ )}
                 (p q : Term{ğ“§}{X})
                 (ğ‘© : SubalgebraOfClass{ğ“¤}{ğ“ } ğ’¦)
 â†’               ğ’¦ âŠ§ p â‰‹ q
                -----------------
 â†’               âˆ£ ğ‘© âˆ£ âŠ§ p â‰ˆ q

âŠ§-S-invariance {ğ“¤}{X = X} p q (ğ‘© , ğ‘¨ , SA , (KA , BisSA)) Kpq = Î³
 where
  ğ‘©' : Algebra ğ“¤ ğ‘†
  ğ‘©' = âˆ£ SA âˆ£

  h' : hom ğ‘©' ğ‘¨
  h' = (âˆ£ snd SA âˆ£ , snd âˆ¥ snd SA âˆ¥ )

  f : hom ğ‘© ğ‘©'
  f = âˆ£ BisSA âˆ£

  h : hom ğ‘© ğ‘¨
  h = HCompClosed ğ‘© ğ‘©' ğ‘¨ f h'

  hem : is-embedding âˆ£ h âˆ£
  hem = âˆ˜-embedding h'em fem
   where
    h'em : is-embedding âˆ£ h' âˆ£
    h'em = fst âˆ¥ snd SA âˆ¥

    fem : is-embedding âˆ£ f âˆ£
    fem = isoâ†’embedding BisSA

  Î² : ğ‘¨ âŠ§ p â‰ˆ q
  Î² = Kpq KA

  Î¾ : (b : X â†’ âˆ£ ğ‘© âˆ£ ) â†’ âˆ£ h âˆ£ ((p Ì‡ ğ‘©) b) â‰¡ âˆ£ h âˆ£ ((q Ì‡ ğ‘©) b)
  Î¾ b =
   âˆ£ h âˆ£((p Ì‡ ğ‘©) b)  â‰¡âŸ¨ comm-hom-term gfe ğ‘© ğ‘¨ h p b âŸ©
   (p Ì‡ ğ‘¨)(âˆ£ h âˆ£ âˆ˜ b) â‰¡âŸ¨ intensionality Î² (âˆ£ h âˆ£ âˆ˜ b) âŸ©
   (q Ì‡ ğ‘¨)(âˆ£ h âˆ£ âˆ˜ b) â‰¡âŸ¨ (comm-hom-term gfe ğ‘© ğ‘¨ h q b)â»Â¹ âŸ©
   âˆ£ h âˆ£((q Ì‡ ğ‘©) b)  âˆ

  hlc : {b b' : domain âˆ£ h âˆ£} â†’ âˆ£ h âˆ£ b â‰¡ âˆ£ h âˆ£ b' â†’ b â‰¡ b'
  hlc hbâ‰¡hb' = (embeddings-are-lc âˆ£ h âˆ£ hem) hbâ‰¡hb'

  Î³ : ğ‘© âŠ§ p â‰ˆ q
  Î³ = gfe Î» b â†’ hlc (Î¾ b)

\end{code}

------------------------------------------------------------

#### <a id="homomorphism-invariance-of-âŠ§">Homomorphism-invariance of âŠ§</a>

Below we will prove that a class models an identity if and only if all homomorphic images of algebras in the class model the same identity.  But first we prove the slightly simpler special case involving just a single algebra, rather than a class of algebras.

\begin{code}

âŠ§-H-invariance : {ğ“¤ ğ“§ : Universe}{X : ğ“§ Ì‡} (p q : Term{ğ“§}{X})
                 (ğ‘¨ : Algebra ğ“¤ ğ‘†)(Ï† : hom (ğ‘» X) ğ‘¨)
 â†’               ğ‘¨ âŠ§ p â‰ˆ q
                 ------------------
 â†’               âˆ£ Ï† âˆ£ p â‰¡ âˆ£ Ï† âˆ£ q

âŠ§-H-invariance {X = X} p q ğ‘¨ Ï† Î² =
  âˆ£ Ï† âˆ£ p              â‰¡âŸ¨ ap âˆ£ Ï† âˆ£ (term-agreement p) âŸ©
  âˆ£ Ï† âˆ£ ((p Ì‡ ğ‘» X) â„Š )   â‰¡âŸ¨ (comm-hom-term gfe (ğ‘» X) ğ‘¨ Ï† p â„Š ) âŸ©
  (p Ì‡ ğ‘¨) (âˆ£ Ï† âˆ£ âˆ˜ â„Š ) â‰¡âŸ¨ intensionality Î² (âˆ£ Ï† âˆ£ âˆ˜ â„Š ) âŸ©
  (q Ì‡ ğ‘¨) (âˆ£ Ï† âˆ£ âˆ˜ â„Š ) â‰¡âŸ¨ (comm-hom-term gfe (ğ‘» X) ğ‘¨ Ï† q â„Š )â»Â¹ âŸ©
  âˆ£ Ï† âˆ£ ((q Ì‡ ğ‘» X) â„Š ) â‰¡âŸ¨ (ap âˆ£ Ï† âˆ£ (term-agreement q))â»Â¹ âŸ©
  âˆ£ Ï† âˆ£ q              âˆ

\end{code}

Recall that an identity is satisfied by all algebras in a class if and only if that identity is compatible with all homomorphisms from the term algebra `ğ‘» X` into algebras of the class. More precisely, if ğ“š is a class of ğ‘†-algebras and ğ‘, ğ‘ terms in the language of ğ‘†, then,

ğ’¦ âŠ§ p â‰ˆ q  â‡”  âˆ€ ğ‘¨ âˆˆ ğ’¦,  âˆ€ h : hom (ğ‘» X) ğ‘¨,  h âˆ˜ (ğ‘ Ì‡ (ğ‘» X)) = h âˆ˜ (ğ‘ Ì‡ (ğ‘» X)).

\begin{code}

-- â‡’ (the "only if" direction)
âŠ§-class-H-invariance : {ğ“¤ ğ“§ : Universe}{X : ğ“§ Ì‡}
                       {ğ’¦ : Pred (Algebra ğ“¤ ğ‘†) (OV ğ“¤)}
                       (p q : Term) â†’  ğ’¦ âŠ§ p â‰‹ q
                       ------------------------------------
 â†’                      âˆ€ (ğ‘¨ : Algebra ğ“¤ ğ‘†)(KA : ğ’¦ ğ‘¨)(h : hom (ğ‘» X) ğ‘¨)
                           â†’     âˆ£ h âˆ£ âˆ˜ (p Ì‡ ğ‘» X) â‰¡ âˆ£ h âˆ£ âˆ˜ (q Ì‡ ğ‘» X)

âŠ§-class-H-invariance {X = X} p q Î± ğ‘¨ KA h = Î³
 where
  Î² : âˆ€(ğ’‚ : X â†’ âˆ£ ğ‘» X âˆ£ ) â†’ (p Ì‡ ğ‘¨)(âˆ£ h âˆ£ âˆ˜ ğ’‚) â‰¡ (q Ì‡ ğ‘¨)(âˆ£ h âˆ£ âˆ˜ ğ’‚)
  Î² ğ’‚ = intensionality (Î± KA) (âˆ£ h âˆ£ âˆ˜ ğ’‚)

  Î¾ : âˆ€(ğ’‚ : X â†’ âˆ£ ğ‘» X âˆ£ ) â†’ âˆ£ h âˆ£ ((p Ì‡ ğ‘» X) ğ’‚) â‰¡ âˆ£ h âˆ£ ((q Ì‡ ğ‘» X) ğ’‚)
  Î¾ ğ’‚ =
   âˆ£ h âˆ£ ((p Ì‡ ğ‘» X) ğ’‚)  â‰¡âŸ¨ comm-hom-term gfe (ğ‘» X) ğ‘¨ h p ğ’‚ âŸ©
   (p Ì‡ ğ‘¨)(âˆ£ h âˆ£ âˆ˜ ğ’‚) â‰¡âŸ¨ Î² ğ’‚ âŸ©
   (q Ì‡ ğ‘¨)(âˆ£ h âˆ£ âˆ˜ ğ’‚) â‰¡âŸ¨ (comm-hom-term gfe (ğ‘» X) ğ‘¨ h q ğ’‚)â»Â¹ âŸ©
   âˆ£ h âˆ£ ((q Ì‡ ğ‘» X) ğ’‚)  âˆ

  Î³ : âˆ£ h âˆ£ âˆ˜ (p Ì‡ ğ‘» X) â‰¡ âˆ£ h âˆ£ âˆ˜ (q Ì‡ ğ‘» X)
  Î³ = gfe Î¾

-- â‡ (the "if" direction)
âŠ§-class-H-coinvariance : {ğ“¤ ğ“§ : Universe}{X : ğ“§ Ì‡}
                         {ğ’¦ : Pred (Algebra ğ“¤ ğ‘†) (OV ğ“¤)}
                         (p q : Term)
 â†’                       ( (ğ‘¨ : Algebra ğ“¤ ğ‘†)(KA : ğ‘¨ âˆˆ ğ’¦) (h : hom (ğ‘» X) ğ‘¨)
                                    â†’ âˆ£ h âˆ£ âˆ˜ (p Ì‡ ğ‘» X) â‰¡ âˆ£ h âˆ£ âˆ˜ (q Ì‡ ğ‘» X) )
                         ----------------------------------------------------
 â†’                       ğ’¦ âŠ§ p â‰‹ q

âŠ§-class-H-coinvariance {X = X} p q Î² {ğ‘¨} KA = Î³
  where
   h : (ğ’‚ : X â†’ âˆ£ ğ‘¨ âˆ£) â†’ hom (ğ‘» X) ğ‘¨
   h ğ’‚ = lift-hom ğ‘¨ ğ’‚

   Î³ : ğ‘¨ âŠ§ p â‰ˆ q
   Î³ = gfe Î» ğ’‚ â†’
    (p Ì‡ ğ‘¨) ğ’‚            â‰¡âŸ¨ ğ“‡â„¯ğ’»ğ“ âŸ©
    (p Ì‡ ğ‘¨)(âˆ£ h ğ’‚ âˆ£ âˆ˜ â„Š)   â‰¡âŸ¨(comm-hom-term gfe (ğ‘» X) ğ‘¨ (h ğ’‚) p â„Š)â»Â¹ âŸ©
    (âˆ£ h ğ’‚ âˆ£ âˆ˜ (p Ì‡ ğ‘» X)) â„Š  â‰¡âŸ¨ ap (Î» - â†’ - â„Š) (Î² ğ‘¨ KA (h ğ’‚)) âŸ©
    (âˆ£ h ğ’‚ âˆ£ âˆ˜ (q Ì‡ ğ‘» X)) â„Š  â‰¡âŸ¨ (comm-hom-term gfe (ğ‘» X) ğ‘¨ (h ğ’‚) q â„Š) âŸ©
    (q Ì‡ ğ‘¨)(âˆ£ h ğ’‚ âˆ£ âˆ˜ â„Š)   â‰¡âŸ¨ ğ“‡â„¯ğ’»ğ“ âŸ©
    (q Ì‡ ğ‘¨) ğ’‚             âˆ

âŠ§-class-H-compatibility : {ğ“¤ ğ“§ : Universe}{X : ğ“§ Ì‡}
                    {ğ’¦ : Pred (Algebra ğ“¤ ğ‘†) (OV ğ“¤)}
                    (p q : Term{ğ“§}{X})
                    ----------------------------------------
 â†’                  (ğ’¦ âŠ§ p â‰‹ q) â‡” ( (ğ‘¨ : Algebra ğ“¤ ğ‘†)(KA : ğ‘¨ âˆˆ ğ’¦)(hh : hom (ğ‘» X) ğ‘¨)
                                           â†’   âˆ£ hh âˆ£ âˆ˜ (p Ì‡ ğ‘» X) â‰¡ âˆ£ hh âˆ£ âˆ˜ (q Ì‡ ğ‘» X) )

âŠ§-class-H-compatibility p q = âŠ§-class-H-invariance p q , âŠ§-class-H-coinvariance p q

\end{code}

-------------------------------------

[â† UALib.Varieties.ModelTheory](UALib.Varieties.ModelTheory.html)
<span style="float:right;">[UALib.Varieties.Varieties â†’](UALib.Varieties.Varieties.html)</span>

{% include UALib.Links.md %}

<!-- #### Term operations preserve identities -->

