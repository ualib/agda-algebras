---
layout: default
title : UALib.Varieties.EquationalLogic module (The Agda Universal Algebra Library)
date : 2021-01-14
author: William DeMeo
---

### <a id="equational-logic-types">Equational Logic Types</a>

This section presents the [UALib.Varieties.EquationalLogic][] module of the [Agda Universal Algebra Library][].

We prove closure properties, or "invariance," of the models relation defined in [UALib.Varieties.ModelTheory][] module .  Proofs are given of the following facts (which are needed, for example, in the proof the Birkhoff HSP Theorem).

* [Algebraic invariance](#algebraic-invariance). The âŠ§ relation is an *algebraic invariant* (stable under isomorphism).

* [Product invariance](#product-invariance). Identities modeled by a class of algebras are also modeled by all products of algebras in the class.

* [Subalgebra invariance](#subalgebra-invariance). Identities modeled by a class of algebras are also modeled by all subalgebras of algebras in the class;

* [Homomorphism invariance](#homomorphism-invariance). Identities modeled by a class of algebras are also modeled by all homomorphic images (equivalently, all quotients) of algebras in the class;

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import UALib.Algebras using (Signature; ğ“; ğ“¥; Algebra; _â† _)
open import UALib.Prelude.Preliminaries using (global-dfunext; Universe; _Ì‡)


module UALib.Varieties.EquationalLogic
 {ğ‘† : Signature ğ“ ğ“¥}{gfe : global-dfunext}
 {ğ• : {ğ“¤ ğ“§ : Universe}{X : ğ“§ Ì‡ }(ğ‘¨ : Algebra ğ“¤ ğ‘†) â†’ X â†  ğ‘¨}
 where


open import UALib.Varieties.ModelTheory {ğ‘† = ğ‘†}{gfe}{ğ•} public
open import UALib.Prelude.Preliminaries using (âˆ˜-embedding; domain; embeddings-are-lc) public

\end{code}

-------------------------------------

#### <a id="computing-with-âŠ§">Computing with âŠ§</a>

We have formally defined ğ‘¨ âŠ§ ğ‘ â‰ˆ ğ‘, which represents the assertion that p â‰ˆ  q holds when this identity is interpreted in the algebra  ğ‘¨; syntactically,  ğ‘ Ì‡  ğ‘¨ â‰¡  ğ‘ Ì‡  ğ‘¨.  It should be emphasized that the expression  ğ‘ Ì‡  ğ‘¨ â‰¡  ğ‘ Ì‡  ğ‘¨ is interpreted computationally as an \emph{extensional equality}, by which we mean that for each *assignment function*  ğ’‚ :  X â†’ âˆ£ ğ‘¨ âˆ£, assigning values in the domain of  ğ‘¨ to the variable symbols in  X, we have (ğ‘ Ì‡  ğ‘¨)  ğ’‚ â‰¡ (ğ‘ Ì‡  ğ‘¨)  ğ’‚.

---------------------------------

#### <a id="algebraic-invariance">Algebraic invariance</a>

The binary relation âŠ§ would be practically useless if it were not an *algebraic invariant* (i.e., invariant under isomorphism).

\begin{code}

âŠ§-I-invariance : {ğ“  ğ“¤ ğ“§ : Universe}{X : ğ“§ Ì‡}{ğ‘¨ : Algebra ğ“  ğ‘†}{ğ‘© : Algebra ğ“¤ ğ‘†}
                 (p q : Term{ğ“§}{X})  â†’  ğ‘¨ âŠ§ p â‰ˆ q  â†’  ğ‘¨ â‰… ğ‘©  â†’  ğ‘© âŠ§ p â‰ˆ q

âŠ§-I-invariance {ğ‘¨ = ğ‘¨}{ğ‘© = ğ‘©} p q Apq (f , g , fâˆ¼g , gâˆ¼f) = Î³
 where
  Î³ : p Ì‡ ğ‘© â‰¡ q Ì‡ ğ‘©
  Î³ = gfe Î» x â†’
      (p Ì‡ ğ‘©) x â‰¡âŸ¨ ğ“‡â„¯ğ’»ğ“ âŸ©
      (p Ì‡ ğ‘©) (âˆ£ ğ’¾ğ’¹ ğ‘© âˆ£ âˆ˜ x) â‰¡âŸ¨ ap (Î» - â†’ (p Ì‡ ğ‘©) -) (gfe Î» i â†’ ((fâˆ¼g)(x i))â»Â¹)  âŸ©
      (p Ì‡ ğ‘©) ((âˆ£ f âˆ£ âˆ˜ âˆ£ g âˆ£) âˆ˜ x) â‰¡âŸ¨ (comm-hom-term gfe ğ‘¨ ğ‘© f p (âˆ£ g âˆ£ âˆ˜ x))â»Â¹ âŸ©
      âˆ£ f âˆ£ ((p Ì‡ ğ‘¨) (âˆ£ g âˆ£ âˆ˜ x)) â‰¡âŸ¨ ap (Î» - â†’ âˆ£ f âˆ£ (- (âˆ£ g âˆ£ âˆ˜ x))) Apq âŸ©
      âˆ£ f âˆ£ ((q Ì‡ ğ‘¨) (âˆ£ g âˆ£ âˆ˜ x)) â‰¡âŸ¨ comm-hom-term gfe ğ‘¨ ğ‘© f q (âˆ£ g âˆ£ âˆ˜ x) âŸ©
      (q Ì‡ ğ‘©) ((âˆ£ f âˆ£ âˆ˜ âˆ£ g âˆ£) âˆ˜  x) â‰¡âŸ¨ ap (Î» - â†’ (q Ì‡ ğ‘©) -) (gfe Î» i â†’ (fâˆ¼g) (x i)) âŸ©
      (q Ì‡ ğ‘©) x âˆ

\end{code}

As the proof makes clear, we show ğ‘© âŠ§ p â‰ˆ q by showing that p Ì‡ ğ‘© â‰¡ q Ì‡ ğ‘© holds *extensionally*, that is, `âˆ€ x, (p Ì‡ ğ‘©) x â‰¡ (q Ì‡ ğ‘©) x`.

--------------------------------------

#### <a id="Lift-invariance">Lift-invariance</a>

The âŠ§ relation is also invariant under the algebraic lift and lower operations.

\begin{code}

âŠ§-lift-alg-invariance : {ğ“¤ ğ“¦ ğ“§ : Universe}{X : ğ“§ Ì‡}
                        (ğ‘¨ : Algebra ğ“¤ ğ‘†)(p q : Term{ğ“§}{X})
                        -----------------------------------
 â†’                      ğ‘¨ âŠ§ p â‰ˆ q  â†’  lift-alg ğ‘¨ ğ“¦ âŠ§ p â‰ˆ q

âŠ§-lift-alg-invariance ğ‘¨ p q Apq = âŠ§-I-invariance p q Apq lift-alg-â‰…


âŠ§-lower-alg-invariance : {ğ“¤ ğ“¦ ğ“§ : Universe}{X : ğ“§ Ì‡}(ğ‘¨ : Algebra ğ“¤ ğ‘†)
                         (p q : Term{ğ“§}{X})
                         -----------------------------------
 â†’                       lift-alg ğ‘¨ ğ“¦ âŠ§ p â‰ˆ q  â†’  ğ‘¨ âŠ§ p â‰ˆ q

âŠ§-lower-alg-invariance ğ‘¨ p q lApq = âŠ§-I-invariance p q lApq (sym-â‰… lift-alg-â‰…)

\end{code}

---------------------------------------------

#### <a id="subalgebraic-invariance">Subalgebraic invariance</a>
We show that identities modeled by a class of algebras is also modeled by all subalgebras of the class.  In other terms, every term equation `p â‰ˆ q` that is satisfied by all `ğ‘¨ âˆˆ ğ’¦` is also satisfied by every subalgebra of a member of ğ’¦.

\begin{code}

âŠ§-S-invariance : {ğ“¤ ğ“  ğ“§ : Universe}{X : ğ“§ Ì‡}{ğ’¦ : Pred (Algebra ğ“  ğ‘†) (OV ğ“ )}(p q : Term)
                 (ğ‘© : SubalgebraOfClass{ğ“¤}{ğ“ } ğ’¦)
                 ----------------------------
 â†’               ğ’¦ âŠ§ p â‰‹ q   â†’   âˆ£ ğ‘© âˆ£ âŠ§ p â‰ˆ q

âŠ§-S-invariance {X = X} p q (ğ‘© , ğ‘¨ , SA , (ka , BisSA)) Kpq = gfe Î» b â†’
                                                              (embeddings-are-lc âˆ£ h âˆ£ hem)(Î¾ b)
 where
  h' : hom âˆ£ SA âˆ£ ğ‘¨
  h' = (âˆ£ snd SA âˆ£ , snd âˆ¥ snd SA âˆ¥ )

  h : hom ğ‘© ğ‘¨
  h = HCompClosed ğ‘© (âˆ£ SA âˆ£) ğ‘¨ (âˆ£ BisSA âˆ£) h'

  hem : is-embedding âˆ£ h âˆ£
  hem = âˆ˜-embedding (fst âˆ¥ snd SA âˆ¥) (isoâ†’embedding BisSA)

  Î¾ : (b : X â†’ âˆ£ ğ‘© âˆ£ ) â†’ âˆ£ h âˆ£ ((p Ì‡ ğ‘©) b) â‰¡ âˆ£ h âˆ£ ((q Ì‡ ğ‘©) b)
  Î¾ b = âˆ£ h âˆ£((p Ì‡ ğ‘©) b)   â‰¡âŸ¨ comm-hom-term gfe ğ‘© ğ‘¨ h p b âŸ©
        (p Ì‡ ğ‘¨)(âˆ£ h âˆ£ âˆ˜ b) â‰¡âŸ¨ intensionality (Kpq ka) (âˆ£ h âˆ£ âˆ˜ b) âŸ©
        (q Ì‡ ğ‘¨)(âˆ£ h âˆ£ âˆ˜ b) â‰¡âŸ¨ (comm-hom-term gfe ğ‘© ğ‘¨ h q b)â»Â¹ âŸ©
        âˆ£ h âˆ£((q Ì‡ ğ‘©) b)   âˆ

\end{code}

------------------------------------------------------------

#### <a id="product-invariance">Product invariance</a>

An identities satisfied by all algebras in a class are also satisfied by the product of algebras in that class.

\begin{code}

âŠ§-P-invariance : {ğ“¤ ğ“§ : Universe}{X : ğ“§ Ì‡}(p q : Term{ğ“§}{X})
                 (I : ğ“¤ Ì‡)(ğ’œ : I â†’ Algebra ğ“¤ ğ‘†)
                 -----------------------------------------
 â†’               (âˆ€ i â†’ (ğ’œ i) âŠ§ p â‰ˆ q)  â†’  â¨… ğ’œ âŠ§ p â‰ˆ q

âŠ§-P-invariance p q I ğ’œ ğ’œpq = Î³
 where
  Î³ : p Ì‡ â¨… ğ’œ  â‰¡  q Ì‡ â¨… ğ’œ
  Î³ = gfe Î» a â†’
   (p Ì‡ â¨… ğ’œ) a                           â‰¡âŸ¨ interp-prod gfe p ğ’œ a âŸ©
   (Î» i â†’ ((p Ì‡ (ğ’œ i)) (Î» x â†’ (a x) i))) â‰¡âŸ¨ gfe (Î» i â†’ cong-app (ğ’œpq i) (Î» x â†’ (a x) i)) âŸ©
   (Î» i â†’ ((q Ì‡ (ğ’œ i)) (Î» x â†’ (a x) i))) â‰¡âŸ¨ (interp-prod gfe q ğ’œ a)â»Â¹ âŸ©
   (q Ì‡ â¨… ğ’œ) a                           âˆ


âŠ§-P-class-invariance : {ğ“¤ ğ“§ : Universe}{X : ğ“§ Ì‡}{ğ’¦ : Pred (Algebra ğ“¤ ğ‘†) (OV ğ“¤)}
                       (p q : Term{ğ“§}{X})(I : ğ“¤ Ì‡)(ğ’œ : I â†’ Algebra ğ“¤ ğ‘†)
 â†’                     (âˆ€ i â†’ ğ’œ i âˆˆ ğ’¦)
                      ---------------------------
 â†’                     ğ’¦ âŠ§ p â‰‹ q  â†’  â¨… ğ’œ âŠ§ p â‰ˆ q

âŠ§-P-class-invariance p q I ğ’œ Kğ’œ Î± = Î³
  where
   Î² : âˆ€ i â†’ (ğ’œ i) âŠ§ p â‰ˆ q
   Î² i = Î± (Kğ’œ i)

   Î³ : p Ì‡ â¨… ğ’œ â‰¡ q Ì‡ â¨… ğ’œ
   Î³ = âŠ§-P-invariance p q I ğ’œ Î²

\end{code}

Another fact that will turn out to be useful is that a product of a collection of algebras models p â‰ˆ q if the lift of each algebra in the collection models p â‰ˆ q.

\begin{code}

âŠ§-P-lift-invariance : {ğ“¤ ğ“¦ ğ“§ : Universe}{X : ğ“§ Ì‡}(p q : Term{ğ“§}{X})
                      (I : ğ“¤ Ì‡ ) (ğ’œ : I â†’ Algebra ğ“¤ ğ‘†)
                      ----------------------------------------------------
 â†’                    (âˆ€ i â†’ (lift-alg (ğ’œ i) ğ“¦) âŠ§ p â‰ˆ q)  â†’  â¨… ğ’œ âŠ§ p â‰ˆ q

âŠ§-P-lift-invariance {ğ“¤}{ğ“¦} p q I ğ’œ lApq = âŠ§-P-invariance p q I ğ’œ Aipq
  where
   Aipq : (i : I) â†’ (ğ’œ i) âŠ§ p â‰ˆ q
   Aipq i = âŠ§-I-invariance p q (lApq i) (sym-â‰… lift-alg-â‰…)

\end{code}


--------------------------------------------

#### <a id="homomorphisc-invariance">Homomorphic invariance</a>

[Those mainly interested in the formal proof of Birkhoff's HSP theorem can safely skip this section; it is not needed elsewhere.]

If an algebra ğ‘¨ models an identity p â‰ˆ q, then the pair (p , q) belongs to the kernel of every homomorphism Ï† : hom (ğ‘» X) ğ‘¨ from the term algebra to ğ‘¨; that is, every homomorphism from ğ‘» X to ğ‘¨ maps p and q to the same element of ğ‘¨.

\begin{code}

âŠ§-H-invariance : {ğ“¤ ğ“§ : Universe}(X : ğ“§ Ì‡)(p q : Term{ğ“§}{X})
                 (ğ‘¨ : Algebra ğ“¤ ğ‘†)(Ï† : hom (ğ‘» X) ğ‘¨)
                 -----------------------------
 â†’               ğ‘¨ âŠ§ p â‰ˆ q  â†’  âˆ£ Ï† âˆ£ p â‰¡ âˆ£ Ï† âˆ£ q

âŠ§-H-invariance X p q ğ‘¨ Ï† Î² =
  âˆ£ Ï† âˆ£ p              â‰¡âŸ¨ ap âˆ£ Ï† âˆ£ (term-agreement p) âŸ©
  âˆ£ Ï† âˆ£ ((p Ì‡ ğ‘» X) â„Š)  â‰¡âŸ¨ (comm-hom-term gfe (ğ‘» X) ğ‘¨ Ï† p â„Š ) âŸ©
  (p Ì‡ ğ‘¨) (âˆ£ Ï† âˆ£ âˆ˜ â„Š)  â‰¡âŸ¨ intensionality Î² (âˆ£ Ï† âˆ£ âˆ˜ â„Š ) âŸ©
  (q Ì‡ ğ‘¨) (âˆ£ Ï† âˆ£ âˆ˜ â„Š)  â‰¡âŸ¨ (comm-hom-term gfe (ğ‘» X) ğ‘¨ Ï† q â„Š )â»Â¹ âŸ©
  âˆ£ Ï† âˆ£ ((q Ì‡ ğ‘» X) â„Š)  â‰¡âŸ¨ (ap âˆ£ Ï† âˆ£ (term-agreement q))â»Â¹ âŸ©
  âˆ£ Ï† âˆ£ q              âˆ

\end{code}

More generally, an identity is satisfied by all algebras in a class if and only if that identity is invariant under all homomorphisms from the term algebra `ğ‘» X` into algebras of the class. More precisely, if ğ’¦ is a class of ğ‘†-algebras and ğ‘, ğ‘ terms in the language of ğ‘†, then,

ğ’¦ âŠ§ p â‰ˆ q  â‡”  âˆ€ ğ‘¨ âˆˆ ğ’¦,  âˆ€ Ï† : hom (ğ‘» X) ğ‘¨,  Ï† âˆ˜ (ğ‘ Ì‡ (ğ‘» X)) = Ï† âˆ˜ (ğ‘ Ì‡ (ğ‘» X)).

\begin{code}

-- â‡’ (the "only if" direction)
âŠ§-H-class-invariance : {ğ“¤ ğ“§ : Universe}(X : ğ“§ Ì‡){ğ’¦ : Pred (Algebra ğ“¤ ğ‘†)(OV ğ“¤)}(p q : Term)
 â†’                     ğ’¦ âŠ§ p â‰‹ q
 â†’                     (ğ‘¨ : Algebra ğ“¤ ğ‘†)(Ï† : hom (ğ‘» X) ğ‘¨)
                       ---------------------------------
 â†’                     ğ‘¨ âˆˆ ğ’¦  â†’  âˆ£ Ï† âˆ£ âˆ˜ (p Ì‡ ğ‘» X) â‰¡ âˆ£ Ï† âˆ£ âˆ˜ (q Ì‡ ğ‘» X)

âŠ§-H-class-invariance X p q Î± ğ‘¨ Ï† ka = gfe Î¾
 where
  Î¾ : âˆ€(ğ’‚ : X â†’ âˆ£ ğ‘» X âˆ£ ) â†’ âˆ£ Ï† âˆ£ ((p Ì‡ ğ‘» X) ğ’‚) â‰¡ âˆ£ Ï† âˆ£ ((q Ì‡ ğ‘» X) ğ’‚)

  Î¾ ğ’‚ = âˆ£ Ï† âˆ£ ((p Ì‡ ğ‘» X) ğ’‚)  â‰¡âŸ¨ comm-hom-term gfe (ğ‘» X) ğ‘¨ Ï† p ğ’‚ âŸ©
        (p Ì‡ ğ‘¨)(âˆ£ Ï† âˆ£ âˆ˜ ğ’‚)   â‰¡âŸ¨ intensionality (Î± ka) (âˆ£ Ï† âˆ£ âˆ˜ ğ’‚) âŸ©
        (q Ì‡ ğ‘¨)(âˆ£ Ï† âˆ£ âˆ˜ ğ’‚)   â‰¡âŸ¨ (comm-hom-term gfe (ğ‘» X) ğ‘¨ Ï† q ğ’‚)â»Â¹ âŸ©
        âˆ£ Ï† âˆ£ ((q Ì‡ ğ‘» X) ğ’‚)  âˆ

-- â‡ (the "if" direction)
âŠ§-H-class-coinvariance : {ğ“¤ ğ“§ : Universe}(X : ğ“§ Ì‡){ğ’¦ : Pred (Algebra ğ“¤ ğ‘†)(OV ğ“¤)}(p q : Term)
 â†’                       ((ğ‘¨ : Algebra ğ“¤ ğ‘†)(Ï† : hom (ğ‘» X) ğ‘¨)
                            â†’  ğ‘¨ âˆˆ ğ’¦  â†’  âˆ£ Ï† âˆ£ âˆ˜ (p Ì‡ ğ‘» X) â‰¡ âˆ£ Ï† âˆ£ âˆ˜ (q Ì‡ ğ‘» X))
                         -------------------------------------------------
 â†’                       ğ’¦ âŠ§ p â‰‹ q

âŠ§-H-class-coinvariance X p q Î² {ğ‘¨} ka = Î³
  where
   Ï† : (ğ’‚ : X â†’ âˆ£ ğ‘¨ âˆ£) â†’ hom (ğ‘» X) ğ‘¨
   Ï† ğ’‚ = lift-hom ğ‘¨ ğ’‚

   Î³ : ğ‘¨ âŠ§ p â‰ˆ q
   Î³ = gfe Î» ğ’‚ â†’
        (p Ì‡ ğ‘¨)(âˆ£ Ï† ğ’‚ âˆ£ âˆ˜ â„Š)     â‰¡âŸ¨(comm-hom-term gfe (ğ‘» X) ğ‘¨ (Ï† ğ’‚) p â„Š)â»Â¹ âŸ©
        (âˆ£ Ï† ğ’‚ âˆ£ âˆ˜ (p Ì‡ ğ‘» X)) â„Š  â‰¡âŸ¨ ap (Î» - â†’ - â„Š) (Î² ğ‘¨ (Ï† ğ’‚) ka) âŸ©
        (âˆ£ Ï† ğ’‚ âˆ£ âˆ˜ (q Ì‡ ğ‘» X)) â„Š  â‰¡âŸ¨ (comm-hom-term gfe (ğ‘» X) ğ‘¨ (Ï† ğ’‚) q â„Š) âŸ©
        (q Ì‡ ğ‘¨)(âˆ£ Ï† ğ’‚ âˆ£ âˆ˜ â„Š)     âˆ


âŠ§-H-compatibility : {ğ“¤ ğ“§ : Universe}(X : ğ“§ Ì‡){ğ’¦ : Pred (Algebra ğ“¤ ğ‘†) (OV ğ“¤)}(p q : Term)
                    ----------------------------------------------------------------
 â†’                  ğ’¦ âŠ§ p â‰‹ q â‡” ((ğ‘¨ : Algebra ğ“¤ ğ‘†)(Ï† : hom (ğ‘» X) ğ‘¨)
                                    â†’   ğ‘¨ âˆˆ ğ’¦  â†’  âˆ£ Ï† âˆ£ âˆ˜ (p Ì‡ ğ‘» X) â‰¡ âˆ£ Ï† âˆ£ âˆ˜ (q Ì‡ ğ‘» X))

âŠ§-H-compatibility X p q = âŠ§-H-class-invariance X p q , âŠ§-H-class-coinvariance X p q

\end{code}

-------------------------------------

[â† UALib.Varieties.ModelTheory](UALib.Varieties.ModelTheory.html)
<span style="float:right;">[UALib.Varieties.Varieties â†’](UALib.Varieties.Varieties.html)</span>

{% include UALib.Links.md %}

