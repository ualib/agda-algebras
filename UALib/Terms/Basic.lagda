---
layout: default
title : Terms.Basic module (The Agda Universal Algebra Library)
date : 2021-01-14
author: William DeMeo
---

### <a id="basic-definitions">Basic definitions</a>

This section presents the [Terms.Basic][] module of the [Agda Universal Algebra Library][].

The theoretical background that begins each subsection below is based on Cliff Bergman's textbook [Bergman (2012)][], specifically, Section 4.3.  Apart from notation, our presentation is similar to Bergman's, but we will be more concise, omitting some details and all examples, in order to more quickly arrive at our objective, which is to use type theory to express the concepts and formalize them in the [Agda][] language.  We refer the reader to [Bergman (2012)][] for a more complete exposition of classical (informal) universal algebra.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Algebras.Signatures using (Signature; ğ“; ğ“¥)
open import MGS-Subsingleton-Theorems using (global-dfunext)

module Terms.Basic {ğ‘† : Signature ğ“ ğ“¥} {gfe : global-dfunext} where

open import Homomorphisms.HomomorphicImages{ğ‘† = ğ‘†}{gfe} public

\end{code}

#### <a id="the-type-of-terms">The type of terms</a>

Fix a signature `ğ‘†` and let `X` denote an arbitrary nonempty collection of variable symbols. Assume the symbols in `X` are distinct from the operation symbols of `ğ‘†`, that is `X âˆ© âˆ£ ğ‘† âˆ£ = âˆ…`.

By a **word** in the language of `ğ‘†`, we mean a nonempty, finite sequence of members of `X âˆª âˆ£ ğ‘† âˆ£`. We denote the concatenation of such sequences by simple juxtaposition.

Let `Sâ‚€` denote the set of nullary operation symbols of `ğ‘†`. We define by induction on `n` the sets `ğ‘‡â‚™` of **words** over `X âˆª âˆ£ ğ‘† âˆ£` as follows (cf. [Bergman (2012)][] Def. 4.19):

`ğ‘‡â‚€ := X âˆª Sâ‚€` and `ğ‘‡â‚™â‚Šâ‚ := ğ‘‡â‚™ âˆª ğ’¯â‚™`

where `ğ’¯â‚™` is the collection of all `ğ‘“ ğ’•` such that `ğ‘“ : âˆ£ ğ‘† âˆ£` and `ğ’• : âˆ¥ ğ‘† âˆ¥ ğ‘“ â†’ ğ‘‡â‚™`. (Recall, `âˆ¥ ğ‘† âˆ¥ ğ‘“` is the arity of the operation symbol ğ‘“.)

We define the collection of **terms** in the signature `ğ‘†` over `X` by `Term X := â‹ƒâ‚™ ğ‘‡â‚™`. By an ğ‘†-**term** we mean a term in the language of `ğ‘†`.

The definition of `Term X` is recursive, indicating that an inductive type could be used to represent the semantic notion of terms in type theory. Indeed, such a representation is given by the following inductive type.

\begin{code}

data Term {ğ“§ : Universe}(X : ğ“§ Ì‡ ) : ov ğ“§ Ì‡  where
  generator : X â†’ Term X
  node : (f : âˆ£ ğ‘† âˆ£)(ğ’• : âˆ¥ ğ‘† âˆ¥ f â†’ Term X) â†’ Term X

open Term

\end{code}

**Notation**. As usual, the type `X` represents an arbitrary collection of variable symbols. Throughout this module the name of the first constructor of the `Term` type will remain `generator`. However, in all of the modules that follow this one, we will use the shorthand `â„Š` to denote the `generator` constructor.



#### <a id="the-term-algebra">The term algebra</a>

For a given signature `ğ‘†`, if the type `Term X` is nonempty (equivalently, if `X` or `âˆ£ ğ‘† âˆ£` is nonempty), then we can define an algebraic structure, denoted by `ğ‘» X` and called the **term algebra in the signature** `ğ‘†` **over** `X`.  Terms are viewed as acting on other terms, so both the domain and basic operations of the algebra are the terms themselves.

* For each operation symbol `ğ‘“ : âˆ£ ğ‘† âˆ£`, denote by `ğ‘“ Ì‚ (ğ‘» X)` the operation on `Term X` which maps a tuple `ğ‘¡ : âˆ¥ ğ‘† âˆ¥ ğ‘“ â†’ âˆ£ ğ‘» X âˆ£` to the formal term `ğ‘“ ğ‘¡`.

* Define `ğ‘» X` to be the algebra with universe `âˆ£ ğ‘» X âˆ£ := Term X` and operations `ğ‘“ Ì‚ (ğ‘» X)`, one for each symbol `ğ‘“` in `âˆ£ ğ‘† âˆ£`.

In [Agda][] the term algebra can be defined as simply as one would hope.

\begin{code}

--The term algebra ğ‘» X.
ğ‘» : {ğ“§ : Universe}(X : ğ“§ Ì‡ ) â†’ Algebra (ov ğ“§) ğ‘†
ğ‘» X = Term X , node

\end{code}



#### <a id="the-universal-property">The universal property</a>

The term algebra `ğ‘» X` is *absolutely free*, or *universal*, for algebras in the signature `ğ‘†`. That is, for every ğ‘†-algebra `ğ‘¨`, the following hold.

1.  Every map `h : ğ‘‹ â†’ âˆ£ ğ‘¨ âˆ£` lifts to a homomorphism from `ğ‘» X` to `ğ‘¨`.
2.  The induced homomorphism is unique.

We now prove this in [Agda][], starting with the fact that every map from `X` to `âˆ£ ğ‘¨ âˆ£` lifts to a map from `âˆ£ ğ‘» X âˆ£` to `âˆ£ ğ‘¨ âˆ£` in a natural way, by induction on the structure of the term.

\begin{code}

module _ {ğ“¤ ğ“§ : Universe}{X : ğ“§ Ì‡ } where

 free-lift : (ğ‘¨ : Algebra ğ“¤ ğ‘†)(h : X â†’ âˆ£ ğ‘¨ âˆ£) â†’ âˆ£ ğ‘» X âˆ£ â†’ âˆ£ ğ‘¨ âˆ£

 free-lift _ h (generator x) = h x

 free-lift ğ‘¨ h (node f ğ’•) = (f Ì‚ ğ‘¨) Î» i â†’ free-lift ğ‘¨ h (ğ’• i)

\end{code}

Next, we verify that the lift so defined is a homomorphism.

\begin{code}

 lift-hom : (ğ‘¨ : Algebra ğ“¤ ğ‘†) â†’ (X â†’ âˆ£ ğ‘¨ âˆ£) â†’ hom (ğ‘» X) ğ‘¨

 lift-hom ğ‘¨ h = free-lift ğ‘¨ h , Î» f a â†’ ap (_ Ì‚ ğ‘¨) ğ“‡â„¯ğ’»ğ“

\end{code}

Finally, we prove that the resulting homomorphism is unique.

\begin{code}


 free-unique : funext ğ“¥ ğ“¤ â†’ (ğ‘¨ : Algebra ğ“¤ ğ‘†)(g h : hom (ğ‘» X) ğ‘¨)
  â†’            (âˆ€ x â†’ âˆ£ g âˆ£ (generator x) â‰¡ âˆ£ h âˆ£ (generator x))
               -------------------------------------------------
  â†’            âˆ€ (t : Term X) â†’  âˆ£ g âˆ£ t â‰¡ âˆ£ h âˆ£ t

 free-unique _ _ _ _ p (generator x) = p x

 free-unique fe ğ‘¨ g h p (node ğ‘“ ğ’•) = âˆ£ g âˆ£ (node ğ‘“ ğ’•)            â‰¡âŸ¨ âˆ¥ g âˆ¥ ğ‘“ ğ’• âŸ©
                                    (ğ‘“ Ì‚ ğ‘¨)(Î» i â†’ âˆ£ g âˆ£ (ğ’• i))   â‰¡âŸ¨ Î± âŸ©
                                    (ğ‘“ Ì‚ ğ‘¨)(Î» i â†’ âˆ£ h âˆ£ (ğ’• i))   â‰¡âŸ¨ (âˆ¥ h âˆ¥ ğ‘“ ğ’•)â»Â¹ âŸ©
                                    âˆ£ h âˆ£ (node ğ‘“ ğ’•)            âˆ
  where
  Î± : (ğ‘“ Ì‚ ğ‘¨) (âˆ£ g âˆ£ âˆ˜ ğ’•) â‰¡ (ğ‘“ Ì‚ ğ‘¨) (âˆ£ h âˆ£ âˆ˜ ğ’•)
  Î± = ap (ğ‘“ Ì‚ ğ‘¨) (fe Î» i â†’ free-unique fe ğ‘¨ g h p (ğ’• i))

\end{code}

Since it's absolutely free, the term algebra is the domain of a homomorphism to any algebra. Moreover, if we are given a surjective mapping `h` from `X` onto an algebra `ğ‘¨`, then the homomorphism constructed with `lift-hom ğ‘¨ h` will be an epimorphism from `ğ‘» X` onto `ğ‘¨`.

\begin{code}

 lift-of-epi-is-epi : {ğ‘¨ : Algebra ğ“¤ ğ‘†}{hâ‚€ : X â†’ âˆ£ ğ‘¨ âˆ£}
                      ---------------------------------
  â†’                   Epic hâ‚€ â†’ Epic âˆ£ lift-hom ğ‘¨ hâ‚€ âˆ£

 lift-of-epi-is-epi {ğ‘¨}{hâ‚€} hE y = Î³
  where
  hâ‚€â»Â¹y = Inv hâ‚€ (hE y)

  Î· : y â‰¡ âˆ£ lift-hom ğ‘¨ hâ‚€ âˆ£ (generator hâ‚€â»Â¹y)
  Î· = (InvIsInv hâ‚€ (hE y))â»Â¹

  Î³ : Image âˆ£ lift-hom ğ‘¨ hâ‚€ âˆ£ âˆ‹ y
  Î³ = eq y (generator hâ‚€â»Â¹y) Î·

\end{code}


In the modules [Birkhoff.FreeAlgebra][] and [Birkhoff.HSPTheorem][] `lift-hom` and `lift-of-epi-is-epi` are used to construct such epimorphisms.


--------------------------------------

<p></p>

[â†‘ Terms](Terms.html)
<span style="float:right;">[Terms.Operations â†’](Terms.Operations.html)</span>

{% include UALib.Links.md %}
