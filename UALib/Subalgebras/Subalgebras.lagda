---
layout: default
title : Subalgebras.Subalgebras module (The Agda Universal Algebra Library)
date : 2021-01-14
author: William DeMeo
---

### <a id="subalgebras">Subalgebras</a>

The [Subalgebras.Subalgebras][] module of the [Agda Universal Algebra Library][] defines the `Subalgebra` type, representing the subalgebra of a given algebra, as well as the collection of all subalgebras of a given class of algebras.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Algebras.Signatures using (Signature; ğ“; ğ“¥)

module Subalgebras.Subalgebras {ğ‘† : Signature ğ“ ğ“¥} where

open import Subalgebras.Subuniverses {ğ‘† = ğ‘†} public
open import MGS-Embeddings using (âˆ˜-embedding; id-is-embedding) public

\end{code}


#### <a id="subalgebra-type">Subalgebra type</a>

Given algebras `ğ‘¨ : Algebra ğ“¤ ğ‘†` and `ğ‘© : Algebra ğ“¦ ğ‘†`, we say that `ğ‘©` is a **subalgebra** of `ğ‘¨` just in case `ğ‘©` can be *homomorphically embedded* in `ğ‘¨`; that is, there exists a map `h : âˆ£ ğ‘© âˆ£ â†’ âˆ£ ğ‘¨ âˆ£` that is both a homomorphism and an embedding.<sup>[1](Subalgebras.Subalgebras.html#fn1)</sup>

\begin{code}

_IsSubalgebraOf_ : {ğ“¦ ğ“¤ : Universe}(ğ‘© : Algebra ğ“¦ ğ‘†)(ğ‘¨ : Algebra ğ“¤ ğ‘†) â†’ ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âŠ” ğ“¦ Ì‡
ğ‘© IsSubalgebraOf ğ‘¨ = Î£ h ê‰ hom ğ‘© ğ‘¨ , is-embedding âˆ£ h âˆ£

Subalgebra : {ğ“¦ ğ“¤ : Universe} â†’ Algebra ğ“¤ ğ‘† â†’ ov ğ“¦ âŠ” ğ“¤ Ì‡
Subalgebra {ğ“¦} ğ‘¨ = Î£ ğ‘© ê‰ (Algebra ğ“¦ ğ‘†) , ğ‘© IsSubalgebraOf ğ‘¨

\end{code}

Note the order of the arguments.  The universe `ğ“¦` comes first because in certain situations we must explicitly specify this universe, whereas we can almost always leave the universe `ğ“¤` implicit. (See, for example, the definition of `_IsSubalgebraOfClass_` below.)




#### <a id="consequences-of-first-homomorphism-theorem">Consequences of First Homomorphism Theorem</a>

We take this opportunity to prove an important lemma that makes use of the `IsSubalgebraOf` type defined above.  It is the following: If `ğ‘¨` and `ğ‘©` are `ğ‘†`-algebras and `h : hom ğ‘¨ ğ‘©` a homomorphism from `ğ‘¨` to `ğ‘©`, then the quotient `ğ‘¨ â•± ker h` is (isomorphic to) a subalgebra of `ğ‘©`.  This is an easy corollary of the First Homomorphism Theorem proved in the [Homomorphisms.Noether][] module.

\begin{code}

-- open Congruence

module _ {ğ“¤ ğ“¦ : Universe}(ğ‘¨ : Algebra ğ“¤ ğ‘†)(ğ‘© : Algebra ğ“¦ ğ‘†)(h : hom ğ‘¨ ğ‘©)
         -- extensionality assumptions:
         (pe : prop-ext ğ“¤ ğ“¦)(fe : dfunext ğ“¥ ğ“¦)

         -- truncation assumptions:
         (UIPcod : is-set âˆ£ ğ‘© âˆ£)
         (UMPblk : âˆ€ C â†’ is-subsingleton (IsBlock C))
         (UMPker : is-subsingleton-valued âˆ£ kercon ğ‘© {fe} h âˆ£) where

 FirstHomCorollary|Set : (ğ‘¨ [ ğ‘© ]/ker h){fe} IsSubalgebraOf ğ‘©
 FirstHomCorollary|Set = Ï•hom , Ï•emb
  where
  hh = FirstHomTheorem|Set ğ‘¨ ğ‘© h pe fe UIPcod UMPker UMPblk
  Ï•hom : hom ((ğ‘¨ [ ğ‘© ]/ker h) {fe}) ğ‘©
  Ï•hom = âˆ£ hh âˆ£

  Ï•emb : is-embedding âˆ£ Ï•hom âˆ£
  Ï•emb = âˆ¥ snd âˆ¥ hh âˆ¥ âˆ¥

\end{code}

If we apply the foregoing theorem to the special case in which the `ğ‘¨` is term algebra `ğ‘» X`, we obtain the following result which will be useful later.

\begin{code}

module _ {ğ“¤ ğ“¦ ğ“§ : Universe}(X : ğ“§ Ì‡)(ğ‘© : Algebra ğ“¦ ğ‘†)(h : hom (ğ‘» X) ğ‘©)
        -- extensionality assumptions:
         (pe : prop-ext (ov ğ“§) ğ“¦)(fe : dfunext ğ“¥ ğ“¦)

         -- truncation assumptions:
         (UIPcod : is-set âˆ£ ğ‘© âˆ£)
         (UMPblk : âˆ€ C â†’ is-subsingleton (IsBlock C))
         (UMPker : is-subsingleton-valued âˆ£ kercon ğ‘© {fe} h âˆ£) where

 free-quot-subalg : ((ğ‘» X) [ ğ‘© ]/ker h){fe} IsSubalgebraOf ğ‘©
 free-quot-subalg = FirstHomCorollary|Set{ğ“¤ = ov ğ“§}(ğ‘» X) ğ‘© h pe fe UIPcod UMPblk UMPker

\end{code}

**Notation**. For convenience, we define the following shorthand for the subalgebra relation.

\begin{code}

_â‰¤_ : {ğ“¦ ğ“¤ : Universe} â†’ Algebra ğ“¦ ğ‘† â†’ Algebra ğ“¤ ğ‘† â†’ ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âŠ” ğ“¦ Ì‡
ğ‘© â‰¤ ğ‘¨ = ğ‘© IsSubalgebraOf ğ‘¨

\end{code}

From now on we will use `ğ‘© â‰¤ ğ‘¨` to express the assertion that `ğ‘©` is a subalgebra of `ğ‘¨`.


#### <a id="subalgebras-of-a-class">Subalgebras of a class</a>

One of our goals is to formally express and prove properties of classes of algebraic structures.  Fixing a signature `ğ‘†` and a universe `ğ“¤`, we represent classes of `ğ‘†`-algebras with domains of type `ğ“¤ Ì‡` as predicates over the `Algebra ğ“¤ ğ‘†` type. In the syntax of the [UALib][], such predicates inhabit the type `Pred (Algebra ğ“¤ ğ‘†) ğ“©`, for some universe ğ“©.

Suppose `ğ’¦ : Pred (Algebra ğ“¤ ğ‘†) ğ“©` denotes a class of `ğ‘†`-algebras and `ğ‘© : Algebra ğ“¦ ğ‘†` denotes an arbitrary `ğ‘†`-algebra. Then we might wish to consider the assertion that `ğ‘©` is a subalgebra of an algebra in the class `ğ’¦`.  The next type we define allows us to express this assertion as `ğ‘© IsSubalgebraOfClass ğ’¦`.

\begin{code}

module _ {ğ“¦ ğ“¤ ğ“© : Universe} where

 _IsSubalgebraOfClass_ : Algebra ğ“¦ ğ‘† â†’ Pred (Algebra ğ“¤ ğ‘†) ğ“© â†’ ov (ğ“¤ âŠ” ğ“¦) âŠ” ğ“© Ì‡
 ğ‘© IsSubalgebraOfClass ğ’¦ = Î£ ğ‘¨ ê‰ Algebra ğ“¤ ğ‘† , Î£ sa ê‰ Subalgebra{ğ“¦} ğ‘¨ , (ğ‘¨ âˆˆ ğ’¦) Ã— (ğ‘© â‰… âˆ£ sa âˆ£)

\end{code}

Using this type, we express the collection of all subalgebras of algebras in a class by the type `SubalgebraOfClass`, which we now define.

\begin{code}

SubalgebraOfClass : {ğ“¦ ğ“¤ : Universe} â†’ Pred (Algebra ğ“¤ ğ‘†)(ov ğ“¤) â†’ ov (ğ“¤ âŠ” ğ“¦) Ì‡
SubalgebraOfClass {ğ“¦} ğ’¦ = Î£ ğ‘© ê‰ Algebra ğ“¦ ğ‘† , ğ‘© IsSubalgebraOfClass ğ’¦

\end{code}




#### <a id="subalgebra-lemmas">Subalgebra lemmas</a>

We conclude this module by proving a number of useful facts about subalgebras. Some of the formal statements below may appear to be redundant, and indeed they are to some extent. However, each one differs slightly from the next, if only with respect to the explicitness or implicitness of their arguments.  The aim is to make it as convenient as possible to apply the lemmas in different situations.  (We're in the [UALib][] utility closet now; elegance is not the priority.)

First we show that the subalgebra relation is a *preorder*; i.e., it is a reflexive, transitive binary relation.<sup>[2](Subalgebras.Subalgebras.html#fn2)</sup>

\begin{code}

â‰¤-reflexive : (ğ‘¨ : Algebra ğ“¤ ğ‘†) â†’ ğ‘¨ â‰¤ ğ‘¨
â‰¤-reflexive ğ‘¨ = (ğ‘–ğ‘‘ âˆ£ ğ‘¨ âˆ£ , id-is-hom) , id-is-embedding

â‰¤-refl : {ğ‘¨ : Algebra ğ“¤ ğ‘†} â†’ ğ‘¨ â‰¤ ğ‘¨
â‰¤-refl {ğ‘¨ = ğ‘¨} = â‰¤-reflexive ğ‘¨


â‰¤-transitivity : (ğ‘¨ : Algebra ğ“§ ğ‘†)(ğ‘© : Algebra ğ“¨ ğ‘†)(ğ‘ª : Algebra ğ“© ğ‘†)
 â†’               ğ‘ª â‰¤ ğ‘© â†’ ğ‘© â‰¤ ğ‘¨ â†’ ğ‘ª â‰¤ ğ‘¨

â‰¤-transitivity ğ‘¨ ğ‘© ğ‘ª CB BA = (âˆ˜-hom ğ‘ª ğ‘¨ âˆ£ CB âˆ£ âˆ£ BA âˆ£) , âˆ˜-embedding âˆ¥ BA âˆ¥ âˆ¥ CB âˆ¥

â‰¤-trans : (ğ‘¨ : Algebra ğ“§ ğ‘†){ğ‘© : Algebra ğ“¨ ğ‘†}{ğ‘ª : Algebra ğ“© ğ‘†} â†’ ğ‘ª â‰¤ ğ‘© â†’ ğ‘© â‰¤ ğ‘¨ â†’ ğ‘ª â‰¤ ğ‘¨
â‰¤-trans ğ‘¨ {ğ‘©}{ğ‘ª} = â‰¤-transitivity ğ‘¨ ğ‘© ğ‘ª

\end{code}

Next we prove that if two algebras are isomorphic and one of them is a subalgebra of `ğ‘¨`, then so is the other.

\begin{code}

â‰¤-iso : (ğ‘¨ : Algebra ğ“§ ğ‘†){ğ‘© : Algebra ğ“¨ ğ‘†}{ğ‘ª : Algebra ğ“© ğ‘†}
 â†’      ğ‘ª â‰… ğ‘© â†’ ğ‘© â‰¤ ğ‘¨ â†’ ğ‘ª â‰¤ ğ‘¨

â‰¤-iso ğ‘¨ {ğ‘©} {ğ‘ª} CB BA = (g âˆ˜ f , gfhom) , gfemb
 where
  f : âˆ£ ğ‘ª âˆ£ â†’ âˆ£ ğ‘© âˆ£
  f = fst âˆ£ CB âˆ£
  g : âˆ£ ğ‘© âˆ£ â†’ âˆ£ ğ‘¨ âˆ£
  g = fst âˆ£ BA âˆ£

  gfemb : is-embedding (g âˆ˜ f)
  gfemb = âˆ˜-embedding (âˆ¥ BA âˆ¥) (isoâ†’embedding CB)

  gfhom : is-homomorphism ğ‘ª ğ‘¨ (g âˆ˜ f)
  gfhom = âˆ˜-is-hom ğ‘ª ğ‘¨ {f}{g} (snd âˆ£ CB âˆ£) (snd âˆ£ BA âˆ£)


â‰¤-trans-â‰… : (ğ‘¨ : Algebra ğ“§ ğ‘†){ğ‘© : Algebra ğ“¨ ğ‘†}(ğ‘ª : Algebra ğ“© ğ‘†)
 â†’          ğ‘¨ â‰¤ ğ‘© â†’ ğ‘¨ â‰… ğ‘ª â†’ ğ‘ª â‰¤ ğ‘©

â‰¤-trans-â‰… ğ‘¨ {ğ‘©} ğ‘ª Aâ‰¤B Bâ‰…C = â‰¤-iso ğ‘© (â‰…-sym Bâ‰…C) Aâ‰¤B -- ğ‘¨ ğ‘ª Aâ‰¤B (sym-â‰… Bâ‰…C)


â‰¤-TRANS-â‰… : (ğ‘¨ : Algebra ğ“§ ğ‘†){ğ‘© : Algebra ğ“¨ ğ‘†}(ğ‘ª : Algebra ğ“© ğ‘†)
 â†’          ğ‘¨ â‰¤ ğ‘© â†’ ğ‘© â‰… ğ‘ª â†’ ğ‘¨ â‰¤ ğ‘ª

â‰¤-TRANS-â‰… ğ‘¨ ğ‘ª Aâ‰¤B Bâ‰…C = (âˆ˜-hom ğ‘¨ ğ‘ª âˆ£ Aâ‰¤B âˆ£ âˆ£ Bâ‰…C âˆ£) , âˆ˜-embedding (isoâ†’embedding Bâ‰…C)(âˆ¥ Aâ‰¤B âˆ¥)


â‰¤-mono : (ğ‘© : Algebra ğ“¦ ğ‘†){ğ’¦ ğ’¦' : Pred (Algebra ğ“¤ ğ‘†) ğ“©}
 â†’       ğ’¦ âŠ† ğ’¦' â†’ ğ‘© IsSubalgebraOfClass ğ’¦ â†’ ğ‘© IsSubalgebraOfClass ğ’¦'

â‰¤-mono ğ‘© KK' KB = âˆ£ KB âˆ£ , fst âˆ¥ KB âˆ¥ , KK' (âˆ£ snd âˆ¥ KB âˆ¥ âˆ£) , âˆ¥ (snd âˆ¥ KB âˆ¥) âˆ¥

\end{code}


#### <a id="lifts-of-subalgebras">Lifts of subalgebras</a>


\begin{code}

module _ {ğ’¦ : Pred (Algebra ğ“¤ ğ‘†)(ov ğ“¤)}{ğ‘© : Algebra ğ“¤ ğ‘†} where

 Lift-is-sub : ğ‘© IsSubalgebraOfClass ğ’¦ â†’ (Lift-alg ğ‘© ğ“¤) IsSubalgebraOfClass ğ’¦
 Lift-is-sub (ğ‘¨ , (sa , (KA , Bâ‰…sa))) = ğ‘¨ , sa , KA , â‰…-trans (â‰…-sym Lift-â‰…) Bâ‰…sa


Lift-â‰¤ : (ğ‘¨ : Algebra ğ“§ ğ‘†){ğ‘© : Algebra ğ“¨ ğ‘†}{ğ“© : Universe} â†’ ğ‘© â‰¤ ğ‘¨ â†’ Lift-alg ğ‘© ğ“© â‰¤ ğ‘¨
Lift-â‰¤ ğ‘¨ Bâ‰¤A = â‰¤-iso ğ‘¨ (â‰…-sym Lift-â‰…) Bâ‰¤A

â‰¤-Lift : (ğ‘¨ : Algebra ğ“§ ğ‘†){ğ“© : Universe}{ğ‘© : Algebra ğ“¨ ğ‘†} â†’ ğ‘© â‰¤ ğ‘¨ â†’ ğ‘© â‰¤ Lift-alg ğ‘¨ ğ“©
â‰¤-Lift ğ‘¨ {ğ“©} {ğ‘©} Bâ‰¤A = â‰¤-TRANS-â‰… ğ‘© {ğ‘¨} (Lift-alg ğ‘¨ ğ“©) Bâ‰¤A Lift-â‰…


module _ {ğ“§ ğ“¨ ğ“© ğ“¦ : Universe} where

 Lift-â‰¤-Lift : {ğ‘¨ : Algebra ğ“§ ğ‘†}(ğ‘© : Algebra ğ“¨ ğ‘†) â†’ ğ‘¨ â‰¤ ğ‘© â†’ Lift-alg ğ‘¨ ğ“© â‰¤ Lift-alg ğ‘© ğ“¦
 Lift-â‰¤-Lift {ğ‘¨} ğ‘© Aâ‰¤B = â‰¤-trans (Lift-alg ğ‘© ğ“¦) (â‰¤-trans ğ‘© lAA Aâ‰¤B) Bâ‰¤lB
   where

   lAA : (Lift-alg ğ‘¨ ğ“©) â‰¤ ğ‘¨
   lAA = Lift-â‰¤ ğ‘¨ {ğ‘¨} â‰¤-refl

   Bâ‰¤lB : ğ‘© â‰¤ Lift-alg ğ‘© ğ“¦
   Bâ‰¤lB = â‰¤-Lift ğ‘© â‰¤-refl

\end{code}


---------------------------------

<sup>1</sup> <span class="footnote" id="fn1">An alternative which could end up being simpler and easier to work with would be to proclaim that `ğ‘©` is a subalgebra of `ğ‘¨` iff there is a *monic* homomorphism from `ğµ` into `ğ‘¨`. In preparation for the next major release of the [UALib][], we will investigate the consequences of taking that path instead of the stricter embedding requirement we chose for the definition of the type `IsSubalgebraOf`.</span>

<sup>2</sup> <span class="footnote" id="fn2"> Recall, in the [Relations.Quotients][] module, we defined *preorder* for binary relation types. Here, however, we will content ourselves with merely proving reflexivity and transitivity of the subalgebra relation `_â‰¤_`, without worry about first defining it as an inhabitant of an honest-to-goodness binary relation type, of the sort introduced in the [Relations.Discrete][] module. Perhaps we will address this matter in a future release of the [UALib][].</span>

<br>
<br>

[â† Subalgebras.Subuniverses](Subalgebras.Subuniverses.html)
<span style="float:right;">[Subalgebras.Univalent â†’](Subalgebras.Univalent.html)</span>

{% include UALib.Links.md %}

