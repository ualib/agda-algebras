---
layout: default
title : Subalgebras.Subalgebras module (The Agda Universal Algebra Library)
date : 2021-01-14
author: William DeMeo
---

### <a id="subalgebras">Subalgebras</a>

The [Subalgebras.Subalgebras][] module of the [Agda Universal Algebra Library][] defines the `Subalgebra` type, representing the subalgebra of a given algebra, as well as the collection of all subalgebras of a given class of algebras.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Algebras.Signatures using (Signature; ğ“; ğ“¥)
open import MGS-Subsingleton-Theorems using (global-dfunext)

module Subalgebras.Subalgebras {ğ‘† : Signature ğ“ ğ“¥}{gfe : global-dfunext} where

open import Subalgebras.Subuniverses {ğ‘† = ğ‘†}{gfe} public
open import MGS-Embeddings using (âˆ˜-embedding; id-is-embedding) public

\end{code}


#### <a id="subalgebra-type">Subalgebra type</a>

Given algebras `ğ‘¨ : Algebra ğ“¦ ğ‘†` and `ğ‘© : Algebra ğ“¤ ğ‘†`, we say that `ğ‘©` is a **subalgebra** of `ğ‘¨` just in case `ğ‘©` can be *homomorphically embedded* in `ğ‘¨`; in other terms, there exists a map `h : âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£` from the universe of `ğ‘¨` to the universe of `ğ‘©` such that `h` is both a homomorphism and an embedding.<sup>[1](Subalgebras.Subalgebras.html#fn1)</sup>

\begin{code}

module _ {ğ“¤ ğ“¦ : Universe} where

 _IsSubalgebraOf_ : (ğ‘© : Algebra ğ“¤ ğ‘†)(ğ‘¨ : Algebra ğ“¦ ğ‘†) â†’ ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âŠ” ğ“¦ Ì‡
 ğ‘© IsSubalgebraOf ğ‘¨ = Î£ h ê‰ hom ğ‘© ğ‘¨ , is-embedding âˆ£ h âˆ£

 Subalgebra : Algebra ğ“¦ ğ‘† â†’ ğ“ âŠ” ğ“¥ âŠ” ğ“¦ âŠ” ğ“¤ âº Ì‡
 Subalgebra ğ‘¨ = Î£ ğ‘© ê‰ (Algebra ğ“¤ ğ‘†) , ğ‘© IsSubalgebraOf ğ‘¨

\end{code}


#### <a id="consequences-of-first-homomorphism-theorem">Consequences of First Homomorphism Theorem</a>

We take this opportunity to prove an important lemma using the `IsSubalgebraOf` type defined above.  If `ğ‘¨` and `ğ‘©` is an ğ‘†-algebra, and if `h : hom ğ‘¨ ğ‘©` is a homomorphism from `ğ‘¨` to `ğ‘©`, then the quotient `ğ‘¨ â•± ker h` is (isomorphic to) a subalgebra of `ğ‘©`.  This is an easy corollary of the First Homomorphism Theorem proved in the [Homomorphisms.Noether][] module.

\begin{code}

open Congruence

FirstHomCorollary : {ğ“¤ ğ“¦ : Universe}(ğ‘¨ : Algebra ğ“¤ ğ‘†)(ğ‘© : Algebra ğ“¦ ğ‘†)(h : hom ğ‘¨ ğ‘©)
                    --extensionality assumptions:
 â†’                     prop-ext ğ“¤ ğ“¦ â†’ is-set âˆ£ ğ‘© âˆ£
 â†’                     (âˆ€ a x â†’ is-subsingleton (âŸ¨ kercon ğ‘© h âŸ© a x))
 â†’                     (âˆ€ C â†’ is-subsingleton (ğ’{A = âˆ£ ğ‘¨ âˆ£}{âŸ¨ kercon ğ‘© h âŸ©} C))
                    -------------------------------------------------------------
 â†’                  (ğ‘¨ [ ğ‘© ]/ker h) IsSubalgebraOf ğ‘©

FirstHomCorollary ğ‘¨ ğ‘© h pe Bset ssR ssA = Ï•hom , Ï•emb
 where
 FirstHomThm : Î£ Ï• ê‰ hom (ğ‘¨ [ ğ‘© ]/ker h) ğ‘© , (âˆ£ h âˆ£ â‰¡ âˆ£ Ï• âˆ£ âˆ˜ âˆ£ Ï€ker ğ‘© h âˆ£ )
                                              Ã— Monic âˆ£ Ï• âˆ£ Ã— is-embedding âˆ£ Ï• âˆ£

 FirstHomThm = FirstHomomorphismTheorem {ğ‘¨ = ğ‘¨}{ğ‘© = ğ‘©}{h} {pe} {Bset}{ssR}{ssA}

 Ï•hom : hom (ğ‘¨ [ ğ‘© ]/ker h) ğ‘©
 Ï•hom = âˆ£ FirstHomThm âˆ£

 Ï•emb : is-embedding âˆ£ Ï•hom âˆ£
 Ï•emb = snd (snd (snd FirstHomThm))

\end{code}

In the special case we apply this to later (e.g., to prove Birkhoff's HSP theorem) the algebra `ğ‘¨` is the term algebra `ğ‘» X`. We record this special case here so that it's easier to apply later.

\begin{code}

free-quot-subalg : {ğ“¤ ğ“§ : Universe}(X : ğ“§ Ì‡)(ğ‘© : Algebra ğ“¤ ğ‘†)(h : hom (ğ‘» X) ğ‘©)
                   --extensionality assumptions --
 â†’                 prop-ext (ov ğ“§) ğ“¤
                   --truncation assumptions --
 â†’                 is-set âˆ£ ğ‘© âˆ£
 â†’                 (âˆ€ p q â†’ is-subsingleton (âŸ¨ kercon ğ‘© h âŸ© p q))
 â†’                 (âˆ€ C â†’ is-subsingleton (ğ’{A = âˆ£ ğ‘» X âˆ£}{âŸ¨ kercon ğ‘© h âŸ©} C))
                   -------------------------------------------------------------------
 â†’                 ((ğ‘» X) [ ğ‘© ]/ker h) IsSubalgebraOf ğ‘©

free-quot-subalg X ğ‘© h pe Bset ssR ssB = FirstHomCorollary (ğ‘» X) ğ‘© h pe Bset ssR ssB

\end{code}




##### <a id="syntactic-sugar">Syntactic sugar</a>

We use the convenient â‰¤ notation for the subalgebra relation.

\begin{code}
_â‰¤_ : {ğ“¤ ğ“  : Universe}(ğ‘© : Algebra ğ“¤ ğ‘†)(ğ‘¨ : Algebra ğ“  ğ‘†) â†’ ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âŠ” ğ“  Ì‡
ğ‘© â‰¤ ğ‘¨ = ğ‘© IsSubalgebraOf ğ‘¨
\end{code}


#### <a id="subalgebras-of-a-class">Subalgebras of a class</a>

\begin{code}

module _ {ğ“¤ ğ“¦ ğ“© : Universe} where

 _IsSubalgebraOfClass_ : (ğ‘© : Algebra ğ“¦ ğ‘†) â†’ Pred (Algebra ğ“¤ ğ‘†) ğ“© â†’ ov ğ“¤ âŠ” ğ“¦ âŠ” ğ“© Ì‡
 _IsSubalgebraOfClass_ ğ‘© ğ’¦ = Î£ ğ‘¨ ê‰ (Algebra ğ“¤ ğ‘†) , Î£ SA ê‰ (Subalgebra{ğ“¤} ğ‘¨) ,
                                                           (ğ‘¨ âˆˆ ğ’¦)  Ã— (ğ‘© â‰… âˆ£ SA âˆ£)

 SUBALGEBRAOFCLASS : Pred (Algebra ğ“¤ ğ‘†) ğ“© â†’ ov (ğ“¤ âŠ” ğ“¦) âŠ” ğ“© Ì‡
 SUBALGEBRAOFCLASS ğ’¦ = Î£ ğ‘© ê‰ (Algebra ğ“¦ ğ‘†) , ğ‘© IsSubalgebraOfClass ğ’¦

SubalgebraOfClass : {ğ“¤ ğ“¦ : Universe} â†’ Pred (Algebra ğ“¤ ğ‘†)(ov ğ“¤) â†’ ov (ğ“¤ âŠ” ğ“¦) Ì‡
SubalgebraOfClass {ğ“¤}{ğ“¦} = SUBALGEBRAOFCLASS {ğ“¤}{ğ“¦}{ğ“© = ov ğ“¤}

\end{code}

#### <a id="subalgebra-lemmas">Subalgebra lemmas</a>

Here are a number of useful facts about subalgebras.  Many of them seem redundant, and they are to some extent.  However, each one differs slightly from the next, if only with respect to the explicitness or implicitness of their arguments.  The aim is to make it as convenient as possible to apply the lemmas in different situations.  (We're in the UALib utility closet now, and elegance is not the priority.)

\begin{code}

module _ {ğ“§ ğ“¨ ğ“© : Universe} where

 --Transitivity of IsSubalgebra (explicit args)
 TRANS-â‰¤ : (ğ‘¨ : Algebra ğ“§ ğ‘†)(ğ‘© : Algebra ğ“¨ ğ‘†)(ğ‘ª : Algebra ğ“© ğ‘†)
  â†’        ğ‘© â‰¤ ğ‘¨  â†’  ğ‘ª â‰¤ ğ‘©
           ----------------
  â†’        ğ‘ª â‰¤ ğ‘¨

 TRANS-â‰¤ ğ‘¨ ğ‘© ğ‘ª BA CB = (HomComp ğ‘ª ğ‘¨ âˆ£ CB âˆ£ âˆ£ BA âˆ£) , âˆ˜-embedding âˆ¥ BA âˆ¥ âˆ¥ CB âˆ¥


 --Transitivity of IsSubalgebra (implicit args)
 Trans-â‰¤ : (ğ‘¨ : Algebra ğ“§ ğ‘†){ğ‘© : Algebra ğ“¨ ğ‘†}(ğ‘ª : Algebra ğ“© ğ‘†)
  â†’        ğ‘© â‰¤ ğ‘¨ â†’ ğ‘ª â‰¤ ğ‘© â†’ ğ‘ª â‰¤ ğ‘¨

 Trans-â‰¤ ğ‘¨ {ğ‘©} ğ‘ª = TRANS-â‰¤ ğ‘¨ ğ‘© ğ‘ª


 --Transitivity of IsSubalgebra (implicit args)
 trans-â‰¤ : {ğ‘¨ : Algebra ğ“§ ğ‘†}{ğ‘© : Algebra ğ“¨ ğ‘†}{ğ‘ª : Algebra ğ“© ğ‘†}
  â†’        ğ‘© â‰¤ ğ‘¨ â†’ ğ‘ª â‰¤ ğ‘© â†’ ğ‘ª â‰¤ ğ‘¨

 trans-â‰¤ {ğ‘¨}{ğ‘©}{ğ‘ª} = TRANS-â‰¤ ğ‘¨ ğ‘© ğ‘ª


 transitivity-â‰¤ : (ğ‘¨ : Algebra ğ“§ ğ‘†){ğ‘© : Algebra ğ“¨ ğ‘†}{ğ‘ª : Algebra ğ“© ğ‘†}
  â†’               ğ‘¨ â‰¤ ğ‘© â†’ ğ‘© â‰¤ ğ‘ª â†’ ğ‘¨ â‰¤ ğ‘ª

 transitivity-â‰¤ ğ‘¨ {ğ‘©}{ğ‘ª} Aâ‰¤B Bâ‰¤C = Ï• , Ï•emb
  where
  Ï• : hom ğ‘¨ ğ‘ª
  Ï• = (fst âˆ£ Bâ‰¤C âˆ£) âˆ˜ (fst âˆ£ Aâ‰¤B âˆ£) , 
      âˆ˜-hom ğ‘¨ ğ‘© ğ‘ª {fst âˆ£ Aâ‰¤B âˆ£}{fst âˆ£ Bâ‰¤C âˆ£}(snd âˆ£ Aâ‰¤B âˆ£)(snd âˆ£ Bâ‰¤C âˆ£)

  Ï•emb : is-embedding âˆ£ Ï• âˆ£
  Ï•emb = âˆ˜-embedding (âˆ¥ Bâ‰¤C âˆ¥)(âˆ¥ Aâ‰¤B âˆ¥)


--Reflexivity of IsSubalgebra (explicit arg)
REFL-â‰¤ : {ğ“¤ : Universe}(ğ‘¨ : Algebra ğ“¤ ğ‘†) â†’ ğ‘¨ â‰¤ ğ‘¨
REFL-â‰¤ ğ‘¨ = (ğ‘–ğ‘‘ âˆ£ ğ‘¨ âˆ£ , id-is-hom) , id-is-embedding


--Reflexivity of IsSubalgebra (implicit arg)
refl-â‰¤ : {ğ“¤ : Universe}{ğ‘¨ : Algebra ğ“¤ ğ‘†} â†’ ğ‘¨ â‰¤ ğ‘¨
refl-â‰¤ {ğ‘¨ = ğ‘¨} = REFL-â‰¤ ğ‘¨


module _ {ğ“§ ğ“¨ ğ“© : Universe} where

 --Reflexivity of IsSubalgebra (explicit arg)
 ISO-â‰¤ : (ğ‘¨ : Algebra ğ“§ ğ‘†)(ğ‘© : Algebra ğ“¨ ğ‘†)(ğ‘ª : Algebra ğ“© ğ‘†)
  â†’      ğ‘© â‰¤ ğ‘¨   â†’   ğ‘ª â‰… ğ‘©
         -----------------
  â†’      ğ‘ª â‰¤ ğ‘¨

 ISO-â‰¤ ğ‘¨ ğ‘© ğ‘ª Bâ‰¤A Câ‰…B = (g âˆ˜ f , gfhom) , gfemb
  where
   f : âˆ£ ğ‘ª âˆ£ â†’ âˆ£ ğ‘© âˆ£
   f = fst âˆ£ Câ‰…B âˆ£
   g : âˆ£ ğ‘© âˆ£ â†’ âˆ£ ğ‘¨ âˆ£
   g = fst âˆ£ Bâ‰¤A âˆ£

   gfemb : is-embedding (g âˆ˜ f)
   gfemb = âˆ˜-embedding (âˆ¥ Bâ‰¤A âˆ¥) (isoâ†’embedding Câ‰…B)

   gfhom : is-homomorphism ğ‘ª ğ‘¨ (g âˆ˜ f)
   gfhom = âˆ˜-hom ğ‘ª ğ‘© ğ‘¨ {f}{g} (snd âˆ£ Câ‰…B âˆ£) (snd âˆ£ Bâ‰¤A âˆ£)


 Iso-â‰¤ : (ğ‘¨ : Algebra ğ“§ ğ‘†){ğ‘© : Algebra ğ“¨ ğ‘†}(ğ‘ª : Algebra ğ“© ğ‘†)
  â†’      ğ‘© â‰¤ ğ‘¨ â†’ ğ‘ª â‰… ğ‘© â†’ ğ‘ª â‰¤ ğ‘¨

 Iso-â‰¤ ğ‘¨ {ğ‘©} ğ‘ª = ISO-â‰¤ ğ‘¨ ğ‘© ğ‘ª


 iso-â‰¤ : {ğ‘¨ : Algebra ğ“§ ğ‘†}{ğ‘© : Algebra ğ“¨ ğ‘†}(ğ‘ª : Algebra ğ“© ğ‘†)
  â†’      ğ‘© â‰¤ ğ‘¨ â†’ ğ‘ª â‰… ğ‘© â†’ ğ‘ª â‰¤ ğ‘¨

 iso-â‰¤ {ğ‘¨}{ğ‘©} ğ‘ª = ISO-â‰¤ ğ‘¨ ğ‘© ğ‘ª


module _ {ğ“§ ğ“¨ ğ“© : Universe} where

 trans-â‰¤-â‰… : (ğ‘¨ : Algebra ğ“§ ğ‘†){ğ‘© : Algebra ğ“¨ ğ‘†}(ğ‘ª : Algebra ğ“© ğ‘†)
  â†’          ğ‘¨ â‰¤ ğ‘© â†’ ğ‘¨ â‰… ğ‘ª â†’ ğ‘ª â‰¤ ğ‘©

 trans-â‰¤-â‰… ğ‘¨ {ğ‘©} ğ‘ª Aâ‰¤B Bâ‰…C = ISO-â‰¤ ğ‘© ğ‘¨ ğ‘ª Aâ‰¤B (sym-â‰… Bâ‰…C)


 TRANS-â‰¤-â‰… : (ğ‘¨ : Algebra ğ“§ ğ‘†){ğ‘© : Algebra ğ“¨ ğ‘†}(ğ‘ª : Algebra ğ“© ğ‘†)
  â†’          ğ‘¨ â‰¤ ğ‘© â†’ ğ‘© â‰… ğ‘ª â†’ ğ‘¨ â‰¤ ğ‘ª

 TRANS-â‰¤-â‰… ğ‘¨ ğ‘ª Aâ‰¤B Bâ‰…C = (HomComp ğ‘¨ ğ‘ª âˆ£ Aâ‰¤B âˆ£ âˆ£ Bâ‰…C âˆ£) ,
                         âˆ˜-embedding (isoâ†’embedding Bâ‰…C)(âˆ¥ Aâ‰¤B âˆ¥)


mono-â‰¤ : {ğ“¤ ğ“¦ ğ“© : Universe}(ğ‘© : Algebra ğ“¦ ğ‘†){ğ’¦ ğ’¦' : Pred (Algebra ğ“¤ ğ‘†) ğ“©}
 â†’       ğ’¦ âŠ† ğ’¦' â†’ ğ‘© IsSubalgebraOfClass ğ’¦ â†’ ğ‘© IsSubalgebraOfClass ğ’¦'

mono-â‰¤ ğ‘© KK' KB = âˆ£ KB âˆ£ , fst âˆ¥ KB âˆ¥ , KK' (âˆ£ snd âˆ¥ KB âˆ¥ âˆ£) , âˆ¥ (snd âˆ¥ KB âˆ¥) âˆ¥


lift-alg-is-sub : {ğ“¤ : Universe}{ğ’¦ : Pred (Algebra ğ“¤ ğ‘†)(ov ğ“¤)}{ğ‘© : Algebra ğ“¤ ğ‘†}
 â†’                ğ‘© IsSubalgebraOfClass ğ’¦ â†’ (lift-alg ğ‘© ğ“¤) IsSubalgebraOfClass ğ’¦

lift-alg-is-sub (ğ‘¨ , (sa , (KA , Bâ‰…sa))) =
 ğ‘¨ , sa , KA , trans-â‰… (sym-â‰… lift-alg-â‰…) Bâ‰…sa


module _ {ğ“§ ğ“¨ ğ“© : Universe} where

 lift-alg-â‰¤-lift : (ğ‘¨ : Algebra ğ“§ ğ‘†){ğ‘© : Algebra ğ“¨ ğ‘†} â†’ ğ‘¨ â‰¤ ğ‘© â†’ ğ‘¨ â‰¤ lift-alg ğ‘© ğ“©
 lift-alg-â‰¤-lift ğ‘¨ {ğ‘©} Aâ‰¤B = TRANS-â‰¤-â‰… ğ‘¨ {ğ‘©} (lift-alg ğ‘© ğ“©) Aâ‰¤B lift-alg-â‰…

 lift-alg-â‰¤-lower : (ğ‘¨ : Algebra ğ“§ ğ‘†){ğ‘© : Algebra ğ“¨ ğ‘†} â†’ ğ‘© â‰¤ ğ‘¨ â†’ lift-alg ğ‘© ğ“© â‰¤ ğ‘¨
 lift-alg-â‰¤-lower ğ‘¨ {ğ‘©} Bâ‰¤A = iso-â‰¤{ğ“© = (ğ“¨ âŠ” ğ“©)}{ğ‘¨}{ğ‘©}(lift-alg ğ‘© ğ“©) Bâ‰¤A (sym-â‰… lift-alg-â‰…)

 lift-alg-â‰¤-lift' : (ğ‘¨ : Algebra ğ“§ ğ‘†){ğ‘© : Algebra ğ“¨ ğ‘†} â†’ ğ‘© â‰¤ ğ‘¨ â†’ ğ‘© â‰¤ lift-alg ğ‘¨ ğ“©
 lift-alg-â‰¤-lift' ğ‘¨ {ğ‘©} Bâ‰¤A = TRANS-â‰¤-â‰… ğ‘© {ğ‘¨} (lift-alg ğ‘¨ ğ“©) Bâ‰¤A lift-alg-â‰…


module _ {ğ“§ ğ“¨ ğ“© ğ“¦ : Universe} where

 lift-alg-â‰¤ : (ğ‘¨ : Algebra ğ“§ ğ‘†){ğ‘© : Algebra ğ“¨ ğ‘†} â†’ ğ‘¨ â‰¤ ğ‘© â†’ lift-alg ğ‘¨ ğ“© â‰¤ lift-alg ğ‘© ğ“¦

 lift-alg-â‰¤ ğ‘¨ {ğ‘©} Aâ‰¤B =
  transitivity-â‰¤ lA {ğ‘©}{lift-alg ğ‘© ğ“¦} (transitivity-â‰¤ lA {ğ‘¨}{ğ‘©} lAA Aâ‰¤B) Bâ‰¤lB
   where
   lA : Algebra (ğ“§ âŠ” ğ“©) ğ‘†
   lA = lift-alg ğ‘¨ ğ“©

   lAA : lA â‰¤ ğ‘¨
   lAA = lift-alg-â‰¤-lower ğ‘¨ {ğ‘¨} refl-â‰¤

   Bâ‰¤lB : ğ‘© â‰¤ lift-alg ğ‘© ğ“¦
   Bâ‰¤lB = lift-alg-â‰¤-lift ğ‘© {ğ‘©} refl-â‰¤


module _ {ğ“¤ ğ“¦ : Universe} where

 lift-alg-sub-lift : (ğ‘¨ : Algebra ğ“¤ ğ‘†){ğ‘ª : Algebra (ğ“¤ âŠ” ğ“¦) ğ‘†} â†’ ğ‘ª â‰¤ ğ‘¨ â†’ ğ‘ª â‰¤ lift-alg ğ‘¨ ğ“¦
 lift-alg-sub-lift ğ‘¨ {ğ‘ª} Câ‰¤A = TRANS-â‰¤-â‰… ğ‘ª {ğ‘¨} (lift-alg ğ‘¨ ğ“¦) Câ‰¤A lift-alg-â‰…


\end{code}

---------------------------------

<span class="footnote" id="fn2"><sup>1</sup> A simpler alternative would be to proclaim `ğ‘©` a subalgebra of `ğ‘¨` iff there is a *monic* homomorphism from `ğµ` into `ğ‘¨`. We should investigate the consequences of taking that path instead of the stricter embedding requirement we chose for the definition of the type `IsSubalgebraOf`.</span>

[â† Subalgebras.Subuniverses](Subalgebras.Subuniverses.html)
<span style="float:right;">[Subalgebras.Univalent â†’](Subalgebras.Univalent.html)</span>

{% include UALib.Links.md %}

