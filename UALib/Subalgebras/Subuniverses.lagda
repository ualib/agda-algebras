---
layout: default
title : Subalgebras.Subuniverses module (The Agda Universal Algebra Library)
date : 2021-01-14
author: William DeMeo
---

### <a id="subuniverses">Subuniverses</a>

This section presents the [Subalgebras.Subuniverses][] module of the [Agda Universal Algebra Library][].

We start by defining a type that represents the important concept of **subuniverse**. Suppose ğ‘¨ is an algebra.  A subset B âŠ† âˆ£ ğ‘¨ âˆ£ is said to be **closed under the operations of** ğ‘¨ if for each ğ‘“ âˆˆ âˆ£ ğ‘† âˆ£ and all tuples ğ’ƒ : âˆ¥ ğ‘† âˆ¥ ğ‘“ â†’ ğµ the element (ğ‘“ Ì‚ ğ‘¨) ğ’ƒ belongs to B. If a subset B âŠ† ğ´ is closed under the operations of ğ‘¨, then we call B a **subuniverse** of ğ‘¨.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Algebras.Signatures using (Signature; ğ“; ğ“¥)
open import MGS-Subsingleton-Theorems using (global-dfunext)

module Subalgebras.Subuniverses {ğ‘† : Signature ğ“ ğ“¥}{gfe : global-dfunext} where

open import Terms.Operations{ğ‘† = ğ‘†}{gfe} public
open import Relation.Unary using (â‹‚) public

\end{code}

We first show how to represent in [Agda][] the collection of subuniverses of an algebra A.  Since a subuniverse is viewed as a subset of the domain of A, we define it as a predicate on âˆ£ A âˆ£.  Thus, the collection of subuniverses is a predicate on predicates on âˆ£ A âˆ£.

\begin{code}

module _ {ğ“¤ ğ“¦ : Universe} where

 Subuniverses : (ğ‘¨ : Algebra ğ“¤ ğ‘†) â†’ Pred (Pred âˆ£ ğ‘¨ âˆ£ ğ“¦)(ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âŠ” ğ“¦)
 Subuniverses ğ‘¨ B = (ğ‘“ : âˆ£ ğ‘† âˆ£)(ğ‘ : âˆ¥ ğ‘† âˆ¥ ğ‘“ â†’ âˆ£ ğ‘¨ âˆ£) â†’ Im ğ‘ âŠ† B â†’ (ğ‘“ Ì‚ ğ‘¨) ğ‘ âˆˆ B

\end{code}

Here's one way to construct an algebra out of a subuniverse.

\begin{code}

 SubunivAlg : (ğ‘¨ : Algebra ğ“¤ ğ‘†)(B : Pred âˆ£ ğ‘¨ âˆ£ ğ“¦) â†’ B âˆˆ Subuniverses ğ‘¨ â†’ Algebra (ğ“¤ âŠ” ğ“¦) ğ‘†
 SubunivAlg ğ‘¨ B BâˆˆSubA = Î£ B , Î» ğ‘“ ğ‘ â†’ (ğ‘“ Ì‚ ğ‘¨)(fst âˆ˜ ğ‘) , BâˆˆSubA ğ‘“ (fst âˆ˜ ğ‘)(snd âˆ˜ ğ‘)

\end{code}



#### <a id="subuniverses-as-records">Subuniverses as records</a>

We could define the type of subuniverses as a record as follows.

\begin{code}

 record Subuniverse {ğ‘¨ : Algebra ğ“¤ ğ‘†} : ov (ğ“¤ âŠ” ğ“¦) Ì‡ where
  constructor mksub
  field
    sset  : Pred âˆ£ ğ‘¨ âˆ£ ğ“¦
    isSub : sset âˆˆ Subuniverses ğ‘¨

\end{code}

For example, we could use such a type to prove that the equalizer of two homomorphisms is a subuniverse.

\begin{code}

 ğ‘¬ğ‘¯-is-subuniverse : {ğ‘¨ : Algebra ğ“¤ ğ‘†}(ğ‘© : Algebra ğ“¦ ğ‘†)
                     (g h : hom ğ‘¨ ğ‘©) â†’ Subuniverse {ğ‘¨ = ğ‘¨}

 ğ‘¬ğ‘¯-is-subuniverse ğ‘© g h = mksub (ğ‘¬ğ‘¯ ğ‘© g h) Î» ğ‘“ ğ’‚ x â†’ ğ‘¬ğ‘¯-closed ğ‘© g h ğ‘“ ğ’‚ x

\end{code}

#### <a id="subuniverse-generation">Subuniverse Generation</a>

If ğ‘¨ is an algebra and B âŠ† âˆ£ ğ‘¨ âˆ£ a subset of the universe of ğ‘¨, then the **subuniverse of** ğ‘¨ **generated by** B is typically denoted by Sg<sup>ğ‘¨</sup>(B) and defined to be the smallest subuniverse of ğ‘¨ containing B.  Equivalently,

Sg<sup>ğ‘¨</sup>(B)  =  â‹‚ { U : U is a subuniverse of ğ‘¨ and  B âŠ† U }.

We now define an inductive type `Sg` that represents the subuniverse generated by a given collection of elements from the domain of an algebra.

\begin{code}

 data Sg (ğ‘¨ : Algebra ğ“¤ ğ‘†)(X : Pred âˆ£ ğ‘¨ âˆ£ ğ“¦) : Pred âˆ£ ğ‘¨ âˆ£ (ğ“ âŠ” ğ“¥ âŠ” ğ“¦ âŠ” ğ“¤) where
  var : âˆ€ {v} â†’ v âˆˆ X â†’ v âˆˆ Sg ğ‘¨ X
  app : (ğ‘“ : âˆ£ ğ‘† âˆ£)(ğ‘ : âˆ¥ ğ‘† âˆ¥ ğ‘“ â†’ âˆ£ ğ‘¨ âˆ£) â†’ Im ğ‘ âŠ† Sg ğ‘¨ X â†’ (ğ‘“ Ì‚ ğ‘¨) ğ‘ âˆˆ Sg ğ‘¨ X

\end{code}

Let's now prove that, for any subset `X` of the domain `âˆ£ ğ‘¨ âˆ£` of an algebra `ğ‘¨`, the type `Sg X` does indeed represent a subuniverse of ğ‘¨.

\begin{code}

module _ {ğ“¤ ğ“¦ : Universe} where

 sgIsSub : {ğ‘¨ : Algebra ğ“¤ ğ‘†}{X : Pred âˆ£ ğ‘¨ âˆ£ ğ“¦} â†’ Sg ğ‘¨ X âˆˆ Subuniverses ğ‘¨
 sgIsSub = app

\end{code}

Next we show, by induction on the shape of its elements, `Sg X` is the smallest subuniverse of `ğ‘¨` containing `X`.

\begin{code}

 sgIsSmallest : {ğ“¡ : Universe}(ğ‘¨ : Algebra ğ“¤ ğ‘†){X : Pred âˆ£ ğ‘¨ âˆ£ ğ“¦}(Y : Pred âˆ£ ğ‘¨ âˆ£ ğ“¡)
  â†’             Y âˆˆ Subuniverses ğ‘¨  â†’  X âŠ† Y  â†’  Sg ğ‘¨ X âŠ† Y

 sgIsSmallest _ _ _ XinY (var Xv) = XinY Xv
 sgIsSmallest ğ‘¨ Y YsubA XinY (app ğ‘“ ğ‘ SgXa) = faâˆˆY
  where
  IH : Im ğ‘ âŠ† Y
  IH i = sgIsSmallest ğ‘¨ Y YsubA XinY (SgXa i)

  faâˆˆY : (ğ‘“ Ì‚ ğ‘¨) ğ‘ âˆˆ Y
  faâˆˆY = YsubA ğ‘“ ğ‘ IH

\end{code}

When the element of `Sg X` is constructed as `app f {a} imaâŠ†SgX`, we may assume (the induction hypothesis) that the arguments `a` belong to `Y`. Then the result of applying `f` to `a` must also belong to `Y`, since `Y` is a subuniverse.



#### <a id="subuniverse-lemmas">Subuniverse Lemmas</a>

Here we formalize a few basic properties of subuniverses.

First, the intersection of subuniverses is again a subuniverse.

\begin{code}

module _ {ğ“˜ ğ“¤ ğ“¦ : Universe} where

 sub-inter-is-sub : (ğ‘¨ : Algebra ğ“¤ ğ‘†)(I : ğ“˜ Ì‡)(ğ’œ : I â†’ Pred âˆ£ ğ‘¨ âˆ£ ğ“¦)
  â†’                 ((i : I) â†’ ğ’œ i âˆˆ Subuniverses ğ‘¨)
                    --------------------------------
  â†’                 â‹‚ I ğ’œ âˆˆ Subuniverses ğ‘¨

 sub-inter-is-sub ğ‘¨ I ğ’œ Ai-is-Sub ğ‘“ a imaâŠ†â‹‚A = Î±
  where
  Î± : (ğ‘“ Ì‚ ğ‘¨) a âˆˆ â‹‚ I ğ’œ
  Î± i = Ai-is-Sub i ğ‘“ a Î» j â†’ imaâŠ†â‹‚A j i

\end{code}

Next, subuniverses are closed under the action of term operations.

\begin{code}

module _ {ğ“¤ ğ“¦ : Universe} where

 sub-term-closed : {ğ“§ : Universe}{X : ğ“§ Ì‡}(ğ‘¨ : Algebra ğ“¤ ğ‘†)(B : Pred âˆ£ ğ‘¨ âˆ£ ğ“¦)
  â†’                (B âˆˆ Subuniverses ğ‘¨) â†’ (t : Term X)(b : X â†’ âˆ£ ğ‘¨ âˆ£)
  â†’                (âˆ€ x â†’ b x âˆˆ B) â†’ ((t Ì‡ ğ‘¨) b) âˆˆ B

 sub-term-closed ğ‘¨ B AB (â„Š x) b bâˆˆB = bâˆˆB x
 sub-term-closed ğ‘¨ B AB (node f ğ’•) b bâˆˆB = AB f (Î» z â†’ (ğ’• z Ì‡ ğ‘¨) b)
                                               (Î» x â†’ sub-term-closed ğ‘¨ B AB (ğ’• x) b bâˆˆB)
\end{code}

Alternatively, we could express the preceeding fact using an inductive type.

\begin{code}

 data TermImage (ğ‘¨ : Algebra ğ“¤ ğ‘†)(Y : Pred âˆ£ ğ‘¨ âˆ£ ğ“¦) : Pred âˆ£ ğ‘¨ âˆ£ (ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âŠ” ğ“¦) where
  var : âˆ€ {y : âˆ£ ğ‘¨ âˆ£} â†’ y âˆˆ Y â†’ y âˆˆ TermImage ğ‘¨ Y
  app : âˆ€ ğ‘“ ğ‘¡ â†’ (âˆ€ i â†’ ğ‘¡ i âˆˆ TermImage ğ‘¨ Y) â†’ (ğ‘“ Ì‚ ğ‘¨) ğ‘¡ âˆˆ TermImage ğ‘¨ Y

\end{code}

By what we proved above, it should come as no surprise that `TermImage ğ‘¨ Y` is a subuniverse of ğ‘¨ that contains Y.

\begin{code}

 TermImageIsSub : {ğ‘¨ : Algebra ğ“¤ ğ‘†}{Y : Pred âˆ£ ğ‘¨ âˆ£ ğ“¦} â†’ TermImage ğ‘¨ Y âˆˆ Subuniverses ğ‘¨
 TermImageIsSub = app

 YâŠ†TermImageY : {ğ‘¨ : Algebra ğ“¤ ğ‘†}{Y : Pred âˆ£ ğ‘¨ âˆ£ ğ“¦} â†’ Y âŠ† TermImage ğ‘¨ Y
 YâŠ†TermImageY {a} aâˆˆY = var aâˆˆY

\end{code}

Since `Sg ğ‘¨ Y` is the smallest subuniverse containing Y, we obtain the following inclusion.

\begin{code}

 SgYâŠ†TermImageY : (ğ‘¨ : Algebra ğ“¤ ğ‘†)(Y : Pred âˆ£ ğ‘¨ âˆ£ ğ“¦) â†’ Sg ğ‘¨ Y âŠ† TermImage ğ‘¨ Y
 SgYâŠ†TermImageY ğ‘¨ Y = sgIsSmallest ğ‘¨ (TermImage ğ‘¨ Y) TermImageIsSub YâŠ†TermImageY

\end{code}




#### <a id="properties-of-homomorphisms">Homomorphic images are subuniverses</a>

Now that we have developed the machinery of subuniverse generation, we can prove two basic facts that play an important role in many theorems about algebraic structures.

First, the image of a homomorphism is a subuniverse of its codomain.

\begin{code}

 hom-image-is-sub : {ğ‘¨ : Algebra ğ“¤ ğ‘†}{ğ‘© : Algebra ğ“¦ ğ‘†}
                    (Ï• : hom ğ‘¨ ğ‘©)  â†’  (HomImage ğ‘© Ï•) âˆˆ Subuniverses ğ‘©

 hom-image-is-sub {ğ‘¨}{ğ‘©} Ï• f b bâˆˆImf = eq ((f Ì‚ ğ‘©) b) ((f Ì‚ ğ‘¨) ar) Î³
  where
  ar : âˆ¥ ğ‘† âˆ¥ f â†’ âˆ£ ğ‘¨ âˆ£
  ar = Î» x â†’ Inv âˆ£ Ï• âˆ£(b x)(bâˆˆImf x)

  Î¶ : âˆ£ Ï• âˆ£ âˆ˜ ar â‰¡ b
  Î¶ = gfe (Î» x â†’ InvIsInv âˆ£ Ï• âˆ£(b x)(bâˆˆImf x))

  Î³ : (f Ì‚ ğ‘©) b â‰¡ âˆ£ Ï• âˆ£((f Ì‚ ğ‘¨)(Î» x â†’ Inv âˆ£ Ï• âˆ£(b x)(bâˆˆImf x)))
  Î³ = (f Ì‚ ğ‘©) b            â‰¡âŸ¨ ap (f Ì‚ ğ‘©)(Î¶ â»Â¹) âŸ©
      (f Ì‚ ğ‘©)(âˆ£ Ï• âˆ£ âˆ˜ ar)  â‰¡âŸ¨(âˆ¥ Ï• âˆ¥ f ar)â»Â¹ âŸ©
      âˆ£ Ï• âˆ£((f Ì‚ ğ‘¨) ar)    âˆ

\end{code}


Next we prove the important fact that homomorphisms are uniquely determined by their values on a generating set.

\begin{code}

 HomUnique : funext ğ“¥ ğ“¤ â†’ {ğ‘¨ ğ‘© : Algebra ğ“¤ ğ‘†}
             (X : Pred âˆ£ ğ‘¨ âˆ£ ğ“¤)  (g h : hom ğ‘¨ ğ‘©)
  â†’          (âˆ€ (x : âˆ£ ğ‘¨ âˆ£)  â†’  x âˆˆ X  â†’  âˆ£ g âˆ£ x â‰¡ âˆ£ h âˆ£ x)
             --------------------------------------------
  â†’          (âˆ€ (a : âˆ£ ğ‘¨ âˆ£) â†’ a âˆˆ Sg ğ‘¨ X â†’ âˆ£ g âˆ£ a â‰¡ âˆ£ h âˆ£ a)

 HomUnique _ _ _ _ gxâ‰¡hx a (var x) = (gxâ‰¡hx) a x

 HomUnique fe {ğ‘¨}{ğ‘©} X g h gxâ‰¡hx a (app ğ‘“ ğ’‚ imğ’‚âŠ†SgX) =
  âˆ£ g âˆ£ ((ğ‘“ Ì‚ ğ‘¨) ğ’‚)     â‰¡âŸ¨ âˆ¥ g âˆ¥ ğ‘“ ğ’‚ âŸ©
  (ğ‘“ Ì‚ ğ‘©)(âˆ£ g âˆ£ âˆ˜ ğ’‚ )   â‰¡âŸ¨ ap (ğ‘“ Ì‚ ğ‘©)(fe induction-hypothesis) âŸ©
  (ğ‘“ Ì‚ ğ‘©)(âˆ£ h âˆ£ âˆ˜ ğ’‚)    â‰¡âŸ¨ ( âˆ¥ h âˆ¥ ğ‘“ ğ’‚ )â»Â¹ âŸ©
  âˆ£ h âˆ£ ((ğ‘“ Ì‚ ğ‘¨) ğ’‚ )    âˆ
  where induction-hypothesis = Î» x â†’ HomUnique fe {ğ‘¨}{ğ‘©} X g h gxâ‰¡hx (ğ’‚ x) ( imğ’‚âŠ†SgX x )

\end{code}


---------------------------------

[â†‘ Subalgebras](Subalgebras.html)
<span style="float:right;">[Subalgebras.Subalgebras â†’](Subalgebras.Subalgebras.html)</span>


{% include UALib.Links.md %}
