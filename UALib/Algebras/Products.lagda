---
layout: default
title : Algebras.Products module (Agda Universal Algebra Library)
date : 2021-01-12
author: William DeMeo
---


### <a id="product-algebras">Product Algebras</a>

This is the [Algebras.Products][] module of the [Agda Universal Algebra Library][].

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

module Algebras.Products where

open import Algebras.Algebras public

module products {ğ‘† : Signature ğ“ ğ“¥} where

\end{code}

From now on, the modules of the [UALib][] will assume a fixed signature `ğ‘† : Signature ğ“ ğ“¥`.

Recall the informal definition of a *product* of `ğ‘†`-algebras. Given a type `I : ğ“˜ Ì‡` and a family `ğ’œ : I â†’ Algebra ğ“¤ ğ‘†`, the *product* `â¨… ğ’œ` is the algebra whose domain is the Cartesian product `Î  ğ‘– ê‰ I , âˆ£ ğ’œ ğ‘– âˆ£` of the domains of the algebras in `ğ’œ`, and whose operations are defined as follows: if `ğ‘“` is a `J`-ary operation symbol and if `ğ‘ : Î  ğ‘– ê‰ I , J â†’ ğ’œ ğ‘–` is an `I`-tuple of `J`-tuples such that `ğ‘ ğ‘–` is a `J`-tuple of elements of `ğ’œ ğ‘–` (for each `ğ‘–`), then `(ğ‘“ Ì‚ â¨… ğ’œ) ğ‘ := (i : I) â†’ (ğ‘“ Ì‚ ğ’œ i)(ğ‘ i)`.

In the [UALib][] a *product of* ğ‘†-*algebras* is represented by the following type.

\begin{code}

 module _ {ğ“¤ ğ“˜ : Level}{I : Set ğ“˜ } where

  â¨… : (ğ’œ : I â†’ Algebra ğ“¤ ğ‘† ) â†’ Algebra (ğ“˜ âŠ” ğ“¤) ğ‘†

  â¨… ğ’œ = (Î  i ê‰ I , âˆ£ ğ’œ i âˆ£) ,            -- domain of the product algebra
       Î» ğ‘“ ğ‘ i â†’ (ğ‘“ Ì‚ ğ’œ i) Î» x â†’ ğ‘ x i   -- basic operations of the product algebra

\end{code}

(Alternative acceptable notation for the domain of the product is `âˆ€ i â†’ âˆ£ ğ’œ i âˆ£`.)

The type just defined is the one that will be used throughout the [UALib][] whenever the product of an indexed collection of algebras (of type `Algebra`) is required.  However, for the sake of completeness, here is how one could define a type representing the product of algebras inhabiting the record type `algebra`.

\begin{code}

  open algebra

  â¨…' : (ğ’œ : I â†’ algebra ğ“¤ ğ‘†) â†’ algebra (ğ“˜ âŠ” ğ“¤) ğ‘†

  â¨…' ğ’œ = record { univ = âˆ€ i â†’ univ (ğ’œ i) ;                 -- domain
                 op = Î» ğ‘“ ğ‘ i â†’ (op (ğ’œ i)) ğ‘“ Î» x â†’ ğ‘ x i } -- basic operations

\end{code}



**Notation**. Given a signature `ğ‘† : Signature ğ“ ğ“¥`, the type `Algebra ğ“¤ ğ‘†` has type `ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âº Ì‡`.  Such types occur so often in the [UALib][] that we define the following shorthand for their universes.

\begin{code}

 ov : Level â†’ Level
 ov ğ“¤ = ğ“ âŠ” ğ“¥ âŠ” lsuc ğ“¤

\end{code}



#### <a id="products-of-classes-of-algebras">Products of classes of algebras</a>

An arbitrary class `ğ’¦` of algebras is represented as a predicate over the type `Algebra ğ“¤ ğ‘†`, for some universe level `ğ“¤` and signature `ğ‘†`. That is, `ğ’¦ : Pred (Algebra ğ“¤ ğ‘†) ğ“¦`, for some type `ğ“¦`. Later we will formally state and prove that the product of all subalgebras of algebras in `ğ’¦` belongs to the class `SP(ğ’¦)` of subalgebras of products of algebras in `ğ’¦`. That is, `â¨… S(ğ’¦) âˆˆ SP(ğ’¦ )`. This turns out to be a nontrivial exercise.

To begin, we need to define types that represent products over arbitrary (nonindexed) families such as `ğ’¦` or `S(ğ’¦)`. Observe that `Î  ğ’¦` is certainly not what we want.  For recall that `Pred (Algebra ğ“¤ ğ‘†) ğ“¦` is just an alias for the function type `Algebra ğ“¤ ğ‘† â†’ ğ“¦ Ì‡`, and the semantics of the latter takes `ğ’¦ ğ‘¨` (and `ğ‘¨ âˆˆ ğ’¦`) to mean that `ğ‘¨` belongs to the class `ğ’¦`. Thus, by definition,

`Î  ğ’¦ = Î  ğ‘¨ ê‰ (Algebra ğ“¤ ğ‘†) , ğ’¦ ğ‘¨` &nbsp; &nbsp; `=` &nbsp; &nbsp; `âˆ€ (ğ‘¨ : Algebra ğ“¤ ğ‘†) â†’ ğ‘¨ âˆˆ ğ’¦`,

which asserts that every inhabitant of the type `Algebra ğ“¤ ğ‘†` belongs to `ğ’¦`.  Evidently this is not the product algebra that we seek.

What we need is a type that serves to index the class `ğ’¦`, and a function `ğ”„` that maps an index to the inhabitant of `ğ’¦` at that index. But `ğ’¦` is a predicate (of type `(Algebra ğ“¤ ğ‘†) â†’ ğ“¦ Ì‡`) and the type `Algebra ğ“¤ ğ‘†` seems rather nebulous in that there is no natural indexing class with which to "enumerate" all inhabitants of `Algebra ğ“¤ ğ‘†` belonging to `ğ’¦`.<sup>[1](Algebras.Product.html#fn1)</sup>

The solution is to essentially take `ğ’¦` itself to be the indexing type, at least heuristically that is how one can view the type `â„‘` that we now define.<sup>[2](Algebras.Product.html#fn2)</sup>

\begin{code}

 module _ {ğ’¦ : Pred (Algebra ğ“¤ ğ‘†)(ğ“ âŠ” ğ“¥ âŠ” lsuc ğ“¤)} where
  â„‘ : Set(ğ“ âŠ” ğ“¥ âŠ” lsuc ğ“¤)
  â„‘ = Î£ ğ‘¨ ê‰ (Algebra _ ğ‘†) , (ğ‘¨ âˆˆ ğ’¦)

\end{code}

Taking the product over the index type `â„‘` requires a function that maps an index `i : â„‘` to the corresponding algebra.  Each `i : â„‘` is a pair, `(ğ‘¨ , p)`, where `ğ‘¨` is an algebra and `p` is a proof that `ğ‘¨` belongs to `ğ’¦`, so the function mapping an index to the corresponding algebra is simply the first projection.

\begin{code}

  ğ”„ : â„‘ â†’ Algebra ğ“¤ ğ‘†
  ğ”„ i = âˆ£ i âˆ£

\end{code}

Finally, we define `class-product` which represents the product of all members of ğ’¦.

\begin{code}

  class-product : Algebra (ğ“ âŠ” ğ“¥ âŠ” lsuc ğ“¤) ğ‘†
  class-product = â¨… ğ”„

\end{code}

If `p : ğ‘¨ âˆˆ ğ’¦`, we view the pair `(ğ‘¨ , p) âˆˆ â„‘` as an *index* over the class, so we can think of `ğ”„ (ğ‘¨ , p)` (which is simply `ğ‘¨`) as the projection of the product `â¨… ğ”„` onto the `(ğ‘¨ , p)`-th component.



-----------------------

<sup>1</sup><span class="footnote" id="fn1"> If you haven't seen this before, give it some thought and see if the correct type comes to you organically.</span>

<sup>2</sup><span class="footnote" id="fn2"> **Unicode Hints**. Some of our types are denoted with with Gothic ("mathfrak") symbols. To produce them in [agda2-mode][], type `\Mf` followed by a letter. For example, `\MfI` â† `â„‘`.</span>

[â† Algebras.Algebras](Algebras.Algebras.html)
<span style="float:right;">[Algebras.Congruences â†’](Algebras.Congruences.html)</span>

{% include UALib.Links.md %}

<!--

Alternatively, we could have defined the class product in a way that explicitly displays the index, like so.

 class-product' : Pred (Algebra ğ“¤ ğ‘†)(ov ğ“¤) â†’ Algebra (ğ“§ âŠ” ov ğ“¤) ğ‘†
 class-product' ğ’¦ = â¨… Î» (i : (Î£ ğ‘¨ ê‰ (Algebra ğ“¤ ğ‘†) , (ğ‘¨ âˆˆ ğ’¦) Ã— (X â†’ âˆ£ ğ‘¨ âˆ£))) â†’ âˆ£ i âˆ£

-->

