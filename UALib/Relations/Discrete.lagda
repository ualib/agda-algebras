---
layout: default
title : UALib.Relations.Discrete module (The Agda Universal Algebra Library)
date : 2021-02-28
author: William DeMeo
---

### <a id="unary-relations">Discrete Relations</a>

This section presents the [UALib.Relations.Discrete][] module of the [Agda Universal Algebra Library][], which covers *unary* and *binary relations*.  We refer to these as "discrete relations" to contrast them with the *general* and *dependent relations* we take up in the next module ([Relations.Continuous][]). We call the latter "continuous relations" because they can have arbitrary arity (general relations) and they can be defined over arbitrary families of types (dependent relations).

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

module Relations.Discrete where

open import Prelude.Lifts public

\end{code}

#### <a id="unary-relations">Unary relations</a>

We need a mechanism for implementing the notion of subsets in Agda. A typical one is called `Pred` (for predicate). More generally, `Pred A ğ“¤` can be viewed as the type of a property that elements of type `A` might satisfy. We write `P : Pred A ğ“¤` to represent the semantic concept of a collection of elements of type `A` that satisfy the property `P`.

Here is the definition, which is similar to the one found in the`Relation/Unary.agda` file of the [Agda Standard Library][].

\begin{code}

module _ {ğ“¤ : Universe} where

 Pred : ğ“¤ Ì‡ â†’ (ğ“¦ : Universe) â†’ ğ“¤ âŠ” ğ“¦ âº Ì‡
 Pred A ğ“¦ = A â†’ ğ“¦ Ì‡

\end{code}



Below we will often consider predicates over the class of all algebras of a particular type. We will define the type of algebras `Algebra ğ“¤ ğ‘†` (for some universe level ğ“¤). Like all types, `Algebra ğ“¤ ğ‘†` itself has a type which happens to be ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âº Ì‡ (as we will see in the module [UALib.Algebras](UALib.Algebras.Algebras.html)). Therefore, the type of `Pred (Algebra ğ“¤ ğ‘†) ğ“¤` will be ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âº Ì‡ as well.

The inhabitants of the type `Pred (Algebra ğ“¤ ğ‘†)` ğ“¤ are maps of the form `ğ‘¨ â†’ ğ“¤ Ì‡`; given an algebra `ğ‘¨ : Algebra ğ“¤ ğ‘†`, we have `Pred ğ‘¨ ğ“¤ = ğ‘¨ â†’ ğ“¤ Ì‡`.



#### <a id="membership-and-inclusion-relations">Membership and inclusion relations</a>

We introduce notation so that we may indicate that `x` "belongs to" or "inhabits" at type `P`, or that `x` "has property" `P`, by writing either `x âˆˆ P` or `P x` (cf. `Relation/Unary.agda` in the [Agda Standard Library][]).

\begin{code}

module _ {ğ“§ ğ“¨ : Universe} where

 _âˆˆ_ : {A : ğ“§ Ì‡ } â†’ A â†’ Pred A ğ“¨ â†’ ğ“¨ Ì‡
 x âˆˆ P = P x

 open import MGS-MLTT using (Â¬) public

 _âˆ‰_ : {A : ğ“§ Ì‡ } â†’ A â†’ Pred A ğ“¨ â†’ ğ“¨ Ì‡
 x âˆ‰ P = Â¬ (x âˆˆ P)

 infix 4 _âˆˆ_ _âˆ‰_

\end{code}

The "subset" relation is denoted, as usual, with the `âŠ†` symbol (cf. `Relation/Unary.agda` in the [Agda Standard Library][]).

\begin{code}

_âŠ†_ : {ğ“§ ğ“¨ ğ“© : Universe}{A : ğ“§ Ì‡ } â†’ Pred A ğ“¨ â†’ Pred A ğ“© â†’ ğ“§ âŠ” ğ“¨ âŠ” ğ“© Ì‡
P âŠ† Q = âˆ€ {x} â†’ x âˆˆ P â†’ x âˆˆ Q

_âŠ‡_ : {ğ“§ ğ“¨ ğ“© : Universe}{A : ğ“§ Ì‡ } â†’ Pred A ğ“¨ â†’ Pred A ğ“© â†’ ğ“§ âŠ” ğ“¨ âŠ” ğ“© Ì‡
P âŠ‡ Q = Q âŠ† P

infix 4 _âŠ†_ _âŠ‡_

\end{code}

#### <a id="the-extensionality-axiom">The axiom of extensionality</a>

In type theory everything is represented as a type and, as we have just seen, this includes subsets.  Equality of types is a nontrivial matter, and thus so is equality of subsets when represented as unary predicates.  Fortunately, it is straightforward to write down the type that represents what we typically means in informal mathematics for two subsets to be equal. In the [UALib][] we denote this type by `â‰` and define it as follows.<sup>[1](Relations.Discrete.html#fn1)</sup>

\begin{code}

_â‰_ : {ğ“§ ğ“¨ ğ“© : Universe}{A : ğ“§ Ì‡ } â†’ Pred A ğ“¨ â†’ Pred A ğ“© â†’ ğ“§ âŠ” ğ“¨ âŠ” ğ“© Ì‡
P â‰ Q = (P âŠ† Q) Ã— (Q âŠ† P)

infix 4 _â‰_

\end{code}

Thus, a proof of `P â‰ Q` is a pair `(p , q)` where `p` is a proof of the first inclusion (that is, `p : P âŠ† Q`)  and `q` is a proof of the second.

If `P` and `Q` are definitionally equal (i.e., `P â‰¡ Q`), then of course both `P âŠ† Q` and `P âŠ‡ Q` hold, so `P â‰ Q` holds.

\begin{code}

Pred-â‰¡ : {ğ“§ ğ“¨ : Universe}{A : ğ“§ Ì‡}{P Q : Pred A ğ“¨} â†’ P â‰¡ Q â†’ P â‰ Q
Pred-â‰¡ refl = (Î» z â†’ z) , (Î» z â†’ z)

\end{code}

The converse is not provable in [MLTT][]. However, we can define its type and postulate that it holds axiomatically, if we wish.  This is called the **axiom of extensionality**.

\begin{code}

ext-axiom : {ğ“§ : Universe} â†’ ğ“§ Ì‡ â†’ (ğ“¨ : Universe) â†’ ğ“§ âŠ” ğ“¨ âº Ì‡
ext-axiom A ğ“¨ = âˆ€ (P Q : Pred A ğ“¨) â†’ P â‰ Q â†’ P â‰¡ Q
\end{code}

We treat this axiom in greater generally and detail in the [Relations.Truncation][] module.



#### <a id="predicates-toolbox">Predicates toolbox</a>

Here is a small collection of tools that will come in handy later.  Hopefully the meaning of each is self-explanatory.

\begin{code}

_âˆˆâˆˆ_ : {ğ“§ ğ“¨ ğ“© : Universe}{A : ğ“§ Ì‡ } {B : ğ“¨ Ì‡ } â†’ (A  â†’  B) â†’ Pred B ğ“© â†’ ğ“§ âŠ” ğ“© Ì‡
_âˆˆâˆˆ_ f S = (x : _) â†’ f x âˆˆ S

Pred-refl : {ğ“§ ğ“¨ : Universe}{A : ğ“§ Ì‡}{P Q : Pred A ğ“¨}
 â†’          P â‰¡ Q â†’ (a : A) â†’ a âˆˆ P â†’ a âˆˆ Q
Pred-refl refl _ = Î» z â†’ z

Pred-â‰¡â†’âŠ† : {ğ“§ ğ“¨ : Universe}{A : ğ“§ Ì‡}{P Q : Pred A ğ“¨}
 â†’          P â‰¡ Q â†’ (P âŠ† Q)
Pred-â‰¡â†’âŠ† refl = (Î» z â†’ z)

Pred-â‰¡â†’âŠ‡ : {ğ“§ ğ“¨ : Universe}{A : ğ“§ Ì‡}{P Q : Pred A ğ“¨}
 â†’          P â‰¡ Q â†’ (P âŠ‡ Q)
Pred-â‰¡â†’âŠ‡ refl = (Î» z â†’ z)

-- Disjoint Union.
data _âŠ_ {ğ“§ ğ“¨ : Universe}(A : ğ“§ Ì‡) (B : ğ“¨ Ì‡) : ğ“§ âŠ” ğ“¨ Ì‡ where
 injâ‚ : (x : A) â†’ A âŠ B
 injâ‚‚ : (y : B) â†’ A âŠ B
infixr 1 _âŠ_

-- Union.
_âˆª_ : {ğ“§ ğ“¨ ğ“© : Universe}{A : ğ“§ Ì‡} â†’ Pred A ğ“¨ â†’ Pred A ğ“© â†’ Pred A _
P âˆª Q = Î» x â†’ x âˆˆ P âŠ x âˆˆ Q
infixr 1 _âˆª_


open import MGS-MLTT using (ğŸ˜)

-- The empty set.
âˆ… : {ğ“§ : Universe}{A : ğ“§ Ì‡} â†’ Pred A ğ“¤â‚€
âˆ… = Î» _ â†’ ğŸ˜


-- Singletons.
ï½›_ï½ : {ğ“§ : Universe}{A : ğ“§ Ì‡} â†’ A â†’ Pred A _
ï½› x ï½ = x â‰¡_

Im_âŠ†_ : {ğ“§ ğ“¨ ğ“© : Universe} {A : ğ“§ Ì‡ } {B : ğ“¨ Ì‡ } â†’ (A â†’ B) â†’ Pred B ğ“© â†’ ğ“§ âŠ” ğ“© Ì‡
Im_âŠ†_ {A = A} f S = (x : A) â†’ f x âˆˆ S

img : {ğ“§ : Universe}{X : ğ“§ Ì‡ } {Y : ğ“§ Ì‡ }
      (f : X â†’ Y) (P : Pred Y ğ“§)
 â†’    Im f âŠ† P â†’  X â†’ Î£ P
img {Y = Y} f P ImfâŠ†P = Î» xâ‚ â†’ f xâ‚ , ImfâŠ†P xâ‚

\end{code}



#### <a id="predicate-transport">Predicate transport</a>

The following is a pair of useful "transport" lemmas for predicates.

\begin{code}

module _ {ğ“§ ğ“¨ : Universe} where

 cong-app-pred : {A : ğ“§ Ì‡ }{Bâ‚ Bâ‚‚ : Pred A ğ“¨}
                 (x : A) â†’  x âˆˆ Bâ‚  â†’  Bâ‚ â‰¡ Bâ‚‚
                ------------------------------
  â†’                         x âˆˆ Bâ‚‚
 cong-app-pred x xâˆˆBâ‚ refl = xâˆˆBâ‚

 cong-pred : {A : ğ“§ Ì‡ }{B : Pred A ğ“¨}
             (x y : A) â†’  x âˆˆ B  â†’  x â‰¡ y
             ----------------------------
  â†’                       y âˆˆ B
 cong-pred x .x xâˆˆB refl = xâˆˆB

\end{code}


--------------------------------------


#### <a id="binary-relations">Binary Relations</a>

In set theory, a binary relation on a set `A` is simply a subset of the product `A Ã— A`.  As such, we could model these as predicates over the type `A Ã— A`, or as relations of type `A â†’ A â†’ ğ“¡ Ì‡` (for some universe ğ“¡). We define these below.

A generalization of the notion of binary relation is a *relation from* `A` *to* `B`, which we define first and treat binary relations on a single `A` as a special case.

\begin{code}

module _ {ğ“¤ : Universe} where

 REL : {ğ“¡ : Universe} â†’ ğ“¤ Ì‡ â†’ ğ“¡ Ì‡ â†’ (ğ“ : Universe) â†’ (ğ“¤ âŠ” ğ“¡ âŠ” ğ“ âº) Ì‡
 REL A B ğ“ = A â†’ B â†’ ğ“ Ì‡

\end{code}

Given types `A` and `B`, a binary relation from `A` to `B` is not the same as a unary predicate over the type `A â†’ B`.  The binary relation has type `A â†’ (B â†’ ğ“ Ì‡)` whereas a unary predicate over `A â†’ B` has type `(A â†’ B) â†’ ğ“ Ì‡` .

#### <a id="kernels">Kernels</a>

The kernel of a function can be defined in many ways. For example,

\begin{code}

 KER : {ğ“¡ : Universe} {A : ğ“¤ Ì‡ } {B : ğ“¡ Ì‡ } â†’ (A â†’ B) â†’ ğ“¤ âŠ” ğ“¡ Ì‡
 KER {ğ“¡} {A} g = Î£ x ê‰ A , Î£ y ê‰ A , g x â‰¡ g y

\end{code}

or as a unary relation (predicate) over the Cartesian product,

\begin{code}

 KER-pred : {ğ“¡ : Universe} {A : ğ“¤ Ì‡}{B : ğ“¡ Ì‡} â†’ (A â†’ B) â†’ Pred (A Ã— A) ğ“¡
 KER-pred g (x , y) = g x â‰¡ g y

\end{code}

or as a relation from `A` to `B`,

\begin{code}

 Rel : ğ“¤ Ì‡ â†’ (ğ“ : Universe) â†’ ğ“¤ âŠ” ğ“ âº Ì‡
 Rel A ğ“ = REL A A ğ“

 KER-rel : {ğ“¡ : Universe}{A : ğ“¤ Ì‡ } {B : ğ“¡ Ì‡ } â†’ (A â†’ B) â†’ Rel A ğ“¡
 KER-rel g x y = g x â‰¡ g y

\end{code}

#### <a id="examples">Examples</a>

\begin{code}
 ker : {A B : ğ“¤ Ì‡ } â†’ (A â†’ B) â†’ ğ“¤ Ì‡
 ker = KER{ğ“¤}

 ker-rel : {A B : ğ“¤ Ì‡ } â†’ (A â†’ B) â†’ Rel A ğ“¤
 ker-rel = KER-rel {ğ“¤}

 ker-pred : {A B : ğ“¤ Ì‡ } â†’ (A â†’ B) â†’ Pred (A Ã— A) ğ“¤
 ker-pred = KER-pred {ğ“¤}

 --The identity relation.
 ğŸ : {A : ğ“¤ Ì‡ } â†’ ğ“¤ Ì‡
 ğŸ {A} = Î£ a ê‰ A , Î£ b ê‰ A , a â‰¡ b

 --...as a binary relation...
 ğŸ-rel : {A : ğ“¤ Ì‡ } â†’ Rel A ğ“¤
 ğŸ-rel a b = a â‰¡ b

 --...as a binary predicate...
 ğŸ-pred : {A : ğ“¤ Ì‡ } â†’ Pred (A Ã— A) ğ“¤
 ğŸ-pred (a , a') = a â‰¡ a'

 ğŸ-pred' : {A : ğ“¤ Ì‡ } â†’ ğ“¤ Ì‡
 ğŸ-pred' {A} = Î£ p ê‰ (A Ã— A) , âˆ£ p âˆ£ â‰¡ âˆ¥ p âˆ¥


 open import MGS-MLTT using (ğŸ™)

 -- The total relation A Ã— A
 ğŸ : {A : ğ“¤ Ì‡ } â†’ Rel A ğ“¤â‚€
 ğŸ a b = ğŸ™
\end{code}



#### <a id="implication">Implication</a>

We denote and define implication for binary predicates (relations) as follows.

\begin{code}

-- (syntactic sugar)
_on_ : {ğ“§ ğ“¨ ğ“© : Universe}{A : ğ“§ Ì‡}{B : ğ“¨ Ì‡}{C : ğ“© Ì‡}
 â†’     (B â†’ B â†’ C) â†’ (A â†’ B) â†’ (A â†’ A â†’ C)

R on g = Î» x y â†’ R (g x) (g y)


_â‡’_ : {ğ“¦ ğ“§ ğ“¨ ğ“© : Universe}{A : ğ“¦ Ì‡ } {B : ğ“§ Ì‡ }
 â†’    REL A B ğ“¨ â†’ REL A B ğ“© â†’ ğ“¦ âŠ” ğ“§ âŠ” ğ“¨ âŠ” ğ“© Ì‡

P â‡’ Q = âˆ€ {i j} â†’ P i j â†’ Q i j

infixr 4 _â‡’_

\end{code}

We can combine `_on_` and _â‡’_ to define a nice, general implication operation. This is borrowed from the [Agda Standard Library][]; we have merely translated into [Type Topology][]/[UALib][] notation.

\begin{code}

_=[_]â‡’_ : {ğ“¦ ğ“§ ğ“¨ ğ“© : Universe}{A : ğ“¦ Ì‡ } {B : ğ“§ Ì‡ }
 â†’        Rel A ğ“¨ â†’ (A â†’ B) â†’ Rel B ğ“© â†’ ğ“¦ âŠ” ğ“¨ âŠ” ğ“© Ì‡

P =[ g ]â‡’ Q = P â‡’ (Q on g)

infixr 4 _=[_]â‡’_

\end{code}


#### <a id="compatibility-of-binary-relations">Compatibility of binary relations</a>

Before discussing general and dependent relations, we pause to define some types that are useful for asserting and proving facts about *compatibility* of functions with binary relations. The first definition simply lifts a binary relation on `A` to a binary relation on tuples of type `I â†’ A`. N.B. This is not to be confused with the sort of (universe) lifting that we defined in the [Prelude.Lifts][] module.

\begin{code}

module _ {ğ“¤ ğ“¥ ğ“¦ : Universe}{I : ğ“¥ Ì‡}{A : ğ“¤ Ì‡} where

 lift-rel : Rel A ğ“¦ â†’ (I â†’ A) â†’ (I â†’ A) â†’ ğ“¥ âŠ” ğ“¦ Ì‡
 lift-rel R ğ‘ ğ‘' = âˆ€ i â†’ R (ğ‘ i) (ğ‘' i)

 compatible-fun : (f : (I â†’ A) â†’ A)(R : Rel A ğ“¦) â†’ ğ“¥ âŠ” ğ“¤ âŠ” ğ“¦ Ì‡
 compatible-fun f R  = (lift-rel R) =[ f ]â‡’ R

\end{code}

We used the slick implication notation in the definition of `compatible-fun`, but we could have defined it more explicitly, like so.

\begin{code}

 compatible-fun' : (f : (I â†’ A) â†’ A)(R : Rel A ğ“¦) â†’ ğ“¥ âŠ” ğ“¤ âŠ” ğ“¦ Ì‡
 compatible-fun' f R  = âˆ€ ğ‘ ğ‘' â†’ (lift-rel R) ğ‘ ğ‘' â†’ R (f ğ‘) (f ğ‘')

\end{code}

However, this is a rare case in which the more elegant syntax may result in simpler proofs when applying the definition. (See, for example, `compatible-term` in the [Terms.Operations][] module.)



--------------------------------------

<sup>1</sup><span class="footnote" id="fn1"> In [agda2-mode][] type `\doteq` or `\.=` to produce `â‰`.</span>

<p></p>

[â†‘ Relations](Relations.html)
<span style="float:right;">[Relations.Continuous â†’](Relations.Continuous.html)</span>

{% include UALib.Links.md %}
