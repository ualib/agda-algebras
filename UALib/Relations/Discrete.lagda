---
layout: default
title : UALib.Relations.Discrete module (The Agda Universal Algebra Library)
date : 2021-02-28
author: William DeMeo
---

### <a id="unary-relations">Discrete Relations</a>

This section presents the [UALib.Relations.Discrete][] module of the [Agda Universal Algebra Library][], which covers *unary* and *binary relations*.  We refer to these as "discrete relations" to contrast them with the ("continuous") *general* and *dependent relations* we introduce in the next module ([Relations.Continuous][]). We call the latter "continuous relations" because they can have arbitrary arity (general relations) and they can be defined over arbitrary families of types (dependent relations).

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

module Relations.Discrete where

open import Prelude.Lifts public

\end{code}

#### <a id="unary-relations">Unary relations</a>

In set theory, given two sets `A` and `P`, we say that `P` is a *subset* of `A`, and we write `P âŠ† A`, just in case `âˆ€ x (x âˆˆ P â†’ x âˆˆ A)`. We need a mechanism for representing this notion in Agda. A typical approach is to use a *predicate* type, denoted by `Pred`.

Given two universes `ğ“¤ ğ“¦` and a type `A : ğ“¤ Ì‡`, the type `Pred A ğ“¦` represents *properties* that inhabitants of type `A` may or may not satisfy.  We write `P : Pred A ğ“¤` to represent the semantic concept of the collection of inhabitants of `A` that satisfy (or belong to) `P`. Here is the definition.<sup>[1](Relations.Discrete.html#fn1)</sup>
(which is similar to the one found in the `Relation/Unary.agda` file of the [Agda Standard Library][]).

\begin{code}

module _ {ğ“¤ : Universe} where

 Pred : ğ“¤ Ì‡ â†’ (ğ“¦ : Universe) â†’ ğ“¤ âŠ” ğ“¦ âº Ì‡
 Pred A ğ“¦ = A â†’ ğ“¦ Ì‡

\end{code}

Later we consider predicates over the class of algebras in a given signature.  In the [Algebras][] module we will define the type `Algebra ğ“¤ ğ‘†` of `ğ‘†`-algebras with domain type `ğ“¤ Ì‡`, and the type `Pred (Algebra ğ“¤ ğ‘†) ğ“¤`, will represent classes of `ğ‘†`-algebras with certain properties.


#### <a id="membership-and-inclusion-relations">Membership and inclusion relations</a>

Like the [Agda Standard Library][], the [UALib][] includes types that represent the *element inclusion* and *subset inclusion* relations from set theory. For example, given a predicate `P`, we may represent that  "`x` belongs to `P`" or that "`x` has property `P`," by writing either `x âˆˆ P` or `P x`.  The definition of `âˆˆ` is standard. Nonetheless, here it is.<sup>[1]</sup>

\begin{code}

module _ {ğ“§ ğ“¨ : Universe} {A : ğ“§ Ì‡ } where

 _âˆˆ_ : A â†’ Pred A ğ“¨ â†’ ğ“¨ Ì‡
 x âˆˆ P = P x

\end{code}

The "subset" relation is denoted, as usual, with the `âŠ†` symbol (cf. `Relation/Unary.agda` in the [Agda Standard Library][]).

\begin{code}

module _ {ğ“§ ğ“¨ ğ“© : Universe}{A : ğ“§ Ì‡ } where

 _âŠ†_ : Pred A ğ“¨ â†’ Pred A ğ“© â†’ ğ“§ âŠ” ğ“¨ âŠ” ğ“© Ì‡
 P âŠ† Q = âˆ€ {x} â†’ x âˆˆ P â†’ x âˆˆ Q

 infix 4 _âŠ†_


\end{code}

#### <a id="the-extensionality-axiom">The axiom of extensionality</a>

In type theory everything is represented as a type and, as we have just seen, this includes subsets.  Equality of types is a nontrivial matter, and thus so is equality of subsets when represented as unary predicates.  Fortunately, it is straightforward to write down the type that represents what we typically means in informal mathematics for two subsets to be equal. In the [UALib][] we denote this type by `â‰` and define it as follows.<sup>[2](Relations.Discrete.html#fn2)</sup>

\begin{code}

module _ {ğ“§ ğ“¨ ğ“© : Universe}{A : ğ“§ Ì‡ } where

 _â‰_ : Pred A ğ“¨ â†’ Pred A ğ“© â†’ ğ“§ âŠ” ğ“¨ âŠ” ğ“© Ì‡
 P â‰ Q = (P âŠ† Q) Ã— (Q âŠ† P)

 infix 4 _â‰_

\end{code}

Thus, a proof of `P â‰ Q` is a pair `(p , q)` where where `p : P âŠ† Q` and `q : Q âŠ† P` are proofs of the first and second inclusions, respectively. If `P` and `Q` are definitionally equal (i.e., `P â‰¡ Q`), then both `P âŠ† Q` and `Q âŠ† P` hold, so `P â‰ Q` also holds, as we now confirm.

\begin{code}

module _ {ğ“§ ğ“¨ : Universe}{A : ğ“§ Ì‡} where

 Pred-â‰¡ : {P Q : Pred A ğ“¨} â†’ P â‰¡ Q â†’ P â‰ Q
 Pred-â‰¡ refl = (Î» z â†’ z) , (Î» z â†’ z)

\end{code}

The converse is not provable in [MLTT][]. However, we can define its type and postulate that it holds axiomatically, if we wish.  This is called the *axiom of extensionality* and a type that represents this axiom is the following.

\begin{code}

module _ {ğ“§ : Universe} where

 ext-axiom : ğ“§ Ì‡ â†’ (ğ“¨ : Universe) â†’ ğ“§ âŠ” ğ“¨ âº Ì‡
 ext-axiom A ğ“¨ = âˆ€ (P Q : Pred A ğ“¨) â†’ P â‰ Q â†’ P â‰¡ Q

\end{code}

Note that the type `ext-axiom` does not itself postulate the axiom of extensionality.  It merely defines the axiom.  If we want to postulate it, we must assume we have a witness, or inhabitant of the type. We could do this in Agda in a number of ways, but probably the easiest is to simply add the witness as a parameter to a module, like so.<sup>[3](Relations.Discrete#fn3)</sup>

\begin{code}

module ext-axiom-postulated {ğ“§ ğ“¨ : Universe}{A : ğ“§ Ì‡} {ea : ext-axiom A ğ“¨} where

\end{code}

We treat other notions of extensionality in the [Relations.Truncation][] module.



#### <a id="predicates-toolbox">Predicates toolbox</a>

Here is a small collection of tools that will come in handy later. The first provides convenient notation for asserting that the image of a function (the first argument) is contained in a predicate (the second argument).

\begin{code}

module _ {ğ“§ ğ“¨ ğ“© : Universe}{A : ğ“§ Ì‡ } {B : ğ“¨ Ì‡ } where

 Im_âŠ†_ : (A â†’ B) â†’ Pred B ğ“© â†’ ğ“§ âŠ” ğ“© Ì‡
 Im f âŠ† S = âˆ€ x â†’ f x âˆˆ S

\end{code}

The following inductive type represents *disjoint union*.<sup>[2](Relations.Discrete#fn2)</sup>

\begin{code}

data _âŠ_ {ğ“§ ğ“¨ : Universe}(A : ğ“§ Ì‡) (B : ğ“¨ Ì‡) : ğ“§ âŠ” ğ“¨ Ì‡ where
 injâ‚ : (x : A) â†’ A âŠ B
 injâ‚‚ : (y : B) â†’ A âŠ B

\end{code}

And this can be used to represent *union*, as follows.

\begin{code}

_âˆª_ : {ğ“§ ğ“¨ ğ“© : Universe}{A : ğ“§ Ì‡} â†’ Pred A ğ“¨ â†’ Pred A ğ“© â†’ Pred A _
P âˆª Q = Î» x â†’ x âˆˆ P âŠ x âˆˆ Q

infixr 1 _âŠ_ _âˆª_

\end{code}

The *empty set* is naturally represented by the *empty type*, `ğŸ˜`.<sup>[2](Relations.Discrete#fn2), [4](Relations.Discrete#fn4)</sup>

\begin{code}

open import Empty-Type using (ğŸ˜)

âˆ… : {ğ“§ : Universe}{A : ğ“§ Ì‡} â†’ Pred A ğ“¤â‚€
âˆ… _ = ğŸ˜

\end{code}


Before closing our little predicates toolbox, let's insert a type that provides a natural way to represent *singletons*.

\begin{code}

ï½›_ï½ : {ğ“§ : Universe}{A : ğ“§ Ì‡} â†’ A â†’ Pred A _
ï½› x ï½ = x â‰¡_

\end{code}


--------------------------------------


#### <a id="binary-relations">Binary Relations</a>

In set theory, a binary relation on a set `A` is simply a subset of the product `A Ã— A`.  As such, we could model such a relation as a (unary) predicate over the type `A Ã— A`, or as a relation of type `A â†’ A â†’ ğ“¡ Ì‡` (for some universe ğ“¡). Note, however, this is not the same as a unary predicate over the function type `A â†’ A` since the latter has type  `(A â†’ A) â†’ ğ“¡ Ì‡`, while a binary relation should have type `A â†’ (A â†’ ğ“¡ Ì‡)`.

A generalization of the notion of binary relation is a *relation from* `A` *to* `B`, which we define first and treat binary relations on a single `A` as a special case.

\begin{code}

module _ {ğ“¤ ğ“¡ : Universe} where

 REL : ğ“¤ Ì‡ â†’ ğ“¡ Ì‡ â†’ (ğ“ : Universe) â†’ (ğ“¤ âŠ” ğ“¡ âŠ” ğ“ âº) Ì‡
 REL A B ğ“ = A â†’ B â†’ ğ“ Ì‡

\end{code}

In the special case, where `ğ“¤ â‰¡ ğ“¡` and `A â‰¡ B`, we have

\begin{code}

module _ {ğ“¤ : Universe} where

 Rel : ğ“¤ Ì‡ â†’ (ğ“ : Universe) â†’ ğ“¤ âŠ” ğ“ âº Ì‡
 Rel A ğ“ = REL A A ğ“

\end{code}


#### <a id="kernels">Kernels</a>

The *kernel* of `f : A â†’ B` is defined informally by `{(x , y) âˆˆ A Ã— A : f x = f y}`. This can be represented in type theory and Agda in a number of ways, each of which may be useful in a particular context. For example, we could define the kernel to be an inhabitant of a (binary) relation type, a (unary) predicate type, a (curried) Sigma type, or an (uncurried) Sigma type.


\begin{code}

module _ {ğ“¤ ğ“¡ : Universe}{A : ğ“¤ Ì‡}{B : ğ“¡ Ì‡} where

 ker : (A â†’ B) â†’ Rel A ğ“¡
 ker g x y = g x â‰¡ g y

 kernel : (A â†’ B) â†’ Pred (A Ã— A) ğ“¡
 kernel g (x , y) = g x â‰¡ g y

 ker-sigma : (A â†’ B) â†’ ğ“¤ âŠ” ğ“¡ Ì‡
 ker-sigma g = Î£ x ê‰ A , Î£ y ê‰ A , g x â‰¡ g y

 ker-sigma' : (A â†’ B) â†’ ğ“¤ âŠ” ğ“¡ Ì‡
 ker-sigma' g = Î£ (x , y) ê‰ (A Ã— A) , g x â‰¡ g y

\end{code}


Similarly, the *identity relation* (which is equivalent to the kernel of an injective function) can be represented using any one of the following four types.<sup>[2](Relations.Discrete#fn2)</sup>

\begin{code}

module _ {ğ“¤ : Universe}{A : ğ“¤ Ì‡ } where

 ğŸ : Rel A ğ“¤
 ğŸ a b = a â‰¡ b

 ğŸ-pred : Pred (A Ã— A) ğ“¤
 ğŸ-pred (a , a') = a â‰¡ a'

 ğŸ-sigma : ğ“¤ Ì‡
 ğŸ-sigma = Î£ a ê‰ A , Î£ b ê‰ A , a â‰¡ b

 ğŸ-sigma' : ğ“¤ Ì‡
 ğŸ-sigma' = Î£ (x , y) ê‰ (A Ã— A) , x â‰¡ y

\end{code}

The *total relation* over `A`, which in set theory is the full Cartesian product `A Ã— A`, could be represented using the one-element type from the `Unit-Type` module of [Type Topology][], as follows.

\begin{code}

 open import Unit-Type using (ğŸ™)

 ğŸ : Rel A ğ“¤â‚€
 ğŸ a b = ğŸ™
\end{code}



#### <a id="implication">Implication</a>

We denote and define implication for binary predicates (relations) as follows. (These are borrowed from the [Agda Standard Library][]; we have merely translated them into [Type Topology][]/[UALib][] notation.)

\begin{code}

module _ {ğ“§ ğ“¨ ğ“© : Universe}{A : ğ“§ Ì‡}{B : ğ“¨ Ì‡}{C : ğ“© Ì‡} where

 _on_ : (B â†’ B â†’ C) â†’ (A â†’ B) â†’ (A â†’ A â†’ C)
 R on g = Î» x y â†’ R (g x) (g y)


module _ {ğ“¦ ğ“§ ğ“¨ ğ“© : Universe}{A : ğ“¦ Ì‡ } {B : ğ“§ Ì‡ } where

 _â‡’_ : REL A B ğ“¨ â†’ REL A B ğ“© â†’ ğ“¦ âŠ” ğ“§ âŠ” ğ“¨ âŠ” ğ“© Ì‡
 P â‡’ Q = âˆ€ {i j} â†’ P i j â†’ Q i j

 infixr 4 _â‡’_

\end{code}

The `_on_` and `_â‡’_` types combine to give a nice, general implication operation.

\begin{code}

module _ {ğ“¦ ğ“§ ğ“¨ ğ“© : Universe}{A : ğ“¦ Ì‡ } {B : ğ“§ Ì‡ } where

 _=[_]â‡’_ : Rel A ğ“¨ â†’ (A â†’ B) â†’ Rel B ğ“© â†’ ğ“¦ âŠ” ğ“¨ âŠ” ğ“© Ì‡
 P =[ g ]â‡’ Q = P â‡’ (Q on g)

 infixr 4 _=[_]â‡’_

\end{code}


#### <a id="compatibility-of-binary-relations">Compatibility of binary relations</a>

Before discussing general and dependent relations, we pause to define some types that are useful for asserting and proving facts about *compatibility* of functions with binary relations. The first definition simply lifts a binary relation on `A` to a binary relation on tuples of type `I â†’ A`. N.B. This is not to be confused with the sort of (universe) lifting that we defined in the [Prelude.Lifts][] module.

\begin{code}

module _ {ğ“¤ ğ“¥ ğ“¦ : Universe}{I : ğ“¥ Ì‡}{A : ğ“¤ Ì‡} where

 lift-rel : Rel A ğ“¦ â†’ (I â†’ A) â†’ (I â†’ A) â†’ ğ“¥ âŠ” ğ“¦ Ì‡
 lift-rel R u v = âˆ€ i â†’ R (u i) (v i)

 compatible-fun : (f : (I â†’ A) â†’ A)(R : Rel A ğ“¦) â†’ ğ“¥ âŠ” ğ“¤ âŠ” ğ“¦ Ì‡
 compatible-fun f R  = (lift-rel R) =[ f ]â‡’ R

\end{code}

We used the slick implication notation in the definition of `compatible-fun`, but we could have defined it more explicitly, like so.

\begin{code}

 compatible-fun' : (f : (I â†’ A) â†’ A)(R : Rel A ğ“¦) â†’ ğ“¥ âŠ” ğ“¤ âŠ” ğ“¦ Ì‡
 compatible-fun' f R  = âˆ€ u v â†’ (lift-rel R) u v â†’ R (f u) (f v)

\end{code}

However, this is a rare case in which the more elegant syntax may result in simpler proofs when applying the definition. (See, for example, `compatible-term` in the [Terms.Operations][] module.)



--------------------------------------

<sup>1</sup><span class="footnote" id="fn1">cf. `Relation/Unary.agda` in the [Agda Standard Library][].</span>

<sup>2</sup><span class="footnote" id="fn2">**Unicode Hints**. In [agda2-mode][] type `\doteq` or `\.=` to produce `â‰`; type `\u+` or `\uplus` to produce `âŠ`; type `\b0` to produce `ğŸ˜`; type `\B0` to produce `ğŸ`.</span>

<sup>3</sup><span class="footnote" id="fn3">Agda also has a `postulate` mechanism that we could use, but this would require omitting the `--safe` pragma from the `OPTIONS` directive at the start of the module.</span>

<sup>4</sup><span class="footnote" id="fn5">The empty type is defined in the `Empty-Type` module of [Type Topology][] as an inductive type with no constructors: `data ğŸ˜ {ğ“¤} : ğ“¤ Ì‡ where -- (empty body)`</span>


<p></p>

[â†‘ Relations](Relations.html)
<span style="float:right;">[Relations.Continuous â†’](Relations.Continuous.html)</span>

{% include UALib.Links.md %}


