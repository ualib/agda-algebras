---
layout: default
title : Homomorphisms.Basic module (The Agda Universal Algebra Library)
date : 2021-01-13
author: William DeMeo
---

### <a id="basic-definitions">Basic Definitions</a>

This section describes the [Homomorphisms.Basic] module of the [Agda Universal Algebra Library][].

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Algebras.Signatures using (Signature; ğ“; ğ“¥)
open import MGS-Subsingleton-Theorems using (global-dfunext)

module Homomorphisms.Basic {ğ‘† : Signature ğ“ ğ“¥}{gfe : global-dfunext} where

open import Algebras.Congruences{ğ‘† = ğ‘†} public
open import MGS-MLTT using (_â‰¡âŸ¨_âŸ©_; _âˆ) public

\end{code}

#### <a id="homomorphisms">Homomorphisms</a>

If `ğ‘¨` and `ğ‘©` are `ğ‘†`-algebras, then a **homomorphism** is a function `â„ : âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£` from the domain of `ğ‘¨` to the domain of `ğ‘©` that is compatible (or commutes) with all of the basic operations of the signature; that is, for all operation symbols `ğ‘“ : âˆ£ ğ‘† âˆ£` and all tuples `ğ’‚ : âˆ¥ ğ‘† âˆ¥ ğ‘“ â†’ âˆ£ ğ‘¨ âˆ£` of `ğ‘¨`, the following holds:<sup>[1](Homomorphisms.Basic.html#fn1)</sup>

`h ((ğ‘“ Ì‚ ğ‘¨) ğ’‚) â‰¡ (ğ‘“ Ì‚ ğ‘©) (h âˆ˜ ğ’‚)`.

To formalize this concept, we first define a type representing the assertion that a function `h : âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£`, from the domain of `ğ‘¨` to the domain of `ğ‘©`, *commutes* (or is *compatible*) with an operation ğ‘“, interpreted in the algebras `ğ‘¨` and `ğ‘©`.  Pleasingly, the defining equation of the previous paragraph can be expressed in Agda without any adulteration.

\begin{code}

module _ {ğ“¤ ğ“¦ : Universe} where

 compatible-op-map : (ğ‘¨ : Algebra ğ“¤ ğ‘†)(ğ‘© : Algebra ğ“¦ ğ‘†)
  â†’                  âˆ£ ğ‘† âˆ£ â†’ (âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£) â†’ ğ“¤ âŠ” ğ“¥ âŠ” ğ“¦ Ì‡

 compatible-op-map ğ‘¨ ğ‘© ğ‘“ h = âˆ€ ğ‘ â†’ h ((ğ‘“ Ì‚ ğ‘¨) ğ‘) â‰¡ (ğ‘“ Ì‚ ğ‘©) (h âˆ˜ ğ‘)

\end{code}

Note the appearance of the shorthand `âˆ€ ğ‘` in the definition of `compatible-op-map`.  We can get away with this in place of `(ğ‘ : âˆ¥ ğ‘† âˆ¥ ğ‘“ â†’ âˆ£ ğ‘¨ âˆ£)` since Agda is able to infer that the `ğ‘` here must be a tuple on `âˆ£ ğ‘¨ âˆ£` of "length" `âˆ¥ ğ‘† âˆ¥ ğ‘“` (the arity of `ğ‘“`).

We now define the type `hom ğ‘¨ ğ‘©` of homomorphisms from `ğ‘¨` to `ğ‘©` by first defining the property `is-homomorphism`.

\begin{code}

 is-homomorphism : (ğ‘¨ : Algebra ğ“¤ ğ‘†)(ğ‘© : Algebra ğ“¦ ğ‘†) â†’ (âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£) â†’ ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âŠ” ğ“¦ Ì‡
 is-homomorphism ğ‘¨ ğ‘© g = âˆ€ ğ‘“  â†’  compatible-op-map ğ‘¨ ğ‘© ğ‘“ g

 hom : Algebra ğ“¤ ğ‘† â†’ Algebra ğ“¦ ğ‘†  â†’ ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âŠ” ğ“¦ Ì‡
 hom ğ‘¨ ğ‘© = Î£ g ê‰ (âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£ ) , is-homomorphism ğ‘¨ ğ‘© g

\end{code}

#### <a id="exmples-of-homomorphisms">Examples of homomorphisms</a>

Let's look at a few examples of homomorphisms. We begin with some very special cases in which the function in question commutes with the basic operations of *all* algebras and so, no matter the algebras involved, is always a homomorphism (trivially).

The most obvious example of a homomorphism is the identity map, which is proved to be a homomorphism as follows.

\begin{code}

module _ {ğ“¤ : Universe} where

 id-is-hom : {ğ‘¨ : Algebra ğ“¤ ğ‘†} â†’ is-homomorphism ğ‘¨ ğ‘¨ (ğ‘–ğ‘‘ âˆ£ ğ‘¨ âˆ£)
 id-is-hom _ _ = refl

 ğ’¾ğ’¹ : (A : Algebra ğ“¤ ğ‘†) â†’ hom A A
 ğ’¾ğ’¹ _ = (Î» x â†’ x) , id-is-hom

\end{code}

Next, `lift` and `lower`, defined in the [Overture.Lifts][] module, are (the maps of) homomorphisms.  Again, the proofs are trivial.

\begin{code}

 open Lift

 Lift-is-hom : {ğ‘¨ : Algebra ğ“¤ ğ‘†}{ğ“¦ : Universe} â†’ is-homomorphism ğ‘¨ (Lift-alg ğ‘¨ ğ“¦) lift
 Lift-is-hom _ _ = refl

 ğ“ğ’¾ğ’»ğ“‰ : {ğ‘¨ : Algebra ğ“¤ ğ‘†}{ğ“¦ : Universe} â†’ hom ğ‘¨ (Lift-alg ğ‘¨ ğ“¦)
 ğ“ğ’¾ğ’»ğ“‰ = (lift , Lift-is-hom)

 lower-is-hom : {ğ‘¨ : Algebra ğ“¤ ğ‘†}{ğ“¦ : Universe} â†’ is-homomorphism (Lift-alg ğ‘¨ ğ“¦) ğ‘¨ lower
 lower-is-hom _ _ = refl

 ğ“â„´ğ“Œâ„¯ğ“‡ : (ğ‘¨ : Algebra ğ“¤ ğ‘†){ğ“¦ : Universe} â†’ hom (Lift-alg ğ‘¨ ğ“¦) ğ‘¨
 ğ“â„´ğ“Œâ„¯ğ“‡ ğ‘¨ = (lower , lower-is-hom{ğ‘¨})

\end{code}




#### <a id="monomorphisms-and-epimorphisms">Monomorphisms and epimorphisms</a>

We represent **monomorphisms** (injective homomorphisms) and **epimorphisms** (surjective homomorphisms) by the following types.

\begin{code}

module _ {ğ“¤ ğ“¦ : Universe} where

 is-monomorphism : (ğ‘¨ : Algebra ğ“¤ ğ‘†)(ğ‘© : Algebra ğ“¦ ğ‘†) â†’ (âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£) â†’ ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âŠ” ğ“¦ Ì‡
 is-monomorphism ğ‘¨ ğ‘© g = is-homomorphism ğ‘¨ ğ‘© g Ã— Monic g

 mon : Algebra ğ“¤ ğ‘† â†’ Algebra ğ“¦ ğ‘†  â†’ ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âŠ” ğ“¦ Ì‡
 mon ğ‘¨ ğ‘© = Î£ g ê‰ (âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£) , is-monomorphism ğ‘¨ ğ‘© g

 is-epimorphism : (ğ‘¨ : Algebra ğ“¤ ğ‘†)(ğ‘© : Algebra ğ“¦ ğ‘†) â†’ (âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£) â†’ ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âŠ” ğ“¦ Ì‡
 is-epimorphism ğ‘¨ ğ‘© g = is-homomorphism ğ‘¨ ğ‘© g Ã— Epic g

 epi : Algebra ğ“¤ ğ‘† â†’ Algebra ğ“¦ ğ‘†  â†’ ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âŠ” ğ“¦ Ì‡
 epi ğ‘¨ ğ‘© = Î£ g ê‰ (âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£) , is-epimorphism ğ‘¨ ğ‘© g

\end{code}

It will be convenient to have a function that takes an inhabitant of `mon` (or `epi`) and extracts the homomorphism part, or *hom reduct* (that is, the pair consisting of the map and a proof that the map is a homomorphism).

\begin{code}

 mon-to-hom : (ğ‘¨ : Algebra ğ“¤ ğ‘†){ğ‘© : Algebra ğ“¦ ğ‘†} â†’ mon ğ‘¨ ğ‘© â†’ hom ğ‘¨ ğ‘©
 mon-to-hom ğ‘¨ Ï• = âˆ£ Ï• âˆ£ , fst âˆ¥ Ï• âˆ¥

 epi-to-hom : {ğ‘¨ : Algebra ğ“¤ ğ‘†}(ğ‘© : Algebra ğ“¦ ğ‘†) â†’ epi ğ‘¨ ğ‘© â†’ hom ğ‘¨ ğ‘©
 epi-to-hom _ Ï• = âˆ£ Ï• âˆ£ , fst âˆ¥ Ï• âˆ¥

\end{code}




#### <a id="equalizers-in-agda">Equalizers in Agda</a>

Recall, the equalizer of two functions (resp., homomorphisms) `g h : A â†’ B` is the subset of `A` on which the values of the functions `g` and `h` agree.  We define the equalizer of functions and homomorphisms in Agda as follows.

\begin{code}

module _ {ğ“¤ ğ“¦ : Universe}{ğ‘¨ : Algebra ğ“¤ ğ‘†} {fe : dfunext ğ“¥ ğ“¦} where

 ğ¸ : {ğ‘© : Algebra ğ“¦ ğ‘†}(g h : âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£) â†’ Pred âˆ£ ğ‘¨ âˆ£ ğ“¦
 ğ¸ g h x = g x â‰¡ h x

 ğ¸hom : (ğ‘© : Algebra ğ“¦ ğ‘†) â†’ hom ğ‘¨ ğ‘© â†’ hom ğ‘¨ ğ‘© â†’ Pred âˆ£ ğ‘¨ âˆ£ ğ“¦
 ğ¸hom _ g h x = âˆ£ g âˆ£ x â‰¡ âˆ£ h âˆ£ x

\end{code}

We will define subuniverses in the [Subalgebras.Subuniverses] module, but we note here that the equalizer of homomorphisms from `ğ‘¨` to `ğ‘©` will turn out to be subuniverse of `ğ‘¨`.  Indeed, this is easily proved as follows.

\begin{code}

 ğ¸hom-closed : (ğ‘© : Algebra ğ“¦ ğ‘†)(g h : hom ğ‘¨ ğ‘©)
  â†’            âˆ€ ğ‘“ a  â†’  Î  x ê‰ âˆ¥ ğ‘† âˆ¥ ğ‘“ , (a x âˆˆ ğ¸hom ğ‘© g h)
               ----------------------------------------------
  â†’            âˆ£ g âˆ£ ((ğ‘“ Ì‚ ğ‘¨) a) â‰¡ âˆ£ h âˆ£ ((ğ‘“ Ì‚ ğ‘¨) a)

 ğ¸hom-closed ğ‘© g h ğ‘“ a p = âˆ£ g âˆ£ ((ğ‘“ Ì‚ ğ‘¨) a)   â‰¡âŸ¨ âˆ¥ g âˆ¥ ğ‘“ a âŸ©
                           (ğ‘“ Ì‚ ğ‘©)(âˆ£ g âˆ£ âˆ˜ a)  â‰¡âŸ¨ ap (ğ‘“ Ì‚ ğ‘©)(fe p) âŸ©
                           (ğ‘“ Ì‚ ğ‘©)(âˆ£ h âˆ£ âˆ˜ a)  â‰¡âŸ¨ (âˆ¥ h âˆ¥ ğ‘“ a)â»Â¹ âŸ©
                           âˆ£ h âˆ£ ((ğ‘“ Ì‚ ğ‘¨) a)   âˆ

\end{code}

The typing judgments for the arguments that we left implicit are `ğ‘“ : âˆ£ ğ‘† âˆ£` and `ğ‘ : âˆ¥ ğ‘† âˆ¥ ğ‘“ â†’ âˆ£ ğ‘¨ âˆ£`


#### <a id="kernels-of-homomorphisms">Kernels of Homomorphisms</a>

The kernel of a homomorphism is a congruence relation and conversely for every congruence relation Î¸, there exists a homomorphism with kernel Î¸ (namely, that canonical projection onto the quotient modulo Î¸).

\begin{code}


open Congruence

module _ {ğ“¤ ğ“¦ : Universe}{ğ‘¨ : Algebra ğ“¤ ğ‘†} where

 homker-compatible : (ğ‘© : Algebra ğ“¦ ğ‘†)(h : hom ğ‘¨ ğ‘©) â†’ compatible ğ‘¨ (ker âˆ£ h âˆ£)
 homker-compatible ğ‘© h f {u}{v} Kerhab = Î³
  where
  Î³ : âˆ£ h âˆ£ ((f Ì‚ ğ‘¨) u)  â‰¡ âˆ£ h âˆ£ ((f Ì‚ ğ‘¨) v)
  Î³ = âˆ£ h âˆ£ ((f Ì‚ ğ‘¨) u)  â‰¡âŸ¨ âˆ¥ h âˆ¥ f u âŸ©
      (f Ì‚ ğ‘©)(âˆ£ h âˆ£ âˆ˜ u) â‰¡âŸ¨ ap (f Ì‚ ğ‘©)(gfe Î» x â†’ Kerhab x) âŸ©
      (f Ì‚ ğ‘©)(âˆ£ h âˆ£ âˆ˜ v) â‰¡âŸ¨ (âˆ¥ h âˆ¥ f v)â»Â¹ âŸ©
      âˆ£ h âˆ£ ((f Ì‚ ğ‘¨) v)  âˆ


 homker-equivalence : (ğ‘© : Algebra ğ“¦ ğ‘†)(h : hom ğ‘¨ ğ‘©) â†’ IsEquivalence (ker âˆ£ h âˆ£)
 homker-equivalence ğ‘© h = map-kernel-IsEquivalence âˆ£ h âˆ£

\end{code}

It is convenient to define a function that takes a homomorphism and constructs a congruence from its kernel.  We call this function `kercon`.

\begin{code}

 kercon : (ğ‘© : Algebra ğ“¦ ğ‘†) â†’ hom ğ‘¨ ğ‘© â†’ Congruence ğ‘¨
 kercon ğ‘© h = mkcon (ker âˆ£ h âˆ£)(homker-equivalence ğ‘© h)(homker-compatible ğ‘© h)

\end{code}

With this congruence we construct the corresponding quotient, along with some syntactic sugar to denote it.

\begin{code}

module _ {ğ“¤ ğ“¦ : Universe} where

 kerquo : {ğ‘¨ : Algebra ğ“¤ ğ‘†}(ğ‘© : Algebra ğ“¦ ğ‘†)(h : hom ğ‘¨ ğ‘©) â†’ Algebra (ğ“¤ âŠ” ğ“¦ âº) ğ‘†
 kerquo {ğ‘¨} ğ‘© h = ğ‘¨ â•± (kercon ğ‘© h)

 _[_]/ker_ : (ğ‘¨ : Algebra ğ“¤ ğ‘†)(ğ‘© : Algebra ğ“¦ ğ‘†)(h : hom ğ‘¨ ğ‘©) â†’ Algebra (ğ“¤ âŠ” ğ“¦ âº) ğ‘†
 ğ‘¨ [ ğ‘© ]/ker h = kerquo ğ‘© h

 infix 60 _[_]/ker_

\end{code}

Thus, given `h : hom ğ‘¨ ğ‘©`, we can construct the quotient of `ğ‘¨` modulo the kernel of `h`, and the syntax for this quotient in the [UALib][] is `ğ‘¨ [ ğ‘© ]/ker h`.



#### <a id="natural-projection">The canonical projection</a>

Given an algebra `ğ‘¨` and a congruence `Î¸`, the *natural* or *canonical projection* is a map from `ğ‘¨` onto `ğ‘¨ â•± Î¸` that is constructed, and proved epimorphic, as follows.

\begin{code}

 Ï€epi : {ğ‘¨ : Algebra ğ“¤ ğ‘†} (Î¸ : Congruence{ğ“¦} ğ‘¨) â†’ epi ğ‘¨ (ğ‘¨ â•± Î¸)
 Ï€epi {ğ‘¨} Î¸ = cÏ€ , cÏ€-is-hom , cÏ€-is-epic where

  cÏ€ : âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘¨ â•± Î¸ âˆ£
  cÏ€ a = âŸ¦ a âŸ§{âŸ¨ Î¸ âŸ©}

  cÏ€-is-hom : is-homomorphism ğ‘¨ (ğ‘¨ â•± Î¸) cÏ€
  cÏ€-is-hom _ _ = refl

  cÏ€-is-epic : Epic cÏ€
  cÏ€-is-epic (.(âŸ¨ Î¸ âŸ© a) , a , refl) = Image_âˆ‹_.im a

\end{code}

To obtain the homomorphism part (or "hom reduct") of the canonical epimorphism, we apply `epi-to-hom`.

\begin{code}

 Ï€hom : {ğ‘¨ : Algebra ğ“¤ ğ‘†}(Î¸ : Congruence{ğ“¦} ğ‘¨) â†’ hom ğ‘¨ (ğ‘¨ â•± Î¸)
 Ï€hom {ğ‘¨} Î¸ = epi-to-hom (ğ‘¨ â•± Î¸) (Ï€epi Î¸)

\end{code}

We combine the foregoing to define a function that takes ğ‘†-algebras `ğ‘¨` and `ğ‘©`, and a homomorphism `h : hom ğ‘¨ ğ‘©` and returns the canonical epimorphism from `ğ‘¨` onto `ğ‘¨ [ ğ‘© ]/ker h`. (Recall, the latter is the special notation we defined above for the quotient of `ğ‘¨` modulo the kernel of `h`.)

\begin{code}

 Ï€ker : {ğ‘¨ : Algebra ğ“¤ ğ‘†}(ğ‘© : Algebra ğ“¦ ğ‘†)(h : hom ğ‘¨ ğ‘©)  â†’  epi ğ‘¨ (ğ‘¨ [ ğ‘© ]/ker h)
 Ï€ker {ğ‘¨} ğ‘© h = Ï€epi (kercon ğ‘© h)

\end{code}


The kernel of the canonical projection of `ğ‘¨` onto `ğ‘¨ / Î¸` is equal to `Î¸`, but since equality of inhabitants of certain types (like `Congruence` or `Rel`) can be a tricky business, we settle for proving the containment `ğ‘¨ / Î¸ âŠ† Î¸`. Of the two containments, this is the easier one to prove; luckily it is also the one we need later.

\begin{code}

module _ {ğ“¤ ğ“¦ : Universe} where

 ker-in-con : (ğ‘¨ : Algebra ğ“¤ ğ‘†)(Î¸ : Congruence{ğ“¦} ğ‘¨)(x y : âˆ£ ğ‘¨ âˆ£)
  â†’           âŸ¨ kercon (ğ‘¨ â•± Î¸) (Ï€hom Î¸) âŸ© x y  â†’  âŸ¨ Î¸ âŸ© x y

 ker-in-con ğ‘¨ Î¸ x y hyp = â•±-â‰¡ Î¸ hyp

\end{code}



#### <a id="product-homomorphisms">Product homomorphisms</a>

Suppose we have an algebra `ğ‘¨`, a type `I : ğ“˜ Ì‡`, and a family `â„¬ : I â†’ Algebra ğ“¦ ğ‘†` of algebras.  We sometimes refer to the inhabitants of `I` as *indices*, and call `â„¬` an *indexed family of algebras*.

If in addition we have a family `ğ’½ : (i : I) â†’ hom ğ‘¨ (â„¬ i)` of homomorphisms, then we can construct a homomorphism from `ğ‘¨` to the product `â¨… â„¬` in the natural way.

\begin{code}

module _ {ğ“¤ ğ“˜ ğ“¦ : Universe} {fe : dfunext ğ“˜ ğ“¦} where

 â¨…-hom-co : {ğ‘¨ : Algebra ğ“¤ ğ‘†}{I : ğ“˜ Ì‡}(â„¬ : I â†’ Algebra ğ“¦ ğ‘†)
  â†’         Î  i ê‰ I , hom ğ‘¨ (â„¬ i)  â†’  hom ğ‘¨ (â¨… â„¬)

 â¨…-hom-co {ğ‘¨} â„¬ ğ’½ = Ï• , Ï•hom
  where
  Ï• : âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ â¨… â„¬ âˆ£
  Ï• a = Î» i â†’ âˆ£ ğ’½ i âˆ£ a

  Ï•hom : is-homomorphism ğ‘¨ (â¨… â„¬) Ï•
  Ï•hom ğ‘“ ğ’¶ = fe Î» i â†’ âˆ¥ ğ’½ i âˆ¥ ğ‘“ ğ’¶

\end{code}

The family `ğ’½` of homomorphisms inhabits the dependent type `Î  i ê‰ I , hom ğ‘¨ (â„¬ i)`.  The syntax we use to represent this type is available to us because of the way `-Î ` is defined in the [Type Topology][] library.  We like this syntax because it is very close to the notation one finds in the standard type theory literature.  However,
we could equally well have used one of the following alternatives, which may be closer to "standard Agda" syntax:

`Î  Î» i â†’ hom ğ‘¨ (â„¬ i)` &nbsp; or &nbsp; `(i : I) â†’ hom ğ‘¨ (â„¬ i)` &nbsp; or &nbsp; `âˆ€ i â†’ hom ğ‘¨ (â„¬ i)`.

The foregoing generalizes easily to the case in which the domain is also a product of a family of algebras. That is, if we are given `ğ’œ : I â†’ Algebra ğ“¤ ğ‘† and â„¬ : I â†’ Algebra ğ“¦ ğ‘†` (two families of `ğ‘†`-algebras), and `ğ’½ :  Î  i ê‰ I , hom (ğ’œ i)(â„¬ i)` (a family of homomorphisms), then we can construct a homomorphism from `â¨… ğ’œ` to `â¨… â„¬` in the following natural way.

\begin{code}

 â¨…-hom : {I : ğ“˜ Ì‡}(ğ’œ : I â†’ Algebra ğ“¤ ğ‘†)(â„¬ : I â†’ Algebra ğ“¦ ğ‘†)
  â†’      Î  i ê‰ I , hom (ğ’œ i)(â„¬ i)  â†’  hom (â¨… ğ’œ)(â¨… â„¬)

 â¨…-hom ğ’œ â„¬ ğ’½ = Ï• , Ï•hom
  where
  Ï• : âˆ£ â¨… ğ’œ âˆ£ â†’ âˆ£ â¨… â„¬ âˆ£
  Ï• = Î» x i â†’ âˆ£ ğ’½ i âˆ£ (x i)

  Ï•hom : is-homomorphism (â¨… ğ’œ) (â¨… â„¬) Ï•
  Ï•hom ğ‘“ ğ’¶ = fe Î» i â†’ âˆ¥ ğ’½ i âˆ¥ ğ‘“ (Î» x â†’ ğ’¶ x i)

\end{code}



#### <a id="projection-homomorphisms">Projection homomorphisms</a>

Later we will need a proof of the fact that projecting out of a product algebra onto one of its factors is a homomorphism.

\begin{code}

module _ {ğ“˜ ğ“¦ : Universe} where

 â¨…-projection-hom : {I : ğ“˜ Ì‡}(â„¬ : I â†’ Algebra ğ“¦ ğ‘†) â†’ Î  i ê‰ I , hom (â¨… â„¬) (â„¬ i)

 â¨…-projection-hom â„¬ = Î» i â†’ ğ’½ i , ğ’½hom i
  where
  ğ’½ : âˆ€ i â†’ âˆ£ â¨… â„¬ âˆ£ â†’ âˆ£ â„¬ i âˆ£
  ğ’½ i = Î» x â†’ x i

  ğ’½hom : âˆ€ i â†’ is-homomorphism (â¨… â„¬) (â„¬ i) (ğ’½ i)
  ğ’½hom _ _ _ = refl

\end{code}

Of course, we could prove a more general result involving projections onto multiple factors, but so far the single-factor result has sufficed.



--------------------------------------

<sup>1</sup><span class="footnote" id="fn1">
Recall, `h âˆ˜ ğ’‚` is the tuple whose i-th component is `h (ğ’‚ i)`.</span>

<span class="footnote">Instead of "homomorphism," we sometimes use the nickname "hom" to refer to such a map.</span>


<br>

[â†‘ Homomorphisms](Homomorphisms.html)
<span style="float:right;">[Homomorphisms.Noether â†’](Homomorphisms.Noether.html)</span>

{% include UALib.Links.md %}



<!--
Î¸ is contained in the kernel of the canonical projection onto ğ‘¨ / Î¸.
con-in-ker : {ğ“¤ ğ“¦ : Universe}(ğ‘¨ : Algebra ğ“¤ ğ‘†) (Î¸ : Congruence{ğ“¤}{ğ“¦} ğ‘¨)
 â†’ âˆ€ x y â†’ (âŸ¨ Î¸ âŸ© x y) â†’ (âŸ¨ (kercon ğ‘¨ {ğ‘¨ â•± Î¸} (canonical-hom{ğ“¤}{ğ“¦} ğ‘¨ Î¸)) âŸ© x y)
con-in-ker ğ‘¨ Î¸ x y hyp = Î³
 where
  h : hom ğ‘¨ (ğ‘¨ â•± Î¸)
  h = canonical-hom ğ‘¨ Î¸

  Îº : Congruence ğ‘¨
  Îº = kercon ğ‘¨ {ğ‘¨ â•± Î¸} h

  Î³ : âŸ¦ x âŸ§ {âŸ¨ Î¸ âŸ©}â‰¡ âŸ¦ y âŸ§{âŸ¨ Î¸ âŸ©}
  Î³ = {!!}
-->



<!-- The definition of homomorphism in the [Agda UALib][] is an *extensional* one; that is, the homomorphism condition holds pointwise. Generally speaking, we say that two functions ğ‘“ ğ‘” : X â†’ Y are extensionally equal iff they are pointwise equal, that is, for all x : X we have ğ‘“ x â‰¡ ğ‘” x. -->

