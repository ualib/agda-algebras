---
layout: default
title : Homomorphisms.Noether module (The Agda Universal Algebra Library)
date : 2021-01-13
author: William DeMeo
---

### <a id="homomorphism-theorems">Homomorphism Theorems</a>

This chapter presents the [Homomorphisms.Noether][] module of the [Agda Universal Algebra Library][].

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Algebras.Signatures using (Signature; ğ“; ğ“¥)

module Homomorphisms.Noether {ğ‘† : Signature ğ“ ğ“¥} where

open import Homomorphisms.Basic{ğ‘† = ğ‘†} public

\end{code}


#### <a id="the-first-homomorphism-theorem">The First Homomorphism Theorem</a>

Here we formalize a version of the *first homomorphism theorem*, sometimes called *Noether's first homomorphism theorem*, after Emmy Noether who was among the first proponents of the abstract approach to the subject that we now call "modern algebra").

Informally, the theorem states that every homomorphism from `ğ‘¨` to `ğ‘©` (`ğ‘†`-algebras) factors through the quotient algebra `ğ‘¨ â•± ker h` (`ğ‘¨` modulo the kernel of the given homomorphism).  In other terms, given `h : hom ğ‘¨ ğ‘©` there exists `Ï† : hom (ğ‘¨ â•± ker h) ğ‘©` which, when composed with the canonical projection `Ï€ker : ğ‘¨ â†  ğ‘¨ â•± ker h`, is equal to `h`; that is, `h = Ï† âˆ˜ Ï€ker`.  Moreover, `Ï†` is a *monomorphism* (injective homomorphism) and is unique.

Our formal proof of this theorem will require function extensionality as well as a couple of truncation assumptions. The function extensionality postulate (`fe`) will be clear enough.  As for truncation, proving that `Ï†` is monic will require the following postulates:<sup>[1](Homomorphisms.Noether.html#fn1)</sup>

+ *Uniqueness of (kernel) Relation Proofs* (`URPk`): the kernel of `h` inhabits the type `Predâ‚‚` of *binary propositions* so there is at most one proof that a given pair belongs to the kernel relation;
+ *Uniqueness of (block) Identity Proofs* (`UIPb`): given any pair of blocks of the kernel there is at most one proof that the given blocks are equal;

And proving that `Ï†` is an embedding requires

+ *Uniqueness of (codomain) Identity Proofs* (`UIPc`): the codomain `âˆ£ ğ‘© âˆ£` is a *set*, that is, has unique identity proofs.

Note that the classical, informal statement of the theorem does not demand that `Ï†` be an embedding (in our sense of having subsingleton fibers), and if we left this out of the consequent of the formal theorem statement below, then we could omit from the antecedent the assumption that âˆ£ ğ‘© âˆ£ is a set.

Without further ado, we present our formalization of the first homomorphism theorem.<sup>[2](Homomorphisms.Noether.html#fn2)</sup>

\begin{code}

open Congruence

module first-hom-thm|Set {ğ“¤ ğ“¦ : Universe}(ğ‘¨ : Algebra ğ“¤ ğ‘†)(ğ‘© : Algebra ğ“¦ ğ‘†)(h : hom ğ‘¨ ğ‘©)
 -- extensionality assumptions:
    (pe : prop-ext ğ“¤ ğ“¦)(fe : dfunext ğ“¥ ğ“¦)

 -- truncation assumptions:
    (UIPc : is-set âˆ£ ğ‘© âˆ£)
    (URPk : is-subsingleton-valued âŸ¨ kercon fe ğ‘© h âŸ©)
    (UIPb : âˆ€ C â†’ is-subsingleton (IsBlock C))
 where

 FirstHomTheorem|Set :
  Î£ Ï† ê‰ hom ((ğ‘¨ [ ğ‘© ]/ker h) {fe}) ğ‘© , (âˆ£ h âˆ£ â‰¡ âˆ£ Ï† âˆ£ âˆ˜ âˆ£ Ï€ker ğ‘© h {fe} âˆ£) Ã— Monic âˆ£ Ï† âˆ£ Ã— is-embedding âˆ£ Ï† âˆ£

 FirstHomTheorem|Set = (Ï† , Ï†hom) , Ï†com , Ï†mon , Ï†emb
  where
  Î¸ : Congruence ğ‘¨
  Î¸ = kercon fe ğ‘© h

  Î¾ : IsEqv âŸ¨ Î¸ âŸ©
  Î¾ = record {is-equivalence = is-equivalence  Î¸ ; is-truncated = URPk}

  Ï† : âˆ£ (ğ‘¨ [ ğ‘© ]/ker h) {fe} âˆ£ â†’ âˆ£ ğ‘© âˆ£
  Ï† a = âˆ£ h âˆ£ âŒ a âŒŸ

  Ï†hom : is-homomorphism ((ğ‘¨ [ ğ‘© ]/ker h){fe}) ğ‘© Ï†
  Ï†hom ğ‘“ ğ’‚ =  âˆ£ h âˆ£ ( (ğ‘“ Ì‚ ğ‘¨) (Î» x â†’ âŒ ğ’‚ x âŒŸ) ) â‰¡âŸ¨ âˆ¥ h âˆ¥ ğ‘“ (Î» x â†’ âŒ ğ’‚ x âŒŸ)  âŸ©
             (ğ‘“ Ì‚ ğ‘©) (âˆ£ h âˆ£ âˆ˜ (Î» x â†’ âŒ ğ’‚ x âŒŸ)) â‰¡âŸ¨ ap (ğ‘“ Ì‚ ğ‘©) (fe Î» x â†’ refl) âŸ©
             (ğ‘“ Ì‚ ğ‘©) (Î» x â†’ Ï† (ğ’‚ x))             âˆ

  Ï†mon : Monic Ï†
  Ï†mon (_ , (u , refl)) (_ , (v , refl)) Ï†uv = block-ext|Set pe {âŸ¨ Î¸ âŸ© , Î¾} UIPb  Ï†uv

  Ï†com : âˆ£ h âˆ£ â‰¡ Ï† âˆ˜ âˆ£ Ï€ker ğ‘© h {fe} âˆ£
  Ï†com = refl

  Ï†emb : is-embedding Ï†
  Ï†emb = monic-is-embedding|Set Ï† UIPc Ï†mon

\end{code}

Below we will prove that the homomorphism `Ï†`, whose existence we just proved, is unique (see `NoetherHomUnique`), but first we show that if we add to the hypotheses of the first homomorphism theorem the assumption that `h` is surjective, then we obtain the so-called *first isomorphism theorem*.  Naturally, we let `FirstHomTheorem|Set` do most of the work. (Note that the proof also requires an additional local function extensionality postulate.)

\begin{code}

 FirstIsoTheorem|Set : dfunext ğ“¦ ğ“¦ â†’ Epic âˆ£ h âˆ£

  â†’ Î£ f ê‰ epi((ğ‘¨ [ ğ‘© ]/ker h){fe}) ğ‘© , (âˆ£ h âˆ£ â‰¡ âˆ£ f âˆ£ âˆ˜ âˆ£ Ï€ker ğ‘© h {fe} âˆ£) Ã— Monic âˆ£ f âˆ£ Ã— is-embedding âˆ£ f âˆ£

 FirstIsoTheorem|Set fev hE = (fmap , fhom , fepic) , refl , fst(snd âˆ¥ FHT âˆ¥) , snd(snd âˆ¥ FHT âˆ¥)
  where
  FHT = FirstHomTheorem|Set  -- (Ï† , Ï†hom) , Ï†com , Ï†mon , Ï†emb

  fmap : âˆ£ (ğ‘¨ [ ğ‘© ]/ker h){fe} âˆ£ â†’ âˆ£ ğ‘© âˆ£
  fmap = fst âˆ£ FHT âˆ£

  fhom : is-homomorphism ((ğ‘¨ [ ğ‘© ]/ker h){fe}) ğ‘© fmap
  fhom = snd âˆ£ FHT âˆ£

  fepic : Epic fmap
  fepic b = Î³ where
   a : âˆ£ ğ‘¨ âˆ£
   a = EpicInv âˆ£ h âˆ£ hE b

   bfa : b â‰¡ fmap âŸª a âŸ«
   bfa = (cong-app (EpicInvIsRightInv {fe = fev} âˆ£ h âˆ£ hE) b)â»Â¹

   Î³ : Image fmap âˆ‹ b
   Î³ = Image_âˆ‹_.eq b âŸª a âŸ« bfa

\end{code}

Now we prove that the homomorphism `Ï†`, whose existence is guaranteed by `FirstHomomorphismTheorem`, is unique.

\begin{code}

module _ {ğ“¤ ğ“¦ : Universe}(ğ‘¨ : Algebra ğ“¤ ğ‘†)(ğ‘© : Algebra ğ“¦ ğ‘†)(h : hom ğ‘¨ ğ‘©) where

 NoetherHomUnique : {fe : dfunext ğ“¥ ğ“¦}(f g : hom ((ğ‘¨ [ ğ‘© ]/ker h){fe}) ğ‘©)
  â†’                 âˆ£ h âˆ£ â‰¡ âˆ£ f âˆ£ âˆ˜ âˆ£ Ï€ker ğ‘© h {fe} âˆ£ â†’ âˆ£ h âˆ£ â‰¡ âˆ£ g âˆ£ âˆ˜ âˆ£ Ï€ker ğ‘© h {fe} âˆ£
                    -------------------------------------------------------------------------
  â†’                 âˆ€ a  â†’  âˆ£ f âˆ£ a â‰¡ âˆ£ g âˆ£ a

 NoetherHomUnique f g hfk hgk (_ , (a , refl)) =
  âˆ£ f âˆ£ (_ , (a , refl)) â‰¡âŸ¨ cong-app(hfk â»Â¹)a âŸ©
  âˆ£ h âˆ£ a                                â‰¡âŸ¨ cong-app(hgk)a âŸ©
  âˆ£ g âˆ£ (_ , (a , refl)) âˆ

\end{code}

If, in addition, we postulate extensionality of functions defined on the domain `ğ‘¨ [ ğ‘© ]/ker h`, then we obtain the following variation of the last result.<sup>[1](Homomorphisms.Noether.html#fn1)</sup>

\begin{code}

 fe-NoetherHomUnique : {fuww : funext (ğ“¤ âŠ” ğ“¦ âº) ğ“¦}{fe : dfunext ğ“¥ ğ“¦}(f g : hom((ğ‘¨ [ ğ‘© ]/ker h){fe})ğ‘©)
  â†’                    âˆ£ h âˆ£ â‰¡ âˆ£ f âˆ£ âˆ˜ âˆ£ Ï€ker ğ‘© h {fe} âˆ£ â†’ âˆ£ h âˆ£ â‰¡ âˆ£ g âˆ£ âˆ˜ âˆ£ Ï€ker ğ‘© h {fe} âˆ£
                       -------------------------------------------------------------------------
  â†’                    âˆ£ f âˆ£ â‰¡ âˆ£ g âˆ£

 fe-NoetherHomUnique {fuww}{fe} f g hfk hgk = fuww (NoetherHomUnique{fe} f g hfk hgk)

\end{code}

The proof of `NoetherHomUnique` goes through for the special case of epimorphisms, as we now verify.

\begin{code}

 NoetherIsoUnique : {fe : dfunext ğ“¥ ğ“¦}(f g : epi ((ğ‘¨ [ ğ‘© ]/ker h){fe}) ğ‘©)
  â†’                 âˆ£ h âˆ£ â‰¡ âˆ£ f âˆ£ âˆ˜ âˆ£ Ï€ker ğ‘© h{fe} âˆ£ â†’ âˆ£ h âˆ£ â‰¡ âˆ£ g âˆ£ âˆ˜ âˆ£ Ï€ker ğ‘© h{fe} âˆ£
                    ---------------------------------------------------------------------
  â†’                 âˆ€ a â†’ âˆ£ f âˆ£ a â‰¡ âˆ£ g âˆ£ a

 NoetherIsoUnique {fe} f g hfk hgk = NoetherHomUnique {fe}(epi-to-hom ğ‘© f) (epi-to-hom ğ‘© g) hfk hgk

\end{code}





#### <a id="homomorphism-composition">Homomorphism composition</a>

The composition of homomorphisms is again a homomorphism.  We formalize this in a number of alternative ways.

\begin{code}

module _ {ğ“§ ğ“¨ ğ“© : Universe} (ğ‘¨ : Algebra ğ“§ ğ‘†){ğ‘© : Algebra ğ“¨ ğ‘†}(ğ‘ª : Algebra ğ“© ğ‘†) where

 âˆ˜-hom : hom ğ‘¨ ğ‘©  â†’  hom ğ‘© ğ‘ª  â†’  hom ğ‘¨ ğ‘ª
 âˆ˜-hom (g , ghom) (h , hhom) = h âˆ˜ g , Î³ where

  Î³ : âˆ€ ğ‘“ a â†’ (h âˆ˜ g)((ğ‘“ Ì‚ ğ‘¨) a) â‰¡ (ğ‘“ Ì‚ ğ‘ª)(h âˆ˜ g âˆ˜ a)

  Î³ ğ‘“ a = (h âˆ˜ g) ((ğ‘“ Ì‚ ğ‘¨) a) â‰¡âŸ¨ ap h ( ghom ğ‘“ a ) âŸ©
          h ((ğ‘“ Ì‚ ğ‘©) (g âˆ˜ a)) â‰¡âŸ¨ hhom ğ‘“ ( g âˆ˜ a ) âŸ©
          (ğ‘“ Ì‚ ğ‘ª) (h âˆ˜ g âˆ˜ a) âˆ


 âˆ˜-is-hom : {f : âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£} {g : âˆ£ ğ‘© âˆ£ â†’ âˆ£ ğ‘ª âˆ£}
  â†’         is-homomorphism ğ‘¨ ğ‘© f â†’ is-homomorphism ğ‘© ğ‘ª g
  â†’         is-homomorphism ğ‘¨ ğ‘ª (g âˆ˜ f)

 âˆ˜-is-hom {f} {g} fhom ghom = âˆ¥ âˆ˜-hom (f , fhom) (g , ghom) âˆ¥

\end{code}



#### <a id="homomorphism-decomposition">Homomorphism decomposition</a>

If `g : hom ğ‘¨ ğ‘©`, `h : hom ğ‘¨ ğ‘ª`, `h` is surjective, and `ker h âŠ† ker g`, then there exists `Ï† : hom ğ‘ª ğ‘©` such that `g = Ï† âˆ˜ h`, that is, such that the following diagram commutes;

```
ğ‘¨---- h -->>ğ‘ª
 \         .
  \       .
   g     âˆƒÏ†
    \   .
     \ .
      V
      ğ‘©
```

This, or some variation of it, is sometimes referred to as the Second Isomorphism Theorem.  We formalize its statement and proof as follows. (Notice that the proof is constructive.)

\begin{code}
module _ {ğ“¤ : Universe}{ğ‘¨ ğ‘© ğ‘ª : Algebra ğ“¤ ğ‘†} where
 homFactor : funext ğ“¤ ğ“¤ â†’ (g : hom ğ‘¨ ğ‘©)(h : hom ğ‘¨ ğ‘ª)
  â†’          kernel âˆ£ h âˆ£ âŠ† kernel âˆ£ g âˆ£ â†’ Epic âˆ£ h âˆ£
             -------------------------------------------
  â†’          Î£ Ï† ê‰ (hom ğ‘ª ğ‘©) , âˆ£ g âˆ£ â‰¡ âˆ£ Ï† âˆ£ âˆ˜ âˆ£ h âˆ£

 homFactor fe(g , ghom)(h , hhom) KhâŠ†Kg hEpi = (Ï† , Ï†IsHomCB) , gÏ†h
  where
  hInv : âˆ£ ğ‘ª âˆ£ â†’ âˆ£ ğ‘¨ âˆ£
  hInv = Î» c â†’ (EpicInv h hEpi) c

  Ï† : âˆ£ ğ‘ª âˆ£ â†’ âˆ£ ğ‘© âˆ£
  Ï† = Î» c â†’ g ( hInv c )

  Î¾ : âˆ€ x â†’ kernel h (x , hInv (h x))
  Î¾ x = (cong-app (EpicInvIsRightInv {fe = fe} h hEpi) (h x))â»Â¹

  gÏ†h : g â‰¡ Ï† âˆ˜ h
  gÏ†h = fe  Î» x â†’ KhâŠ†Kg (Î¾ x)

  Î¶ : (ğ‘“ : âˆ£ ğ‘† âˆ£)(ğ’„ : âˆ¥ ğ‘† âˆ¥ ğ‘“ â†’ âˆ£ ğ‘ª âˆ£)(x : âˆ¥ ğ‘† âˆ¥ ğ‘“) â†’  ğ’„ x â‰¡ (h âˆ˜ hInv)(ğ’„ x)
  Î¶  ğ‘“ ğ’„ x = (cong-app (EpicInvIsRightInv {fe = fe} h hEpi) (ğ’„ x))â»Â¹

  Î¹ : (ğ‘“ : âˆ£ ğ‘† âˆ£)(ğ’„ : âˆ¥ ğ‘† âˆ¥ ğ‘“ â†’ âˆ£ ğ‘ª âˆ£) â†’  ğ’„ â‰¡ h âˆ˜ (hInv âˆ˜ ğ’„)
  Î¹ ğ‘“ ğ’„ = ap (Î» - â†’ - âˆ˜ ğ’„)(EpicInvIsRightInv {fe = fe} h hEpi)â»Â¹

  useker : âˆ€ ğ‘“ ğ’„ â†’ g(hInv (h((ğ‘“ Ì‚ ğ‘¨)(hInv âˆ˜ ğ’„)))) â‰¡ g((ğ‘“ Ì‚ ğ‘¨)(hInv âˆ˜ ğ’„))
  useker ğ‘“ c = KhâŠ†Kg (cong-app (EpicInvIsRightInv{fe = fe} h hEpi)
                               (h ((ğ‘“ Ì‚ ğ‘¨)(hInv âˆ˜ c))) )

  Ï†IsHomCB : (ğ‘“ : âˆ£ ğ‘† âˆ£)(ğ’„ : âˆ¥ ğ‘† âˆ¥ ğ‘“ â†’ âˆ£ ğ‘ª âˆ£) â†’ Ï†((ğ‘“ Ì‚ ğ‘ª) ğ’„) â‰¡ (ğ‘“ Ì‚ ğ‘©)(Ï† âˆ˜ ğ’„)

  Ï†IsHomCB ğ‘“ ğ’„ =  g (hInv ((ğ‘“ Ì‚ ğ‘ª) ğ’„))              â‰¡âŸ¨ i   âŸ©
                 g (hInv ((ğ‘“ Ì‚ ğ‘ª)(h âˆ˜ (hInv âˆ˜ ğ’„)))) â‰¡âŸ¨ ii  âŸ©
                 g (hInv (h ((ğ‘“ Ì‚ ğ‘¨)(hInv âˆ˜ ğ’„))))   â‰¡âŸ¨ iii âŸ©
                 g ((ğ‘“ Ì‚ ğ‘¨)(hInv âˆ˜ ğ’„))              â‰¡âŸ¨ iv  âŸ©
                 (ğ‘“ Ì‚ ğ‘©)(Î» x â†’ g (hInv (ğ’„ x)))      âˆ
   where
   i   = ap (g âˆ˜ hInv) (ap (ğ‘“ Ì‚ ğ‘ª) (Î¹ ğ‘“ ğ’„))
   ii  = ap (g âˆ˜ hInv) (hhom ğ‘“ (hInv âˆ˜ ğ’„))â»Â¹
   iii = useker ğ‘“ ğ’„
   iv  = ghom ğ‘“ (hInv âˆ˜ ğ’„)

\end{code}

Here's a more general version.

```
ğ‘¨ --- Î³ ->> ğ‘ª
 \         .
  \       .
   Î²     âˆƒÏ†
    \   .
     \ .
      V
      ğ‘©
```

\begin{code}

module _ {ğ“§ ğ“¨ ğ“© : Universe}{ğ‘¨ : Algebra ğ“§ ğ‘†}{ğ‘ª : Algebra ğ“© ğ‘†} where

 HomFactor : funext ğ“§ ğ“¨ â†’ funext ğ“© ğ“© â†’ (ğ‘© : Algebra ğ“¨ ğ‘†)(Î± : hom ğ‘¨ ğ‘©)(Î² : hom ğ‘¨ ğ‘ª)
  â†’          kernel âˆ£ Î² âˆ£ âŠ† kernel âˆ£ Î± âˆ£ â†’ Epic âˆ£ Î² âˆ£
             -------------------------------------------
  â†’          Î£ Ï† ê‰ (hom ğ‘ª ğ‘©) , âˆ£ Î± âˆ£ â‰¡ âˆ£ Ï† âˆ£ âˆ˜ âˆ£ Î² âˆ£

 HomFactor fxy fzz ğ‘© Î± Î² KÎ²Î± Î²E = (Ï† , Ï†IsHomCB) , Î±Ï†Î²
  where
  Î²Inv : âˆ£ ğ‘ª âˆ£ â†’ âˆ£ ğ‘¨ âˆ£
  Î²Inv = Î» y â†’ (EpicInv âˆ£ Î² âˆ£ Î²E) y

  Ï† : âˆ£ ğ‘ª âˆ£ â†’ âˆ£ ğ‘© âˆ£
  Ï† = Î» y â†’ âˆ£ Î± âˆ£ ( Î²Inv y )

  Î¾ : (x : âˆ£ ğ‘¨ âˆ£) â†’ kernel âˆ£ Î² âˆ£ (x , Î²Inv (âˆ£ Î² âˆ£ x))
  Î¾ x =  ( cong-app (EpicInvIsRightInv {fe = fzz} âˆ£ Î² âˆ£ Î²E) ( âˆ£ Î² âˆ£ x ) )â»Â¹

  Î±Ï†Î² : âˆ£ Î± âˆ£ â‰¡ Ï† âˆ˜ âˆ£ Î² âˆ£
  Î±Ï†Î² = fxy Î» x â†’ KÎ²Î± (Î¾ x)

  Î¹ : (ğ‘“ : âˆ£ ğ‘† âˆ£)(ğ’„ : âˆ¥ ğ‘† âˆ¥ ğ‘“ â†’ âˆ£ ğ‘ª âˆ£) â†’ ğ’„ â‰¡  âˆ£ Î² âˆ£ âˆ˜ (Î²Inv âˆ˜ ğ’„)
  Î¹ ğ‘“ ğ’„ = ap (Î» - â†’ - âˆ˜ ğ’„) (EpicInvIsRightInv{fe = fzz} âˆ£ Î² âˆ£ Î²E)â»Â¹

  useker : âˆ€ ğ‘“ ğ’„ â†’ âˆ£ Î± âˆ£ (Î²Inv (âˆ£ Î² âˆ£((ğ‘“ Ì‚ ğ‘¨)(Î²Inv âˆ˜ ğ’„)))) â‰¡ âˆ£ Î± âˆ£((ğ‘“ Ì‚ ğ‘¨)(Î²Inv âˆ˜ ğ’„))
  useker ğ‘“ ğ’„ = KÎ²Î± (cong-app (EpicInvIsRightInv {fe = fzz} âˆ£ Î² âˆ£ Î²E)
                             (âˆ£ Î² âˆ£ ((ğ‘“ Ì‚ ğ‘¨)(Î²Inv âˆ˜ ğ’„))))

  Ï†IsHomCB : âˆ€ ğ‘“ ğ’„ â†’ Ï† ((ğ‘“ Ì‚ ğ‘ª) ğ’„) â‰¡ ((ğ‘“ Ì‚ ğ‘©)(Ï† âˆ˜ ğ’„))

  Ï†IsHomCB ğ‘“ ğ’„ = âˆ£ Î± âˆ£ (Î²Inv ((ğ‘“ Ì‚ ğ‘ª) ğ’„))                   â‰¡âŸ¨ i   âŸ©
                âˆ£ Î± âˆ£ (Î²Inv ((ğ‘“ Ì‚ ğ‘ª)(âˆ£ Î² âˆ£ âˆ˜ (Î²Inv âˆ˜ ğ’„)))) â‰¡âŸ¨ ii  âŸ©
                âˆ£ Î± âˆ£ (Î²Inv (âˆ£ Î² âˆ£ ((ğ‘“ Ì‚ ğ‘¨)(Î²Inv âˆ˜ ğ’„))))   â‰¡âŸ¨ iii âŸ©
                âˆ£ Î± âˆ£ ((ğ‘“ Ì‚ ğ‘¨)(Î²Inv âˆ˜ ğ’„))                  â‰¡âŸ¨ iv  âŸ©
                ((ğ‘“ Ì‚ ğ‘©)(Î» x â†’ âˆ£ Î± âˆ£ (Î²Inv (ğ’„ x))))        âˆ
   where
   i   = ap (âˆ£ Î± âˆ£ âˆ˜ Î²Inv) (ap (ğ‘“ Ì‚ ğ‘ª) (Î¹ ğ‘“ ğ’„))
   ii  = ap (âˆ£ Î± âˆ£ âˆ˜ Î²Inv) (âˆ¥ Î² âˆ¥ ğ‘“ (Î²Inv âˆ˜ ğ’„))â»Â¹
   iii = useker ğ‘“ ğ’„
   iv  = âˆ¥ Î± âˆ¥ ğ‘“ (Î²Inv âˆ˜ ğ’„)

\end{code}

If, in addition to the hypotheses of the last theorem, we assume Î± is epic, then so is Ï†. (Note that the proof also requires an additional local function extensionality postulate, `funext ğ“¨ ğ“¨`.)

\begin{code}

 HomFactorEpi : funext ğ“§ ğ“¨ â†’ funext ğ“© ğ“© â†’ funext ğ“¨ ğ“¨
  â†’             (ğ‘© : Algebra ğ“¨ ğ‘†)(Î± : hom ğ‘¨ ğ‘©)(Î² : hom ğ‘¨ ğ‘ª)
  â†’             kernel âˆ£ Î² âˆ£ âŠ† kernel âˆ£ Î± âˆ£ â†’ Epic âˆ£ Î² âˆ£ â†’ Epic âˆ£ Î± âˆ£
                ----------------------------------------------------------
  â†’             Î£ Ï† ê‰ (epi ğ‘ª ğ‘©) , âˆ£ Î± âˆ£ â‰¡ âˆ£ Ï† âˆ£ âˆ˜ âˆ£ Î² âˆ£

 HomFactorEpi fxy fzz fyy ğ‘© Î± Î² kerincl Î²e Î±e = (fst âˆ£ Ï†F âˆ£ , (snd âˆ£ Ï†F âˆ£ , Ï†E)) , âˆ¥ Ï†F âˆ¥
  where
  Ï†F : Î£ Ï† ê‰ (hom ğ‘ª ğ‘©) , âˆ£ Î± âˆ£ â‰¡ âˆ£ Ï† âˆ£ âˆ˜ âˆ£ Î² âˆ£
  Ï†F = HomFactor fxy fzz ğ‘© Î± Î² kerincl Î²e

  Î²inv : âˆ£ ğ‘ª âˆ£ â†’ âˆ£ ğ‘¨ âˆ£
  Î²inv = Î» c â†’ (EpicInv âˆ£ Î² âˆ£ Î²e) c

  Î±inv : âˆ£ ğ‘© âˆ£ â†’ âˆ£ ğ‘¨ âˆ£
  Î±inv = Î» b â†’ (EpicInv âˆ£ Î± âˆ£ Î±e) b

  Ï† : âˆ£ ğ‘ª âˆ£ â†’ âˆ£ ğ‘© âˆ£
  Ï† = Î» c â†’ âˆ£ Î± âˆ£ ( Î²inv c )

  Ï†E : Epic Ï†
  Ï†E = epic-factor {fe = fyy} âˆ£ Î± âˆ£ âˆ£ Î² âˆ£ Ï† âˆ¥ Ï†F âˆ¥ Î±e

\end{code}


--------------------------------------

<sup>1</sup><span class="footnote" id="fn1"> See [Relations.Truncation][] for a discussion of *truncation*, *sets*, and *uniqueness of proofs*.</span>

<sup>2</sup><span class="footnote" id="fn2"> In this module we are already assuming *global* function extensionality (`gfe`), and we could just appeal to `gfe` (e.g., in the proof of `FirstHomomorphismTheorem`) instead of adding local function extensionality (\ab{fe}) to the list of assumptions.  However, we sometimes add an extra extensionality postulate in order to highlight where and how the principle is applied.}</span>

<br>
<br>

[â† Homomorphisms.Basic](Homomorphisms.Basic.html)
<span style="float:right;">[Homomorphisms.Isomorphisms â†’](Homomorphisms.Isomorphisms.html)</span>

{% include UALib.Links.md %}
