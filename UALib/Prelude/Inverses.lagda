---
layout: default
title : UALib.Prelude.Inverses module (Agda Universal Algebra Library)
date : 2021-01-12
author: William DeMeo
---

### <a id="inverses">Inverses</a>

This section presents the [UALib.Prelude.Inverses][] module of the [Agda Universal Algebra Library][].
Here we define (the syntax of) a type for the (semantic concept of) **inverse image** of a function.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

module Prelude.Inverses where

open import Prelude.Equality public 
open import Identity-Type renaming (_â‰¡_ to infix 0 _â‰¡_ ; refl to ğ“‡â„¯ğ’»ğ“) public
-- pattern refl x = ğ“‡â„¯ğ’»ğ“ {x = x}

module _ {ğ“¤ ğ“¦ : Universe} where

 open import MGS-Subsingleton-Theorems using (funext)

 data Image_âˆ‹_ {A : ğ“¤ Ì‡ }{B : ğ“¦ Ì‡ }(f : A â†’ B) : B â†’ ğ“¤ âŠ” ğ“¦ Ì‡
  where
  im : (x : A) â†’ Image f âˆ‹ f x
  eq : (b : B) â†’ (a : A) â†’ b â‰¡ f a â†’ Image f âˆ‹ b

 ImageIsImage : {A : ğ“¤ Ì‡ }{B : ğ“¦ Ì‡ }
                (f : A â†’ B) (b : B) (a : A)
  â†’              b â‰¡ f a
                ---------------------------
  â†’              Image f âˆ‹ b

 ImageIsImage {A}{B} f b a bâ‰¡fa = eq b a bâ‰¡fa

\end{code}

Note that an inhabitant of `Image f âˆ‹ b` is a dependent pair `(a , p)`, where `a : A` and `p : b â‰¡ f a` is a proof that `f` maps `a` to `b`.  Thus, a proof that `b` belongs to the image of `f` (i.e., an inhabitant of `Image f âˆ‹ b`), always has a witness `a : A`, and a proof that `b = f a`, so a (pseudo)inverse can actually be *computed*.

For convenience, we define a pseudo-inverse function, which we call `Inv`, that takes `b : B` and `(a , p) : Image f âˆ‹ b` and returns `a`.

\begin{code}

 Inv : {A : ğ“¤ Ì‡ }{B : ğ“¦ Ì‡ }(f : A â†’ B)(b : B) â†’ Image f âˆ‹ b  â†’  A
 Inv f .(f a) (im a) = a
 Inv f _ (eq _ a _) = a

\end{code}

Of course, we can prove that `Inv f` is really the (right-) inverse of `f`.

\begin{code}

 open import MGS-MLTT using (_â»Â¹) public

 InvIsInv : {A : ğ“¤ Ì‡ } {B : ğ“¦ Ì‡ } (f : A â†’ B)
            (b : B) (bâˆˆImgf : Image f âˆ‹ b)
           ---------------------------------
  â†’         f (Inv f b bâˆˆImgf) â‰¡ b
 InvIsInv f .(f a) (im a) = refl _
 InvIsInv f _ (eq _ _ p) = p â»Â¹

\end{code}





#### <a id="surjective-functions">Surjective functions</a>

An epic (or surjective) function from type `A : ğ“¤ Ì‡` to type `B : ğ“¦ Ì‡` is as an inhabitant of the `Epic` type, which we define as follows.

\begin{code}

 Epic : {A : ğ“¤ Ì‡ } {B : ğ“¦ Ì‡ } (g : A â†’ B) â†’  ğ“¤ âŠ” ğ“¦ Ì‡
 Epic g = âˆ€ y â†’ Image g âˆ‹ y

\end{code}

We obtain the right-inverse (or pseudoinverse) of an epic function `f` by applying the function `EpicInv` (which we now define) to the function `f` along with a proof, `fE : Epic f`, that `f` is surjective.

\begin{code}

 EpicInv : {A : ğ“¤ Ì‡ } {B : ğ“¦ Ì‡ }
           (f : A â†’ B) â†’ Epic f
           -------------------------
  â†’           B â†’ A

 EpicInv f fE b = Inv f b (fE b)

\end{code}

The function defined by `EpicInv f fE` is indeed the right-inverse of `f`.

\begin{code}

 open import MGS-MLTT using (_âˆ˜_; ğ‘–ğ‘‘) public
 open import MGS-Subsingleton-Theorems using (funext)

 EpicInvIsRightInv : funext ğ“¦ ğ“¦ â†’ {A : ğ“¤ Ì‡ } {B : ğ“¦ Ì‡ }
                     (f : A â†’ B)  (fE : Epic f)
                     ----------------------------
  â†’                   f âˆ˜ (EpicInv f fE) â‰¡ ğ‘–ğ‘‘ B

 EpicInvIsRightInv fe f fE = fe (Î» x â†’ InvIsInv f x (fE x))

\end{code}





#### <a id="injective-functions">Injective functions</a>

We say that a function `g : A â†’ B` is monic (or injective) if we have a proof of `Monic g`, where

\begin{code}

 Monic : {A : ğ“¤ Ì‡ } {B : ğ“¦ Ì‡ }(g : A â†’ B) â†’ ğ“¤ âŠ” ğ“¦ Ì‡
 Monic g = âˆ€ aâ‚ aâ‚‚ â†’ g aâ‚ â‰¡ g aâ‚‚ â†’ aâ‚ â‰¡ aâ‚‚

\end{code}

Again, we obtain a pseudoinverse. Here it is obtained by applying the function `MonicInv` to `g` and a proof that `g` is monic.

\begin{code}

 --The (pseudo-)inverse of a monic function
 MonicInv : {A : ğ“¤ Ì‡ } {B : ğ“¦ Ì‡ }
            (f : A â†’ B)  â†’  Monic f
           -----------------------------
  â†’         (b : B) â†’  Image f âˆ‹ b  â†’  A

 MonicInv f _ = Î» b Imfâˆ‹b â†’ Inv f b Imfâˆ‹b

\end{code}

The function defined by `MonicInv f fM` is the left-inverse of `f`.

\begin{code}

 --The (psudo-)inverse of a monic is the left inverse.
 MonicInvIsLeftInv : {A : ğ“¤ Ì‡ }{B : ğ“¦ Ì‡ }
                     (f : A â†’ B) (fmonic : Monic f)(x : A)
                    ----------------------------------------
   â†’                 (MonicInv f fmonic) (f x) (im x) â‰¡ x

 MonicInvIsLeftInv f fmonic x = refl _

\end{code}





#### <a id="composition-laws">Composition laws</a>

\begin{code}

module _ {ğ“§ ğ“¨ ğ“© : Universe} where

 open import MGS-Subsingleton-Theorems using (funext)
 open import MGS-Subsingleton-Truncation using (_âˆ™_) public

 epic-factor : funext ğ“¨ ğ“¨ â†’ {A : ğ“§ Ì‡}{B : ğ“¨ Ì‡}{C : ğ“© Ì‡}
               (Î² : A â†’ B)(Î¾ : A â†’ C)(Ï• : C â†’ B)
  â†’            Î² â‰¡ Ï• âˆ˜ Î¾ â†’  Epic Î² â†’ Epic Ï•

 epic-factor fe {A}{B}{C} Î² Î¾ Ï• compId Î²e y = Î³
  where
   Î²inv : B â†’ A
   Î²inv = EpicInv Î² Î²e

   Î¶ : Î² (Î²inv y) â‰¡ y
   Î¶ = ap (Î» - â†’ - y) (EpicInvIsRightInv fe Î² Î²e)
   Î· : (Ï• âˆ˜ Î¾) (Î²inv y) â‰¡ y
   Î· = (ap (Î» - â†’ - (Î²inv y)) (compId â»Â¹)) âˆ™ Î¶
   Î³ : Image Ï• âˆ‹ y
   Î³ = eq y (Î¾ (Î²inv y)) (Î· â»Â¹)

\end{code}





#### <a id="neutral-elements">Neutral elements</a>

The next three lemmas appeared in the `UF-Base` and `UF-Equiv` modules which were (at one time) part of Matin Escsardo's UF Agda repository.

\begin{code}

refl-left-neutral : {ğ“§ : Universe} {X : ğ“§ Ì‡ } {x y : X} (p : x â‰¡ y) â†’ (refl _) âˆ™ p â‰¡ p
refl-left-neutral (refl _) = refl _

refl-right-neutral : {ğ“§ : Universe}{X : ğ“§ Ì‡ } {x y : X} (p : x â‰¡ y) â†’ p â‰¡ p âˆ™ (refl _)
refl-right-neutral p = refl _

identifications-in-fibers : {ğ“§ ğ“¨ : Universe} {X : ğ“§ Ì‡ } {Y : ğ“¨ Ì‡ } (f : X â†’ Y)
                            (y : Y) (x x' : X) (p : f x â‰¡ y) (p' : f x' â‰¡ y)
 â†’                          (Î£ Î³ ê‰ x â‰¡ x' , ap f Î³ âˆ™ p' â‰¡ p) â†’ (x , p) â‰¡ (x' , p')
identifications-in-fibers f .(f x) x .x ğ“‡â„¯ğ’»ğ“ p' (ğ“‡â„¯ğ’»ğ“ , r) = g
 where
  g : x , ğ“‡â„¯ğ’»ğ“ â‰¡ x , p'
  g = ap (Î» - â†’ (x , -)) (r â»Â¹ âˆ™ refl-left-neutral _)

\end{code}

#### Injective functions are set embeddings

This is the first point at which [truncation](UALib.Preface.html#truncation) comes into play.  An [embedding](https://www.cs.bham.ac.uk/~mhe/HoTT-UF-in-Agda-Lecture-Notes/HoTT-UF-Agda.html#embeddings) is defined in the [Type Topology][] library as follows.<sup>1</sup>

\begin{code}

open import MGS-MLTT using (domain; codomain) public

module hidden where

 is-subsingleton : {ğ“§ : Universe} â†’ ğ“§ Ì‡ â†’ ğ“§ Ì‡
 is-subsingleton X = (x y : X) â†’ x â‰¡ y

 fiber : {ğ“§ ğ“¨ : Universe}{X : ğ“§ Ì‡ }{Y : ğ“¨ Ì‡ }(f : X â†’ Y) â†’ Y â†’ ğ“§ âŠ” ğ“¨ Ì‡
 fiber f y = Î£ x ê‰ domain f , f x â‰¡ y

 is-embedding : {ğ“§ ğ“¨ : Universe}{X : ğ“§ Ì‡ } {Y : ğ“¨ Ì‡ } â†’ (X â†’ Y) â†’ ğ“§ âŠ” ğ“¨ Ì‡
 is-embedding f = (y : codomain f) â†’ is-subsingleton (fiber f y)

\end{code}

This is a very nice, natural way to represent what we usually mean in mathematics by embedding.  Observe that an embedding does not simply correspond to an injective map.  However, if we assume that the codomain `B` has unique identity proofs (i.e., is a set), then we can prove that a monic function into `B` is an embedding as follows:

\begin{code}

open import MGS-MLTT using (transport)
open import MGS-Subsingleton-Truncation using (fiber) public
open import MGS-Embeddings using (is-embedding; to-Î£-â‰¡; invertible; equivs-are-embeddings; invertibles-are-equivs) public
open import MGS-Embeddings using (is-set)

monic-into-set-is-embedding : {ğ“§ ğ“¨ : Universe}{A : ğ“§ Ì‡}{B : ğ“¨ Ì‡} â†’ is-set B
 â†’                            (f : A â†’ B)  â†’  Monic f
                              -----------------------
 â†’                            is-embedding f

monic-into-set-is-embedding Bset f fmon b (a , faâ‰¡b) (a' , fa'â‰¡b) = Î³
 where
  faa' : f a â‰¡ f a'
  faa' = â‰¡-Trans (f a) (f a') faâ‰¡b (fa'â‰¡b â»Â¹)

  aa' : a â‰¡ a'
  aa' = fmon a a' faa'

  ğ’œ : domain f â†’ _ Ì‡
  ğ’œ a = f a â‰¡ b

  arg1 : Î£ p ê‰ (a â‰¡ a') , (transport ğ’œ p faâ‰¡b) â‰¡ fa'â‰¡b
  arg1 = aa' , Bset (f a') b (transport ğ’œ aa' faâ‰¡b) fa'â‰¡b

  Î³ : a , faâ‰¡b â‰¡ a' , fa'â‰¡b
  Î³ = to-Î£-â‰¡ arg1

\end{code}

Of course, invertible maps are embeddings.

\begin{code}

invertibles-are-embeddings : {ğ“§ ğ“¨ : Universe}
                             {X : ğ“§ Ì‡} {Y : ğ“¨ Ì‡} (f : X â†’ Y)
 â†’                           invertible f â†’ is-embedding f

invertibles-are-embeddings f fi = equivs-are-embeddings f (invertibles-are-equivs f fi)

\end{code}

Finally, if we have a proof `p : is-embedding f` that the map `f` is an embedding, here's a tool that can make it easier to apply `p`.

\begin{code}

embedding-elim : {ğ“§ ğ“¨ : Universe}{X : ğ“§ Ì‡} {Y : ğ“¨ Ì‡}
                 (f : X â†’ Y) â†’ is-embedding f
 â†’               âˆ€ x x' â†’ f x â‰¡ f x' â†’ x â‰¡ x'

embedding-elim f femb x x' fxfx' = ap prâ‚ ((femb (f x)) fa fb)
 where
  fa : fiber f (f x)
  fa = x , ğ“‡â„¯ğ’»ğ“

  fb : fiber f (f x)
  fb = x' , (fxfx' â»Â¹)

\end{code}


-------------------------------------

<sup>1</sup> Whenever we wish to hide some code from the rest of the development, we will enclose it in a module called `hidden.` In this case, we don't want the code inside the `hidden` module to conflict with the original definitions of these functions from Escardo's Type Topology library, which we will import later.  As long as we don't invoke `open hidden`, the code inside the `hidden` model remains essentially hidden (for the purposes of resolving conflicts, though Agda *will* type-check the code).


[â† Prelude.Equality](Prelude.Equality.html)
<span style="float:right;">[Prelude.Extensionality â†’](Prelude.Extensionality.html)</span>

{% include UALib.Links.md %}
