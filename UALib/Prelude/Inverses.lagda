---
layout: default
title : UALib.Prelude.Inverses module (Agda Universal Algebra Library)
date : 2021-01-12
author: William DeMeo
---

### <a id="inverses">Inverses</a>

This section presents the [UALib.Prelude.Inverses][] module of the [Agda Universal Algebra Library][].

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

module Prelude.Inverses where

open import Prelude.Extensionality public

open import MGS-Embeddings
 using (equivs-are-embeddings; invertible; invertibles-are-equivs) public

\end{code}

We begin by defining an inductive type that represents the semantic concept of **inverse image** of a function.

\begin{code}

module _ {ğ“¤ ğ“¦ : Universe} where

 data Image_âˆ‹_ {A : ğ“¤ Ì‡ }{B : ğ“¦ Ì‡ }(f : A â†’ B) : B â†’ ğ“¤ âŠ” ğ“¦ Ì‡
  where
  im : (x : A) â†’ Image f âˆ‹ f x
  eq : (b : B) â†’ (a : A) â†’ b â‰¡ f a â†’ Image f âˆ‹ b


 ImageIsImage : {A : ğ“¤ Ì‡}{B : ğ“¦ Ì‡}(f : A â†’ B)(b : B)(a : A)
                ---------------------------------------------
  â†’             b â‰¡ f a â†’ Image f âˆ‹ b

 ImageIsImage f b a bâ‰¡fa = eq b a bâ‰¡fa

\end{code}

The following restatement of the last lemma simply rearranges the arguments in case that makes it clearer.

\begin{code}

 ImageIsImage' : {A : ğ“¤ Ì‡}{B : ğ“¦ Ì‡}(f : A â†’ B)(a : A)(b : B)
                  ---------------------------------------------
  â†’               f a â‰¡ b  â†’  Image f âˆ‹ b

 ImageIsImage' f a b fab = eq b a (fab â»Â¹)

\end{code}

Note that an inhabitant of `Image f âˆ‹ b` is a dependent pair `(a , p)`, where `a : A` and `p : b â‰¡ f a` is a proof that `f` maps `a` to `b`.  Since the proof that `b` belongs to the image of `f` is always accompanied by a "witness" `a : A`, we can actually *compute* a (pseudo)inverse of `f`. For convenience, we define this inverse function, which we call `Inv`, and which takes an arbitrary `b : B` and a witness-proof pair, `(a , p) : Image f âˆ‹ b`, and returns `a`.

\begin{code}

 Inv : {A : ğ“¤ Ì‡ }{B : ğ“¦ Ì‡ }(f : A â†’ B){b : B} â†’ Image f âˆ‹ b  â†’  A
 Inv f {.(f a)} (im a) = a
 Inv f (eq _ a _) = a

\end{code}

We can prove that `Inv f` is the right-inverse of `f`, as follows.

\begin{code}

 InvIsInv : {A : ğ“¤ Ì‡}{B : ğ“¦ Ì‡}(f : A â†’ B){b : B}(bâˆˆImgf : Image f âˆ‹ b) â†’ f(Inv f bâˆˆImgf) â‰¡ b

 InvIsInv f {.(f a)} (im a) = refl _

 InvIsInv f (eq _ _ p) = p â»Â¹

\end{code}





#### <a id="surjective-functions">Surjective functions</a>

An epic (or surjective) function from type `A : ğ“¤ Ì‡` to type `B : ğ“¦ Ì‡` is as an inhabitant of the `Epic` type, which we define as follows.

\begin{code}

 Epic : {A : ğ“¤ Ì‡ } {B : ğ“¦ Ì‡ } (g : A â†’ B) â†’  ğ“¤ âŠ” ğ“¦ Ì‡
 Epic g = âˆ€ y â†’ Image g âˆ‹ y

\end{code}

We obtain the right-inverse (or pseudoinverse) of an epic function `f` by applying the function `EpicInv` (which we now define) to the function `f` along with a proof, `fE : Epic f`, that `f` is surjective.

\begin{code}

 EpicInv : {A : ğ“¤ Ì‡ } {B : ğ“¦ Ì‡ }
           (f : A â†’ B) â†’ Epic f
           --------------------
  â†’        B â†’ A

 EpicInv f fE b = Inv f (fE b)

\end{code}

The function defined by `EpicInv f fE` is indeed the right-inverse of `f`. To state this, we'll use the function composition operation, `âˆ˜`, which is already defined in the [Type Topology][] library as follows.

\begin{code}

module hide-âˆ˜ {ğ“¤ ğ“¦ : Universe} where

 _âˆ˜_ : {X : ğ“¤ Ì‡ } {Y : ğ“¦ Ì‡}{Z : Y â†’ ğ“¦ Ì‡ }
  â†’    Î  Z â†’ (f : X â†’ Y) â†’ (x : X) â†’ Z (f x)

 g âˆ˜ f = Î» x â†’ g (f x)

open import MGS-MLTT using (_âˆ˜_) public


module _ {ğ“¤ ğ“¦ : Universe} where

 EpicInvIsRightInv : funext ğ“¦ ğ“¦ â†’ {A : ğ“¤ Ì‡ } {B : ğ“¦ Ì‡ }
                     (f : A â†’ B)  (fE : Epic f)
                     --------------------------
  â†’                  f âˆ˜ (EpicInv f fE) â‰¡ ğ‘–ğ‘‘ B

 EpicInvIsRightInv fe f fE = fe (Î» x â†’ InvIsInv f (fE x))

\end{code}





#### <a id="injective-functions">Injective functions</a>

We say that a function `g : A â†’ B` is **monic** (or **injective** or **one-to-one**) if it doesn't map distinct elements to a common point. This property is formalized quite naturally using the `Monic` type, which we now define.

\begin{code}

 Monic : {A : ğ“¤ Ì‡ } {B : ğ“¦ Ì‡ }(g : A â†’ B) â†’ ğ“¤ âŠ” ğ“¦ Ì‡
 Monic g = âˆ€ aâ‚ aâ‚‚ â†’ g aâ‚ â‰¡ g aâ‚‚ â†’ aâ‚ â‰¡ aâ‚‚

\end{code}

Again, we obtain a pseudoinverse. Here it is obtained by applying the function `MonicInv` to `g` and a proof that `g` is monic.

\begin{code}

 --The (pseudo-)inverse of a monic function
 MonicInv : {A : ğ“¤ Ì‡ }{B : ğ“¦ Ì‡ }(f : A â†’ B) â†’ Monic f
  â†’         (b : B) â†’ Image f âˆ‹ b â†’ A

 MonicInv f _ = Î» b Imfâˆ‹b â†’ Inv f Imfâˆ‹b

\end{code}

The function defined by `MonicInv f fM` is the left-inverse of `f`.

\begin{code}

 --The (psudo-)inverse of a monic is the left inverse.
 MonicInvIsLeftInv : {A : ğ“¤ Ì‡ }{B : ğ“¦ Ì‡ }(f : A â†’ B)(fmonic : Monic f)(x : A)
   â†’                 (MonicInv f fmonic)(f x)(im x) â‰¡ x

 MonicInvIsLeftInv f fmonic x = refl _

\end{code}



#### <a id="composition-laws">Composition laws</a>

\begin{code}

module _ {ğ“§ ğ“¨ ğ“© : Universe} where

 epic-factor : funext ğ“¨ ğ“¨ â†’ {A : ğ“§ Ì‡}{B : ğ“¨ Ì‡}{C : ğ“© Ì‡}
               (Î² : A â†’ B)(Î¾ : A â†’ C)(Ï• : C â†’ B)
  â†’            Î² â‰¡ Ï• âˆ˜ Î¾ â†’  Epic Î² â†’ Epic Ï•

 epic-factor fe {A}{B}{C} Î² Î¾ Ï• compId Î²e y = Î³
  where
   Î²inv : B â†’ A
   Î²inv = EpicInv Î² Î²e

   Î¶ : Î² (Î²inv y) â‰¡ y
   Î¶ = ap (Î» - â†’ - y) (EpicInvIsRightInv fe Î² Î²e)
   Î· : (Ï• âˆ˜ Î¾) (Î²inv y) â‰¡ y
   Î· = (ap (Î» - â†’ - (Î²inv y)) (compId â»Â¹)) âˆ™ Î¶
   Î³ : Image Ï• âˆ‹ y
   Î³ = eq y (Î¾ (Î²inv y)) (Î· â»Â¹)

\end{code}





#### <a id="embeddings">Embeddings</a>
The type `is-embedding f` denotes the assertion that `f` is a function all of whose fibers are subsingletons.

\begin{code}
module hide-is-embedding {ğ“¤ ğ“¦ : Universe} where

 is-embedding : {X : ğ“¤ Ì‡ } {Y : ğ“¦ Ì‡ } â†’ (X â†’ Y) â†’ ğ“¤ âŠ” ğ“¦ Ì‡
 is-embedding f = âˆ€ y â†’ is-subsingleton (fiber f y)

open import MGS-Embeddings using (is-embedding) public

\end{code}

This is a natural way to represent what we usually mean in mathematics by embedding.  Observe that an embedding does not simply correspond to an injective map.  However, if we assume that the codomain `B` has unique identity proofs (i.e., is a set), then we can prove that a monic function into `B` is an embedding. We will do so in the [Relations.Truncation][] module when we take up the topic of sets in some detail.

Of course, invertible maps are embeddings.

\begin{code}

invertibles-are-embeddings : {ğ“§ ğ“¨ : Universe}
                             {X : ğ“§ Ì‡} {Y : ğ“¨ Ì‡} (f : X â†’ Y)
 â†’                           invertible f â†’ is-embedding f

invertibles-are-embeddings f fi = equivs-are-embeddings f (invertibles-are-equivs f fi)

\end{code}

Finally, embeddings are monic; from a proof `p : is-embedding f` that `f` is an embedding we can construct a proof of `Monic f`.

\begin{code}


embedding-is-monic : {ğ“§ ğ“¨ : Universe} {X : ğ“§ Ì‡}{Y : ğ“¨ Ì‡}
                     (f : X â†’ Y) â†’ is-embedding f â†’ Monic f

embedding-is-monic f femb x x' fxfx' = ap prâ‚ ((femb (f x)) fa fb)
 where
 fa : fiber f (f x)
 fa = x , ğ“‡â„¯ğ’»ğ“

 fb : fiber f (f x)
 fb = x' , (fxfx' â»Â¹)

\end{code}


-------------------------------------

<p></p>

[â† Prelude.Extensionality](Prelude.Extensionality.html)
<span style="float:right;">[Prelude.Lifts â†’](Prelude.Lifts.html)</span>


{% include UALib.Links.md %}


<!-- 
This is the first point at which [truncation](UALib.Preface.html#truncation) comes into play.  An [embedding](https://www.cs.bham.ac.uk/~mhe/HoTT-UF-in-Agda-Lecture-Notes/HoTT-UF-Agda.html#embeddings) is defined in the [Type Topology][] library, using the `is-subsingleton` type [described earlier](Prelude.Extensionality.html#alternative-extensionality-type), as follows.
-->
