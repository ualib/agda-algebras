---
layout: default
title : UALib.Prelude.Inverses module (Agda Universal Algebra Library)
date : 2021-01-12
author: William DeMeo
---

### <a id="inverses">Inverses</a>

This is the [UALib.Prelude.Inverses][] module of the [Agda Universal Algebra Library][].

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

module Prelude.Inverses where

open import Prelude.Extensionality public

open import MGS-Embeddings
 using (equivs-are-embeddings; invertible; invertibles-are-equivs) public

\end{code}

We begin by defining an inductive type that represents the semantic concept of *inverse image* of a function.

\begin{code}

module _ {ğ“¤ ğ“¦ : Universe}{A : ğ“¤ Ì‡ }{B : ğ“¦ Ì‡ } where

 data Image_âˆ‹_ (f : A â†’ B) : B â†’ ğ“¤ âŠ” ğ“¦ Ì‡
  where
  im : (x : A) â†’ Image f âˆ‹ f x
  eq : (b : B) â†’ (a : A) â†’ b â‰¡ f a â†’ Image f âˆ‹ b

\end{code}

Next we verify that the type just defined is what we expect.

\begin{code}

 ImageIsImage : (f : A â†’ B)(b : B)(a : A) â†’ b â‰¡ f a â†’ Image f âˆ‹ b
 ImageIsImage f b a bâ‰¡fa = eq b a bâ‰¡fa

\end{code}

An inhabitant of `Image f âˆ‹ b` is a dependent pair `(a , p)`, where `a : A` and `p : b â‰¡ f a` is a proof that `f` maps `a` to `b`.  Since the proof that `b` belongs to the image of `f` is always accompanied by a "witness" `a : A`, we can actually *compute* a (pseudo)inverse of `f`. For convenience, we define this inverse function, which we call `Inv`, and which takes an arbitrary `b : B` and a (*witness*, *proof*)-pair, `(a , p) : Image f âˆ‹ b`, and returns the witness `a`.

\begin{code}

 Inv : (f : A â†’ B){b : B} â†’ Image f âˆ‹ b  â†’  A
 Inv f {.(f a)} (im a) = a
 Inv f (eq _ a _) = a

\end{code}

We can prove that `Inv f` is the *right-inverse* of `f`, as follows.

\begin{code}

 InvIsInv : (f : A â†’ B){b : B}(q : Image f âˆ‹ b) â†’ f(Inv f q) â‰¡ b
 InvIsInv f {.(f a)} (im a) = refl
 InvIsInv f (eq _ _ p) = p â»Â¹

\end{code}





#### <a id="epics">Epics (surjective functions)</a>

An epic (or surjective) function from `A` to `B` is as an inhabitant of the `Epic` type, which we now define.

\begin{code}

 Epic : (A â†’ B) â†’  ğ“¤ âŠ” ğ“¦ Ì‡
 Epic f = âˆ€ y â†’ Image f âˆ‹ y

\end{code}

We obtain the right-inverse (or pseudoinverse) of an epic function `f` by applying the function `EpicInv` (which we now define) to the function `f` along with a proof, `fE : Epic f`, that `f` is surjective.

\begin{code}

 EpicInv : (f : A â†’ B) â†’ Epic f â†’ B â†’ A
 EpicInv f fE b = Inv f (fE b)

\end{code}

The function defined by `EpicInv f fE` is indeed the right-inverse of `f`. To state this, we'll use the function composition operation, `âˆ˜`, which is already defined in the [Type Topology][] library as follows.

\begin{code}

module hide-âˆ˜ {ğ“¤ ğ“¦ : Universe}{A : ğ“¤ Ì‡}{B : ğ“¦ Ì‡} where

 _âˆ˜_ : {C : B â†’ ğ“¦ Ì‡ } â†’ Î  C â†’ (f : A â†’ B) â†’ (x : A) â†’ C (f x)
 g âˆ˜ f = Î» x â†’ g (f x)

open import MGS-MLTT using (_âˆ˜_) public


module _ {ğ“¤ ğ“¦ : Universe}{fe : funext ğ“¦ ğ“¦}{A : ğ“¤ Ì‡}{B : ğ“¦ Ì‡} where

 EpicInvIsRightInv : (f : A â†’ B)(fE : Epic f) â†’ f âˆ˜ (EpicInv f fE) â‰¡ ğ‘–ğ‘‘ B
 EpicInvIsRightInv f fE = fe (Î» x â†’ InvIsInv f (fE x))

\end{code}

We can also prove the following composition law for epics.

\begin{code}

 epic-factor : {ğ“© : Universe}{C : ğ“© Ì‡}(f : A â†’ B)(g : A â†’ C)(h : C â†’ B)
  â†’            f â‰¡ h âˆ˜ g â†’ Epic f â†’ Epic h

 epic-factor f g h compId fe y = Î³
  where
   finv : B â†’ A
   finv = EpicInv f fe

   Î¶ : f (finv y) â‰¡ y
   Î¶ = cong-app (EpicInvIsRightInv f fe) y

   Î· : (h âˆ˜ g) (finv y) â‰¡ y
   Î· = (cong-app (compId â»Â¹)(finv y)) âˆ™ Î¶

   Î³ : Image h âˆ‹ y
   Î³ = eq y (g (finv y)) (Î· â»Â¹)

\end{code}






#### <a id="monics">Monics (injective functions)</a>

We say that a function `f : A â†’ B` is *monic* (or *injective* or *one-to-one*) if it doesn't map distinct elements to a common point. This property is formalized quite naturally using the `Monic` type, which we now define.

\begin{code}

module _ {ğ“¤ ğ“¦ : Universe}{A : ğ“¤ Ì‡}{B : ğ“¦ Ì‡} where

 Monic : (f : A â†’ B) â†’ ğ“¤ âŠ” ğ“¦ Ì‡
 Monic f = âˆ€ x y â†’ f x â‰¡ f y â†’ x â‰¡ y

\end{code}

Again, we obtain a pseudoinverse. Here it is obtained by applying the function `MonicInv` to `g` and a proof that `g` is monic.

\begin{code}

 MonicInv : (f : A â†’ B) â†’ Monic f â†’ (b : B) â†’ Image f âˆ‹ b â†’ A
 MonicInv f _ = Î» b imfb â†’ Inv f imfb

\end{code}

The function defined by `MonicInv f fM` is the left-inverse of `f`.

\begin{code}

 MonicInvIsLeftInv : {f : A â†’ B}{fM : Monic f}{x : A} â†’ (MonicInv f fM)(f x)(im x) â‰¡ x
 MonicInvIsLeftInv = refl

\end{code}





#### <a id="embeddings">Embeddings</a>

The `is-embedding` type is defined in the [Type Topology][] library in the following way.

\begin{code}
module hide-is-embedding {ğ“¤ ğ“¦ : Universe}{A : ğ“¤ Ì‡}{B : ğ“¦ Ì‡} where

 is-embedding : (A â†’ B) â†’ ğ“¤ âŠ” ğ“¦ Ì‡
 is-embedding f = âˆ€ b â†’ is-subsingleton (fiber f b)

open import MGS-Embeddings using (is-embedding) public

\end{code}

Thus, `is-embedding f` asserts that `f` is a function all of whose fibers are subsingletons. This is a natural way to represent what we usually mean in mathematics by embedding.  Observe that an embedding does not simply correspond to an injective map.  However, if we assume that the codomain `B` has unique identity proofs (i.e., is a set), then we can prove that a monic function into `B` is an embedding. We will do so in the [Relations.Truncation][] module when we take up the topic of sets in some detail.

Finding a proof that a function is an embedding isn't always easy, but one path that is often straightforward is to first prove that the function is invertible and then invoke the following theorem.

\begin{code}

module _ {ğ“¤ ğ“¦ : Universe}{A : ğ“¤ Ì‡}{B : ğ“¦ Ì‡} where

 invertibles-are-embeddings : (f : A â†’ B) â†’ invertible f â†’ is-embedding f
 invertibles-are-embeddings f fi = equivs-are-embeddings f (invertibles-are-equivs f fi)

\end{code}

Finally, embeddings are monic; from a proof `p : is-embedding f` that `f` is an embedding we can construct a proof of `Monic f`.  We confirm this as follows.

\begin{code}

 embedding-is-monic : (f : A â†’ B) â†’ is-embedding f â†’ Monic f
 embedding-is-monic f femb x y fxfy = ap prâ‚ ((femb (f x)) fx fy)
  where
  fx : fiber f (f x)
  fx = x , refl

  fy : fiber f (f x)
  fy = y , (fxfy â»Â¹)

\end{code}


-------------------------------------

<p></p>

[â† Prelude.Extensionality](Prelude.Extensionality.html)
<span style="float:right;">[Prelude.Lifts â†’](Prelude.Lifts.html)</span>


{% include UALib.Links.md %}


<!-- 
This is the first point at which [truncation](UALib.Preface.html#truncation) comes into play.  An [embedding](https://www.cs.bham.ac.uk/~mhe/HoTT-UF-in-Agda-Lecture-Notes/HoTT-UF-Agda.html#embeddings) is defined in the [Type Topology][] library, using the `is-subsingleton` type [described earlier](Prelude.Extensionality.html#alternative-extensionality-type), as follows.
-->
