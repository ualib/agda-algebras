.. math:: \newcommand{\Sg}{\mathsf{Sg}} \newcommand\hom{\operatorname{Hom}} \newcommand\epi{\operatorname{Epi}} \newcommand\aut{\operatorname{Aut}} \newcommand\mono{\operatorname{Mono}} \newcommand\Af{\ensuremath{\langle A, f \rangle}} 

.. role:: cat

.. role:: code

.. _universal-algebra-in-lean:

=========================
Universal Algebra in Lean
=========================

This section demonstrates the utility of dependent and inductive types by expressing some fundamental concepts of universal algebra in Lean.

In particular, we will formally represent each of the following:  *operation*, *algebra*, *subuniverse*, and *term algebra*.

Our formal representations of these concepts will be clear, concise, and computable. Moreover, we strive to develop a notation and syntax that will feel natural to working algebraists.

Our goal here is to demonstrate the power of Lean's type system for expressing mathematical concepts precisely and constructively, and to show that if we make careful design choices at the start of our development, then our formal theorems *and their proofs* can approximate the efficiency and readability of analogous informal presentations found in the mathematics literature.

Recall, the symbols â„•, Ï‰, and ``nat`` are synonymous and all denote the **type of natural numbers**.

The Lean code described in this section is found in the following files of the lean-ualib: ``basic.lean``, ``subuniverse.lean``, ``free.lean``, terms.lean``. [3]_

-----------------------------------------------------

.. index:: signature, operation, operation symbol, similarity type, arity, arity type, variety, equational class, algebraic structure 

Arity and Operations 
--------------------------

We start with the **type of operation symbols**, which depends on our semantic notion of **arity**, also captured by a type.

Argument lists that are passed to operations are represented by tuples which are simply functions, say, of type Î² â†’ Î±, where Î² is the **arity type** of the operation to which the tuple will be passed as an argument list.

Heuristically, it's fine to think of | Î² | as the "number" of arguments the operation takes, but the implementation is much more general than that. In particular, there is no reason to restrict, *a priori*, the arity type to be a finite set.

An **operation** takes a tuple (or, list of "arguments") of type Î² â†’ Î± and returns an element of type Î±.  Here, Î± is the type on which the operation is defined.

In Lean, if Î± and Î² are types, we define the **type of Î²-ary operations on Î±**, denoted by ``op``, to be the function type (Î² â†’ Î±) â†’ Î±.

.. code-block:: lean

    import data.set
    definition op (Î² Î±) := (Î² â†’ Î±) â†’ Î±

An example of an operation of type ``op(Î²Î±)`` is the **projection function** Ï€ , defined on the type Î±, as follows:

.. code-block:: lean

    import data.set
    definition op (Î² Î±) := (Î² â†’ Î±) â†’ Î±
    -- BEGIN
    definition Ï€ {Î² Î±} (i) : op Î² Î± := Î» t, t i
    -- END

The operation ``Ï€ i`` maps a given tuple ``t : Î² â†’ Î±`` to ``t i``, its "value" at input ``i``.

For instance, if we have types ``Î±, Î²``, and variables ``i:Î²`` and ``t:Î² â†’ Î±``, then the command ``#check Ï€ i t`` shows that the type of ``Ï€ i t`` is ``Î±``, as expected, since ``Ï€ i t = t i``.

.. code-block:: lean

    import data.set
    definition op (Î² Î±) := (Î² â†’ Î±) â†’ Î±
    definition Ï€ {Î² Î±} (i) : op Î² Î± := Î» t, t i
    -- BEGIN
    variables (Î± : Type*) (Î² : Type*) (i : Î²) (t : Î² â†’ Î±) 
    #check Ï€ i f       -- answer: Ï€ i f : Î± 
    -- END

Here are a couple of examples that are a bit more concrete.

.. code-block:: lean

    -- Example: the tuple p1 = (1, 2, 3, ...).
    definition p1 : â„• â†’ â„• := Î» n, n+1

    -- What's the 3rd projection of p1?
    #eval Ï€ 3 p1                         -- answer: 4

    -- Example: the constant tuple sevens = (7, 7, 7, ...)
    definition sevens : â„• â†’ â„• := Î» n, 7

    -- What's the 3rd projection of sevens?
    #eval Ï€ 3 sevens                      -- answer: 7

------------------------------------------------------

.. _signature:

Signature
---------

A **signature** :math:`Ïƒ = (F, Ï)` consists of

  #. :math:`F :=` a set of **operation symbols**;
  #. :math:`Ï: F â†’ N :=` a **similarity type**.
  
..  giving the **arity**, ``Ïf``, of each operation symbol ``f:F``.
  
For each operation symbol :math:`f : F`, the value :math:`Ï f` is called the **arity** of :math:`f`.  This value has type :math:`N`, which is the **arity type**.

In classical universal algebra we typically assume :math:`N = Ï‰ := â„•`, but for most of the basic theory this choice is inconsequential. [1]_

.. index:: type of signatures

.. index:: operation symbol, arity function, 

We now given a first pass at implementing signatures and operations in Lean, highlighting the similarity between the formal and the (classical) informal presentation of these concepts.

We define a signature as a structure with two fields, the type ``F`` of **operation symbols** and an **arity function** ``Ï : F â†’ Type*``, which takes each operation symbol ``f`` to its arity ``Ï f``.

.. code-block:: lean

    import data.set
    definition op (Î² Î±) := (Î² â†’ Î±) â†’ Î±
    definition Ï€ {Î² Î±} (i) : op Î² Î± := Î» f, f i
    variables (Î± : Type*) (Î² : Type*) (i : Î²) (f : Î² â†’ Î±) 
    -- BEGIN
    -- Signature
    -- F : a set of operation symbols
    -- Ï : returns the arity of a given operation symbol
    structure signature := mk :: (F : Type*) (Ï : F â†’ Type*)
    -- END

Next we open up a ``section`` so we can define some parameters (such as the signature ``Ïƒ``) that won't change often throughout the development. [4]_

Then we define the **type of interpretations of operations** on the carrier type ``Î±``. 

First, let us fix a signature ``Ïƒ`` and define some convenient notation.

.. code-block:: lean

    import data.set
    definition op (Î² Î±) := (Î² â†’ Î±) â†’ Î±
    definition Ï€ {Î² Î±} (i) : op Î² Î± := Î» f, f i
    variables (Î± : Type*) (Î² : Type*) (i : Î²) (f : Î² â†’ Î±) 
    structure signature := mk :: (F : Type*) (Ï : F â†’ Type*)
    -- BEGIN
    section
      parameter (Ïƒ : signature)
      local notation `F` := Ïƒ.F
      local notation `Ï` := Ïƒ.Ï 
    end
    -- END

The ``local notation`` directive lets us write ``f : F`` (instead of ``f : Ïƒ.F``) to indicate that the operation symbol ``f`` has type ``F``.

Similarly, the second notation directive lets us denote the arity of ``f`` by ``Ï f`` (instead of ``Ïƒ.Ï f``).

With this notation, the Lean syntax matches our standard informal algebra syntax almost exactly! [5]_ 

-------------------------------------

.. _universal-algebra:

Universal algebra
------------------

Classical universal algebra is the study of **varieties** (or **equational classes**) of algebraic structures. 

An **algebraic structure** is denoted by :math:`ğ€ = âŸ¨A, F^{ğ€}âŸ©` and consists of 

  #. :math:`A :=` a set, called the *universe* (or *carrier*) of the algebra,
  #. :math:`F^{ğ€} = \{f^{ğ€} âˆ£ f âˆˆ F, f^{ğ€} : (Ïf â†’ A) â†’ A\} :=` a set of operations defined on :math:`A`, and
  #. a collection of identities satisfied by the elements and operations of ğ€.

Some of the renewed interest in universal algebra has focused on representations of algebras in categories other than :math:`\mathbf{Set}`, such as multisorted algebras, higher-type universal algebra, etc. (:cite:`MR2757312`, :cite:`MR3003214`, :cite:`finster:2018`, :cite:`gepner:2018`, :cite:`MR1173632`). These are natural generalizations that we will become part of the ``lean-ualib`` library, but only after we have an easily accessible implementation of the classical core of (single-sorted, set-based) universal algebra.

Suppose :math:`A` is a set and :math:`f` is a :math:`Ï f`-ary operation on :math:`A`. In this case, we often write :math:`f : A^{Ïf} â†’ A`.

If the arity type :math:`N` happens to be the set â„• of natural numbers, then :math:`Ï f` denotes the set :math:`\{0, 1, \dots, Ïf-1\}`.

A function :math:`g` of type :math:`Ïf â†’ A` is then simply a :math:`Ï f`-tuple of elements from :math:`A`. [2]_

Fix :math:`m : â„•`.

An :math:`m`-tuple, :math:`ğš = (a_0, a_1, \dots , a_{m-1}) : A^m` is (the graph of) the function :math:`ğš : m â†’ ğ– `, defined for each :math:`i < m` by :math:`ğš i = ğ–º_i`.

Therefore, if :math:`h : A â†’ B`, then :math:`h âˆ˜ a : m â†’ B` is the tuple whose value at :math:`i` is :math:`(h âˆ˜ a) i = h a i = h a_i`, which has type :math:`B`.

On the other hand, if :math:`g` has type :math:`A^m â†’ A`, then for each :math:`a : A` the value :math:`g a` has type :math:`A`.

Suppose 

  + :math:`f : (Ïf â†’ B) â†’ B` is a :math:`Ï f`-ary operation on :math:`B`, 
  + :math:`a : Ïf â†’ A` is a :math:`Ï f`-tuple on :math:`A`, and 
  + :math:`h : A â†’ B`. 

Then :math:`h âˆ˜ a : Ïf â†’ B`, so :math:`f (h âˆ˜ a) : B`.

.. _universal-algebras-in-lean:

Universal algebras in Lean
~~~~~~~~~~~~~~~~~~~~~~~~~~

To represent the interpretation of an algebra on a carrier type Î±, we define a type that we call ``algebra_on``.

.. index:: pair: dependent pair type; Sigma type

.. index:: pair: dependent function type; Pi type

Recall, a **Pi type** ``Î (x:A),B x`` generalizes the function type ``A â†’ B`` and represents a **dependent function type** by allowing the codomain ``B x`` to depend on the value ``x`` of the input argument.

Similarly, a **Sigma type** ``Î£(x:A),B x`` generalizes the Cartesian product ``A Ã— B`` by allowing the type ``B x`` of the second argument of the ordered pair to depend on the value ``x`` of the first. Thus, a Sigma type is called a **dependent pair type**.

Our definition of ``algebra_on`` uses the dependent function type, which makes sense because, if we are given a signature Ïƒ and a carrier type Î±, then an Ïƒ-algebra over Î± is determined by its operations on Î±, and an inhabitant of the type ``algebra_on`` assigns an interpretation to each ``op`` symbol ``f : F``, which yields a function of type ``Ï f â†’ Î± â†’ Î±``. [6]_

.. code-block:: lean

    import data.set
    definition op (Î² Î±) := (Î² â†’ Î±) â†’ Î±
    definition Ï€ {Î² Î±} (i) : op Î² Î± := Î» f, f i
    variables (Î± : Type*) (Î² : Type*) (i : Î²) (f : Î² â†’ Î±) 
    structure signature := mk :: (F : Type*) (Ï : F â†’ Type*)
    -- BEGIN
    section

      parameter (Ïƒ : signature)
      local notation `F` := Ïƒ.F
      local notation `Ï` := Ïƒ.Ï 

      -- Define the interpretation of an algebra on the carrier Î±:
      definition algebra_on (Î± : Type*) := Î  (f : F), op (Ï f) Î±   

      -- This is called `algebra_on` since an algebra is fully
      -- specified by its Cayley (operation) tables. An inhabitant 
      -- of `algebra_on` assigns to each op symbol f : F, of 
      -- arity `Î² = Ïƒ.Ï f`, an interpretation of f, that is, 
      -- a function of type (Î² â†’ Î±) â†’ Î±.
    end
    -- END

Finally, let us consider how to define a **universal algebra** in Lean.

A universal algebra :math:`ğ€ = âŸ¨A,F^ğ€âŸ©` is a pair consisting of a carrier (or universe) :math:`A` along with an set :math:`F^ğ€` of interpretations of the operation symbols in :math:`F`.

Thus, the type of the second component of the pair :math:`âŸ¨A,F^ğ€âŸ©` depends on the first, so it is natural to encode the type of algebras as a dependent pair (or Sigma) type.

.. , that is, a type of the form ``Î£(x:A), B x``.

.. code-block:: lean

    import data.set
    definition op (Î² Î±) := (Î² â†’ Î±) â†’ Î±
    definition Ï€ {Î² Î±} (i) : op Î² Î± := Î» f, f i
    variables (Î± : Type*) (Î² : Type*) (i : Î²) (f : Î² â†’ Î±) 
    structure signature := mk :: (F : Type*) (Ï : F â†’ Type*)
    -- BEGIN
    section

      parameter (Ïƒ : signature)
      local notation `F` := Ïƒ.F
      local notation `Ï` := Ïƒ.Ï 
      definition algebra_on (Î± : Type*) := Î  (f : F), op (Ï f) Î±   

      -- An algebra pairs a carrier with an interpretation of 
      -- the op symbols.
      definition algebra := sigma algebra_on

      -- sigma is the "dependent pair" type: âŸ¨Î±, Î² Î±âŸ© which is
      -- appropriate since an algebra consists of a universe 
      -- (of type Î±), and operations on that universe; the
      -- type of the operations depends on the universe type.

    end
    -- END

(For a disection of Lean's ``sigma`` type, see :numref:`Appendix Section %s <sigma-type>`.)

Finally, we show how to get ahold of the carrier and operations of an algebra by instantiating them as follows:

.. code-block:: lean

    import data.set
    definition op (Î² Î±) := (Î² â†’ Î±) â†’ Î±
    definition Ï€ {Î² Î±} (i) : op Î² Î± := Î» f, f i
    variables (Î± : Type*) (Î² : Type*) (i : Î²) (f : Î² â†’ Î±) 
    structure signature := mk :: (F : Type*) (Ï : F â†’ Type*)
    -- BEGIN
    section

      parameter (Ïƒ : signature)
      local notation `F` := Ïƒ.F
      local notation `Ï` := Ïƒ.Ï 
      definition algebra_on (Î± : Type*) := Î  (f : F), op (Ï f) Î±   
      definition algebra := sigma algebra_on

      instance alg_carrier : has_coe_to_sort algebra := 
      âŸ¨_, sigma.fstâŸ©
      
      instance alg_operations : has_coe_to_fun algebra := 
      âŸ¨_, sigma.sndâŸ©

    end
    -- END

The last two lines are tagged with ``has_coe_to_sort`` and ``has_coe_to_fun``, respectively, because here we are using a very nice feature of Lean called **coercions**.

(For a disection of coercions in Lean, see :numref:`Appendix Section %s <coercions>`.)

Using coercions allows us to employ a syntax that is similar (though not identical) to the standard syntax of informal mathematics.

For instance, the standard notation for the interpretation of the operation symbol :math:`f` in the algebra :math:`ğ€ = âŸ¨A, F^ğ€âŸ©` is :math:`f^ğ€`. In our Lean implementation, we use ``A f`` to denote :math:`f^ğ€`. Although this syntax doesn't match the informal syntax exactly, it seems equally elegant and adapting to it should not overburden the user.

Another example that demonstrates the utility of coercions is our definition of ``is_subalgebra``, a function that takes as input two algebraic structures and decides whether the second structure is a subalgebra of the first.  Here is the definition.

.. code-block:: lean

    import data.set
    definition op (Î² Î±) := (Î² â†’ Î±) â†’ Î±
    definition Ï€ {Î² Î±} (i) : op Î² Î± := Î» f, f i
    variables (Î± : Type*) (Î² : Type*) (i : Î²) (f : Î² â†’ Î±) 
    structure signature := mk :: (F : Type*) (Ï : F â†’ Type*)
    section
      parameter (Ïƒ : signature)
      local notation `F` := Ïƒ.F
      local notation `Ï` := Ïƒ.Ï 
      definition algebra_on (Î± : Type*) := Î  (f : F), op (Ï f) Î±   
      definition algebra := sigma algebra_on
      instance alg_carrier : has_coe_to_sort algebra := âŸ¨_, sigma.fstâŸ©
      instance alg_operations : has_coe_to_fun algebra := âŸ¨_, sigma.sndâŸ©
    end
    section

    -- BEGIN
    definition is_subalgebra 
    {Ïƒ : signature} {Î± : Type*} {Î² : Type*}
    (A : algebra_on Ïƒ Î±) {Î² : set Î±} (B : algebra_on Ïƒ Î²) := 
    âˆ€ f b, â†‘(B f b) = A f â†‘b
    -- END

    end 

.. index:: homomorphism

To see this notation in action, let us look at how the ``lean-ualib`` represents the assertion that a function is a Ïƒ-**homomorphism**.

.. code-block:: lean

   import data.set
   definition op (Î² Î±) := (Î² â†’ Î±) â†’ Î±
   definition Ï€ {Î² Î±} (i) : op Î² Î± := Î» f, f i
   variables (Î± : Type*) (Î² : Type*) (i : Î²) (f : Î² â†’ Î±) 
   structure signature := mk :: (F : Type*) (Ï : F â†’ Type*)
   section
     parameter (Ïƒ : signature)
     local notation `F` := Ïƒ.F
     local notation `Ï` := Ïƒ.Ï 
     definition algebra_on (Î± : Type*) := Î  (f : F), op (Ï f) Î±   
     definition algebra := sigma algebra_on
     instance alg_carrier : has_coe_to_sort algebra := âŸ¨_, sigma.fstâŸ©
     instance alg_operations : has_coe_to_fun algebra := âŸ¨_, sigma.sndâŸ©
   end
   section

   definition is_subalgebra {Ïƒ : signature} {Î± : Type*} {Î² : Type*}
   (A : algebra_on Ïƒ Î±) {Î² : set Î±} (B : algebra_on Ïƒ Î²) :=
   âˆ€ f b, â†‘(B f b) = A f â†‘b

   -- BEGIN
   definition homomorphic {Ïƒ : signature}
   {A : algebra Ïƒ} {B : algebra Ïƒ} (h : A â†’ B) := 
   âˆ€ (f : Ïƒ.F) (a : Ïƒ.Ï f â†’ A.fst), h (A f a) = B f (h âˆ˜ a)
   -- END

   end

Comparing this with a common informal language definition of a homomorphism, which is typically something similar to :math:`âˆ€ f \ âˆ€ a \ h (f^ğ€ (a)) = f^ğ (h âˆ˜ a)`, we expect working algebraists to find the ``lean-ualib`` syntax very readable and usable.

-----------------------------------------------------

.. \ref{sec:leans-hierarchy-of-sorts-and-types})

.. index:: subalgebra, subuniverse

.. _subalgebra:

Subalgebra
----------

Two important concepts in universal algebra are **subuniverse** and **subalgebra**.

Our Lean implementation of subuniverse will illustrate one of the underlying themes that motivates our work. Indeed, we demonstrate the power of **inductively defined types**, which are essential for working with infinite objects in a constructive and computable way, and for proving (by induction) properties of these objects. 

A **subuniverse** of an algebra :math:`ğ€ = âŸ¨A, F^ğ€âŸ©` is a subset :math:`B âŠ† A` that is closed under the operations in :math:`F^ğ€`.

We denote by S ğ€ the set of all subuniverses of ğ€.

If :math:`B` is a subuniverse of ğ€ and :math:`F^{ğ|_A} = \{f^ğ€|_B âˆ£ f âˆˆ F\}` is the set of basic operations of ğ€ restricted to the set :math:`B`, then :math:`ğ = âŸ¨B, F^{ğ|_A}âŸ©` is a **subalgebra** of ğ€.

Conversely, all subalgebras are of this form.

If ğ€ is an algebra and :math:`X âŠ† A` a subset of the universe of ğ€, then the **subuniverse of** ğ€ **generated by** :math:`X` is defined as follows:

.. math:: \mathrm{Sg}^{ğ€}(X)  =  â‹‚ \{ U âˆˆ ğ–² ğ€ âˆ£ X âŠ† U \}.
  :label: SgDef

To give another exhibition of the efficiency and ease with which we can formalize basic but important mathematical concepts in Lean, we now present a fundamental theorem about subalgebra generation, first in the informal language, and then formally :ref:`below <subalgebras-in-lean>`.

Notice that the added complexity of the Lean implementation of this theorem is not significant, and the proof seems quite readable (especially when compared to the syntax used by other interactive theorem provers).  

The following is a recursive definition of the subuniverse generated by a set. (See :cite:`Bergman:2012`, Thm. 1.14.)

.. _thm-1-14:

.. proof:theorem:: Subuniverse generation

   Let :math:`ğ€ = âŸ¨A, F^{ğ€}âŸ©`  be  an  algebra in the signature :math:`Ïƒ = (F, Ï)` and let :math:`X âŠ† A`.

   Define, by recursion on :math:`n`, the sets :math:`X_n` as follows:

   .. math:: X_0  &=  X \\
          X_{n+1} &=  X_n âˆª \{ f a  âˆ£ f âˆˆ F, \ a âˆˆ X_n^{Ïf}\}.
      :label: subalgebra-inductive

   Then  :math:`\mathrm{Sg}^{ğ€}(X) = â‹ƒ X_n`.

   .. container:: toggle
 
      .. container:: header
 
         *Proof*.
      
      Let :math:`Y = â‹ƒ_{n < Ï‰} X_n`. Clearly :math:`X_n âŠ† Y âŠ† A`, for every :math:`n < Ï‰`. In particular :math:`X = X_0 âŠ† Y`.

      Let us show that :math:`Y` is a subuniverse of ğ€.
   
      Let :math:`f` be a basic :math:`k`-ary operation and :math:`a âˆˆ Y^k`.
    
      From the construction of :math:`Y`, there is an :math:`n < Ï‰` such that :math:`âˆ€ i,\ a,\ i âˆˆ X_n`.
    
      From its definition, :math:`f a âˆˆ X_{n+1} âŠ† Y`.
    
      Thus :math:`Y` is a subuniverse of ğ€ containing :math:`X`.
    
      By :eq:`SgDef`, :math:`\mathrm{Sg}^{ğ€}(X) âŠ† Y`.
    
      For the opposite inclusion, it is enough to check, by induction on :math:`n`, that :math:`X_n âŠ† \mathrm{Sg}^{ğ€}(X)`.
    
      By definition, :math:`X_0 = X âŠ† \mathrm{Sg}^{ğ€}(X)`.
      
      Assume :math:`X_n âŠ† \mathrm{Sg}^ğ€(X)`.  We show :math:`X_{n+1} âŠ† \mathrm{Sg}^ğ€(X)`.
      
      If :math:`b âˆˆ X_{n+1} - X_n`, then :math:`b = f a` for a basic :math:`k`-ary operation :math:`f` and some :math:`a âˆˆ X_n^k`.
      
      But :math:`âˆ€ i, \ a i âˆˆ \mathrm{Sg}^ğ€(X)` and since this latter object is a subuniverse, :math:`b âˆˆ \mathrm{Sg}^ğ€(X)` as well.
    
      Therefore, :math:`X_{n+1} âŠ† \mathrm{Sg}^ğ€(X)`, as desired.

.. _subalgebras-in-lean:

Subalgebras in Lean 
~~~~~~~~~~~~~~~~~~~

The argument in the proof of :numref:`Theorem %s <thm-1-14>` is of a type that one encounters frequently throughout algebra. It has two parts.

  #. Some set :math:`Y` is shown to be a subuniverse of ğ€ that contains :math:`X`.

  #. Every subuniverse containing :math:`X` is shown to contain :math:`Y` as well.

  #. One concludes that :math:`Y = \mathrm{Sg}^ğ€ (X)`.

We now show how the subalgebra concept and the foregoing argument can be implemented formally in Lean_. [7]_

.. code-block:: lean

    import basic
    import data.set
    namespace subuniverse
    section subs
    -- BEGIN
    open set
    parameter {Î± : Type*}      -- the carrier type 
    parameter {Ïƒ : signature}
    parameter (A : algebra_on Ïƒ Î±) 
    parameter {I : Type}       -- a collection of indices
    parameter {R : I â†’ set Î±}  -- an indexed set of sets of type Î±
    definition F := Ïƒ.F        -- the type of operation symbols
    definition Ï := Ïƒ.Ï        -- the operation arity function
    -- END
    end subs
    end subuniverse

.. code-block:: lean

    definition Sub (Î² : set Î±) : Prop :=
    âˆ€ (f : F) (a : Ï f â†’ Î±), (âˆ€ x, a x in Î²) â†’ (A f a in Î²)

.. code-block:: lean

    import basic
    import data.set
    namespace subuniverse
    section subs
    -- BEGIN
    definition Sg (X : set Î±) : set Î± := â‹‚â‚€ {U | Sub U âˆ§ X âŠ† U}
    -- END
    end subs
    end subuniverse

Lean syntax for the intersection operation on collections of *sets* is ``â‹‚â‚€``. [8]_

Next we need *introduction* and *elimination* rules for arbitrary intersections, plus the useful fact that the intersection of subuniverses is a subuniverse. 

.. code-block:: lean

    import basic
    import data.set
    namespace subuniverse
    section subs
    -- BEGIN
    /- Intersection introduction rule -/
    theorem Inter.intro {s : I â†’ set Î±} : 
    âˆ€ x, (âˆ€ i, x âˆˆ s i) â†’ (x âˆˆ â‹‚ i, s i) :=
    assume x h t âŸ¨a, (eq : t = s a)âŸ©, eq.symm â–¸ h a
    -- END
    end subs
    end subuniverse

.. code-block:: lean

    import basic
    import data.set
    namespace subuniverse
    section subs
    -- BEGIN
    /- Intersection elimination rule -/
    theorem Inter.elim {x : Î±} (C : I â†’ set Î±) : 
    (x âˆˆ â‹‚ i, C i) â†’ (âˆ€ i, x âˆˆ C i) := 
    assume h : x âˆˆ â‹‚ i, C i, by simp at h; apply h
    -- END
    end subs
    end subuniverse
      
.. code-block:: lean

    import basic
    import data.set
    namespace subuniverse
    section subs
    -- BEGIN
    /- Intersection of subuniverses is a subuniverse -/
    lemma sub_of_sub_inter_sub (C : I â†’ set Î±) : 
    (âˆ€ i, Sub (C i)) â†’ Sub â‹‚i, C i :=
    assume h : âˆ€ i, Sub (C i), show Sub (â‹‚i, C i), from 
      assume (f : F) (a : Ï f â†’ Î±) (hâ‚ : âˆ€ x, a x âˆˆ â‹‚i, C i), 
      show A f a âˆˆ â‹‚i, C i, from 
        Inter.intro (A f a) 
        (Î» j, (h j) f a (Î» x, Inter.elim C (hâ‚ x) j))
    -- END
    end subs
    end subuniverse

The next three lemmas show that :math:`\mathrm{Sg} X` is the smallest subuniverse containing :math:`X`.

.. code-block:: lean

    import basic
    import data.set
    namespace subuniverse
    section subs
    -- BEGIN
    -- X is a subset of Sg(X)
    lemma subset_X_of_SgX (X : set Î±) : X âŠ† Sg X := 
    assume x (h : x âˆˆ X), 
    show x âˆˆ â‹‚â‚€ {U | Sub U âˆ§ X âŠ† U}, from 
      assume W (hâ‚ : W âˆˆ {U | Sub U âˆ§ X âŠ† U}), 
      show x âˆˆ W, from 
        have hâ‚‚ : Sub W âˆ§ X âŠ† W, from hâ‚, 
        hâ‚‚.right h
    -- END
    end subs
    end subuniverse
      
.. code-block:: lean

    import basic
    import data.set
    namespace subuniverse
    section subs
    -- BEGIN
    -- A subuniverse that contains X also contains Sg X
    lemma sInter_mem {X : set Î±} (x : Î±) : 
    x âˆˆ Sg X â†’ âˆ€ {R : set Î± }, Sub R â†’ X âŠ† R â†’ x âˆˆ R := 
    assume (hâ‚ : x âˆˆ Sg X) (R : set Î±) (hâ‚‚ : Sub R) (hâ‚ƒ : X âŠ† R), 
    show x âˆˆ R, from hâ‚ R (and.intro hâ‚‚ hâ‚ƒ)
    -- END
    end subs
    end subuniverse
      
.. code-block:: lean

    import basic
    import data.set
    namespace subuniverse
    section subs
    -- BEGIN
    -- Sg X is a Sub
    lemma SgX_is_Sub (X : set Î±) : Sub (Sg X) := 
    assume (f : F) (a : Ï f â†’ Î±) (hâ‚€ : âˆ€ i, a i âˆˆ Sg X), 
    show A f a âˆˆ Sg X, from 
     assume W (h : Sub W âˆ§ X âŠ† W), show A f a âˆˆ W, from 
      have hâ‚ : Sg X âŠ† W, from 
        assume r (hâ‚‚ : r âˆˆ Sg X), show r âˆˆ W, from 
         sInter_mem r hâ‚‚ h.left h.right,
         have h' : âˆ€ i, a i âˆˆ W, from assume i, hâ‚ (hâ‚€ i),
         (h.left f a h')
    -- END
    end subs
    end subuniverse

---------------------------------------------------

.. _inductively-defined-type:

Inductively defined types
-------------------------

A primary motivation for this project was our observation that, on the one hand, many important constructs in universal algebra can be defined inductively, and on the other hand, type theory in general, and Lean in particular, offers excellent support for defining inductive types and powerful tactics for proving their properties.

These two facts suggest that there should be much to gain from implementing universal algebra in an expressive type system that offers powerful tools for proving theorems about inductively defined types.

.. index:: subuniverse generated by a set

As such, we are pleased to present the following inductive type that implements the **subuniverse generated by a set**; cf. the definition :eq:`subalgebra-inductive` given in the informal language.

.. code-block:: lean

    inductive Y (X : set Î±) : set Î±
    | var (x : Î±) : x âˆˆ X â†’ Y x
    | app (f : F) (a : Ï f â†’ Î±) : (âˆ€ i, Y (a i)) â†’ Y (A f a)
  
Next we prove that the type ``Y X`` defines a subuniverse, and that it is, in fact, equal to :math:`\mathrm{Sg}^ğ€(X)`.

.. code-block:: lean

    -- Y X is a subuniverse
    lemma Y_is_Sub (X : set Î±) : Sub (Y X) := 
    assume f a (h: âˆ€ i, Y X (a i)), show Y X (A f a), from 
    Y.app f a h 
   
    -- Y X is the subuniverse generated by X
    theorem sg_inductive (X : set Î±) : Sg X = Y X :=
    have hâ‚€ : X âŠ† Y X, from 
      assume x (h : x âˆˆ X), 
      show x âˆˆ Y X, from Y.var x h,
    have hâ‚ : Sub (Y X), from 
      assume f a (h : âˆ€ x, Y X (a x)), 
      show Y X (A f a), from Y.app f a h,
    have inc_l : Sg X âŠ† Y X, from 
       assume u (h : u âˆˆ Sg X), 
       show u âˆˆ Y X, from (sInter_mem u) h hâ‚ hâ‚€,
    have inc_r : Y X âŠ† Sg X, from
       assume a (h: a âˆˆ Y X), show a âˆˆ Sg X, from
         have h' : a âˆˆ Y X â†’ a âˆˆ Sg X, from 
           Y.rec
           --base: a = x âˆˆ X
           ( assume x (h1 : x âˆˆ X), 
             show x âˆˆ Sg X, from subset_X_of_SgX X h1 )
           --inductive: a = A f b for some b with âˆ€ i, b i âˆˆ Sg X
           ( assume f b (h2 : âˆ€ i, b i âˆˆ Y X) (h3 : âˆ€ i, b i âˆˆ Sg X),
             show A f b âˆˆ Sg X, from SgX_is_Sub X f b h3 ),
         h' h,
    subset.antisymm inc_l inc_r

Observe that the last proof proceeds exactly as would a typical informal proof that two sets are equal---prove two subset inclusions and then apply the ``subset.antisymm`` rule, :math:`A âŠ† B â†’ B âŠ† A â†’ A = B`.

.. index:: recursor

We proved ``Y X âŠ† Sg X`` in this case by induction using the **recursor**, ``Y.rec``, which Lean creates for us automatically whenever an inductive type is defined.

The Lean keyword ``assume`` is syntactic sugar for ``Î»``; this and other notational conveniences, such as Lean's ``have...from`` and ``show...from`` syntax, make it possible to render formal proofs in a very clear and readable way.

----------------------------------------------

.. index:: variables, word, term, free algebra

.. _terms-and-free-algebra:

Terms and free algebras
-----------------------

Fix a signature :math:`Ïƒ = (F, Ï)`, let :math:`X` be a set of **variables** and assume :math:`X âˆ© F = âˆ…`.

For every :math:`n < Ï‰`, let  :math:`F_n = Ï^{-1} \{n\}` be the set of :math:`ğ—‡`-ary operation symbols.

By a **word** on :math:`X âˆª F` we mean a nonempty, finite sequence of members of :math:`X âˆª T`.

We denote the concatenation of sequences by simple juxtaposition. We define, by recursion on :math:`n`, the sets :math:`T_n` of words on :math:`X âˆª F` by

.. math::      T_0 &= X âˆª F_0;\\
           T_{n+1} &= T_n âˆª \{ f s âˆ£ f âˆˆ  F, \ s : Ïf â†’ T_n \}. 

Define the set of **terms in the signature** Ïƒ **over** :math:`X` by :math:`T_Ï(X) = â‹ƒ_{n < Ï‰}T_n`.

The definition of :math:`T_Ï (X)` is recursive, indicating that *the set of terms in a signature can be implemented in Lean using an inductive type*.

We will confirm this in the next subsection, but before doing so, we impose an algebraic structure on :math:`T_Ï(X)`, and then state and prove some basic but important facts about this algebra.

These will also be formalized in the next section, giving us another chance to compare informal language proofs to their formal Lean counterparts, and to show off inductively defined types in Lean.

If :math:`w` is a term, let :math:`|w|` be the least :math:`n` such that :math:`w âˆˆ T_n`, called the *height* of :math:`w`. [9]_ The height is a useful index for recursion and induction.

Notice that the set :math:`T_Ï (X)` is nonempty iff either :math:`X` or :math:`F_0` is nonempty. As long as :math:`T_Ï (X)` is nonempty, we can impose upon this set an algebraic structure, as follows:

For every basic operation symbol :math:`f âˆˆ F` let :math:`f^{ğ“_Ï (X)}` be the operation on :math:`ğ“_Ï (X)` that maps each tuple :math:`t : Ïf â†’ T_Ï (X)` to the term :math:`ft`.

We define :math:`ğ“_Ï (X)` to be the algebra with universe :math:`T_Ï (X)` and with basic operations :math:`\{f^{ğ“_Ï (X)} | f âˆˆ F\}`. [10]_

Indeed, Part (2) of :ref:`Theorem 4.21 <thm-4-21>` below asserts that :math:`ğ“_Ï (X)` is *universal for* \sigma-algebras.

To prove this, we need the following basic lemma, which states that a homomorphism is uniquely determined by its restriction to a generating set. (See also :cite:`Bergman:2012`, Ex. 1.16.6.)

.. _ex_1-16-6-brief:

.. proof:lemma::

   Let :math:`f` and :math:`g` be homomorphisms from ğ€ to ğ. If :math:`X âŠ† A` and :math:`X` generates ğ€ and :math:`f|_X = g|_X`, then :math:`f = g`.

   .. container:: toggle
 
      .. container:: header
 
         *Proof*.
      
      Suppose the subset :math:`X âŠ† A` generates ğ€ and suppose :math:`f|_X = g|_X`. Fix an arbitrary element :math:`a âˆˆ A`.

      We show :math:`f(a) = g(a)`. Since :math:`X` generates ğ€, there exists a (say, :math:`n`-ary) term :math:`t` and a tuple :math:`(x_1, \dots, x_n) âˆˆ X^n` such that :math:`a = t^{ğ€}(x_1, \dots, x_n)`. Therefore,

      .. math:: f(a) = f(t^{ğ€}(x_1, \dots, x_n)) &= t^{ğ}(f(x_1), \dots, f(x_n)) \\
                                    &= t^{ğ}(g(x_1), \dots, g(x_n)) = g(t^{ğ€}(x_1, \dots, x_n)) = g(a).

Here is another useful theorem. (See also :cite:`Bergman:2012`, Thm. 4.21.) 

.. _thm-4-21:

.. proof:theorem::

   Let :math:`Ïƒ = (F, Ï)` be a signature.

   #. :math:`ğ“_Ï (X)` is generated by X.
   #. For every Ïƒ-algebra ğ€ and every function :math:`h : X â†’ A` there is a unique homomorphism :math:`g : ğ“_Ï (X) â†’ ğ€` such that :math:`g|_X = h`.

   .. container:: toggle
 
      .. container:: header
 
         *Proof*.
      
      The definition of :math:`ğ“_Ï (X)` exactly parallels the construction in :ref:`Theorem 1.14 <thm-1-14>`. That accounts for (1).

      For (2), define :math:`g(t)` by induction on :math:`Ït`. Suppose :math:`Ït = 0`. Then :math:`t âˆˆ X âˆª F`.
      
      If :math:`t âˆˆ X` then define :math:`g(t) = h(t)`. For :math:`t âˆ‰ X`, :math:`g(t) = t^{ğ€}`.
      
      Note that since ğ€ is an \sigma-algebra and ğ— is a nullary operation symbol, :math:`t^{ğ€}` is defined.
    
      For the inductive step, let :math:`|t| = n + 1`. Then :math:`t = f(s_1, \dots, s_k)` for some :math:`f âˆˆ F_k` and :math:`s_1, \dots, s_k` each of height at most :math:`n`.
      
      We define :math:`g(t) = f^{ğ€}(g(s_1), \dots, g(s_k))`.
      
      By its very definition, ğ—€ is a homomorphism.
      
      Finally, the uniqueness of ğ—€ follows from :ref:`Lemma 1.16 <ex_1-16-6-brief>`. 

.. _terms-and-free-algebras-in-lean:

Terms and free algebras in Lean [11]_
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

As a second demonstration of inductive types in Lean, we define a type representing the (infinite) collection :math:`ğ“(X)` of all terms of a given signature.

.. code-block:: lean

    import basic
    section
      parameters {Ïƒ : signature} (X :Type*) 
      local notation `F` := Ïƒ.F
      local notation `Ï` := Ïƒ.Ï 
    
      inductive term
      | var : X â†’ term
      | app (f : F) : (Ï f â†’ term) â†’ term
  
      def Term : algebra S := âŸ¨term, term.appâŸ©
    end

The set of terms along with the operations :math:`F^{ğ“} := \{\mathsf{app} f | f : F\}` forms an algebra :math:`ğ“(X) = âŸ¨T(X), F^{ğ“}âŸ©` in the signature :math:`Ïƒ = (F, Ï)`.

Suppose :math:`ğ€ = âŸ¨A, F^{ğ€}âŸ©` is an algebra in the same signature and :math:`h : X â†’ A` is an arbitrary function.  We will show that :math:`h : X â†’ A` has a unique *extension* (or *lift*) to a homomorphism from :math:`ğ“(X)` to ğ€.

Since ğ€ and :math:`h : X â†’ A` are arbitrary, this unique homomorphic lifting property holds universally; accordingly we say that the term algebra :math:`ğ“(X)` is *universal* for Ïƒ-algebras. Some authors say, ":math:`ğ“(X)` is *absolutely free* for Ïƒ-algebras," in this and only this case.

Before implementing the formal proof of this fact in Lean, let us first define some domain specific syntactic sugar.

.. code-block:: lean

    section
      open term
      parameters {Ïƒ : signature} (X :Type*) {A : algebra Ïƒ}
      definition F := Ïƒ.F         -- operation symbols
      definition Ï := Ïƒ.Ï         -- arity function
      definition ğ•‹ := @Term Ïƒ     -- term algebra over X
      definition ğ• := @var Ïƒ X    -- generators of the term algebra

If :math:`h : X â†’ A` is a function defined on the generators of the term algebra, then the *lift* (or *extension*) of :math:`h` to all of :math:`ğ•‹(X)` is defined inductively as follows:

.. code-block:: lean

    definition lift_of (h : X â†’ A) : ğ•‹(X) â†’ 
    | (var x) := h x
    | (app f a) := (A f) (Î» x, lift_of (a x))

To prove that the term algebra is universal for Ïƒ-algebras, we show that the lift of an arbitrary function :math:`h : X â†’ A` is a homomorphism and that this lift is unique.

.. code-block:: lean

      -- The lift is a homomorphism.
      lemma lift_is_hom (h : X â†’ A) : homomorphic (lift_of h) :=
      Î» f a, show lift_of h (app f a) = A f (lift_of h âˆ˜ a), from rfl
    
      -- The lift is unique.
      lemma lift_is_unique : âˆ€ {h h' : ğ•‹(X) â†’ A},
      homomorphic h â†’ homomorphic h' â†’ h âˆ˜ ğ• = h' âˆ˜ ğ• â†’ h = h' :=
      assume (h h' : ğ•‹(X) â†’ A) (hâ‚ : homomorphic h)
        (hâ‚‚ : homomorphic h')(hâ‚ƒ : h âˆ˜ ğ• = h' âˆ˜ ğ•),
        show h = h', from 
          have hâ‚€ : âˆ€ t : ğ•‹(X), h t = h' t, from 
            assume t : ğ•‹(X), 
            begin
              induction t with t f a ihâ‚ ,
              show h (ğ• t) = h' (ğ• t),
              { apply congr_fun hâ‚ƒ t },
    
              show h (app f a) = h' (app f a),
              { have ihâ‚‚  : h âˆ˜ a = h' âˆ˜ a, from funext ihâ‚,
                calc h (app f a) = A f (h âˆ˜ a) : hâ‚ f a
                             ... = A f (h' âˆ˜ a) : congr_arg (A f) ihâ‚‚ 
                             ... = h' (app f a) : (hâ‚‚ f a).symm }
            end,
          funext hâ‚€ 
    end

Let :math:`ğ€ = âŸ¨A, F^{ğ€}âŸ©` be a \sigma-algebra.

.. with congruence lattice $\Con\<A, \dots \>$.

.. index:: clone

Recall that a **clone** on a nonempty set :math:`A` is a set of operations on :math:`A` that contains the projection operations and is closed under general composition. 

Let :math:`A` denote the set of all clones on :math:`A`.

The **clone of term operations** of an Ïƒ-algebra ğ€, denoted by :math:`\mathrm{Clo} ğ€`, is the smallest clone on :math:`A` containing the basic operations of ğ€, that is,

.. math:: \mathrm{Clo} ğ€ = â‹‚ \{ U âˆˆ ğ–¢ A âˆ£ F^{ğ€} âŠ† U\}.

The set of :math:`n`-ary members of :math:`\mathrm{Clo} ğ€` is sometimes denoted by :math:`\mathrm{Clo}_n ğ€` (despite the fact that the latter is obviously not a clone).

We now state a theorem that shows how the clone of term operations of a signature can be defined inductively.

.. _thm-4-3:

.. proof:theorem::

   Let :math:`X` be a set and :math:`Ïƒ = (F, Ï)` a signature. Define

   .. math:: F_0 &= X;\\
         F_{n+1} &= F_n âˆª \{ f g âˆ£ f âˆˆ F, g : Ïf â†’ (F_n âˆ© (Ï g â†’ X)) \}, \quad n < Ï‰.

   Then :math:`\mathrm{Clo}^X(F) = â‹ƒ_n F_n`.

Thus *the clone of terms operations can be implemented (e.g., in Lean) as an inductive type*. The following theorem makes this precise. (See also :cite:`Bergman:2012`, Thm. 4.32.)

.. _thm-4-32:

.. proof:theorem::

   Let ğ€ and ğ be algebras of type :math:`Ï`.

   #. For every :math:`n`-ary term :math:`t âˆˆ T_Ï (X_Ï‰)` and homomorphism :math:`g : ğ€ â†’ ğ`,
      
      .. math:: g(t^{ğ€}(a_1,\dots, a_n)) = t^{ğ}(g(a_1),\dots, g(a_n)).

   #. For all :math:`t âˆˆ T_Ï (X_Ï‰)`, :math:`Î¸ âˆˆ \mathrm{Con} ğ€`, :math:`ğš : Ï t â†’ A` and :math:`ğ› : Ï t â†’ A`,
   
      .. math:: ğš \mathrel{Î¸} ğ› âŸ¹ t^{ğ€}(ğš) \mathrel{Î¸} t^{ğ€}(ğ›).

   #. For every subset :math:`Y âŠ† A`,

      .. math:: \mathrm{Sg}^{ğ€}(Y) = \{ t^{ğ€}(a_1, \dots, a_n) : t âˆˆ T(X_n), a_i âˆˆ Y, i â‰¤ n < Ï‰\}.

   .. container:: toggle
 
      .. container:: header
 
         *Proof*.
      
      The first statement is an easy induction on :math:`|t|`.

      The second statement follows from the first by taking :math:`ğ = ğ€/Î¸` and ğ—€ the canonical homomorphism.
  
      For the third statement, again by induction on the height of ğ—, every subalgebra must be closed under the action of :math:`t^{ğ€}`. 
  
      Thus the right-hand side is contained in the left. On the other hand, the right-hand side is clearly a subalgebra containing the elements of :math:`Y` (take :math:`t = x_1`) from which the reverse inclusion follows.

--------------------------------------------------------------

.. rubric:: Footnotes

.. [1]
   As we will see when implementing general operations in Lean, it is unnecessary to commit in advance to a specific arity type :math:`N`. An exception is the *quotient algebra type* since, unless we restrict ourselves to finitary operations, lifting a basic operation to a quotient requires some form of choice.

.. [2]
   Technically, this assumes we identify :math:`g` with its graph, which is fairly common practice. We will try to identify any situations in which the conflation of a function with its graph might cause problems.

.. [3] 
   The ``lean-ualib`` source code is available from `github.com/UniversalAlgebra/lean-ualib`_.

.. [4]   
   The  ``section`` command allows us to open a section throughout which our signature ``Ïƒ`` will be available; ``section`` ends when the keyword ``end`` appears.

.. [5]
   The only exception is that in type theory we make *typing judgments*, denoted by ``:``, rather than set membership judgments, denoted by ``âˆˆ``.

.. [6]
   plus whatever equational laws it may models; our handling of *theories* and *models* in Lean is beyond our current scope; for more information, see `github.com/UniversalAlgebra/lean-ualib`_.

.. [7]
   See https://github.com/UniversalAlgebra/lean-ualib/blob/master/src/subuniverse.lean

.. [8]
   Technically, ``â‹‚â‚€ S`` denotes ``sInter (S : set (set Î±)) : set Î± := {Î» s, a | âˆ€ t âˆˆ s, a âˆˆ t}`` Given a collection ``S : set (set Î±)`` of sets of type ``Î±``, ``â‹‚â‚€ S`` is the intersection of the sets in ``S``, as claimed.

.. [9]
   The **height** of a type is simply type's *level* (see Section ???) and the syntax :math:`Type*` indicates that we do not wish to commit in advance to a specific height.

.. [10]
   The construction of :math:`ğ“_Ï (X)` may seem to be making something out of nothing, but it plays a crucial role in the theory.

.. [11]
   https://github.com/UniversalAlgebra/lean-ualib/blob/master/src/free.lean

.. _Lean: https://leanprover.github.io/

.. _`github.com/UniversalAlgebra/lean-ualib`: https://github.com/UniversalAlgebra/lean-ualib/

.. The clone of *polynomials} of $\alg A$, denoted by $\Pol \alg A$, is the clone generated by the basic operations of $\alg A$ and the constant unary maps on $A$.

.. The set of  :math:`n`-ary members of $\Pol \alg A$ is sometimes denoted by $\Pol_n \alg A$. The smallest clone on a set $A$ is the set of all projections 

.. $\Proj A := \{\pi^n_i \mid 0\leq i < n < \omega\}$, defined as follows: for $0\leq i < n < \omega$, if $a \colon n \to A$, then $\pi^n_i a = a\, i$.
 
.. .. [9] Lean's built-in sigma type is defined as follows: :math:`structure sigma {Î± : Type u} (Î² : Î± â†’ Type v) := mk :: (fst : Î±) (snd : Î² fst)`
