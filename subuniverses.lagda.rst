.. FILE: subuniverses.agda
.. AUTHOR: William DeMeo and Siva Somayyajula
.. DATE: 20 Feb 2020
.. UPDATE: 17 Jun 2020

.. _types for subalgebras:

=====================
Types for Subalgebras
=====================

Preliminaries
------------------

We define subuniverses and subalgebras and prove some basic facts about them in a module called ``subuniverses`` which resides in the ``subuniverses.lagda.rst`` file of the ``agda-ualib`` library.

The file starts, as usual, with a list of imports.

::

  {-# OPTIONS --without-K --exact-split --safe #-}

  open import prelude
  open import basic using (Signature; Algebra; Op)
  open import relations using (transitive)
  open import homomorphisms using (HOM; Hom; hom; is-homomorphism; hom-image-alg; HomImage)

  open import terms
   using (Term; _Ì‡_; _Ì‚_; generator; node; comm-hom-term; comm-hom-term')

  open import Relation.Unary using (â‹‚)


.. _subuniverses in agda:

Subuniverses in Agda
------------------------

We begin the ``subuniverses`` module with a straightforward definition of the collection of subuniverses of an algebra A.  Since a subuniverse is a subset of the domain of A, it is defined as a predicate on âˆ£ A âˆ£.  Thus, the collection of subuniverses is a predicate on predicates on âˆ£ A âˆ£.

::

  module subuniverses {S : Signature ğ“ ğ“¥} where

  Subuniverses : (ğ‘¨ : Algebra ğ“¤ S)
   â†’             Pred (Pred âˆ£ ğ‘¨ âˆ£ ğ“£) (ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âŠ” ğ“£)

  Subuniverses ğ‘¨ B =
   (f : âˆ£ S âˆ£)(a : âˆ¥ S âˆ¥ f â†’ âˆ£ ğ‘¨ âˆ£) â†’ Im a âŠ† B â†’ (f Ì‚ ğ‘¨) a âˆˆ B


.. _obs 7 in agda:

Subuniverse generation
~~~~~~~~~~~~~~~~~~~~~~

Next we formalize the important theorem about subuniverse generation that we presented above in :numref:`Obs %s <obs 7>`.  Recall, if :math:`A = âŸ¨ğ´, â€¦âŸ©` is an ğ‘†-algebra, if :math:`âˆ… â‰  ğ´â‚€ âŠ† ğ´`, and if we define by recursion the sets :math:`A_{n+1} = Aâ‚™ âˆª \{ f a : f âˆˆ F, a : Ï f â†’ Aâ‚™ \}`, then the subuniverse of A generated by ğ´â‚€ is :math:`\mathrm{Sg}^A(Aâ‚€) = â‹ƒâ‚™ Aâ‚™`.

::

  record Subuniverse {A : Algebra ğ“¤ S} : ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âº Ì‡ where
   constructor mksub
   field
     sset  : Pred âˆ£ A âˆ£ ğ“¤
     isSub : sset âˆˆ Subuniverses A

  module _ {A : Algebra ğ“¤ S} where

   data Sg (X : Pred âˆ£ A âˆ£ ğ“£) : Pred âˆ£ A âˆ£ (ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âŠ” ğ“£) where
    var : âˆ€ {v} â†’ v âˆˆ X â†’ v âˆˆ Sg X
    app :  ( f : âˆ£ S âˆ£ ) { a : âˆ¥ S âˆ¥ f â†’ âˆ£ A âˆ£ }
     â†’       Im a âŠ† Sg X
            -----------------
     â†’       âˆ¥ A âˆ¥ f a âˆˆ Sg X

   sgIsSub : (X : Pred âˆ£ A âˆ£ ğ“¤) â†’ Sg X âˆˆ Subuniverses A
   sgIsSub _ f a Î± = app f Î±

   sgIsSmallest : {X : Pred âˆ£ A âˆ£ ğ“¡} {Y : Pred âˆ£ A âˆ£ ğ“¢}
    â†’             Y âˆˆ Subuniverses A
    â†’             X âŠ† Y
                 -----------------
    â†’              Sg X âŠ† Y

   -- By induction on x âˆˆ Sg X, show x âˆˆ Y
   sgIsSmallest _ XâŠ†Y (var vâˆˆX) = XâŠ†Y vâˆˆX

   sgIsSmallest {Y = Y} YIsSub XâŠ†Y (app f {a} imaâŠ†SgX) = appâˆˆY
    where
     -- First, show the args are in Y
     imaâŠ†Y : Im a âŠ† Y
     imaâŠ†Y i = sgIsSmallest YIsSub XâŠ†Y (imaâŠ†SgX i)

     --Since Y is a subuniverse of A, it contains the application
     appâˆˆY : âˆ¥ A âˆ¥ f a âˆˆ Y          --           of f to said args.
     appâˆˆY = YIsSub f a imaâŠ†Y

.. _obs 6 in agda:

Intersections of subalgebras are subalgebras
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

Recall from :numref:`Obs %s <obs 6>` that the intersection â‹‚áµ¢ ğ´áµ¢ of a collection {ğ´áµ¢ âˆ£ ğ´áµ¢ â‰¤ A} of subuniverses of an algebra A is again a subuniverse of A.  We formalize the statement and proof of this easy fact in Agda as follows.

::

  module _
   {A : Algebra ğ“¤ S} {I : ğ“˜ Ì‡}
   {ğ’œ : I â†’ Pred âˆ£ A âˆ£ ğ“£} where

   sub-inter-is-sub : ((i : I) â†’ ğ’œ i âˆˆ Subuniverses A)
    â†’                 â‹‚ I ğ’œ âˆˆ Subuniverses A

   sub-inter-is-sub Ai-is-Sub f a imaâŠ†â‹‚A = Î±
    where
     Î± : âˆ¥ A âˆ¥ f a âˆˆ â‹‚ I ğ’œ
     Î± i = Ai-is-Sub i f a Î» j â†’ imaâŠ†â‹‚A j i


.. _obs 12 in agda:

Subuniverse generation with terms
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

Recall :numref:`Obs %s <obs 12>` presenting subuniverse generation using the images of terms: If ğ‘Œ is a subset of ğ´, then :math:`\mathrm{Sg}^A(Y) = \{t^A a : t âˆˆ T(X), a : X â†’ Y\}`. To formalize this in Agda, we first prove that subuniverses are closed under the action of term operations.

::

  module _
   {X : ğ“¤ Ì‡} -- {X : ğ“ âŠ” ğ“¥ âŠ” ğ“¤ Ì‡}
   {ğ‘¨ ğ‘© : Algebra ğ“¤ S}
   {B : Pred âˆ£ ğ‘¨ âˆ£ ğ“¤}
   (Y : ğ“¤ Ì‡) where

   sub-term-closed : B âˆˆ Subuniverses ğ‘¨
    â†’                (t : Term)(b : X â†’ âˆ£ ğ‘¨ âˆ£)
    â†’                (âˆ€ i â†’ b i âˆˆ B)
                   ---------------------------
    â†’                ((t Ì‡ ğ‘¨) b) âˆˆ B

   sub-term-closed Bâ‰¤A (generator x) b bâˆˆB = bâˆˆB x

   sub-term-closed Bâ‰¤A (node f t) b bâˆˆB =
     Bâ‰¤A f (Î» z â†’ (t z Ì‡ ğ‘¨) b)
           (Î» x â†’ sub-term-closed Bâ‰¤A (t x) b bâˆˆB)

This proves :math:`\mathrm{Sg}^ğ‘¨(Y) âŠ‡ \{ t^ğ‘¨ a : t âˆˆ ğ‘‡(ğ‘‹), a : ğ‘‹ â†’ ğ‘Œ \}`.

Next we prove :math:`\mathrm{Sg}^ğ‘¨(Y) âŠ† \{ t^ğ‘¨ a : t âˆˆ ğ‘‡(ğ‘‹), a : ğ‘‹ â†’ ğ‘Œ \}` by the following steps:

  #. The image of ğ‘Œ under all terms, which we call `TermImage ğ‘Œ`, is a subuniverse of ğ‘¨; i.e.,
     TermImage ğ‘Œ = :math:`\{ t^ğ‘¨ a : t âˆˆ ğ‘‡(ğ‘‹), a : ğ‘‹ â†’ ğ‘Œ \} â‰¤ A`.
  #. ğ‘Œ âŠ† TermImage ğ‘Œ (obvious)
  #. :math:`\mathrm{Sg}^ğ‘¨(Y)` is the smallest subuniverse containing ğ‘Œ (see `sgIsSmallest`) so :math:`\mathrm{Sg}^ğ‘¨(Y)` âŠ† TermImage ğ‘Œ.

(The last item was already proved above; see ``sgIsSmallest``.)

::

   data TermImage (Y : Pred âˆ£ ğ‘¨ âˆ£ ğ“¤) : Pred âˆ£ ğ‘¨ âˆ£ (ğ“ âŠ” ğ“¥ âŠ” ğ“¤) where
    var : âˆ€ {y : âˆ£ ğ‘¨ âˆ£} â†’ y âˆˆ Y â†’ y âˆˆ TermImage Y
    app : (f : âˆ£ S âˆ£) (t : âˆ¥ S âˆ¥ f â†’ âˆ£ ğ‘¨ âˆ£)
     â†’    (Im t âŠ† TermImage Y)  -- âˆ€ i  â†’  t i âˆˆ TermImage Y
         -------------------------------
     â†’    (âˆ¥ ğ‘¨ âˆ¥ f t) âˆˆ TermImage Y

   --1. TermImage is a subuniverse
   TermImageIsSub : (Y : Pred âˆ£ ğ‘¨ âˆ£ ğ“¤)
    â†’               TermImage Y âˆˆ Subuniverses ğ‘¨

   TermImageIsSub Y = Î» f a x â†’ app f a x

   --2. Y âŠ† TermImageY
   YâŠ†TermImageY : (Y : Pred âˆ£ ğ‘¨ âˆ£ ğ“¤)
    â†’             Y âŠ† TermImage Y

   YâŠ†TermImageY Y {a} aâˆˆY = var aâˆˆY


Finally, we can prove the desired inclusion.

::

   SgYâŠ†TermImageY : (Y : Pred âˆ£ ğ‘¨ âˆ£ ğ“¤) â†’ Sg Y âŠ† TermImage Y
   SgYâŠ†TermImageY Y = sgIsSmallest (TermImageIsSub Y)
                                   (YâŠ†TermImageY Y)

**Exercise**. Prove the following by generalizing the relation â‰ƒ to predicates:

.. code-block::

  SgYâ‰ƒTermImageY : (Y : Pred âˆ£ A âˆ£ k) â†’ (TermImage Y) â‰ƒ (Sg Y)
  SgYâ‰ƒTermImageY {x} Y = ?

-----------------------------------------------------------------------------------


Subalgebras in Agda
---------------------

The next submodule is a generalization of MHE's implementation of subgroups. We consider the subalgebras of an single arbitrary(but fixed) algebra ğ‘¨.

Following MHE's analogous development for groups and their subgroups (cf. `Subgroup' <https://www.cs.bham.ac.uk/~mhe/HoTT-UF-in-Agda-Lecture-Notes/HoTT-UF-Agda.html#372215>`_ ) we define the type of subalgebras as follows.

::

  module _ {ğ‘¨ : Algebra ğ“¤ S} (UV : Univalence) where

   Subalgebra : ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âº Ì‡
   Subalgebra = Î£ ğ‘© ê‰ (Algebra ğ“¤ S) ,
                   Î£ h ê‰ (âˆ£ ğ‘© âˆ£ â†’ âˆ£ ğ‘¨ âˆ£) ,
                     is-embedding h Ã— is-homomorphism ğ‘© ğ‘¨ h


Next we present a module that generalizes `MHE's ambient module <https://www.cs.bham.ac.uk/~mhe/HoTT-UF-in-Agda-Lecture-Notes/HoTT-UF-Agda.html#subgroups-sip>`_ . It does for subuniverses what MHE does for subgroups.

Note that we introduce a new definition of the ``subuniverse`` type here.  In cotrast to our earlier definition of ``Subuniverses``, which uses a predicate on ``âˆ£ ğ‘¨ âˆ£`` to represent the underlying set of the subuniverse, here we use the type ``ğ“Ÿ âˆ£ ğ‘¨ âˆ£``, the powerset of the universe of ``ğ‘¨``.

::

   gfe : global-dfunext
   gfe = univalence-gives-global-dfunext UV

   op-closed : (âˆ£ ğ‘¨ âˆ£ â†’ ğ“¦ Ì‡) â†’ ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âŠ” ğ“¦ Ì‡
   op-closed B = (f : âˆ£ S âˆ£)(a : âˆ¥ S âˆ¥ f â†’ âˆ£ ğ‘¨ âˆ£)
    â†’ Im a âŠ† B â†’ B (âˆ¥ ğ‘¨ âˆ¥ f a)  --  â†’ ((i : âˆ¥ S âˆ¥ f) â†’ B (a i)) â†’ B (âˆ¥ ğ‘¨ âˆ¥ f a)

   subuniverse : ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âº Ì‡
   subuniverse = Î£ B ê‰ (ğ“Ÿ âˆ£ ğ‘¨ âˆ£) , op-closed ( _âˆˆâ‚€ B)

   being-op-closed-is-subsingleton : (B : ğ“Ÿ âˆ£ ğ‘¨ âˆ£)
    â†’           is-subsingleton (op-closed ( _âˆˆâ‚€ B ))
   being-op-closed-is-subsingleton B = Î -is-subsingleton gfe
    (Î» f â†’ Î -is-subsingleton gfe
     (Î» a â†’ Î -is-subsingleton gfe
      (Î» _ â†’ âˆˆ-is-subsingleton B (âˆ¥ ğ‘¨ âˆ¥ f a))))

   prâ‚-is-embedding : is-embedding âˆ£_âˆ£
   prâ‚-is-embedding = prâ‚-embedding being-op-closed-is-subsingleton

   --so equality of subalgebras is equality of their underlying
   --subsets in the powerset:
   ap-prâ‚ : (B C : subuniverse) â†’ B â‰¡ C â†’ âˆ£ B âˆ£ â‰¡ âˆ£ C âˆ£
   ap-prâ‚ B C = ap âˆ£_âˆ£

   ap-prâ‚-is-equiv : (B C : subuniverse) â†’ is-equiv (ap-prâ‚ B C)
   ap-prâ‚-is-equiv =
    embedding-gives-ap-is-equiv âˆ£_âˆ£ prâ‚-is-embedding

   subuniverse-is-a-set : is-set subuniverse
   subuniverse-is-a-set B C = equiv-to-subsingleton
                             (ap-prâ‚ B C , ap-prâ‚-is-equiv B C)
                             (powersets-are-sets' UV âˆ£ B âˆ£ âˆ£ C âˆ£)

For a subuniverse B of ğ‘¨, we want to form a subalgebra ğ‘© = âŸ¨B, â€¦âŸ© of ğ‘¨ such that the operations of ğ‘© are those of ğ‘¨ restricted to B.

Here are some useful lemmas extracted from MHE's proof of `subgroup-equality`. The first is called `f` in MHE's proof.

::

   subuniverse-equality-gives-membership-equiv : (B C : subuniverse)
    â†’                                  B â‰¡ C
                        -----------------------------------
    â†’                   ( x : âˆ£ ğ‘¨ âˆ£ ) â†’ (x âˆˆâ‚€ âˆ£ B âˆ£) â‡” (x âˆˆâ‚€ âˆ£ C âˆ£)
   subuniverse-equality-gives-membership-equiv B C Bâ‰¡C x =
    transport (Î» - â†’ x âˆˆâ‚€ âˆ£ - âˆ£) Bâ‰¡C ,
     transport (Î» - â†’ x âˆˆâ‚€ âˆ£ - âˆ£ ) ( Bâ‰¡C â»Â¹ )

The next lemma is called `h` in MHE's proof.

::

   membership-equiv-gives-carrier-equality : (B C : subuniverse)
    â†’          ((x : âˆ£ ğ‘¨ âˆ£) â†’  x âˆˆâ‚€ âˆ£ B âˆ£  â‡”  x âˆˆâ‚€ âˆ£ C âˆ£)
              -----------------------------------------
    â†’                       âˆ£ B âˆ£ â‰¡ âˆ£ C âˆ£
   membership-equiv-gives-carrier-equality B C Ï† =
    subset-extensionality' UV Î± Î²
     where
      Î± :  âˆ£ B âˆ£ âŠ†â‚€ âˆ£ C âˆ£
      Î± x = lr-implication (Ï† x)

      Î² : âˆ£ C âˆ£ âŠ†â‚€ âˆ£ B âˆ£
      Î² x = rl-implication (Ï† x)

The next lemma is called `g` in MHE's proof.

::

   membership-equiv-gives-subuniverse-equality : (B C : subuniverse)
    â†’            (( x : âˆ£ ğ‘¨ âˆ£ ) â†’ x âˆˆâ‚€ âˆ£ B âˆ£ â‡” x âˆˆâ‚€ âˆ£ C âˆ£)
                 ---------------------------------------
    â†’                          B â‰¡ C
   membership-equiv-gives-subuniverse-equality B C =
    inverse (ap-prâ‚ B C)
    (ap-prâ‚-is-equiv B C)
       âˆ˜ (membership-equiv-gives-carrier-equality B C)

   membership-equiv-is-subsingleton : (B C : subuniverse)
    â†’    is-subsingleton (( x : âˆ£ ğ‘¨ âˆ£) â†’ x âˆˆâ‚€ âˆ£ B âˆ£ â‡” x âˆˆâ‚€ âˆ£ C âˆ£)
   membership-equiv-is-subsingleton B C =
    Î -is-subsingleton gfe
     (Î» x â†’ Ã—-is-subsingleton
      (Î -is-subsingleton gfe (Î» _ â†’ âˆˆ-is-subsingleton âˆ£ C âˆ£ x ))
        (Î -is-subsingleton gfe (Î» _ â†’ âˆˆ-is-subsingleton âˆ£ B âˆ£ x )))

So, two subuniverses are equal if and only if they have the same elements (cf. `subgroup-equality <https://www.cs.bham.ac.uk/~mhe/HoTT-UF-in-Agda-Lecture-Notes/HoTT-UF-Agda.html#371022>`_ ):

::

   subuniverse-equality : (B C : subuniverse)
    â†’    (B â‰¡ C)  â‰ƒ  ((x : âˆ£ ğ‘¨ âˆ£)  â†’ (x âˆˆâ‚€ âˆ£ B âˆ£) â‡” (x âˆˆâ‚€ âˆ£ C âˆ£))

   subuniverse-equality B C =
    logically-equivalent-subsingletons-are-equivalent _ _
      (subuniverse-is-a-set B C)
       (membership-equiv-is-subsingleton B C)
        (subuniverse-equality-gives-membership-equiv B C ,
          membership-equiv-gives-subuniverse-equality B C)

The converse of `membership-equiv-gives-carrier-equality` is obvious.

::

   carrier-equality-gives-membership-equiv : (B C : subuniverse)
    â†’                            âˆ£ B âˆ£ â‰¡ âˆ£ C âˆ£
                  ----------------------------------------
    â†’              (( x : âˆ£ ğ‘¨ âˆ£ ) â†’ x âˆˆâ‚€ âˆ£ B âˆ£ â‡” x âˆˆâ‚€ âˆ£ C âˆ£)
   carrier-equality-gives-membership-equiv B C (refl _) x = id , id

   --so we have...
   carrier-equiv : (B C : subuniverse)
    â†’   ((x : âˆ£ ğ‘¨ âˆ£) â†’ x âˆˆâ‚€ âˆ£ B âˆ£ â‡” x âˆˆâ‚€ âˆ£ C âˆ£) â‰ƒ (âˆ£ B âˆ£ â‰¡ âˆ£ C âˆ£)
   carrier-equiv B C =
    logically-equivalent-subsingletons-are-equivalent _ _
     (membership-equiv-is-subsingleton B C)
      (powersets-are-sets' UV âˆ£ B âˆ£ âˆ£ C âˆ£)
       (membership-equiv-gives-carrier-equality B C ,
         carrier-equality-gives-membership-equiv B C)

   -- ...which yields an alternative subuniverse equality lemma.
   subuniverse-equality' : (B C : subuniverse)
    â†’                      (B â‰¡ C) â‰ƒ (âˆ£ B âˆ£ â‰¡ âˆ£ C âˆ£)
   subuniverse-equality' B C =
    (subuniverse-equality B C) â— (carrier-equiv B C)


Identities in subalgebras
-----------------------------

Let S(ğ’¦) denote the class of algebras isomorphic to a subalgebra of a member of ğ’¦.With our new formal definition of Subalgebra, we will show that every term equation, ``p â‰ˆ q``, that is satisfied by all ``A âˆˆ ğ’¦`` is also satisfied by all ``B âˆˆ S(ğ’¦)``. In other words, the collection of identities modeled by a given class of algebras is also modeled by all of the subalgebras of that class.

We first set down some notation for the modeling of identities. The standard notation is ``A âŠ§ p â‰ˆ q``, which means that the identity ``p â‰ˆ q`` is satisfied in A. In otherwords, for all assignments ``a : X â†’ âˆ£ A âˆ£`` of values to variables, we have ``(p Ì‡ A) a â‰¡ (q Ì‡ A) a``.

If ğ’¦ is a class of structures, it is standard to write ``ğ’¦ âŠ§ p â‰ˆ q`` just in case all structures in the class ğ’¦ model the identity p â‰ˆ q.  However, because a class of structures has a different type than a single structure, we will need different notation, so we have settled on writing ``ğ’¦ âŠ§ p â‰‹ q`` to denote this concept.

**Unicode Hint**. In Agda type ``\models`` to produce âŠ§, type ``\~~`` to produce â‰ˆ, and type ``\~~~`` to produce â‰‹.

::

  module _
   {ğ“¤ : Universe}
   {X : ğ“¤ Ì‡ }
   {UV : Univalence} where

   _âŠ§_â‰ˆ_ : Algebra ğ“¤ S
    â†’      Term{X = X} â†’ Term â†’ ğ“¤ Ì‡

   ğ‘¨ âŠ§ p â‰ˆ q = (p Ì‡ ğ‘¨) â‰¡ (q Ì‡ ğ‘¨)

   _âŠ§_â‰‹_ : Pred (Algebra ğ“¤ S) ğ“¦
    â†’      Term{X = X} â†’ Term â†’ ğ“ âŠ” ğ“¥ âŠ” ğ“¦ âŠ” ğ“¤ âº Ì‡

   _âŠ§_â‰‹_ ğ’¦ p q = {ğ‘¨ : Algebra _ S} â†’ ğ’¦ ğ‘¨ â†’ ğ‘¨ âŠ§ p â‰ˆ q

   gdfe : global-dfunext
   gdfe = univalence-gives-global-dfunext UV

   SubalgebrasOfClass : Pred (Algebra ğ“¤ S)(ğ“¤ âº) â†’ ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âº Ì‡
   SubalgebrasOfClass ğ’¦ =
    Î£ ğ‘¨ ê‰ (Algebra _ S) , (ğ‘¨ âˆˆ ğ’¦) Ã— Subalgebra {ğ‘¨ = ğ‘¨} UV

   subalgebras-preserve-identities : (ğ’¦ : Pred (Algebra ğ“¤ S) ( ğ“¤ âº ))(p q : Term{X = X})
    â†’  (ğ’¦ âŠ§ p â‰‹ q) â†’ (SAK : SubalgebrasOfClass ğ’¦)
    â†’  (prâ‚ âˆ¥ (prâ‚‚ SAK) âˆ¥) âŠ§ p â‰ˆ q
   subalgebras-preserve-identities ğ’¦ p q ğ’¦âŠ§pâ‰‹q SAK = Î³
    where

    A : Algebra ğ“¤ S
    A = âˆ£ SAK âˆ£

    Aâˆˆğ’¦ : A âˆˆ ğ’¦
    Aâˆˆğ’¦ = âˆ£ prâ‚‚ SAK âˆ£

    AâŠ§pâ‰ˆq : A âŠ§ p â‰ˆ q
    AâŠ§pâ‰ˆq = ğ’¦âŠ§pâ‰‹q Aâˆˆğ’¦

    subalg : Subalgebra {ğ‘¨ = A} UV
    subalg = âˆ¥ prâ‚‚ SAK âˆ¥

    B : Algebra ğ“¤ S
    B = prâ‚ subalg

    h : âˆ£ B âˆ£ â†’ âˆ£ A âˆ£
    h = âˆ£ prâ‚‚ subalg âˆ£

    hem : is-embedding h
    hem = prâ‚ âˆ¥ prâ‚‚ subalg âˆ¥

    hhm : is-homomorphism B A h
    hhm = prâ‚‚ âˆ¥ prâ‚‚ subalg âˆ¥

    Î¾ : (b : X â†’ âˆ£ B âˆ£ ) â†’ h ((p Ì‡ B) b) â‰¡ h ((q Ì‡ B) b)
    Î¾ b =
     h ((p Ì‡ B) b)  â‰¡âŸ¨ comm-hom-term' gdfe B A (h , hhm) p b âŸ©
     (p Ì‡ A)(h âˆ˜ b) â‰¡âŸ¨ intensionality AâŠ§pâ‰ˆq (h âˆ˜ b) âŸ©
     (q Ì‡ A)(h âˆ˜ b) â‰¡âŸ¨ (comm-hom-term' gdfe B A (h , hhm) q b)â»Â¹ âŸ©
     h ((q Ì‡ B) b)  âˆ

    hlc : {b b' : domain h} â†’ h b â‰¡ h b' â†’ b â‰¡ b'
    hlc hbâ‰¡hb' = (embeddings-are-lc h hem) hbâ‰¡hb'

    Î³ : B âŠ§ p â‰ˆ q
    Î³ = gdfe Î» b â†’ hlc (Î¾ b)

----------------------------------------------------------------------------

.. _hom images in agda:

.. _obs 7.1 in agda:

Homomorphic images in Agda
--------------------------

In this section we show that the image of an (extensional) homomorphism is a subuniverse.  (A version for intensional homs appears below, but the proof is essentially the same.) 

We are about ready to formalize the easy fact that a homomorphic image is a subuniverse, but before doing so, let us go through the steps of the proof informally.  Let ğ‘“ be an operation symbol, let :math:`b : Ï f â†’ âˆ£ B âˆ£` be a (Ï ğ‘“)-tuple of elements of âˆ£ ğ‘© âˆ£, and assume âˆ€ ğ‘–, ğ‘(ğ‘–) âˆˆ Image â„.  We must show :math:`f^ğ‘© b âˆˆ Image h`.  The assumption âˆ€ ğ‘–,  ğ‘(ğ‘–) âˆˆ Image â„ implies that there is a (Ï ğ‘“)-tuple :math:`ğ‘ : Ï f â†’ âˆ£ ğ‘¨ âˆ£`  such that â„ âˆ˜ ğ‘ = ğ‘.  Since â„ is a homomorphism, we have :math:`f^ğ‘© ğ‘  = f^ğ‘© (â„ âˆ˜ ğ‘) = â„ (f^ğ‘¨ ğ‘) âˆˆ` Image â„.

We formalize the proof in Agda as follows.

::

  module _ {A B : Algebra ğ“¤ S} (h : hom A B)  where

   hom-image-is-sub : {funext ğ“¥ ğ“¤} â†’ HomImage{A = A}{B = B} h âˆˆ Subuniverses B
   hom-image-is-sub {fe} f b bâˆˆImf =
    eq (âˆ¥ B âˆ¥ f (Î» x â†’ b x)) ( âˆ¥ A âˆ¥ f ar) Î³
     where
      ar : âˆ¥ S âˆ¥ f â†’ âˆ£ A âˆ£
      ar = Î» x â†’ Inv âˆ£ h âˆ£ (b x) (bâˆˆImf x)

      Î¶ : (Î» x â†’ âˆ£ h âˆ£ (ar x)) â‰¡ (Î» x â†’ b x)
      Î¶ = fe (Î» x â†’ InvIsInv âˆ£ h âˆ£ (b x) (bâˆˆImf x))

      Î³ : âˆ¥ B âˆ¥ f (Î» x â†’ b x)
          â‰¡ âˆ£ h âˆ£ (âˆ¥ A âˆ¥ f (Î» x â†’ Inv âˆ£ h âˆ£ (b x)(bâˆˆImf x)))
      Î³ = âˆ¥ B âˆ¥ f (Î» x â†’ b x)  â‰¡âŸ¨ ap ( âˆ¥ B âˆ¥ f ) (Î¶ â»Â¹) âŸ©
          (âˆ¥ B âˆ¥ f)(âˆ£ h âˆ£ âˆ˜ ar) â‰¡âŸ¨ ( âˆ¥ h âˆ¥ f ar ) â»Â¹ âŸ©
          âˆ£ h âˆ£ (âˆ¥ A âˆ¥ f ar)    âˆ

The intensional-hom-image module
---------------------------------

The image of an intensional HOM is a subuniverse. (N.B. the proof still requires function extensionality. Question: Is it necessary?)

::

  -- HOM image is subuniverse
  module intensional-hom-image
   {A B : Algebra ğ“¤ S} (h : HOM A B)  where

   HOMImage : âˆ£ B âˆ£ â†’ ğ“¤ Ì‡
   HOMImage = Î» b â†’ Image âˆ£ h âˆ£ âˆ‹ b

   HOM-image : ğ“¤ Ì‡
   HOM-image = Î£ (Image_âˆ‹_ âˆ£ h âˆ£)

   fres' : âˆ£ A âˆ£ â†’ Î£ (Image_âˆ‹_ âˆ£ h âˆ£)
   fres' a = âˆ£ h âˆ£ a , im a

   HOM-image-alg : Algebra ğ“¤ S
   HOM-image-alg = HOM-image , ops-interp
    where
     a : {f : âˆ£ S âˆ£} (x : âˆ¥ S âˆ¥ f â†’ HOM-image) (y : âˆ¥ S âˆ¥ f)
      â†’  âˆ£ A âˆ£
     a x y = Inv âˆ£ h âˆ£  âˆ£ x y âˆ£ âˆ¥ x y âˆ¥

     ops-interp : ( f : âˆ£ S âˆ£ ) â†’ Op (âˆ¥ S âˆ¥ f) HOM-image
     ops-interp = Î» f x â†’(âˆ£ h âˆ£ (âˆ¥ A âˆ¥ f (a x)) , im (âˆ¥ A âˆ¥ f (a x)))

   HOM-image-is-sub : funext ğ“¥ ğ“¤ â†’ HOMImage âˆˆ Subuniverses B
   HOM-image-is-sub fe f b bâˆˆImh = eq (âˆ¥ B âˆ¥ f b) (âˆ¥ A âˆ¥ f ar) Î³
    where
     ar : âˆ¥ S âˆ¥ f â†’ âˆ£ A âˆ£
     ar = Î» x â†’ Inv âˆ£ h âˆ£ (b x) (bâˆˆImh x)

     Î¶ : (Î» x â†’ âˆ£ h âˆ£ (ar x)) â‰¡ (Î» x â†’ b x)
     Î¶ = fe (Î» x â†’ InvIsInv âˆ£ h âˆ£ (b x) (bâˆˆImh x) )

     Î³ : âˆ¥ B âˆ¥ f (Î» x â†’ b x)
          â‰¡ âˆ£ h âˆ£ (âˆ¥ A âˆ¥ f (Î» x â†’ Inv âˆ£ h âˆ£ (b x) (bâˆˆImh x)))
     Î³ =   âˆ¥ B âˆ¥ f (Î» x â†’ b x)      â‰¡âŸ¨ ap ( âˆ¥ B âˆ¥ f ) Î¶ â»Â¹ âŸ©
           ( âˆ¥ B âˆ¥ f ) ( âˆ£ h âˆ£ âˆ˜ ar ) â‰¡âŸ¨ intensionality Î¾ ar âŸ©
            âˆ£ h âˆ£ ( âˆ¥ A âˆ¥ f ar )      âˆ
      where
       Ï„ : (Î» f ar â†’ (âˆ¥ B âˆ¥ f)(âˆ£ h âˆ£ âˆ˜ ar))
            â‰¡ (Î» f ar â†’ âˆ£ h âˆ£ (âˆ¥ A âˆ¥ f ar ))
       Ï„ = (âˆ¥ h âˆ¥)â»Â¹
       Î¾ : (Î» (ar : âˆ¥ S âˆ¥ f â†’ âˆ£ A âˆ£) â†’ (âˆ¥ B âˆ¥ f)(âˆ£ h âˆ£ âˆ˜ ar))
            â‰¡ (Î» (ar : âˆ¥ S âˆ¥ f â†’ âˆ£ A âˆ£) â†’ âˆ£ h âˆ£ (âˆ¥ A âˆ¥ f ar))
       Î¾ = dep-intensionality Ï„ f

   finv' : {X : ğ“¤ Ì‡ } (b : X â†’ âˆ£ HOM-image-alg âˆ£) (x : X) â†’ âˆ£ A âˆ£
   finv' = Î» b x â†’ Inv âˆ£ h âˆ£ âˆ£ b x âˆ£ âˆ¥ b x âˆ¥


-------------------------

.. include:: hyperlink_references.rst



