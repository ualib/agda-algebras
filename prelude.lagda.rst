.. FILE: prelude.lagda.rst
.. AUTHOR: William DeMeo and Siva Somayyajula
.. DATE: 21 Apr 2020
.. UPDATE: 8 Jul 2020
.. REF: Some parts of this file are based on the HoTT/UF course notes by Martin HÃ¶tzel Escardo (MHE).
.. SEE: https://www.cs.bham.ac.uk/~mhe/HoTT-UF-in-Agda-Lecture-Notes/ 
.. Throughout, MHE = Martin HÃ¶tzel Escardo.

.. _agda preliminaries:

========================
Agda Preliminaries
========================

**Notation**. Some acronyms we use frequently in this chapter are these.

  * :term:`MHE` = `Martin HÃ¶tzel Escardo <https://www.cs.bham.ac.uk/~mhe/>`_
  * :term:`MLTT` = `Martin-LÃ¶f Type Theory <https://ncatlab.org/nlab/show/Martin-L%C3%B6f+dependent+type+theory>`_

----------------------------------------------------

Options and imports
--------------------

All but the most trivial Agda programs begin by setting some options that effect how Agda behaves and importing from existing libraries (e.g., the `Agda Standard Library`_ or, in our case, :term:`MHE`'s `Type Topology`_ library). In particular, logical axioms and deduction rules can be specified according to what one wishes to assume.

For example, here's the start of the first Agda source file in our library, which we call ``prelude.lagda.rst``.

::

   {-# OPTIONS --without-K --exact-split --safe #-}

This specifies Agda ``OPTIONS`` that we will use throughout the library.

  * ``without-K`` disables `Streicher's K axiom <https://ncatlab.org/nlab/show/axiom+K+%28type+theory%29>`_ ; see also the `section on axiom K <https://agda.readthedocs.io/en/v2.6.1/language/without-k.html>`_ in the `Agda Language Reference`_ manual.

  * ``exact-split`` makes Agda accept only those definitions that behave like so-called *judgmental* or *definitional* equalities.  :term:`MHE` explains this by saying it "makes sure that pattern matching corresponds to Martin-LÃ¶f eliminators;" see also the `Pattern matching and equality section <https://agda.readthedocs.io/en/v2.6.1/tools/command-line-options.html#pattern-matching-and-equality>`_ of the `Agda Tools`_ documentation.

  * ``safe`` ensures that nothing is postulated outright---every non-:term:`MLTT` axiom has to be an explicit assumption (e.g., an argument to a function or module); see also `this section <https://agda.readthedocs.io/en/v2.6.1/tools/command-line-options.html#cmdoption-safe>`_ of the `Agda Tools`_ documentation and the `Safe Agda section <https://agda.readthedocs.io/en/v2.6.1/language/safe-agda.html#safe-agda>`_ of the `Agda Language Reference`_.

Universes
~~~~~~~~~~

We begin the first module of `agda-ualib`_, called ``prelude``, using the Agda directive ``module prelude where``.  We then immediately import the ``Universes`` module from :term:`MHE`'s `Type Topology`_ library.

::

   module prelude where

    open import Universes public

This ``Universes`` module provides, among other things, an elegant notation for type universes that we have fully adopted and we use MHE's notation throughout the agda-ualib_.

:term:`MHE` has authored an outstanding set of notes on `HoTT-UF-in-Agda`_ called `Introduction to Univalent Foundations of Mathematics with Agda`_ . We highly recommend these notes to anyone wanting more details than we provide here about :term:`MLTT` and the Univalent Foundations/HoTT extensions thereof.

Following :term:`MHE`, we refer to universes using capitalized script letters ğ“¤,ğ“¥,ğ“¦,ğ“£.  We add a few more to Martin's list.

::

    variable
      ğ“˜ ğ“™ ğ“š ğ“› ğ“œ ğ“ ğ“ ğ“  ğ“¡ ğ“¢ ğ“§ : Universe

In the ``Universes`` module, :term:`MHE` defines the Ì‡ operator which maps a universe ğ“¤ (i.e., a level) to ``Set ğ“¤``, and the latter has type ``Set (lsuc ğ“¤)``.

The level ``lzero`` is renamed ğ“¤â‚€, so ğ“¤â‚€ Ì‡  is an alias for ``Set lzero`` (which, incidentally, corresponds to ``Sort 0`` in Lean_).

Although it is nice and short, we won't show all of the ``Universes`` module here.  Instead, we highlight the few lines of code from :term:`MHE`'s ``Universes.lagda`` file that makes available the notational devices that we just described and will adopt throughout the `agda-ualib`_.

.. proof:agda:: Universes.lagda excerpt

   .. code-block:: agda

      open import Agda.Primitive public
        using (_âŠ”_)
        renaming (lzero  to  ğ“¤â‚€
                ; lsuc   to  _âº
                ; Level  to  Universe
                ; SetÏ‰   to  ğ“¤Ï‰
                )

      _Ì‡ : (ğ“¤ : Universe) â†’ _
      ğ“¤ Ì‡ = Set ğ“¤

      ğ“¤â‚ = ğ“¤â‚€ âº
      ğ“¤â‚‚ = ğ“¤â‚ âº

      _âºâº : Universe â†’ Universe
      ğ“¤ âºâº = ğ“¤ âº âº


Thus, ğ“¤ Ì‡ is simply an alias for ``Set ğ“¤``, and we have ``Set ğ“¤ : Set (lsuc ğ“¤)``.

Finally, ``Set (lsuc lzero)`` is denoted by ``Set ğ“¤â‚€ âº`` which (:term:`MHE` and) we denote by ``ğ“¤â‚€ âº Ì‡``.

The following dictionary translates between standard Agda syntax and :term:`MHE`/agda-ualib_ notation.

.. code-block:: agda

   Agda              MHE/agda-ualib
   ====              ==============
   Level             Universe
   lzero             ğ“¤â‚€
   ğ“¤ : Level         ğ“¤ : Universe
   Set lzero         ğ“¤â‚€ Ì‡
   Set ğ“¤             ğ“¤ Ì‡
   lsuc lzero        ğ“¤â‚€ âº
   lsuc ğ“¤            ğ“¤ âº
   Set (lsuc lzero)  ğ“¤â‚€ âº Ì‡
   Set (lsuc ğ“¤)      ğ“¤ âº Ì‡
   SetÏ‰              ğ“¤Ï‰

.. +----------------------+--------------------------+-----------------------------+
   | Agda                 |  :term:`MHE` Notation    | Lean analog                 |
   +======================+==========================+=============================+
   | ``Level``            | ``Universe``             | ``universe``                |
   |  ``lzero``           | ``ğ“¤â‚€``                   | ``0 : universe``            |
   | ``Set lzero``        | ``ğ“¤â‚€ Ì‡`` ( = ``Type ğ“¤â‚€``) | ``Sort 0``                  |
   |  ``lsuc lzero``      | ``ğ“¤â‚€ âº``                 | ``1 : universe``            |
   | ``Set (lsuc lzero)`` | ``ğ“¤â‚€ âº Ì‡``                | ``Sort 1 = Type = Type 0``  |
   +----------------------+--------------------------+-----------------------------+

To justify the introduction of this somewhat nonstandard notation for universe levels, :term:`MHE` points out that the Agda library uses ``Level`` for universes (so what we write as ğ“¤ Ì‡ is written ``Set ğ“¤`` in standard Agda), but in univalent mathematics the types in ğ“¤ Ì‡ need not be sets, so the standard Agda notation can be misleading. Furthermore, the standard notation places emphasis on levels rather than universes themselves.

There will be many occasions calling for a type living in the universe that is the least upper bound of two universes, say, ğ“¤ Ì‡ and ğ“¥ Ì‡ . The universe ğ“¤ âŠ” ğ“¥ Ì‡ denotes this least upper bound.  Here ğ“¤ âŠ” ğ“¥ is used to denote the universe level corresponding to the least upper bound of the levels ğ“¤ and ğ“¥, where the ``_âŠ”_`` is an Agda primitive designed for precisely this purpose.


Public imports
~~~~~~~~~~~~~~~

Next we import other parts of :term:`MHE`'s `Type Topology`_ library, using the Agda directive ``public``, which means these imports will be available wherever the ``prelude`` module in imported.  We describe some of these imports later, when making use of them, but we don't describe each one in detail. (The interested or confused reader should consult `HoTT-UF-in-Agda`_ to learn more.)

::

    open import Identity-Type renaming (_â‰¡_ to infix 0 _â‰¡_ ;
     refl to ğ“»â„¯ğ“¯ğ“µ) public

    pattern refl x = ğ“‡â„¯ğ’»ğ“ {x = x}

    open import Sigma-Type renaming (_,_ to infixr 50 _,_) public

    open import MGS-MLTT using (_âˆ˜_; domain; codomain; transport;
     _â‰¡âŸ¨_âŸ©_; _âˆ; prâ‚; prâ‚‚; -Î£; ğ•; Î ; Â¬; _Ã—_; ğ‘–ğ‘‘; _âˆ¼_; _+_; ğŸ˜; ğŸ™; ğŸš;
     _â‡”_; lr-implication; rl-implication; id; _â»Â¹; ap) public

    open import MGS-Equivalences using (is-equiv; inverse;
     invertible) public

    open import MGS-Subsingleton-Theorems using (funext;
     dfunext; is-singleton; is-subsingleton; is-prop; Univalence;
     global-dfunext; univalence-gives-global-dfunext; _â—_; _â‰ƒ_;
     logically-equivalent-subsingletons-are-equivalent;
     Î -is-subsingleton) public

    open import MGS-Powerset renaming (_âˆˆ_ to _âˆˆâ‚€_; _âŠ†_ to _âŠ†â‚€_)
     using (ğ“Ÿ; âˆˆ-is-subsingleton; equiv-to-subsingleton;
     powersets-are-sets'; subset-extensionality') public

    open import MGS-Embeddings using (is-embedding; prâ‚-embedding;
     is-set; _â†ª_; embedding-gives-ap-is-equiv; embeddings-are-lc;
     Ã—-is-subsingleton) public

    open import MGS-Solved-Exercises using (to-subtype-â‰¡) public


.. We don't have the space (or patience!) to describe each of the imports appearing in ``Preliminaries.agda``. Some of them will come up for discussion in due course. Until then, we refer the reader to the above mentioned documentation, as well as the brief :ref:`axiomk` in the appendix; the latter explains the ``--without-K`` option.

.. The full ``prelude.lagda.rst`` file, which defines other notation and objects we will use throughout the library, appears in the appendix :ref:`preliminaries.agda`. We will describe each of the objects defined therein as they come up in later sections.

----------------------------------------------

Dependent pair type
--------------------

Our preferred notations for the first and second projections of a product are ``âˆ£_âˆ£`` and ``âˆ¥_âˆ¥``, respectively; however, we will sometimes use the more standard ``prâ‚`` and ``prâ‚‚`` for compatibility with other libraries and sometimes for readability.

:Unicode Hints: In agda2-mode_ type ``\|`` and ``\||`` to produce ``âˆ£`` and ``âˆ¥``; type ``pr\_1`` and ``pr\_2`` to produce ``prâ‚`` and ``prâ‚‚``.

::

    âˆ£_âˆ£ : {X : ğ“¤ Ì‡ }{Y : X â†’ ğ“¥ Ì‡} â†’ Î£ Y â†’ X
    âˆ£ x , y âˆ£ = x

    âˆ¥_âˆ¥ : {X : ğ“¤ Ì‡ }{Y : X â†’ ğ“¥ Ì‡ } â†’ (z : Î£ Y) â†’ Y (prâ‚ z)
    âˆ¥ x , y âˆ¥ = y

For the :term:`dependent pair type`, we prefer the notation ``Î£ x ê‰ X , y``, which is more pleasing (and more standard in the literature) than Agda's default syntax (``Î£ Î»(x ê‰ X) â†’ y``), and :term:`MHE` has a useful trick that makes the preferred notation available by making index type explicit.

.. code-block:: agda

    infixr -1 -Î£
    -Î£ : {ğ“¤ ğ“¥ : Universe} (X : ğ“¤ Ì‡ ) (Y : X â†’ ğ“¥ Ì‡ ) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
    -Î£ X Y = Î£ Y
    syntax -Î£ X (Î» x â†’ y) = Î£ x ê‰ X , y -- type `ê‰` as `\:4`

.. warning:: The symbol ê‰ is not the same as : despite how similar they may appear. The correct colon in the expression ``Î£ x ê‰ X , y`` above is obtained by typing ``\:4`` in agda2-mode_.

:term:`MHE` explains Sigma induction as follows: "To prove that ``A z`` holds for all ``z : Î£ Y``, for a given property ``A``, we just prove that we have ``A (x , y)`` for all ``x : X`` and ``y : Y x``.  This is called ``Î£`` induction or ``Î£`` elimination (or ``uncurry``).

.. code-block:: agda

    Î£-induction : {X : ğ“¤ Ì‡ }{Y : X â†’ ğ“¥ Ì‡ }{A : Î£ Y â†’ ğ“¦ Ì‡ }
     â†’            ((x : X)(y : Y x) â†’ A (x , y))
                  -------------------------------
     â†’            ((x , y) : Î£ Y) â†’ A (x , y)
    Î£-induction g (x , y) = g x y

    curry : {X : ğ“¤ Ì‡ }{Y : X â†’ ğ“¥ Ì‡ }{A : Î£ Y â†’ ğ“¦ Ì‡ }
     â†’      (((x , y) : Î£ Y ) â†’ A (x , y))
           ---------------------------------
     â†’      ((x : X) (y : Y x) â†’ A (x , y))
    curry f x y = f (x , y)

The special case in which the type ``Y`` doesn't depend on ``X`` is of course the usual Cartesian product.

.. code-block:: agda

    infixr 30 _Ã—_
    _Ã—_ : ğ“¤ Ì‡ â†’ ğ“¥ Ì‡ â†’ ğ“¤ âŠ” ğ“¥ Ì‡
    X Ã— Y = Î£ x ê‰ X , Y

----------------------------------------

Dependent function type
---------------------------

To make the syntax for ``Î `` conform to the standard notation for "Pi types" (or :term:`dependent function type`), :term:`MHE` uses the same trick as the one used above for "Sigma types."

.. code-block:: agda

    Î  : {X : ğ“¤ Ì‡ } (A : X â†’ ğ“¥ Ì‡ ) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
    Î  {ğ“¤} {ğ“¥} {X} A = (x : X) â†’ A x

    -Î  : {ğ“¤ ğ“¥ : Universe}(X : ğ“¤ Ì‡ )(Y : X â†’ ğ“¥ Ì‡ ) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
    -Î  X Y = Î  Y
    infixr -1 -Î 
    syntax -Î  A (Î» x â†’ b) = Î  x ê‰ A , b

..
   --              F
   --         s ------â†’ Fs
   --         âˆ¥          âˆ¥
   -- refl s  âˆ¥          âˆ¥ transport
   --         â‡“         â‡“
   --         t ------â†’ Ft
   --              F

.. The following is useful when we want to recover implicit arguments without mentioning them.
       lhs : {X : ğ“¤ Ì‡ } {x y : X} â†’ x â‰¡ y â†’ X
       lhs {ğ“¤}{X}{x}{y} p = x

       rhs : {X : ğ“¤ Ì‡ } {x y : X} â†’ x â‰¡ y â†’ X
       rhs {ğ“¤}{X}{x}{y} p = y

.. "Composition of identifications. Given two identifications ``p : x â‰¡ y`` and ``q : y â‰¡ z``, we can compose them to get an identification ``p âˆ™ q : x â‰¡ z``. This can also be seen as transitivity of equality.  Because the type of composition doesn't mention ``p`` and ``q``, we can use the non-dependent version of ``â‰¡``-induction."
    _âˆ™_ : {X : ğ“¤ Ì‡}{s t u : X} â†’ s â‰¡ t â†’ t â‰¡ u â†’ s â‰¡ u
    p âˆ™ q = transport ( lhs p â‰¡_ ) q p
    infixl 30 _âˆ™_                    -- NOTATION: type âˆ™ using ``\.``

    infix  40 _â»Â¹
    _â»Â¹ : {X : ğ“¤ Ì‡} â†’ {s t : X} â†’ s â‰¡ t â†’ t â‰¡ s
    p â»Â¹ = transport (_â‰¡ lhs p) p (refl _) --  (lhs p))

-------------------------------------------------------

Application
------------

An important tool that we use often in Agda proofs is application of a function to an identification ``p : x â‰¡ x'``. We apply the ``ap`` operator to obtain the identification ``ap f p : f x â‰¡ f x'`` when given ``p : x â‰¡ x'`` and ``f : X â†’ Y``.

Since ``ap`` is already defined in :term:`MHE`'s `Type Topolgy` library, we don't redefine it here.  However, we do define some variations of ``ap`` that are sometimes useful.

::

    ap-cong : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
              {f g : X â†’ Y} {a b : X}
     â†’         f â‰¡ g   â†’   a â‰¡ b
             -----------------------
     â†’            f a â‰¡ g b

    ap-cong (refl _) (refl _) = refl _

Here is a related tool that we borrow from the ``Relation/Binary/Core.agda`` module of the `Agda standard library`_.

::

    cong-app : {A : ğ“¤ Ì‡ } {B : A â†’ ğ“¦ Ì‡ }
               {f g : (a : A) â†’ B a}
     â†’          f â‰¡ g   â†’   (a : A)
              -----------------------
     â†’              f a â‰¡ g a

    cong-app (refl _) a = refl _

----------------------------------------

Function extensionality
------------------------

Extensional equality of functions, or :term:`function extensionality`, means that any two point-wise equal functions are equal.  As :term:`MHE` points out, this is known to be not provable or disprovable in Martin-LÃ¶f Type Theory (MLTT).

Nonetheless, we will mainly work with pointwise equality of functions, which :term:`MHE` defines (in `Type Topology`_ ) as follows:

.. code-block:: agda

    _âˆ¼_ : {X : ğ“¤ Ì‡ } {A : X â†’ ğ“¥ Ì‡ } â†’ Î  A â†’ Î  A â†’ ğ“¤ âŠ” ğ“¥ Ì‡ 
    f âˆ¼ g = âˆ€ x â†’ f x â‰¡ g x
    infix 0 _âˆ¼_

(The ``_âˆ¼_`` relation will be equivalent to equality of functions, once we have the principle of *univalence* at our disposal.)

-------------------------------------------------------

Predicates, Subsets
---------------------

We need a mechanism for implementing the notion of subsets in Agda.  A typical one is called ``Pred`` (for predicate). More generally, ``Pred A ğ“¤`` can be viewed as the type of a property that elements of type ``A`` might satisfy. We write ``P : Pred A ğ“¤`` (read "``P`` has type ``Pred A ğ“¤``") to represent the subset of elements of ``A`` that satisfy property ``P``.

Here is the definition (which is similar to the one found in the ``Relation/Unary.agda`` file of `Agda standard library`_ ).

::

    Pred : ğ“¤ Ì‡ â†’ (ğ“¥ : Universe) â†’ ğ“¤ âŠ” ğ“¥ âº Ì‡
    Pred A ğ“¥ = A â†’ ğ“¥ Ì‡


The membership relation
~~~~~~~~~~~~~~~~~~~~~~~~~

We introduce notation so that we may indicate that ``x`` "belongs to" a "subset" ``P``, or that ``x`` "has property" ``P``, by writing either ``x âˆˆ P`` or ``P x`` (cf. ``Relation/Unary.agda`` in the `Agda standard library`_ ).

::

    infix 4 _âˆˆ_ _âˆ‰_
    _âˆˆ_ : {A : ğ“¤ Ì‡ } â†’ A â†’ Pred A ğ“¦ â†’ ğ“¦ Ì‡
    x âˆˆ P = P x

    _âˆ‰_ : {A : ğ“¤ Ì‡ } â†’ A â†’ Pred A ğ“¦ â†’ ğ“¦ Ì‡
    x âˆ‰ P = Â¬ (x âˆˆ P)

Subset relations
~~~~~~~~~~~~~~~~~~

The subset relation is then denoted, as usual, with the ``âŠ†`` symbol (cf. ``Relation/Unary.agda`` in the `Agda standard library`_ ).

::

    infix 4 _âŠ†_ _âŠ‡_
    _âŠ†_ : {A : ğ“¤ Ì‡ } â†’ Pred A ğ“¦ â†’ Pred A ğ“£ â†’ ğ“¤ âŠ” ğ“¦ âŠ” ğ“£ Ì‡
    P âŠ† Q = âˆ€ {x} â†’ x âˆˆ P â†’ x âˆˆ Q

    _âŠ‡_ : {A : ğ“¤ Ì‡ } â†’ Pred A ğ“¦ â†’ Pred A ğ“£ â†’ ğ“¤ âŠ” ğ“¦ âŠ” ğ“£ Ì‡
    P âŠ‡ Q = Q âŠ† P

-------------------------------------------------------

Miscellany
--------------

Finally, we include the following list of "utilities" that will come in handy later.  Most of these are self-explanatory, but we make a few remarks below when we feel there is something worth noting.

::

    _âˆˆâˆˆ_ : {A : ğ“¤ Ì‡ } {B : ğ“¦ Ì‡ } â†’ (A  â†’  B) â†’ Pred B ğ“£ â†’ ğ“¤ âŠ” ğ“£ Ì‡
    _âˆˆâˆˆ_ f S = (x : _) â†’ f x âˆˆ S

    Im_âŠ†_ : {A : ğ“¤ Ì‡ } {B : ğ“¥ Ì‡ } â†’ (A â†’ B) â†’ Pred B ğ“£ â†’ ğ“¤ âŠ” ğ“£ Ì‡
    Im_âŠ†_ {A = A} f S = (x : A) â†’ f x âˆˆ S

    img : {X : ğ“¤ Ì‡ } {Y : ğ“¤ Ì‡ }
          (f : X â†’ Y) (P : Pred Y ğ“¤)
     â†’    Im f âŠ† P â†’  X â†’ Î£ P
    img {Y = Y} f P ImfâŠ†P = Î» xâ‚ â†’ f xâ‚ , ImfâŠ†P xâ‚

    â‰¡-elim-left : {Aâ‚ Aâ‚‚ : ğ“¤ Ì‡ } {Bâ‚ Bâ‚‚ : ğ“¦ Ì‡ }
     â†’            (Aâ‚ , Bâ‚) â‰¡ (Aâ‚‚ , Bâ‚‚)
                  ----------------------
     â†’                   Aâ‚ â‰¡ Aâ‚‚
    â‰¡-elim-left e = ap prâ‚ e

    â‰¡-elim-right : {Aâ‚ Aâ‚‚ : ğ“¤ Ì‡ }{Bâ‚ Bâ‚‚ : ğ“¦ Ì‡ }
     â†’             (Aâ‚ , Bâ‚) â‰¡ (Aâ‚‚ , Bâ‚‚)
                  -----------------------
     â†’                    Bâ‚ â‰¡ Bâ‚‚
    â‰¡-elim-right e = ap prâ‚‚ e

    â‰¡-Ã—-intro : {Aâ‚ Aâ‚‚ : ğ“¤ Ì‡ } {Bâ‚ Bâ‚‚ : ğ“¦ Ì‡ }
     â†’           Aâ‚ â‰¡ Aâ‚‚  â†’  Bâ‚ â‰¡ Bâ‚‚
              ------------------------
     â†’          (Aâ‚ , Bâ‚) â‰¡ (Aâ‚‚ , Bâ‚‚)
    â‰¡-Ã—-intro (refl _ ) (refl _ ) = (refl _ )

    cong-app-pred : âˆ€{A : ğ“¤ Ì‡ }{Bâ‚ Bâ‚‚ : Pred A ğ“¤}
                    (x : A) â†’  x âˆˆ Bâ‚  â†’  Bâ‚ â‰¡ Bâ‚‚
                   ------------------------------
     â†’                         x âˆˆ Bâ‚‚
    cong-app-pred x xâˆˆBâ‚ (refl _ ) = xâˆˆBâ‚

    cong-pred : {A : ğ“¤ Ì‡ }{B : Pred A ğ“¤}
                (x y : A) â†’  x âˆˆ B  â†’  x â‰¡ y
                ----------------------------
     â†’                       y âˆˆ B
    cong-pred x .x xâˆˆB (refl _ ) = xâˆˆB


    data Image_âˆ‹_ {A : ğ“¤ Ì‡ }{B : ğ“¦ Ì‡ }(f : A â†’ B) : B â†’ ğ“¤ âŠ” ğ“¦ Ì‡
      where
      im : (x : A) â†’ Image f âˆ‹ f x
      eq : (b : B) â†’ (a : A) â†’ b â‰¡ f a â†’ Image f âˆ‹ b

    -- image_ : {A : ğ“¤ Ì‡ } {B : ğ“¦ Ì‡ } â†’ (A â†’ B) â†’ Pred B (ğ“¤ âŠ” ğ“¦)
    -- image f = Î» b â†’ âˆƒ Î» a â†’ b â‰¡ f a

    ImageIsImage : {A : ğ“¤ Ì‡ }{B : ğ“¦ Ì‡ }
                   (f : A â†’ B) (b : B) (a : A)
     â†’              b â‰¡ f a
                  ----------------------------
     â†’              Image f âˆ‹ b
    ImageIsImage {A = A}{B = B} f b a bâ‰¡fa = eq b a bâ‰¡fa

N.B. the assertion ``Image f âˆ‹ y`` must come with a proof, which is of the form ``âˆƒa f a = y``, so we have a witness. Thus, the inverse can be "computed" in the following way:

::

    Inv : {A : ğ“¤ Ì‡ }{B : ğ“¦ Ì‡ }(f : A â†’ B)(b : B) â†’ Image f âˆ‹ b  â†’  A
    Inv f .(f a) (im a) = a
    Inv f b (eq b a bâ‰¡fa) = a

The special case for Set (i.e., ``ğ“¤â‚€ Ì‡``) is

::

    inv : {A B : ğ“¤â‚€ Ì‡ }(f : A â†’ B)(b : B) â†’ Image f âˆ‹ b â†’ A
    inv {A} {B} = Inv {ğ“¤â‚€}{ğ“¤â‚€}{A}{B}

    InvIsInv : {A : ğ“¤ Ì‡ } {B : ğ“¦ Ì‡ } (f : A â†’ B)
               (b : B) (bâˆˆImgf : Image f âˆ‹ b)
              ---------------------------------
     â†’         f (Inv f b bâˆˆImgf) â‰¡ b
    InvIsInv f .(f a) (im a) = refl _
    InvIsInv f b (eq b a bâ‰¡fa) = bâ‰¡fa â»Â¹

An epic (or surjective) function from ğ“¤ Ì‡ to ğ“¦ Ì‡ (and the special case for  ``ğ“¤â‚€ Ì‡``) is defined as follows.

::

    Epic : {A : ğ“¤ Ì‡ } {B : ğ“¦ Ì‡ } (g : A â†’ B) â†’  ğ“¤ âŠ” ğ“¦ Ì‡
    Epic g = âˆ€ y â†’ Image g âˆ‹ y

    epic : {A B : ğ“¤â‚€ Ì‡ } (g : A â†’ B) â†’ ğ“¤â‚€ Ì‡
    epic = Epic {ğ“¤â‚€} {ğ“¤â‚€}

The (pseudo-)inverse of an epic function is

::

    EpicInv : {A : ğ“¤ Ì‡ } {B : ğ“¦ Ì‡ } (f : A â†’ B) â†’ Epic f â†’ B â†’ A
    EpicInv f fEpic b = Inv f b (fEpic b)


    -- The (psudo-)inverse of an epic is the right inverse.
    EInvIsRInv : funext ğ“¦ ğ“¦ â†’ {A : ğ“¤ Ì‡ } {B : ğ“¦ Ì‡ }
                 (f : A â†’ B)  (fEpic : Epic f)
                ---------------------------------
     â†’           f âˆ˜ (EpicInv f fEpic) â‰¡ ğ‘–ğ‘‘ B
    EInvIsRInv fe f fEpic = fe (Î» x â†’ InvIsInv f x (fEpic x))



Monics (or injective) functions are defined this way.

::

    monic : {A : ğ“¤ Ì‡ } {B : ğ“¦ Ì‡ } (g : A â†’ B) â†’ ğ“¤ âŠ” ğ“¦ Ì‡
    monic g = âˆ€ aâ‚ aâ‚‚ â†’ g aâ‚ â‰¡ g aâ‚‚ â†’ aâ‚ â‰¡ aâ‚‚
    monicâ‚€ : {A B : ğ“¤â‚€ Ì‡ } (g : A â†’ B) â†’ ğ“¤â‚€ Ì‡
    monicâ‚€ = monic {ğ“¤â‚€}{ğ“¤â‚€}

    --The (pseudo-)inverse of a monic function
    monic-inv : {A : ğ“¤ Ì‡ } {B : ğ“¦ Ì‡ } (f : A â†’ B) â†’ monic f
     â†’           (b : B) â†’ Image f âˆ‹ b â†’ A
    monic-inv f fmonic  = Î» b Imfâˆ‹b â†’ Inv f b Imfâˆ‹b

    --The (psudo-)inverse of a monic is the left inverse.
    monic-inv-is-linv : {A : ğ“¤ Ì‡ }{B : ğ“¦ Ì‡ }
                        (f : A â†’ B) (fmonic : monic f)(x : A)
                       ----------------------------------------
      â†’                 (monic-inv f fmonic) (f x) (im x) â‰¡ x
    monic-inv-is-linv f fmonic x = refl _

Finally, we define bijective functions as follows.

::

    bijective : {A B : ğ“¤â‚€ Ì‡ }(g : A â†’ B) â†’ ğ“¤â‚€ Ì‡
    bijective g = epic g Ã— monic g

    Bijective : {A : ğ“¤ Ì‡ }{B : ğ“¦ Ì‡ }(g : A â†’ B) â†’ ğ“¤ âŠ” ğ“¦ Ì‡
    Bijective g = Epic g Ã— monic g


---------------------------------------------

More extensionality
--------------------

Here we collect miscellaneous definitions and proofs related to extensionality that will come in handy later.

::

    -------------------------------------------------------
    --Function extensionality from univalence

    --Ordinary function extensionality
    extensionality : âˆ€ ğ“¤ ğ“¦  â†’ ğ“¤ âº âŠ” ğ“¦ âº Ì‡
    extensionality ğ“¤ ğ“¦ = {A : ğ“¤ Ì‡ } {B : ğ“¦ Ì‡ } {f g : A â†’ B}
     â†’                f âˆ¼ g   â†’   f â‰¡ g

    --Opposite of function extensionality
    intensionality : âˆ€ {ğ“¤ ğ“¦} {A : ğ“¤ Ì‡ } {B : ğ“¦ Ì‡ } {f g : A â†’ B}
     â†’                f â‰¡ g  â†’  (x : A)
                      ------------------
     â†’                    f x â‰¡ g x

    intensionality  (refl _ ) _  = refl _

    --Dependent intensionality
    dep-intensionality : âˆ€ {ğ“¤ ğ“¦}{A : ğ“¤ Ì‡ }{B : A â†’ ğ“¦ Ì‡ }
                         {f g : âˆ€(x : A) â†’ B x}
     â†’                   f â‰¡ g  â†’  (x : A)
                        ------------------
     â†’                    f x â‰¡ g x

    dep-intensionality (refl _ ) _ = refl _

    --------------------------------------
    --Dependent function extensionality
    dep-extensionality : âˆ€ ğ“¤ ğ“¦ â†’ ğ“¤ âº âŠ” ğ“¦ âº Ì‡
    dep-extensionality ğ“¤ ğ“¦ = {A : ğ“¤ Ì‡ } {B : A â†’ ğ“¦ Ì‡ }
      {f g : âˆ€(x : A) â†’ B x} â†’  f âˆ¼ g  â†’  f â‰¡ g

    âˆ€-extensionality : ğ“¤Ï‰
    âˆ€-extensionality = âˆ€  {ğ“¤ ğ“¥} â†’ extensionality ğ“¤ ğ“¥

    âˆ€-dep-extensionality : ğ“¤Ï‰
    âˆ€-dep-extensionality = âˆ€ {ğ“¤ ğ“¥} â†’ dep-extensionality ğ“¤ ğ“¥

    extensionality-lemma : {I : ğ“˜ Ì‡ }{X : ğ“¤ Ì‡ }{A : I â†’ ğ“¥ Ì‡ }
                           (p q : (i : I) â†’ (X â†’ A i) â†’ ğ“£ Ì‡ )
                           (args : X â†’ (Î  A))
     â†’                     p â‰¡ q
       -------------------------------------------------------------
     â†’ (Î» i â†’ (p i)(Î» x â†’ args x i)) â‰¡ (Î» i â†’ (q i)(Î» x â†’ args x i))

    extensionality-lemma p q args pâ‰¡q =
     ap (Î» - â†’ Î» i â†’ (- i) (Î» x â†’ args x i)) pâ‰¡q


Unicode Hints
---------------

We assume you are using Emacs in a buffer agda2-mode_ enabled.

  +--------+----------------------+
  | To get | Type                 |
  +--------+----------------------+
  | ğ“˜, ğ“™   | ``\MCI``, ``\MCJ``   |
  +--------+----------------------+
  | ğ“¤ Ì‡    | ``\MCU \^.``         |
  +--------+----------------------+
  | ğ“¤ âº    | ``\MCU \^+``         |
  +--------+----------------------+
  | ğ“¤â‚€     |  ``\MCU\_0``         |
  +--------+----------------------+
  |  âŠ”     |  ``\sqcup``          |
  +--------+----------------------+
  | ğ´, ğµ   | ``\MiA``, ``\MiB``   |
  +--------+----------------------+
  | ğ‘¨, ğ‘©   | ``\MIA``, ``\MIB``   |
  +--------+----------------------+
  | ğ’œ, â„¬   | ``\McA``, ``\McB``   |
  +--------+----------------------+
  | ğ“, ğ“‘   | ``\MCA``, ``\MCB``   |
  +--------+----------------------+
  | t Ì‡ ğ‘¨  | ``t \^. \MIA``       |
  +--------+----------------------+
  | ğ‘“ Ì‚ ğ‘¨  | ``\Mif \^ \MIA``     |
  +--------+----------------------+
  | â‰¡      | ``\equiv``           |
  +--------+----------------------+
  |  ğ“‡â„¯ğ’»ğ“  | ``\Mcr\Mce\Mcf\Mcl`` |
  +--------+----------------------+
  | â‰¡âŸ¨ âŸ©   | ``\equiv\< \>``      |
  +--------+----------------------+
  | âˆ, â–    | ``\qed``, ``\sq``    |
  +--------+----------------------+
  | Î£, Î    | ``\Sigma``, ``\Pi``  |
  +--------+----------------------+
  | ğ•      | ``\bJ``              |
  +--------+----------------------+
  | Â¬, â»Â¹  |  ``\neg``, ``\^-\^1``|
  +--------+----------------------+
  | Ã—      | ``\times``           |
  +--------+----------------------+
  | ğ‘–ğ‘‘     | ``\Mii\Mid``         |
  +--------+----------------------+
  | ğ“Ÿ      | ``\MCP``             |
  +--------+----------------------+
  | â†ª      | ``\hookrightarrow``  |
  +--------+----------------------+
  | ğŸ˜, ğŸ™   | ``\b0``, ``\b1``     |
  +--------+----------------------+
  | â‡”      | ``\lr2``             |
  +--------+----------------------+
  | âˆ˜, â—   | ``\cdot``, ``\cib``  |
  +--------+----------------------+
  |  Ã—     | ``\times``           |
  +--------+----------------------+
  | âˆ¥_âˆ¥    | ``\||_\||``          |
  +--------+----------------------+
  | âˆ¼,  â‰ƒ  | ``\~``, ``\~-``      |
  +--------+----------------------+
  | âˆˆâ‚€     | ``\in\_0``           |
  +--------+----------------------+
  | âŠ†â‚€     | ``\subseteq\_0``     |
  +--------+----------------------+

.. include:: hyperlink_references.rst

