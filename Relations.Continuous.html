<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Relations.Continuous</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Background">---
layout: default
title : Relations.Continuous module (The Agda Universal Algebra Library)
date : 2021-02-28
author: [agda-algebras development team][]
---

### Continuous Relations

This is the [Relations.Continuous][] module of the [Agda Universal Algebra Library][].

</a><a id="274" class="Markup">\begin{code}</a>

<a id="288" class="Symbol">{-#</a> <a id="292" class="Keyword">OPTIONS</a> <a id="300" class="Pragma">--without-K</a> <a id="312" class="Pragma">--exact-split</a> <a id="326" class="Pragma">--safe</a> <a id="333" class="Symbol">#-}</a>

<a id="338" class="Keyword">module</a> <a id="345" href="Relations.Continuous.html" class="Module">Relations.Continuous</a> <a id="366" class="Keyword">where</a>

<a id="373" class="Keyword">open</a> <a id="378" class="Keyword">import</a> <a id="385" href="Agda.Primitive.html" class="Module">Agda.Primitive</a>    <a id="403" class="Keyword">using</a> <a id="409" class="Symbol">(</a> <a id="411" href="Agda.Primitive.html#810" class="Primitive Operator">_⊔_</a> <a id="415" class="Symbol">;</a> <a id="417" href="Agda.Primitive.html#780" class="Primitive">lsuc</a> <a id="422" class="Symbol">;</a> <a id="424" href="Agda.Primitive.html#597" class="Postulate">Level</a> <a id="430" class="Symbol">)</a>
                           <a id="459" class="Keyword">renaming</a> <a id="468" class="Symbol">(</a> <a id="470" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="474" class="Symbol">to</a> <a id="477" class="Primitive">Type</a> <a id="482" class="Symbol">)</a>

<a id="485" class="Keyword">open</a> <a id="490" class="Keyword">import</a> <a id="497" href="Overture.Preliminaries.html" class="Module">Overture.Preliminaries</a> <a id="520" class="Keyword">using</a> <a id="526" class="Symbol">(</a> <a id="528" href="Overture.Preliminaries.html#5655" class="Function">Π</a> <a id="530" class="Symbol">;</a> <a id="532" href="Overture.Preliminaries.html#5735" class="Function">Π-syntax</a> <a id="541" class="Symbol">)</a>
<a id="543" class="Keyword">open</a> <a id="548" class="Keyword">import</a> <a id="555" href="Relations.Discrete.html" class="Module">Relations.Discrete</a>     <a id="578" class="Keyword">using</a> <a id="584" class="Symbol">(</a> <a id="586" href="Relations.Discrete.html#5315" class="Function">Op</a> <a id="589" class="Symbol">;</a> <a id="591" href="Relations.Discrete.html#5504" class="Function Operator">arity[_]</a> <a id="600" class="Symbol">)</a>

<a id="603" class="Keyword">private</a> <a id="611" class="Keyword">variable</a> <a id="620" href="Relations.Continuous.html#620" class="Generalizable">α</a> <a id="622" href="Relations.Continuous.html#622" class="Generalizable">ρ</a> <a id="624" class="Symbol">:</a> <a id="626" href="Agda.Primitive.html#597" class="Postulate">Level</a>

<a id="633" class="Markup">\end{code}</a><a id="643" class="Background">

#### Motivation

In set theory, an n-ary relation on a set `A` is simply a subset of the n-fold product `A × A × ⋯ × A`.  As such, we could model these as predicates over the type `A × A × ⋯ × A`, or as relations of type `A → A → ⋯ → A → Type β` (for some universe β).  To implement such a relation in type theory, we would need to know the arity in advance, and then somehow form an n-fold arrow →.  It&#39;s easier and more general to instead define an arity type `I : Type 𝓥`, and define the type representing `I`-ary relations on `A` as the function type `(I → A) → Type β`.  Then, if we are specifically interested in an n-ary relation for some natural number `n`, we could take `I` to be a finite set (e.g., of type `Fin n`).

Below we will define `ContRel` to be the type `(I → A) → Type β` and we will call `ContRel` the type of *continuous relations*.  This generalizes the discrete relations we defined in [Relations.Discrete] (unary, binary, etc.) since continuous relations can be of arbitrary arity.  They are not completely general, however, since they are defined over a single type. Said another way, they are *single-sorted* relations. We will remove this limitation when we define the type of *dependent continuous relations* at the end of this module.

Just as `Rel A β` was the single-sorted special case of the multisorted `REL A B β` type, so too will `ContRel I A β` be the single-sorted version of a completely general type of relations. The latter will represent relations that not only have arbitrary arities, but also are defined over arbitrary families of types.

To be more concrete, given an arbitrary family `A : I → Type α` of types, we may have a relation from `A i` to `A j` to `A k` to …, where the collection represented by the &quot;indexing&quot; type `I` might not even be enumerable.&lt;sup&gt;[1](Relations.Continuous.html#fn1)&lt;/sup&gt;

We refer to such relations as *dependent continuous relations* (or *dependent relations* for short) because the definition of a type that represents them requires depedent types.  The `DepRel` type that we define [below](Relations.Continuous.html#dependent-relations) manifests this completely general notion of relation.



#### Continuous and dependent relations

Here we define the types `Rel` and `ΠΡ` (&quot;Pi Rho&quot;). The first of these represents predicates of arbitrary arity over a single type `A`; we call these *continuous relations*.&lt;sup&gt;[1](Relations.Continuous.html#fn1)&lt;/sup&gt;
To define `ΠΡ`, the type of *dependent relations*, we exploit the full power of dependent types and provide a completely general relation type.

Here, the tuples of a relation of type `DepRel I 𝒜 β` will inhabit the dependent function type `𝒜 : I → Type α` (where the codomain may depend on the input coordinate `i : I` of the domain). Heuristically, we can think of an inhabitant of type `DepRel I 𝒜 β` as a relation from `𝒜 i` to `𝒜 j` to `𝒜 k` to …. (This is only a rough heuristic since `I` could denote an uncountable collection.&lt;sup&gt;[2](Relations.Continuous.html#fn2)&lt;/sup&gt;)


</a><a id="3669" class="Markup">\begin{code}</a>

<a id="3683" class="Keyword">module</a> <a id="3690" href="Relations.Continuous.html#3690" class="Module">_</a> <a id="3692" class="Symbol">{</a><a id="3693" href="Relations.Continuous.html#3693" class="Bound">𝓥</a> <a id="3695" class="Symbol">:</a> <a id="3697" href="Agda.Primitive.html#597" class="Postulate">Level</a><a id="3702" class="Symbol">}</a> <a id="3704" class="Keyword">where</a>

 <a id="3712" href="Relations.Continuous.html#3712" class="Function">ar</a> <a id="3715" class="Symbol">:</a> <a id="3717" href="Relations.Continuous.html#477" class="Primitive">Type</a> <a id="3722" class="Symbol">(</a><a id="3723" href="Agda.Primitive.html#780" class="Primitive">lsuc</a> <a id="3728" href="Relations.Continuous.html#3693" class="Bound">𝓥</a><a id="3729" class="Symbol">)</a>
 <a id="3732" href="Relations.Continuous.html#3712" class="Function">ar</a> <a id="3735" class="Symbol">=</a> <a id="3737" href="Relations.Continuous.html#477" class="Primitive">Type</a> <a id="3742" href="Relations.Continuous.html#3693" class="Bound">𝓥</a>

<a id="3745" class="Comment">-- Relations of arbitrary arity over a single sort.</a>
 <a id="3798" href="Relations.Continuous.html#3798" class="Function">Rel</a> <a id="3802" class="Symbol">:</a> <a id="3804" href="Relations.Continuous.html#477" class="Primitive">Type</a> <a id="3809" href="Relations.Continuous.html#620" class="Generalizable">α</a> <a id="3811" class="Symbol">→</a> <a id="3813" href="Relations.Continuous.html#3712" class="Function">ar</a> <a id="3816" class="Symbol">→</a> <a id="3818" class="Symbol">{</a><a id="3819" href="Relations.Continuous.html#3819" class="Bound">ρ</a> <a id="3821" class="Symbol">:</a> <a id="3823" href="Agda.Primitive.html#597" class="Postulate">Level</a><a id="3828" class="Symbol">}</a> <a id="3830" class="Symbol">→</a> <a id="3832" href="Relations.Continuous.html#477" class="Primitive">Type</a> <a id="3837" class="Symbol">(</a><a id="3838" href="Relations.Continuous.html#620" class="Generalizable">α</a> <a id="3840" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="3842" href="Relations.Continuous.html#3693" class="Bound">𝓥</a> <a id="3844" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="3846" href="Agda.Primitive.html#780" class="Primitive">lsuc</a> <a id="3851" href="Relations.Continuous.html#3819" class="Bound">ρ</a><a id="3852" class="Symbol">)</a>
 <a id="3855" href="Relations.Continuous.html#3798" class="Function">Rel</a> <a id="3859" href="Relations.Continuous.html#3859" class="Bound">A</a> <a id="3861" href="Relations.Continuous.html#3861" class="Bound">I</a> <a id="3863" class="Symbol">{</a><a id="3864" href="Relations.Continuous.html#3864" class="Bound">ρ</a><a id="3865" class="Symbol">}</a> <a id="3867" class="Symbol">=</a> <a id="3869" class="Symbol">(</a><a id="3870" href="Relations.Continuous.html#3861" class="Bound">I</a> <a id="3872" class="Symbol">→</a> <a id="3874" href="Relations.Continuous.html#3859" class="Bound">A</a><a id="3875" class="Symbol">)</a> <a id="3877" class="Symbol">→</a> <a id="3879" href="Relations.Continuous.html#477" class="Primitive">Type</a> <a id="3884" href="Relations.Continuous.html#3864" class="Bound">ρ</a>

 <a id="3888" href="Relations.Continuous.html#3888" class="Function">Rel-syntax</a> <a id="3899" class="Symbol">:</a> <a id="3901" href="Relations.Continuous.html#477" class="Primitive">Type</a> <a id="3906" href="Relations.Continuous.html#620" class="Generalizable">α</a> <a id="3908" class="Symbol">→</a> <a id="3910" href="Relations.Continuous.html#3712" class="Function">ar</a> <a id="3913" class="Symbol">→</a> <a id="3915" class="Symbol">(</a><a id="3916" href="Relations.Continuous.html#3916" class="Bound">ρ</a> <a id="3918" class="Symbol">:</a> <a id="3920" href="Agda.Primitive.html#597" class="Postulate">Level</a><a id="3925" class="Symbol">)</a> <a id="3927" class="Symbol">→</a> <a id="3929" href="Relations.Continuous.html#477" class="Primitive">Type</a> <a id="3934" class="Symbol">(</a><a id="3935" href="Relations.Continuous.html#3693" class="Bound">𝓥</a> <a id="3937" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="3939" href="Relations.Continuous.html#620" class="Generalizable">α</a> <a id="3941" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="3943" href="Agda.Primitive.html#780" class="Primitive">lsuc</a> <a id="3948" href="Relations.Continuous.html#3916" class="Bound">ρ</a><a id="3949" class="Symbol">)</a>
 <a id="3952" href="Relations.Continuous.html#3888" class="Function">Rel-syntax</a> <a id="3963" href="Relations.Continuous.html#3963" class="Bound">A</a> <a id="3965" href="Relations.Continuous.html#3965" class="Bound">I</a> <a id="3967" href="Relations.Continuous.html#3967" class="Bound">ρ</a> <a id="3969" class="Symbol">=</a> <a id="3971" href="Relations.Continuous.html#3798" class="Function">Rel</a> <a id="3975" href="Relations.Continuous.html#3963" class="Bound">A</a> <a id="3977" href="Relations.Continuous.html#3965" class="Bound">I</a> <a id="3979" class="Symbol">{</a><a id="3980" href="Relations.Continuous.html#3967" class="Bound">ρ</a><a id="3981" class="Symbol">}</a>

 <a id="3985" class="Keyword">syntax</a> <a id="3992" href="Relations.Continuous.html#3888" class="Function">Rel-syntax</a> <a id="4003" class="Bound">A</a> <a id="4005" class="Bound">I</a> <a id="4007" class="Bound">ρ</a> <a id="4009" class="Symbol">=</a> <a id="4011" class="Function">Rel[</a> <a id="4016" class="Bound">A</a> <a id="4018" class="Function">^</a> <a id="4020" class="Bound">I</a> <a id="4022" class="Function">]</a> <a id="4024" class="Bound">ρ</a>
 <a id="4027" class="Keyword">infix</a> <a id="4033" class="Number">6</a> <a id="4035" href="Relations.Continuous.html#3888" class="Function">Rel-syntax</a>

 <a id="4048" class="Comment">-- The type of arbitrarily multisorted relations of arbitrary arity</a>
 <a id="4117" href="Relations.Continuous.html#4117" class="Function">ΠΡ</a> <a id="4120" class="Symbol">:</a> <a id="4122" class="Symbol">(</a><a id="4123" href="Relations.Continuous.html#4123" class="Bound">I</a> <a id="4125" class="Symbol">:</a> <a id="4127" href="Relations.Continuous.html#3712" class="Function">ar</a><a id="4129" class="Symbol">)</a> <a id="4131" class="Symbol">→</a> <a id="4133" class="Symbol">(</a><a id="4134" href="Relations.Continuous.html#4123" class="Bound">I</a> <a id="4136" class="Symbol">→</a> <a id="4138" href="Relations.Continuous.html#477" class="Primitive">Type</a> <a id="4143" href="Relations.Continuous.html#620" class="Generalizable">α</a><a id="4144" class="Symbol">)</a> <a id="4146" class="Symbol">→</a> <a id="4148" class="Symbol">{</a><a id="4149" href="Relations.Continuous.html#4149" class="Bound">ρ</a> <a id="4151" class="Symbol">:</a> <a id="4153" href="Agda.Primitive.html#597" class="Postulate">Level</a><a id="4158" class="Symbol">}</a> <a id="4160" class="Symbol">→</a> <a id="4162" href="Relations.Continuous.html#477" class="Primitive">Type</a> <a id="4167" class="Symbol">(</a><a id="4168" href="Relations.Continuous.html#3693" class="Bound">𝓥</a> <a id="4170" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="4172" href="Relations.Continuous.html#620" class="Generalizable">α</a> <a id="4174" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="4176" href="Agda.Primitive.html#780" class="Primitive">lsuc</a> <a id="4181" href="Relations.Continuous.html#4149" class="Bound">ρ</a><a id="4182" class="Symbol">)</a>
 <a id="4185" href="Relations.Continuous.html#4117" class="Function">ΠΡ</a> <a id="4188" href="Relations.Continuous.html#4188" class="Bound">I</a> <a id="4190" href="Relations.Continuous.html#4190" class="Bound">𝒜</a> <a id="4192" class="Symbol">{</a><a id="4193" href="Relations.Continuous.html#4193" class="Bound">ρ</a><a id="4194" class="Symbol">}</a> <a id="4196" class="Symbol">=</a> <a id="4198" class="Symbol">((</a><a id="4200" href="Relations.Continuous.html#4200" class="Bound">i</a> <a id="4202" class="Symbol">:</a> <a id="4204" href="Relations.Continuous.html#4188" class="Bound">I</a><a id="4205" class="Symbol">)</a> <a id="4207" class="Symbol">→</a> <a id="4209" href="Relations.Continuous.html#4190" class="Bound">𝒜</a> <a id="4211" href="Relations.Continuous.html#4200" class="Bound">i</a><a id="4212" class="Symbol">)</a> <a id="4214" class="Symbol">→</a> <a id="4216" href="Relations.Continuous.html#477" class="Primitive">Type</a> <a id="4221" href="Relations.Continuous.html#4193" class="Bound">ρ</a>

 <a id="4225" href="Relations.Continuous.html#4225" class="Function">ΠΡ-syntax</a> <a id="4235" class="Symbol">:</a> <a id="4237" class="Symbol">(</a><a id="4238" href="Relations.Continuous.html#4238" class="Bound">I</a> <a id="4240" class="Symbol">:</a> <a id="4242" href="Relations.Continuous.html#3712" class="Function">ar</a><a id="4244" class="Symbol">)</a> <a id="4246" class="Symbol">→</a> <a id="4248" class="Symbol">(</a><a id="4249" href="Relations.Continuous.html#4238" class="Bound">I</a> <a id="4251" class="Symbol">→</a> <a id="4253" href="Relations.Continuous.html#477" class="Primitive">Type</a> <a id="4258" href="Relations.Continuous.html#620" class="Generalizable">α</a><a id="4259" class="Symbol">)</a> <a id="4261" class="Symbol">→</a> <a id="4263" class="Symbol">{</a><a id="4264" href="Relations.Continuous.html#4264" class="Bound">ρ</a> <a id="4266" class="Symbol">:</a> <a id="4268" href="Agda.Primitive.html#597" class="Postulate">Level</a><a id="4273" class="Symbol">}</a> <a id="4275" class="Symbol">→</a> <a id="4277" href="Relations.Continuous.html#477" class="Primitive">Type</a> <a id="4282" class="Symbol">(</a><a id="4283" href="Relations.Continuous.html#3693" class="Bound">𝓥</a> <a id="4285" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="4287" href="Relations.Continuous.html#620" class="Generalizable">α</a> <a id="4289" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="4291" href="Agda.Primitive.html#780" class="Primitive">lsuc</a> <a id="4296" href="Relations.Continuous.html#4264" class="Bound">ρ</a><a id="4297" class="Symbol">)</a>
 <a id="4300" href="Relations.Continuous.html#4225" class="Function">ΠΡ-syntax</a> <a id="4310" href="Relations.Continuous.html#4310" class="Bound">I</a> <a id="4312" href="Relations.Continuous.html#4312" class="Bound">𝒜</a> <a id="4314" class="Symbol">{</a><a id="4315" href="Relations.Continuous.html#4315" class="Bound">ρ</a><a id="4316" class="Symbol">}</a> <a id="4318" class="Symbol">=</a> <a id="4320" href="Relations.Continuous.html#4117" class="Function">ΠΡ</a> <a id="4323" href="Relations.Continuous.html#4310" class="Bound">I</a> <a id="4325" href="Relations.Continuous.html#4312" class="Bound">𝒜</a> <a id="4327" class="Symbol">{</a><a id="4328" href="Relations.Continuous.html#4315" class="Bound">ρ</a><a id="4329" class="Symbol">}</a>

 <a id="4333" class="Keyword">syntax</a> <a id="4340" href="Relations.Continuous.html#4225" class="Function">ΠΡ-syntax</a> <a id="4350" class="Bound">I</a> <a id="4352" class="Symbol">(λ</a> <a id="4355" class="Bound">i</a> <a id="4357" class="Symbol">→</a> <a id="4359" class="Bound">𝒜</a><a id="4360" class="Symbol">)</a> <a id="4362" class="Symbol">=</a> <a id="4364" class="Function">ΠΡ[</a> <a id="4368" class="Bound">i</a> <a id="4370" class="Function">∈</a> <a id="4372" class="Bound">I</a> <a id="4374" class="Function">]</a> <a id="4376" class="Bound">𝒜</a>
 <a id="4379" class="Keyword">infix</a> <a id="4385" class="Number">6</a> <a id="4387" href="Relations.Continuous.html#4225" class="Function">ΠΡ-syntax</a>



<a id="4400" class="Comment">-- Compatibility with general relations</a>
<a id="4440" class="Comment">-- ------------------------------------</a>

 <a id="4482" class="Comment">-- Lift a relation of tuples up to a relation on tuples of tuples.</a>
 <a id="4550" href="Relations.Continuous.html#4550" class="Function">eval-Rel</a> <a id="4559" class="Symbol">:</a> <a id="4561" class="Symbol">{</a><a id="4562" href="Relations.Continuous.html#4562" class="Bound">I</a> <a id="4564" class="Symbol">:</a> <a id="4566" href="Relations.Continuous.html#3712" class="Function">ar</a><a id="4568" class="Symbol">}{</a><a id="4570" href="Relations.Continuous.html#4570" class="Bound">A</a> <a id="4572" class="Symbol">:</a> <a id="4574" href="Relations.Continuous.html#477" class="Primitive">Type</a> <a id="4579" href="Relations.Continuous.html#620" class="Generalizable">α</a><a id="4580" class="Symbol">}</a> <a id="4582" class="Symbol">→</a> <a id="4584" href="Relations.Continuous.html#3798" class="Function">Rel</a> <a id="4588" href="Relations.Continuous.html#4570" class="Bound">A</a> <a id="4590" href="Relations.Continuous.html#4562" class="Bound">I</a><a id="4591" class="Symbol">{</a><a id="4592" href="Relations.Continuous.html#622" class="Generalizable">ρ</a><a id="4593" class="Symbol">}</a> <a id="4595" class="Symbol">→</a> <a id="4597" class="Symbol">(</a><a id="4598" href="Relations.Continuous.html#4598" class="Bound">J</a> <a id="4600" class="Symbol">:</a> <a id="4602" href="Relations.Continuous.html#3712" class="Function">ar</a><a id="4604" class="Symbol">)</a> <a id="4606" class="Symbol">→</a> <a id="4608" class="Symbol">(</a><a id="4609" href="Relations.Continuous.html#4562" class="Bound">I</a> <a id="4611" class="Symbol">→</a> <a id="4613" href="Relations.Continuous.html#4598" class="Bound">J</a> <a id="4615" class="Symbol">→</a> <a id="4617" href="Relations.Continuous.html#4570" class="Bound">A</a><a id="4618" class="Symbol">)</a> <a id="4620" class="Symbol">→</a> <a id="4622" href="Relations.Continuous.html#477" class="Primitive">Type</a> <a id="4627" class="Symbol">(</a><a id="4628" href="Relations.Continuous.html#3693" class="Bound">𝓥</a> <a id="4630" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="4632" href="Relations.Continuous.html#622" class="Generalizable">ρ</a><a id="4633" class="Symbol">)</a>
 <a id="4636" href="Relations.Continuous.html#4550" class="Function">eval-Rel</a> <a id="4645" href="Relations.Continuous.html#4645" class="Bound">R</a> <a id="4647" href="Relations.Continuous.html#4647" class="Bound">J</a> <a id="4649" href="Relations.Continuous.html#4649" class="Bound">t</a> <a id="4651" class="Symbol">=</a> <a id="4653" class="Symbol">∀</a> <a id="4655" class="Symbol">(</a><a id="4656" href="Relations.Continuous.html#4656" class="Bound">j</a> <a id="4658" class="Symbol">:</a> <a id="4660" href="Relations.Continuous.html#4647" class="Bound">J</a><a id="4661" class="Symbol">)</a> <a id="4663" class="Symbol">→</a> <a id="4665" href="Relations.Continuous.html#4645" class="Bound">R</a> <a id="4667" class="Symbol">λ</a> <a id="4669" href="Relations.Continuous.html#4669" class="Bound">i</a> <a id="4671" class="Symbol">→</a> <a id="4673" href="Relations.Continuous.html#4649" class="Bound">t</a> <a id="4675" href="Relations.Continuous.html#4669" class="Bound">i</a> <a id="4677" href="Relations.Continuous.html#4656" class="Bound">j</a>

 <a id="4681" class="Comment">-- A relation R is compatible with an operation 𝑓 if for every tuple t of tuples</a>
 <a id="4763" class="Comment">-- belonging to R, the tuple whose elements are the result of applying 𝑓 to</a>
 <a id="4840" class="Comment">-- sections of t also belongs to R.</a>

 <a id="4878" href="Relations.Continuous.html#4878" class="Function">compatible-Rel</a> <a id="4893" class="Symbol">:</a> <a id="4895" class="Symbol">{</a><a id="4896" href="Relations.Continuous.html#4896" class="Bound">I</a> <a id="4898" href="Relations.Continuous.html#4898" class="Bound">J</a> <a id="4900" class="Symbol">:</a> <a id="4902" href="Relations.Continuous.html#3712" class="Function">ar</a><a id="4904" class="Symbol">}{</a><a id="4906" href="Relations.Continuous.html#4906" class="Bound">A</a> <a id="4908" class="Symbol">:</a> <a id="4910" href="Relations.Continuous.html#477" class="Primitive">Type</a> <a id="4915" href="Relations.Continuous.html#620" class="Generalizable">α</a><a id="4916" class="Symbol">}</a> <a id="4918" class="Symbol">→</a> <a id="4920" href="Relations.Discrete.html#5315" class="Function">Op</a><a id="4922" class="Symbol">(</a><a id="4923" href="Relations.Continuous.html#4906" class="Bound">A</a><a id="4924" class="Symbol">)</a> <a id="4926" href="Relations.Continuous.html#4898" class="Bound">J</a> <a id="4928" class="Symbol">→</a> <a id="4930" href="Relations.Continuous.html#3798" class="Function">Rel</a> <a id="4934" href="Relations.Continuous.html#4906" class="Bound">A</a> <a id="4936" href="Relations.Continuous.html#4896" class="Bound">I</a><a id="4937" class="Symbol">{</a><a id="4938" href="Relations.Continuous.html#622" class="Generalizable">ρ</a><a id="4939" class="Symbol">}</a> <a id="4941" class="Symbol">→</a> <a id="4943" href="Relations.Continuous.html#477" class="Primitive">Type</a> <a id="4948" class="Symbol">(</a><a id="4949" href="Relations.Continuous.html#3693" class="Bound">𝓥</a> <a id="4951" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="4953" href="Relations.Continuous.html#620" class="Generalizable">α</a> <a id="4955" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="4957" href="Relations.Continuous.html#622" class="Generalizable">ρ</a><a id="4958" class="Symbol">)</a>
 <a id="4961" href="Relations.Continuous.html#4878" class="Function">compatible-Rel</a> <a id="4976" href="Relations.Continuous.html#4976" class="Bound">𝑓</a> <a id="4978" href="Relations.Continuous.html#4978" class="Bound">R</a>  <a id="4981" class="Symbol">=</a> <a id="4983" class="Symbol">∀</a> <a id="4985" href="Relations.Continuous.html#4985" class="Bound">t</a> <a id="4987" class="Symbol">→</a> <a id="4989" href="Relations.Continuous.html#4550" class="Function">eval-Rel</a> <a id="4998" href="Relations.Continuous.html#4978" class="Bound">R</a> <a id="5000" href="Relations.Discrete.html#5504" class="Function Operator">arity[</a> <a id="5007" href="Relations.Continuous.html#4976" class="Bound">𝑓</a> <a id="5009" href="Relations.Discrete.html#5504" class="Function Operator">]</a> <a id="5011" href="Relations.Continuous.html#4985" class="Bound">t</a> <a id="5013" class="Symbol">→</a> <a id="5015" href="Relations.Continuous.html#4978" class="Bound">R</a> <a id="5017" class="Symbol">λ</a> <a id="5019" href="Relations.Continuous.html#5019" class="Bound">i</a> <a id="5021" class="Symbol">→</a> <a id="5023" href="Relations.Continuous.html#4976" class="Bound">𝑓</a> <a id="5025" class="Symbol">(</a><a id="5026" href="Relations.Continuous.html#4985" class="Bound">t</a> <a id="5028" href="Relations.Continuous.html#5019" class="Bound">i</a><a id="5029" class="Symbol">)</a>
 <a id="5032" class="Comment">-- (inferred type of t is I → J → A)</a>


 <a id="5072" class="Comment">-- Compatibility of operations with ΠΡ (PiRho) types.</a>

 <a id="5128" href="Relations.Continuous.html#5128" class="Function">eval-ΠΡ</a> <a id="5136" class="Symbol">:</a> <a id="5138" class="Symbol">{</a><a id="5139" href="Relations.Continuous.html#5139" class="Bound">I</a> <a id="5141" href="Relations.Continuous.html#5141" class="Bound">J</a> <a id="5143" class="Symbol">:</a> <a id="5145" href="Relations.Continuous.html#3712" class="Function">ar</a><a id="5147" class="Symbol">}{</a><a id="5149" href="Relations.Continuous.html#5149" class="Bound">𝒜</a> <a id="5151" class="Symbol">:</a> <a id="5153" href="Relations.Continuous.html#5139" class="Bound">I</a> <a id="5155" class="Symbol">→</a> <a id="5157" href="Relations.Continuous.html#477" class="Primitive">Type</a> <a id="5162" href="Relations.Continuous.html#620" class="Generalizable">α</a><a id="5163" class="Symbol">}</a>
  <a id="5167" class="Symbol">→</a>         <a id="5177" href="Relations.Continuous.html#4117" class="Function">ΠΡ</a> <a id="5180" href="Relations.Continuous.html#5139" class="Bound">I</a> <a id="5182" href="Relations.Continuous.html#5149" class="Bound">𝒜</a> <a id="5184" class="Symbol">{</a><a id="5185" href="Relations.Continuous.html#622" class="Generalizable">ρ</a><a id="5186" class="Symbol">}</a>            <a id="5199" class="Comment">-- the relation type: subsets of Π[ i ∈ I ] 𝒜 i</a>
                                  <a id="5281" class="Comment">-- (where Π[ i ∈ I ] 𝒜 i is a type of dependent functions or &quot;tuples&quot;)</a>
  <a id="5354" class="Symbol">→</a>         <a id="5364" class="Symbol">((</a><a id="5366" href="Relations.Continuous.html#5366" class="Bound">i</a> <a id="5368" class="Symbol">:</a> <a id="5370" href="Relations.Continuous.html#5139" class="Bound">I</a><a id="5371" class="Symbol">)</a> <a id="5373" class="Symbol">→</a> <a id="5375" href="Relations.Continuous.html#5141" class="Bound">J</a> <a id="5377" class="Symbol">→</a> <a id="5379" href="Relations.Continuous.html#5149" class="Bound">𝒜</a> <a id="5381" href="Relations.Continuous.html#5366" class="Bound">i</a><a id="5382" class="Symbol">)</a>  <a id="5385" class="Comment">-- an I-tuple of (𝒥 i)-tuples</a>
  <a id="5417" class="Symbol">→</a>         <a id="5427" href="Relations.Continuous.html#477" class="Primitive">Type</a> <a id="5432" class="Symbol">(</a><a id="5433" href="Relations.Continuous.html#3693" class="Bound">𝓥</a> <a id="5435" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="5437" href="Relations.Continuous.html#622" class="Generalizable">ρ</a><a id="5438" class="Symbol">)</a>
 <a id="5441" href="Relations.Continuous.html#5128" class="Function">eval-ΠΡ</a><a id="5448" class="Symbol">{</a><a id="5449" class="Argument">I</a> <a id="5451" class="Symbol">=</a> <a id="5453" href="Relations.Continuous.html#5453" class="Bound">I</a><a id="5454" class="Symbol">}{</a><a id="5456" href="Relations.Continuous.html#5456" class="Bound">J</a><a id="5457" class="Symbol">}{</a><a id="5459" href="Relations.Continuous.html#5459" class="Bound">𝒜</a><a id="5460" class="Symbol">}</a> <a id="5462" href="Relations.Continuous.html#5462" class="Bound">R</a> <a id="5464" href="Relations.Continuous.html#5464" class="Bound">t</a> <a id="5466" class="Symbol">=</a> <a id="5468" class="Symbol">∀</a> <a id="5470" href="Relations.Continuous.html#5470" class="Bound">j</a> <a id="5472" class="Symbol">→</a> <a id="5474" href="Relations.Continuous.html#5462" class="Bound">R</a> <a id="5476" class="Symbol">λ</a> <a id="5478" href="Relations.Continuous.html#5478" class="Bound">i</a> <a id="5480" class="Symbol">→</a> <a id="5482" class="Symbol">(</a><a id="5483" href="Relations.Continuous.html#5464" class="Bound">t</a> <a id="5485" href="Relations.Continuous.html#5478" class="Bound">i</a><a id="5486" class="Symbol">)</a> <a id="5488" href="Relations.Continuous.html#5470" class="Bound">j</a>

 <a id="5492" href="Relations.Continuous.html#5492" class="Function">compatible-ΠΡ</a> <a id="5506" class="Symbol">:</a> <a id="5508" class="Symbol">{</a><a id="5509" href="Relations.Continuous.html#5509" class="Bound">I</a> <a id="5511" href="Relations.Continuous.html#5511" class="Bound">J</a> <a id="5513" class="Symbol">:</a> <a id="5515" href="Relations.Continuous.html#3712" class="Function">ar</a><a id="5517" class="Symbol">}{</a><a id="5519" href="Relations.Continuous.html#5519" class="Bound">𝒜</a> <a id="5521" class="Symbol">:</a> <a id="5523" href="Relations.Continuous.html#5509" class="Bound">I</a> <a id="5525" class="Symbol">→</a> <a id="5527" href="Relations.Continuous.html#477" class="Primitive">Type</a> <a id="5532" href="Relations.Continuous.html#620" class="Generalizable">α</a><a id="5533" class="Symbol">}</a>
  <a id="5537" class="Symbol">→</a>               <a id="5553" class="Symbol">(∀</a> <a id="5556" href="Relations.Continuous.html#5556" class="Bound">i</a> <a id="5558" class="Symbol">→</a> <a id="5560" href="Relations.Discrete.html#5315" class="Function">Op</a> <a id="5563" class="Symbol">(</a><a id="5564" href="Relations.Continuous.html#5519" class="Bound">𝒜</a> <a id="5566" href="Relations.Continuous.html#5556" class="Bound">i</a><a id="5567" class="Symbol">)</a> <a id="5569" href="Relations.Continuous.html#5511" class="Bound">J</a><a id="5570" class="Symbol">)</a>  <a id="5573" class="Comment">-- for each i : I, an operation of type  𝒪(𝒜 i){J} = (J → 𝒜 i) → 𝒜 i</a>
  <a id="5644" class="Symbol">→</a>               <a id="5660" href="Relations.Continuous.html#4117" class="Function">ΠΡ</a> <a id="5663" href="Relations.Continuous.html#5509" class="Bound">I</a> <a id="5665" href="Relations.Continuous.html#5519" class="Bound">𝒜</a> <a id="5667" class="Symbol">{</a><a id="5668" href="Relations.Continuous.html#622" class="Generalizable">ρ</a><a id="5669" class="Symbol">}</a>             <a id="5683" class="Comment">-- a subset of Π[ i ∈ I ] 𝒜 i</a>
                                         <a id="5754" class="Comment">-- (where Π[ i ∈ I ] 𝒜 i is a type of dependent functions or &quot;tuples&quot;)</a>
  <a id="5827" class="Symbol">→</a>               <a id="5843" href="Relations.Continuous.html#477" class="Primitive">Type</a> <a id="5848" class="Symbol">_</a>
 <a id="5851" href="Relations.Continuous.html#5492" class="Function">compatible-ΠΡ</a> <a id="5865" class="Symbol">{</a><a id="5866" class="Argument">I</a> <a id="5868" class="Symbol">=</a> <a id="5870" href="Relations.Continuous.html#5870" class="Bound">I</a><a id="5871" class="Symbol">}{</a><a id="5873" href="Relations.Continuous.html#5873" class="Bound">J</a><a id="5874" class="Symbol">}{</a><a id="5876" href="Relations.Continuous.html#5876" class="Bound">𝒜</a><a id="5877" class="Symbol">}</a> <a id="5879" href="Relations.Continuous.html#5879" class="Bound">𝑓</a> <a id="5881" href="Relations.Continuous.html#5881" class="Bound">R</a>  <a id="5884" class="Symbol">=</a> <a id="5886" href="Overture.Preliminaries.html#5735" class="Function">Π[</a> <a id="5889" href="Relations.Continuous.html#5889" class="Bound">t</a> <a id="5891" href="Overture.Preliminaries.html#5735" class="Function">∈</a> <a id="5893" class="Symbol">((</a><a id="5895" href="Relations.Continuous.html#5895" class="Bound">i</a> <a id="5897" class="Symbol">:</a> <a id="5899" href="Relations.Continuous.html#5870" class="Bound">I</a><a id="5900" class="Symbol">)</a> <a id="5902" class="Symbol">→</a> <a id="5904" href="Relations.Continuous.html#5873" class="Bound">J</a> <a id="5906" class="Symbol">→</a> <a id="5908" href="Relations.Continuous.html#5876" class="Bound">𝒜</a> <a id="5910" href="Relations.Continuous.html#5895" class="Bound">i</a><a id="5911" class="Symbol">)</a> <a id="5913" href="Overture.Preliminaries.html#5735" class="Function">]</a> <a id="5915" href="Relations.Continuous.html#5128" class="Function">eval-ΠΡ</a> <a id="5923" href="Relations.Continuous.html#5881" class="Bound">R</a> <a id="5925" href="Relations.Continuous.html#5889" class="Bound">t</a>

<a id="5928" class="Markup">\end{code}</a><a id="5938" class="Background">

#### Elaboration

The first of these is an *evaluation* function which &quot;lifts&quot; an `I`-ary relation to an `(I → J)`-ary relation. The lifted relation will relate an `I`-tuple of `J`-tuples when the &quot;`I`-slices&quot; (or &quot;rows&quot;) of the `J`-tuples belong to the original relation. The second definition denotes compatibility of an operation with a continuous relation.

Readers who find the syntax of the last two definitions nauseating might be helped by an explication of the semantics of these deifnitions. First, internalize the fact that `𝒶 : I → J → A` denotes an `I`-tuple of `J`-tuples of inhabitants of `A`. Next, recall that a continuous relation `R` denotes a certain collection of `I`-tuples (if `x : I → A`, then `R x` asserts that `x` &quot;belongs to&quot; or &quot;satisfies&quot; `R`).  For such `R`, the type `eval-cont-rel R` represents a certain collection of `I`-tuples of `J`-tuples, namely, the tuples `𝒶 : I → J → A` for which `eval-cont-rel R 𝒶` holds.

For simplicity, pretend for a moment that `J` is a finite set, say, `{1, 2, ..., J}`, so that we can write down a couple of the `J`-tuples as columns. For example, here are the i-th and k-th columns (for some `i k : I`).

```
𝒶 i 1      𝒶 k 1
𝒶 i 2      𝒶 k 2  &lt;-- (a row of I such columns forms an I-tuple)
  ⋮          ⋮
𝒶 i J      𝒶 k J
```

Now `eval-cont-rel R 𝒶` is defined by `∀ j → R (λ i → 𝒶 i j)` which asserts that each row of the `I` columns shown above belongs to the original relation `R`. Finally, `cont-compatible-op` takes a `J`-ary operation `𝑓 : Op J A` and an `I`-tuple `𝒶 : I → J → A` of `J`-tuples, and determines whether the `I`-tuple `λ i → 𝑓 (𝑎 i)` belongs to `R`.



--------------------------------------

{% include UALib.Links.md %}

--------------------------------------

[agda-algebras development team]: https://github.com/ualib/agda-algebras#the-agda-algebras-development-team
</a></pre></body></html>