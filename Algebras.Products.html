<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Algebras.Products</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Background">---
layout: default
title : Algebras.Products module (Agda Universal Algebra Library)
date : 2021-01-12
author: [agda-algebras development team][]
---


### Products of Algebras and Product Algebras

This is the [Algebras.Products][] module of the [Agda Universal Algebra Library][].

</a><a id="286" class="Markup">\begin{code}</a>

<a id="300" class="Symbol">{-#</a> <a id="304" class="Keyword">OPTIONS</a> <a id="312" class="Pragma">--without-K</a> <a id="324" class="Pragma">--exact-split</a> <a id="338" class="Pragma">--safe</a> <a id="345" class="Symbol">#-}</a>


<a id="351" class="Keyword">open</a> <a id="356" class="Keyword">import</a> <a id="363" href="Algebras.Basic.html" class="Module">Algebras.Basic</a> <a id="378" class="Keyword">using</a> <a id="384" class="Symbol">(</a> <a id="386" href="Algebras.Basic.html#1210" class="Generalizable">𝓞</a> <a id="388" class="Symbol">;</a> <a id="390" href="Algebras.Basic.html#1212" class="Generalizable">𝓥</a> <a id="392" class="Symbol">;</a> <a id="394" href="Algebras.Basic.html#3576" class="Function">Signature</a> <a id="404" class="Symbol">)</a>

<a id="407" class="Keyword">module</a> <a id="414" href="Algebras.Products.html" class="Module">Algebras.Products</a> <a id="432" class="Symbol">{</a><a id="433" href="Algebras.Products.html#433" class="Bound">𝑆</a> <a id="435" class="Symbol">:</a> <a id="437" href="Algebras.Basic.html#3576" class="Function">Signature</a> <a id="447" href="Algebras.Basic.html#1210" class="Generalizable">𝓞</a> <a id="449" href="Algebras.Basic.html#1212" class="Generalizable">𝓥</a><a id="450" class="Symbol">}</a> <a id="452" class="Keyword">where</a>


<a id="460" class="Comment">-- Imports from Agda (builtin/primitive) and the Agda Standard Library ---------------------</a>
<a id="553" class="Keyword">open</a> <a id="558" class="Keyword">import</a> <a id="565" href="Agda.Primitive.html" class="Module">Agda.Primitive</a>  <a id="581" class="Keyword">using</a> <a id="587" class="Symbol">(</a> <a id="589" href="Agda.Primitive.html#780" class="Primitive">lsuc</a> <a id="594" class="Symbol">;</a> <a id="596" href="Agda.Primitive.html#810" class="Primitive Operator">_⊔_</a> <a id="600" class="Symbol">;</a> <a id="602" href="Agda.Primitive.html#597" class="Postulate">Level</a> <a id="608" class="Symbol">)</a> <a id="610" class="Keyword">renaming</a> <a id="619" class="Symbol">(</a> <a id="621" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="625" class="Symbol">to</a> <a id="628" class="Primitive">Type</a> <a id="633" class="Symbol">)</a>
<a id="635" class="Keyword">open</a> <a id="640" class="Keyword">import</a> <a id="647" href="Data.Product.html" class="Module">Data.Product</a>    <a id="663" class="Keyword">using</a> <a id="669" class="Symbol">(</a> <a id="671" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">_,_</a> <a id="675" class="Symbol">;</a> <a id="677" href="Agda.Builtin.Sigma.html#166" class="Record">Σ</a> <a id="679" class="Symbol">;</a> <a id="681" href="Data.Product.html#916" class="Function">Σ-syntax</a> <a id="690" class="Symbol">)</a>
<a id="692" class="Keyword">open</a> <a id="697" class="Keyword">import</a> <a id="704" href="Relation.Unary.html" class="Module">Relation.Unary</a>  <a id="720" class="Keyword">using</a> <a id="726" class="Symbol">(</a> <a id="728" href="Relation.Unary.html#1101" class="Function">Pred</a> <a id="733" class="Symbol">;</a> <a id="735" href="Relation.Unary.html#1742" class="Function Operator">_⊆_</a> <a id="739" class="Symbol">;</a> <a id="741" href="Relation.Unary.html#1523" class="Function Operator">_∈_</a> <a id="745" class="Symbol">)</a>


<a id="749" class="Comment">-- Imports from agda-algebras --------------------------------------------------------------</a>
<a id="842" class="Keyword">open</a> <a id="847" class="Keyword">import</a> <a id="854" href="Overture.Preliminaries.html" class="Module">Overture.Preliminaries</a> <a id="877" class="Keyword">using</a> <a id="883" class="Symbol">(</a><a id="884" href="Overture.Preliminaries.html#4848" class="Function Operator">_⁻¹</a><a id="887" class="Symbol">;</a> <a id="889" href="Overture.Preliminaries.html#5250" class="Function">𝑖𝑑</a><a id="891" class="Symbol">;</a> <a id="893" href="Overture.Preliminaries.html#4144" class="Function Operator">∣_∣</a><a id="896" class="Symbol">;</a> <a id="898" href="Overture.Preliminaries.html#4182" class="Function Operator">∥_∥</a><a id="901" class="Symbol">)</a>
<a id="903" class="Keyword">open</a> <a id="908" class="Keyword">import</a> <a id="915" href="Algebras.Basic.html" class="Module">Algebras.Basic</a>         <a id="938" class="Keyword">using</a> <a id="944" class="Symbol">(</a> <a id="946" href="Algebras.Basic.html#6389" class="Function">Algebra</a> <a id="954" class="Symbol">;</a> <a id="956" href="Algebras.Basic.html#9889" class="Function Operator">_̂_</a> <a id="960" class="Symbol">;</a> <a id="962" href="Algebras.Basic.html#8637" class="Record">algebra</a> <a id="970" class="Symbol">)</a>

<a id="973" class="Keyword">private</a> <a id="981" class="Keyword">variable</a> <a id="990" href="Algebras.Products.html#990" class="Generalizable">α</a> <a id="992" href="Algebras.Products.html#992" class="Generalizable">β</a> <a id="994" href="Algebras.Products.html#994" class="Generalizable">ρ</a> <a id="996" href="Algebras.Products.html#996" class="Generalizable">𝓘</a> <a id="998" class="Symbol">:</a> <a id="1000" href="Agda.Primitive.html#597" class="Postulate">Level</a>

<a id="1007" class="Markup">\end{code}</a><a id="1017" class="Background">

From now on, the modules of the [UniversalAlgebra][] library will assume a fixed signature `𝑆 : Signature 𝓞 𝓥`.

Recall the informal definition of a *product* of `𝑆`-algebras. Given a type `I : Type 𝓘` and a family `𝒜 : I → Algebra α 𝑆`, the *product* `⨅ 𝒜` is the algebra whose domain is the Cartesian product `Π 𝑖 ꞉ I , ∣ 𝒜 𝑖 ∣` of the domains of the algebras in `𝒜`, and whose operations are defined as follows: if `𝑓` is a `J`-ary operation symbol and if `𝑎 : Π 𝑖 ꞉ I , J → 𝒜 𝑖` is an `I`-tuple of `J`-tuples such that `𝑎 𝑖` is a `J`-tuple of elements of `𝒜 𝑖` (for each `𝑖`), then `(𝑓 ̂ ⨅ 𝒜) 𝑎 := (i : I) → (𝑓 ̂ 𝒜 i)(𝑎 i)`.

In [UniversalAlgebra][] a *product of* `𝑆`-*algebras* is represented by the following type.

</a><a id="1742" class="Markup">\begin{code}</a>

<a id="⨅"></a><a id="1756" href="Algebras.Products.html#1756" class="Function">⨅</a> <a id="1758" class="Symbol">:</a> <a id="1760" class="Symbol">{</a><a id="1761" href="Algebras.Products.html#1761" class="Bound">I</a> <a id="1763" class="Symbol">:</a> <a id="1765" href="Algebras.Products.html#628" class="Primitive">Type</a> <a id="1770" href="Algebras.Products.html#996" class="Generalizable">𝓘</a> <a id="1772" class="Symbol">}(</a><a id="1774" href="Algebras.Products.html#1774" class="Bound">𝒜</a> <a id="1776" class="Symbol">:</a> <a id="1778" href="Algebras.Products.html#1761" class="Bound">I</a> <a id="1780" class="Symbol">→</a> <a id="1782" href="Algebras.Basic.html#6389" class="Function">Algebra</a> <a id="1790" href="Algebras.Products.html#990" class="Generalizable">α</a> <a id="1792" href="Algebras.Products.html#433" class="Bound">𝑆</a> <a id="1794" class="Symbol">)</a> <a id="1796" class="Symbol">→</a> <a id="1798" href="Algebras.Basic.html#6389" class="Function">Algebra</a> <a id="1806" class="Symbol">(</a><a id="1807" href="Algebras.Products.html#996" class="Generalizable">𝓘</a> <a id="1809" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="1811" href="Algebras.Products.html#990" class="Generalizable">α</a><a id="1812" class="Symbol">)</a> <a id="1814" href="Algebras.Products.html#433" class="Bound">𝑆</a>

<a id="1817" href="Algebras.Products.html#1756" class="Function">⨅</a> <a id="1819" class="Symbol">{</a><a id="1820" class="Argument">I</a> <a id="1822" class="Symbol">=</a> <a id="1824" href="Algebras.Products.html#1824" class="Bound">I</a><a id="1825" class="Symbol">}</a> <a id="1827" href="Algebras.Products.html#1827" class="Bound">𝒜</a> <a id="1829" class="Symbol">=</a> <a id="1831" class="Symbol">(</a> <a id="1833" class="Symbol">∀</a> <a id="1835" class="Symbol">(</a><a id="1836" href="Algebras.Products.html#1836" class="Bound">i</a> <a id="1838" class="Symbol">:</a> <a id="1840" href="Algebras.Products.html#1824" class="Bound">I</a><a id="1841" class="Symbol">)</a> <a id="1843" class="Symbol">→</a> <a id="1845" href="Overture.Preliminaries.html#4144" class="Function Operator">∣</a> <a id="1847" href="Algebras.Products.html#1827" class="Bound">𝒜</a> <a id="1849" href="Algebras.Products.html#1836" class="Bound">i</a> <a id="1851" href="Overture.Preliminaries.html#4144" class="Function Operator">∣</a> <a id="1853" class="Symbol">)</a> <a id="1855" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a>           <a id="1867" class="Comment">-- domain of the product algebra</a>
               <a id="1915" class="Symbol">λ</a> <a id="1917" href="Algebras.Products.html#1917" class="Bound">𝑓</a> <a id="1919" href="Algebras.Products.html#1919" class="Bound">𝑎</a> <a id="1921" href="Algebras.Products.html#1921" class="Bound">i</a> <a id="1923" class="Symbol">→</a> <a id="1925" class="Symbol">(</a><a id="1926" href="Algebras.Products.html#1917" class="Bound">𝑓</a> <a id="1928" href="Algebras.Basic.html#9889" class="Function Operator">̂</a> <a id="1930" href="Algebras.Products.html#1827" class="Bound">𝒜</a> <a id="1932" href="Algebras.Products.html#1921" class="Bound">i</a><a id="1933" class="Symbol">)</a> <a id="1935" class="Symbol">λ</a> <a id="1937" href="Algebras.Products.html#1937" class="Bound">x</a> <a id="1939" class="Symbol">→</a> <a id="1941" href="Algebras.Products.html#1919" class="Bound">𝑎</a> <a id="1943" href="Algebras.Products.html#1937" class="Bound">x</a> <a id="1945" href="Algebras.Products.html#1921" class="Bound">i</a>   <a id="1949" class="Comment">-- basic operations of the product algebra</a>

<a id="1993" class="Markup">\end{code}</a><a id="2003" class="Background">

(Alternative acceptable notation for the domain of the product is `∀ i → ∣ 𝒜 i ∣`.)

The type just defined is the one that will be used throughout the [UniversalAlgebra][] library whenever the product of an indexed collection of algebras (of type `Algebra`) is required.  However, for the sake of completeness, here is how one could define a type representing the product of algebras inhabiting the record type `algebra`.

</a><a id="2428" class="Markup">\begin{code}</a>

<a id="2442" class="Keyword">open</a> <a id="2447" href="Algebras.Basic.html#8637" class="Module">algebra</a>

<a id="⨅&#39;"></a><a id="2456" href="Algebras.Products.html#2456" class="Function">⨅&#39;</a> <a id="2459" class="Symbol">:</a> <a id="2461" class="Symbol">{</a><a id="2462" href="Algebras.Products.html#2462" class="Bound">I</a> <a id="2464" class="Symbol">:</a> <a id="2466" href="Algebras.Products.html#628" class="Primitive">Type</a> <a id="2471" href="Algebras.Products.html#996" class="Generalizable">𝓘</a> <a id="2473" class="Symbol">}(</a><a id="2475" href="Algebras.Products.html#2475" class="Bound">𝒜</a> <a id="2477" class="Symbol">:</a> <a id="2479" href="Algebras.Products.html#2462" class="Bound">I</a> <a id="2481" class="Symbol">→</a> <a id="2483" href="Algebras.Basic.html#8637" class="Record">algebra</a> <a id="2491" href="Algebras.Products.html#990" class="Generalizable">α</a> <a id="2493" href="Algebras.Products.html#433" class="Bound">𝑆</a><a id="2494" class="Symbol">)</a> <a id="2496" class="Symbol">→</a> <a id="2498" href="Algebras.Basic.html#8637" class="Record">algebra</a> <a id="2506" class="Symbol">(</a><a id="2507" href="Algebras.Products.html#996" class="Generalizable">𝓘</a> <a id="2509" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="2511" href="Algebras.Products.html#990" class="Generalizable">α</a><a id="2512" class="Symbol">)</a> <a id="2514" href="Algebras.Products.html#433" class="Bound">𝑆</a>

<a id="2517" href="Algebras.Products.html#2456" class="Function">⨅&#39;</a> <a id="2520" class="Symbol">{</a><a id="2521" href="Algebras.Products.html#2521" class="Bound">I</a><a id="2522" class="Symbol">}</a> <a id="2524" href="Algebras.Products.html#2524" class="Bound">𝒜</a> <a id="2526" class="Symbol">=</a> <a id="2528" class="Keyword">record</a> <a id="2535" class="Symbol">{</a> <a id="2537" href="Algebras.Basic.html#8735" class="Field">carrier</a> <a id="2545" class="Symbol">=</a> <a id="2547" class="Symbol">∀</a> <a id="2549" href="Algebras.Products.html#2549" class="Bound">i</a> <a id="2551" class="Symbol">→</a> <a id="2553" href="Algebras.Basic.html#8735" class="Field">carrier</a> <a id="2561" class="Symbol">(</a><a id="2562" href="Algebras.Products.html#2524" class="Bound">𝒜</a> <a id="2564" href="Algebras.Products.html#2549" class="Bound">i</a><a id="2565" class="Symbol">)</a> <a id="2567" class="Symbol">;</a>                 <a id="2585" class="Comment">-- domain</a>
                     <a id="2616" href="Algebras.Basic.html#8754" class="Field">opsymbol</a> <a id="2625" class="Symbol">=</a> <a id="2627" class="Symbol">λ</a> <a id="2629" href="Algebras.Products.html#2629" class="Bound">𝑓</a> <a id="2631" href="Algebras.Products.html#2631" class="Bound">𝑎</a> <a id="2633" href="Algebras.Products.html#2633" class="Bound">i</a> <a id="2635" class="Symbol">→</a> <a id="2637" class="Symbol">(</a><a id="2638" href="Algebras.Basic.html#8754" class="Field">opsymbol</a> <a id="2647" class="Symbol">(</a><a id="2648" href="Algebras.Products.html#2524" class="Bound">𝒜</a> <a id="2650" href="Algebras.Products.html#2633" class="Bound">i</a><a id="2651" class="Symbol">))</a> <a id="2654" href="Algebras.Products.html#2629" class="Bound">𝑓</a> <a id="2656" class="Symbol">λ</a> <a id="2658" href="Algebras.Products.html#2658" class="Bound">x</a> <a id="2660" class="Symbol">→</a> <a id="2662" href="Algebras.Products.html#2631" class="Bound">𝑎</a> <a id="2664" href="Algebras.Products.html#2658" class="Bound">x</a> <a id="2666" href="Algebras.Products.html#2633" class="Bound">i</a> <a id="2668" class="Symbol">}</a> <a id="2670" class="Comment">-- basic operations</a>

<a id="2691" class="Markup">\end{code}</a><a id="2701" class="Background">



**Notation**. Given a signature `𝑆 : Signature 𝓞 𝓥`, the type `Algebra α 𝑆` has type `Type(𝓞 ⊔ 𝓥 ⊔ lsuc α)`.  Such types occur so often in the [UniversalAlgebra][] library that we define the following shorthand for their universes.

</a><a id="2938" class="Markup">\begin{code}</a>

<a id="ov"></a><a id="2952" href="Algebras.Products.html#2952" class="Function">ov</a> <a id="2955" class="Symbol">:</a> <a id="2957" href="Agda.Primitive.html#597" class="Postulate">Level</a> <a id="2963" class="Symbol">→</a> <a id="2965" href="Agda.Primitive.html#597" class="Postulate">Level</a>
<a id="2971" href="Algebras.Products.html#2952" class="Function">ov</a> <a id="2974" href="Algebras.Products.html#2974" class="Bound">α</a> <a id="2976" class="Symbol">=</a> <a id="2978" href="Algebras.Products.html#447" class="Bound">𝓞</a> <a id="2980" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="2982" href="Algebras.Products.html#449" class="Bound">𝓥</a> <a id="2984" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="2986" href="Agda.Primitive.html#780" class="Primitive">lsuc</a> <a id="2991" href="Algebras.Products.html#2974" class="Bound">α</a>

<a id="2994" class="Markup">\end{code}</a><a id="3004" class="Background">



#### &lt;a id=&quot;products-of-classes-of-algebras&quot;&gt;Products of classes of algebras&lt;/a&gt;

An arbitrary class `𝒦` of algebras is represented as a predicate over the type `Algebra α 𝑆`, for some universe level `α` and signature `𝑆`. That is, `𝒦 : Pred (Algebra α 𝑆) β`, for some type `β`. Later we will formally state and prove that the product of all subalgebras of algebras in `𝒦` belongs to the class `SP(𝒦)` of subalgebras of products of algebras in `𝒦`. That is, `⨅ S(𝒦) ∈ SP(𝒦 )`. This turns out to be a nontrivial exercise.

To begin, we need to define types that represent products over arbitrary (nonindexed) families such as `𝒦` or `S(𝒦)`. Observe that `Π 𝒦` is certainly not what we want.  For recall that `Pred (Algebra α 𝑆) β` is just an alias for the function type `Algebra α 𝑆 → Type β`, and the semantics of the latter takes `𝒦 𝑨` (and `𝑨 ∈ 𝒦`) to mean that `𝑨` belongs to the class `𝒦`. Thus, by definition,

```agda
 Π 𝒦   :=   Π 𝑨 ꞉ (Algebra α 𝑆) , 𝒦 𝑨   :=   ∀ (𝑨 : Algebra α 𝑆) → 𝑨 ∈ 𝒦,
```

which asserts that every inhabitant of the type `Algebra α 𝑆` belongs to `𝒦`.  Evidently this is not the product algebra that we seek.

What we need is a type that serves to index the class `𝒦`, and a function `𝔄` that maps an index to the inhabitant of `𝒦` at that index. But `𝒦` is a predicate (of type `(Algebra α 𝑆) → Type β`) and the type `Algebra α 𝑆` seems rather nebulous in that there is no natural indexing class with which to &quot;enumerate&quot; all inhabitants of `Algebra α 𝑆` belonging to `𝒦`.&lt;sup&gt;[1](Algebras.Product.html#fn1)&lt;/sup&gt;

The solution is to essentially take `𝒦` itself to be the indexing type, at least heuristically that is how one can view the type `ℑ` that we now define.&lt;sup&gt;[2](Algebras.Product.html#fn2)&lt;/sup&gt;

</a><a id="4748" class="Markup">\begin{code}</a>

<a id="4762" class="Keyword">module</a> <a id="4769" href="Algebras.Products.html#4769" class="Module">_</a> <a id="4771" class="Symbol">{</a><a id="4772" href="Algebras.Products.html#4772" class="Bound">𝒦</a> <a id="4774" class="Symbol">:</a> <a id="4776" href="Relation.Unary.html#1101" class="Function">Pred</a> <a id="4781" class="Symbol">(</a><a id="4782" href="Algebras.Basic.html#6389" class="Function">Algebra</a> <a id="4790" href="Algebras.Products.html#990" class="Generalizable">α</a> <a id="4792" href="Algebras.Products.html#433" class="Bound">𝑆</a><a id="4793" class="Symbol">)(</a><a id="4795" href="Algebras.Products.html#2952" class="Function">ov</a> <a id="4798" href="Algebras.Products.html#990" class="Generalizable">α</a><a id="4799" class="Symbol">)}</a> <a id="4802" class="Keyword">where</a>
 <a id="4809" href="Algebras.Products.html#4809" class="Function">ℑ</a> <a id="4811" class="Symbol">:</a> <a id="4813" href="Algebras.Products.html#628" class="Primitive">Type</a> <a id="4818" class="Symbol">(</a><a id="4819" href="Algebras.Products.html#2952" class="Function">ov</a> <a id="4822" href="Algebras.Products.html#4790" class="Bound">α</a><a id="4823" class="Symbol">)</a>
 <a id="4826" href="Algebras.Products.html#4809" class="Function">ℑ</a> <a id="4828" class="Symbol">=</a> <a id="4830" href="Data.Product.html#916" class="Function">Σ[</a> <a id="4833" href="Algebras.Products.html#4833" class="Bound">𝑨</a> <a id="4835" href="Data.Product.html#916" class="Function">∈</a> <a id="4837" href="Algebras.Basic.html#6389" class="Function">Algebra</a> <a id="4845" href="Algebras.Products.html#4790" class="Bound">α</a> <a id="4847" href="Algebras.Products.html#433" class="Bound">𝑆</a> <a id="4849" href="Data.Product.html#916" class="Function">]</a> <a id="4851" href="Algebras.Products.html#4833" class="Bound">𝑨</a> <a id="4853" href="Relation.Unary.html#1523" class="Function Operator">∈</a> <a id="4855" href="Algebras.Products.html#4772" class="Bound">𝒦</a>

<a id="4858" class="Markup">\end{code}</a><a id="4868" class="Background">

Taking the product over the index type `ℑ` requires a function that maps an index `i : ℑ` to the corresponding algebra.  Each `i : ℑ` is a pair, `(𝑨 , p)`, where `𝑨` is an algebra and `p` is a proof that `𝑨` belongs to `𝒦`, so the function mapping an index to the corresponding algebra is simply the first projection.

</a><a id="5189" class="Markup">\begin{code}</a>

 <a id="5204" href="Algebras.Products.html#5204" class="Function">𝔄</a> <a id="5206" class="Symbol">:</a> <a id="5208" href="Algebras.Products.html#4809" class="Function">ℑ</a> <a id="5210" class="Symbol">→</a> <a id="5212" href="Algebras.Basic.html#6389" class="Function">Algebra</a> <a id="5220" href="Algebras.Products.html#4790" class="Bound">α</a> <a id="5222" href="Algebras.Products.html#433" class="Bound">𝑆</a>
 <a id="5225" href="Algebras.Products.html#5204" class="Function">𝔄</a> <a id="5227" href="Algebras.Products.html#5227" class="Bound">i</a> <a id="5229" class="Symbol">=</a> <a id="5231" href="Overture.Preliminaries.html#4144" class="Function Operator">∣</a> <a id="5233" href="Algebras.Products.html#5227" class="Bound">i</a> <a id="5235" href="Overture.Preliminaries.html#4144" class="Function Operator">∣</a>

<a id="5238" class="Markup">\end{code}</a><a id="5248" class="Background">

Finally, we define `class-product` which represents the product of all members of 𝒦.

</a><a id="5336" class="Markup">\begin{code}</a>

 <a id="5351" href="Algebras.Products.html#5351" class="Function">class-product</a> <a id="5365" class="Symbol">:</a> <a id="5367" href="Algebras.Basic.html#6389" class="Function">Algebra</a> <a id="5375" class="Symbol">(</a><a id="5376" href="Algebras.Products.html#2952" class="Function">ov</a> <a id="5379" href="Algebras.Products.html#4790" class="Bound">α</a><a id="5380" class="Symbol">)</a> <a id="5382" href="Algebras.Products.html#433" class="Bound">𝑆</a>
 <a id="5385" href="Algebras.Products.html#5351" class="Function">class-product</a> <a id="5399" class="Symbol">=</a> <a id="5401" href="Algebras.Products.html#1756" class="Function">⨅</a> <a id="5403" href="Algebras.Products.html#5204" class="Function">𝔄</a>

<a id="5406" class="Markup">\end{code}</a><a id="5416" class="Background">

If `p : 𝑨 ∈ 𝒦`, we view the pair `(𝑨 , p) ∈ ℑ` as an *index* over the class, so we can think of `𝔄 (𝑨 , p)` (which is simply `𝑨`) as the projection of the product `⨅ 𝔄` onto the `(𝑨 , p)`-th component.



-----------------------

&lt;sup&gt;1&lt;/sup&gt;&lt;span class=&quot;footnote&quot; id=&quot;fn1&quot;&gt; If you haven&#39;t seen this before, give it some thought and see if the correct type comes to you organically.&lt;/span&gt;

&lt;sup&gt;2&lt;/sup&gt;&lt;span class=&quot;footnote&quot; id=&quot;fn2&quot;&gt; **Unicode Hints**. Some of our types are denoted with with Gothic (&quot;mathfrak&quot;) symbols. To produce them in [agda2-mode][], type `\Mf` followed by a letter. For example, `\MfI` ↝ `ℑ`.&lt;/span&gt;

[← Algebras.Basic](Algebras.Basic.html)
&lt;span style=&quot;float:right;&quot;&gt;[Algebras.Congruences →](Algebras.Congruences.html)&lt;/span&gt;

{% include UALib.Links.md %}

-----------------------------------------------

[agda-algebras development team]: https://github.com/ualib/agda-algebras#the-agda-algebras-development-team
</a></pre></body></html>