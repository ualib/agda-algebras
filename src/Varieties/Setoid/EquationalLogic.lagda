---
layout: default
title : "Varieties.Setoid.EquationalLogic module (The Agda Universal Algebra Library)"
date : "2021-01-14"
author: "agda-algebras development team"
---

#### <a id="entailment-derivation-rules-soundness-and-completeness">Entailment, derivation rules, soundness and completeness</a>

This is the [Varieties.Setoid.EquationalLogic][] module of the [Agda Universal Algebra Library][].

This module is based on [Andreas Abel's Agda formalization of Birkhoff's completeness theorem](http://www.cse.chalmers.se/~abela/agda/MultiSortedAlgebra.pdf).

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Algebras.Basic using ( ğ“ ; ğ“¥ ; Signature )

module Varieties.Setoid.EquationalLogic {ğ‘† : Signature ğ“ ğ“¥} where

-- imports from Agda and the Agda Standard Library -------------------------------------------
open import Agda.Primitive   using ( _âŠ”_ ; lsuc ; Level ) renaming ( Set to Type )
open import Data.Product     using ( _,_ ; Î£-syntax ; _Ã—_ ) renaming ( projâ‚ to fst ; projâ‚‚ to snd )
open import Function.Base    using ( _âˆ˜_ ; flip )
open import Function.Bundles using ( Func )
open import Relation.Binary  using ( Setoid ; IsEquivalence )
open import Relation.Unary   using ( Pred ; _âˆˆ_ )
open import Relation.Binary.PropositionalEquality using ( _â‰¡_ ; refl )
import Relation.Binary.Reasoning.Setoid as SetoidReasoning

-- Imports from the Agda Universal Algebra Library ---------------------------------------------
open import Overture.Preliminaries        using ( âˆ£_âˆ£ )
open import Algebras.Func.Basic   {ğ‘† = ğ‘†} using ( SetoidAlgebra ; ov ) renaming ( âŸ¦_âŸ§ to âŸ¦_âŸ§s )
open import Terms.Basic           {ğ‘† = ğ‘†} using ( Term )
open import Terms.Func.Basic      {ğ‘† = ğ‘†} using ( module Environment ; Sub ; _[_] )

open Setoid        using ( Carrier ; _â‰ˆ_ ; isEquivalence )
open Func          renaming ( f to _âŸ¨$âŸ©_ )
open Term

private variable
 Ï‡ Î± Ï â„“ : Level
 X Î“ Î” : Type Ï‡
 f     : âˆ£ ğ‘† âˆ£

-- Equations
-- An equation is a pair (s , t) of terms in the same context.
record Eq : Type (ov Ï‡) where
 constructor _â‰ˆÌ‡_
 field
  {cxt} : Type Ï‡
  lhs   : Term cxt
  rhs   : Term cxt

open Eq public

-- Equation p â‰ˆÌ‡ q holding in algebra M. (type \~~\^. to get â‰ˆÌ‡) (type \|= to get âŠ¨)
_âŠ¨_ : (M : SetoidAlgebra Î± â„“)(term-identity : Eq{Ï‡}) â†’ Type _
M âŠ¨ (p â‰ˆÌ‡ q) = Equal p q  where open Environment M

module _ {Î¹ : Level}{I : Type Î¹} where

 -- An I-indexed set of equations inhabits the type I â†’ Eq.
 -- For such `E : I â†’ Eq`...

 -- ...`ğ‘¨ âŠ§ E` is the assertion that algebra ğ‘¨ models all equations in a set E.
 _âŠ§_ : (ğ‘¨ : SetoidAlgebra Î± Ï)(E : I â†’ Eq{Ï‡}) â†’ Type _
 ğ‘¨ âŠ§ E = âˆ€ i â†’ Equal (lhs (E i))(rhs (E i))       -- (type \models to get âŠ§)
  where open Environment ğ‘¨

 -- ...`Mod E` is the class of algebras that model all term equations in E.
 Mod : (I â†’ Eq{Ï‡}) â†’ Pred(SetoidAlgebra Î± Ï) (Ï‡ âŠ” Î¹ âŠ” Î± âŠ” Ï)
 Mod E = _âŠ§ E

_âŠ«_ : Pred (SetoidAlgebra Î± Ï) â„“ â†’ Eq{Ï‡} â†’ Type _
ğ’¦ âŠ« eq = âˆ€ ğ‘¨ â†’ ğ’¦ ğ‘¨ â†’ ğ‘¨ âŠ¨ eq                        -- (type \||= to get âŠ«)

module _ {Î± Ï â„“ Ï‡ : Level}{X : Type Ï‡} where

 ThPred : Pred (SetoidAlgebra Î± Ï) â„“ â†’ Pred(Term X Ã— Term X) (â„“ âŠ” Ï‡ âŠ” ov (Î± âŠ” Ï))
 ThPred ğ’¦ = Î» (p , q) â†’ ğ’¦ âŠ« (p â‰ˆÌ‡ q)

 â„‘Th : Pred(Term X Ã— Term X) (â„“ âŠ” Ï‡ âŠ” ov (Î± âŠ” Ï)) â†’ Type (â„“ âŠ” ov (Î± âŠ” Ï âŠ” Ï‡))
 â„‘Th P = Î£[ p âˆˆ (Term X Ã— Term X) ] p âˆˆ P

 Th : (ğ’¦ : Pred (SetoidAlgebra Î± Ï) â„“) â†’ â„‘Th (ThPred ğ’¦) â†’ Eq{Ï‡}
 Th ğ’¦ = Î» i â†’ fst âˆ£ i âˆ£ â‰ˆÌ‡ snd âˆ£ i âˆ£

module _ {Î±}{Ï}{Î¹}{I : Type Î¹} where
 -- An entailment E âŠƒ eq holds iff it holds in all models of E.
 _âŠƒ_ : (E : I â†’ Eq{Ï‡}) (eq : Eq{Ï‡}) â†’ Type _
 E âŠƒ eq = (M : SetoidAlgebra Î± Ï) â†’ M âŠ§ E â†’ M âŠ¨ eq

\end{code}


##### <a id="derivations-in-a-context">Derivations in a context</a>

(Based on [Andreas Abel's Agda formalization of Birkhoff's completeness theorem](http://www.cse.chalmers.se/~abela/agda/MultiSortedAlgebra.pdf).)

\begin{code}

module _ {Ï‡ Î¹ : Level} where

 data _âŠ¢_â–¹_â‰ˆ_ {I : Type Î¹}(E : I â†’ Eq) : (X : Type Ï‡)(p q : Term X) â†’ Type (Î¹ âŠ” ov Ï‡) where
  hyp   : âˆ€ i â†’ let p â‰ˆÌ‡ q = E i in E âŠ¢ _ â–¹ p â‰ˆ q
  app   : âˆ€ {ps qs} â†’ (âˆ€ i â†’ E âŠ¢ Î“ â–¹ ps i â‰ˆ qs i) â†’ E âŠ¢ Î“ â–¹ (node f ps) â‰ˆ (node f qs)
  sub   : âˆ€ {p q} â†’ E âŠ¢ Î” â–¹ p â‰ˆ q â†’ âˆ€ (Ïƒ : Sub Î“ Î”) â†’ E âŠ¢ Î“ â–¹ (p [ Ïƒ ]) â‰ˆ (q [ Ïƒ ])

  refl  : âˆ€ {p}              â†’ E âŠ¢ Î“ â–¹ p â‰ˆ p
  sym   : âˆ€ {p q : Term Î“}   â†’ E âŠ¢ Î“ â–¹ p â‰ˆ q â†’ E âŠ¢ Î“ â–¹ q â‰ˆ p
  trans : âˆ€ {p q r : Term Î“} â†’ E âŠ¢ Î“ â–¹ p â‰ˆ q â†’ E âŠ¢ Î“ â–¹ q â‰ˆ r â†’ E âŠ¢ Î“ â–¹ p â‰ˆ r

\end{code}



##### <a id="soundness-of-the-inference-rules">Soundness of the inference rules</a>

(Based on [Andreas Abel's Agda formalization of Birkhoff's completeness theorem](see: http://www.cse.chalmers.se/~abela/agda/MultiSortedAlgebra.pdf).)

\begin{code}

module Soundness {Ï‡ Î± Ï Î¹ : Level}{I : Type Î¹} (E : I â†’ Eq{Ï‡})
                 (ğ‘¨ : SetoidAlgebra Î± Ï)     -- We assume an algebra M
                 (V : ğ‘¨ âŠ§ E)         -- that models all equations in E.
                 where
 open SetoidAlgebra ğ‘¨ using ( Interp ) renaming (Domain to A)

 -- In any model M that satisfies the equations E, derived equality is actual equality.
 open SetoidReasoning A

 open Environment ğ‘¨ renaming ( âŸ¦_âŸ§s to âŸª_âŸ« )
 open IsEquivalence renaming ( refl to reflâ‰ˆ ; sym to  symâ‰ˆ ; trans to transâ‰ˆ )

 sound : âˆ€ {p q} â†’ E âŠ¢ X â–¹ p â‰ˆ q â†’ ğ‘¨ âŠ¨ (p â‰ˆÌ‡ q)
 sound (hyp i)                      =  V i
 sound (app {f = f} es) Ï           =  Interp .cong (refl , Î» i â†’ sound (es i) Ï)
 sound (sub {p = p} {q} Epq Ïƒ) Ï    =  begin
                                       âŸ¦ p [ Ïƒ ] âŸ§ âŸ¨$âŸ© Ï       â‰ˆâŸ¨ substitution p Ïƒ Ï âŸ©
                                       âŸ¦ p       âŸ§ âŸ¨$âŸ© âŸª Ïƒ âŸ« Ï â‰ˆâŸ¨ sound Epq (âŸª Ïƒ âŸ« Ï)  âŸ©
                                       âŸ¦ q       âŸ§ âŸ¨$âŸ© âŸª Ïƒ âŸ« Ï â‰ˆË˜âŸ¨ substitution  q Ïƒ Ï âŸ©
                                       âŸ¦ q [ Ïƒ ] âŸ§ âŸ¨$âŸ© Ï       âˆ

 sound (refl {p = p})               = reflâ‰ˆ  isEquiv {x = p}
 sound (sym {p = p} {q} Epq)        = symâ‰ˆ   isEquiv {x = p}{q}   (sound Epq)
 sound (trans{p = p}{q}{r} Epq Eqr) = transâ‰ˆ isEquiv {i = p}{q}{r}(sound Epq)(sound Eqr)

\end{code}

The deductive closure of a set E is the set of equations modeled by all models of E; that is, Th Mod E.

The soundness proof above shows âˆ€ X â†’ E âŠ¢ X â–¹ p â‰ˆ q â†’ (p , q) âˆˆ Th Mod â„°.
That is,  âˆ€ X â†’ E âŠ¢ X â–¹ p â‰ˆ q â†’ Mod E âŠ« p â‰ˆ q.

The converse is Birkhoff's completeness theorem: if Mod E âŠ« p â‰ˆ q, then E âŠ¢ X â–¹ p â‰ˆ q.

We will prove that result next.



##### <a id="birkhoffs-completeness-theorem">Birkhoff's completeness theorem</a>

The proof proceeds by constructing a relatively free algebra consisting of term
quotiented by derivable equality E âŠ¢ X â–¹ _â‰ˆ_.  It then suffices to prove
that this model satisfies all equations in $E$.

(Based on [Andreas Abel's Agda formalization of Birkhoff's completeness theorem](http://www.cse.chalmers.se/~abela/agda/MultiSortedAlgebra.pdf).)

We denote by `ğ”½[ X ]` the *relatively free algebra* over `X` (relative to `E`), which is defined as `Term X` modulo `E âŠ¢ X â–¹ _â‰ˆ_`.  This algebra `ğ”½[ X ]` is "free" or "initial" in the variety of algebras satisfying the identities in `E` in the sense that it satisfies the following universal property: for each algebra `ğ‘¨`, if `ğ‘¨ âŠ§ E`, then there is a unique homomorphism from `ğ”½[ X ]` to `ğ‘¨`.

\begin{code}

module FreeAlgebra {Ï‡ : Level}{X : Type Ï‡}{Î¹ : Level}{I : Type Î¹}(E : I â†’ Eq) where
 open SetoidAlgebra

 -- Domain of the relatively free algebra.
 FreeDomain : Type Ï‡ â†’ Setoid _ _
 FreeDomain X = record { Carrier       = Term X
                       ; _â‰ˆ_           = E âŠ¢ X â–¹_â‰ˆ_
                       ; isEquivalence = record { refl = refl ; sym = sym ; trans = trans }
                       }

 -- The interpretation of an operation is simply the operation itself.
 -- This works since E âŠ¢ X â–¹_â‰ˆ_ is a congruence.
 FreeInterp : âˆ€ {X} â†’ Func (âŸ¦ ğ‘† âŸ§s (FreeDomain X)) (FreeDomain X)
 FreeInterp âŸ¨$âŸ© (f , ts) = node f ts
 cong FreeInterp (refl , h) = app h


 -- The relatively free algebra
 ğ”½[_] : Type Ï‡ â†’ SetoidAlgebra _ _
 Domain ğ”½[ X ] = FreeDomain X
 Interp ğ”½[ X ] = FreeInterp

 open Environment ğ”½[ X ]
 open SetoidAlgebra ğ”½[ X ] using ( Interp ) renaming ( Domain to ğ”½ )

 -- The identity substitution Ïƒâ‚€ maps variables to themselves.
 Ïƒâ‚€ : {X : Type Ï‡} â†’ Sub X X
 Ïƒâ‚€ x = â„Š x

 -- Ïƒâ‚€ acts indeed as identity.
 identity : (t : Term X) â†’ E âŠ¢ X â–¹ t [ Ïƒâ‚€ ] â‰ˆ t
 identity (â„Š x) = refl
 identity (node f ts) = app (identity âˆ˜ ts)

\end{code}

Evaluation in the term model is substitution `E âŠ¢ X â–¹ âŸ¦tâŸ§Ïƒ â‰ˆ t[Ïƒ]`. (This would
hold "on the nose" if we had function extensionality.)

\begin{code}

 evaluation : (t : Term Î”) (Ïƒ : Sub X Î”) â†’ E âŠ¢ X â–¹ (âŸ¦ t âŸ§ âŸ¨$âŸ© Ïƒ) â‰ˆ (t [ Ïƒ ])
 evaluation (â„Š x)    Ïƒ = refl
 evaluation (node f ts)  Ïƒ = app (flip (evaluation âˆ˜ ts) Ïƒ)

 -- The term model satisfies all the equations it started out with.
 satisfies : âˆ€ i â†’ ğ”½[ X ] âŠ¨ E i
 satisfies i Ïƒ = begin
                 âŸ¦ p âŸ§ âŸ¨$âŸ© Ïƒ  â‰ˆâŸ¨ evaluation p Ïƒ âŸ©
                 p [ Ïƒ ]        â‰ˆâŸ¨ sub (hyp i) Ïƒ âŸ©
                 q [ Ïƒ ]        â‰ˆË˜âŸ¨ evaluation q Ïƒ âŸ©
                 âŸ¦ q âŸ§ âŸ¨$âŸ© Ïƒ  âˆ
                 where
                  open SetoidReasoning ğ”½
                  p = lhs (E i)
                  q = rhs (E i)

\end{code}

We are finally ready to formally state and prove Birkhoff's Completeness Theorem, which asserts that every valid consequence is derivable.

\begin{code}

 completeness : âˆ€ p q â†’ Mod E âŠ« (p â‰ˆÌ‡ q) â†’ E âŠ¢ X â–¹ p â‰ˆ q
 completeness p q V = begin
       p              â‰ˆË˜âŸ¨ identity p âŸ©
       p [ Ïƒâ‚€ ]       â‰ˆË˜âŸ¨ evaluation p Ïƒâ‚€ âŸ©
       âŸ¦ p âŸ§ âŸ¨$âŸ© Ïƒâ‚€   â‰ˆâŸ¨ V ğ”½[ X ] satisfies Ïƒâ‚€ âŸ©
       âŸ¦ q âŸ§ âŸ¨$âŸ© Ïƒâ‚€   â‰ˆâŸ¨ evaluation q Ïƒâ‚€ âŸ©
       q [ Ïƒâ‚€ ]       â‰ˆâŸ¨ identity q âŸ©
       q              âˆ
  where open SetoidReasoning ğ”½
\end{code}

--------------------------------

<span style="float:left;">[â†‘ Varieties.Setoid](Varieties.Setoid.html)</span>
<span style="float:right;">[Varieties.Setoid.Closure â†’](Varieties.Setoid.Closure.html)</span>

{% include UALib.Links.md %}
