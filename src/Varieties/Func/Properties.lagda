---
layout: default
title : "Varieties.Func.Properties module (The Agda Universal Algebra Library)"
date : "2021-09-24"
author: "agda-algebras development team"
---

### <a id="properties-of-the-models-relation">Properties of the models relation for setoid algebras</a>

We prove some closure and invariance properties of the relation `âŠ§`.  In particular, we prove the following facts (which are needed, for example, in the proof the Birkhoff HSP Theorem).

* [Algebraic invariance](#algebraic-invariance). `âŠ§` is an *algebraic invariant* (stable under isomorphism).

* [Subalgebraic invariance](#subalgebraic-invariance). Identities modeled by a class of algebras are also modeled by all subalgebras of algebras in the class.

* [Product invariance](#product-invariance). Identities modeled by a class of algebras are also modeled by all products of algebras in the class.


\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Algebras.Basic using ( ğ“ ; ğ“¥ ; Signature )

module Varieties.Func.Properties {ğ‘† : Signature ğ“ ğ“¥} where

-- Imports from Agda and the Agda Standard Library -------------------------------------------
open import Agda.Primitive   using ( Level ) renaming ( Set to Type )
open import Data.Product     using ( _,_ )
open import Function.Base    using ( _âˆ˜_ )
open import Function.Bundles using ( Func )
open import Relation.Binary  using ( Setoid )
open import Relation.Unary   using ( Pred ; _âˆˆ_ )
import Relation.Binary.Reasoning.Setoid as SetoidReasoning

-- Imports from the Agda Universal Algebra Library ---------------------------------------------
open import Overture.Preliminaries             using ( âˆ£_âˆ£ ; âˆ¥_âˆ¥ )

open import Algebras.Func.Basic             {ğ‘† = ğ‘†} using ( SetoidAlgebra ; Lift-AlgË¡ ; ov )
open import Algebras.Func.Products          {ğ‘† = ğ‘†} using ( â¨… )
open import Homomorphisms.Func.Basic        {ğ‘† = ğ‘†} using ( hom )
open import Homomorphisms.Func.Isomorphisms {ğ‘† = ğ‘†} using ( _â‰…_ ; mkiso ; Lift-â‰…Ë¡ ; â‰…-sym )
open import Terms.Basic                     {ğ‘† = ğ‘†} using ( Term )
open import Terms.Func.Basic                {ğ‘† = ğ‘†} using ( ğ‘» ; _â‰_ ; module Environment )
open import Terms.Func.Operations           {ğ‘† = ğ‘†} using ( comm-hom-term ; interp-prod ; term-agreement )
open import Subalgebras.Func.Subalgebras    {ğ‘† = ğ‘†} using ( _â‰¤_ ; SubalgebrasOfClass )
open import Varieties.Func.EquationalLogic  {ğ‘† = ğ‘†}  using ( _âŠ§_â‰ˆ_ ; _âŠ«_â‰ˆ_ )

private variable
 Î± Ïáµƒ Î² Ïáµ‡ Ï‡ â„“ : Level

open Func          renaming ( f to _âŸ¨$âŸ©_ )
\end{code}


#### <a id="algebraic-invariance-of-models">Algebraic invariance of âŠ§</a>

The binary relation âŠ§ would be practically useless if it were not an *algebraic invariant* (i.e., invariant under isomorphism).

\begin{code}

open Term
-- open â‰¡-Reasoning
open _â‰…_

open Func using ( cong ) renaming (f to _âŸ¨$âŸ©_ )

module _ {X : Type Ï‡}{ğ‘¨ : SetoidAlgebra Î± Ïáµƒ}
         (ğ‘© : SetoidAlgebra Î² Ïáµ‡)(p q : Term X) where
 open SetoidAlgebra ğ‘¨ using () renaming (Domain to A )
 open Environment ğ‘¨ using () renaming ( âŸ¦_âŸ§ to âŸ¦_âŸ§â‚ )
 open Setoid A using ( ) renaming ( _â‰ˆ_ to _â‰ˆâ‚_ )


 open SetoidAlgebra ğ‘© using () renaming (Domain to B )
 open Environment ğ‘© using () renaming ( âŸ¦_âŸ§ to âŸ¦_âŸ§â‚‚ )
 open Setoid B using ( _â‰ˆ_ ; sym )
 open SetoidReasoning B

 âŠ§-I-invar : ğ‘¨ âŠ§ p â‰ˆ q  â†’  ğ‘¨ â‰… ğ‘©  â†’  ğ‘© âŠ§ p â‰ˆ q
 âŠ§-I-invar Apq (mkiso fh gh fâˆ¼g gâˆ¼f) Ï =
  begin
   âŸ¦ p âŸ§â‚‚ âŸ¨$âŸ© Ï             â‰ˆË˜âŸ¨ cong âŸ¦ p âŸ§â‚‚ (Î» x â†’ fâˆ¼g (Ï x)) âŸ©
   âŸ¦ p âŸ§â‚‚ âŸ¨$âŸ© (f âˆ˜ (g âˆ˜ Ï)) â‰ˆË˜âŸ¨ comm-hom-term fh p (g âˆ˜ Ï) âŸ©
   f (âŸ¦ p âŸ§â‚ âŸ¨$âŸ© (g âˆ˜ Ï))   â‰ˆâŸ¨ cong âˆ£ fh âˆ£ (Apq (g âˆ˜ Ï)) âŸ©
   f (âŸ¦ q âŸ§â‚ âŸ¨$âŸ© (g âˆ˜ Ï))   â‰ˆâŸ¨ comm-hom-term fh q (g âˆ˜ Ï) âŸ©
   âŸ¦ q âŸ§â‚‚ âŸ¨$âŸ© (f âˆ˜ (g âˆ˜ Ï)) â‰ˆâŸ¨ cong âŸ¦ q âŸ§â‚‚ (Î» x â†’ fâˆ¼g (Ï x)) âŸ©
   âŸ¦ q âŸ§â‚‚ âŸ¨$âŸ© Ï             âˆ
  where private f = _âŸ¨$âŸ©_ âˆ£ fh âˆ£ ; g = _âŸ¨$âŸ©_ âˆ£ gh âˆ£

\end{code}


 As the proof makes clear, we show ğ‘© âŠ§ p â‰ˆ q by showing that `ğ‘© âŸ¦ p âŸ§ â‰¡ ğ‘© âŸ¦ q âŸ§ holds *extensionally*, that is, `âˆ€ x, ğ‘© âŸ¦ p âŸ§ x â‰¡ ğ‘© âŸ¦q âŸ§ x`.

#### <a id="lift-invariance">Lift-invariance of âŠ§</a>
The âŠ§ relation is also invariant under the algebraic lift and lower operations.

\begin{code}

-- module _ (wd : SwellDef){Î± Î² Ï‡ : Level}{X : Type Ï‡}{ğ‘¨ : SetoidAlgebra Î± Ïáµƒ} where
module _ {X : Type Ï‡}{ğ‘¨ : SetoidAlgebra Î± Ïáµƒ} where

 âŠ§-Lift-invar : (p q : Term X) â†’ ğ‘¨ âŠ§ p â‰ˆ q â†’ Lift-AlgË¡ ğ‘¨ Î² âŠ§ p â‰ˆ q
 âŠ§-Lift-invar p q Apq = âŠ§-I-invar (Lift-AlgË¡ ğ‘¨ _) p q Apq Lift-â‰…Ë¡

 âŠ§-lower-invar : (p q : Term X) â†’ Lift-AlgË¡ ğ‘¨ Î² âŠ§ p â‰ˆ q  â†’  ğ‘¨ âŠ§ p â‰ˆ q
 âŠ§-lower-invar p q lApq = âŠ§-I-invar ğ‘¨ p q lApq (â‰…-sym Lift-â‰…Ë¡)

\end{code}



#### <a id="subalgebraic-invariance">Subalgebraic invariance of âŠ§</a>

Identities modeled by an algebra `ğ‘¨` are also modeled by every subalgebra of `ğ‘¨`, which fact can be formalized as follows.

\begin{code}

module _ {X : Type Ï‡}{ğ‘¨ : SetoidAlgebra Î± Ïáµƒ}
         (ğ‘© : SetoidAlgebra Î² Ïáµ‡)(p q : Term X) where
 open SetoidAlgebra ğ‘¨ using () renaming (Domain to A )
 open Environment ğ‘¨ using () renaming ( âŸ¦_âŸ§ to âŸ¦_âŸ§â‚ )
 open Setoid A using ( ) renaming ( _â‰ˆ_ to _â‰ˆâ‚_ )


 open SetoidAlgebra ğ‘© using () renaming (Domain to B )
 open Environment ğ‘© using () renaming ( âŸ¦_âŸ§ to âŸ¦_âŸ§â‚‚ )
 open Setoid B using ( _â‰ˆ_ ; sym )
 open SetoidReasoning A


 âŠ§-S-invar : ğ‘¨ âŠ§ p â‰ˆ q  â†’  ğ‘© â‰¤ ğ‘¨  â†’  ğ‘© âŠ§ p â‰ˆ q
 âŠ§-S-invar Apq Bâ‰¤A b = goal
  where
  hh : hom ğ‘© ğ‘¨
  hh = âˆ£ Bâ‰¤A âˆ£
  h = _âŸ¨$âŸ©_ âˆ£ hh âˆ£
  Î¾ : âˆ€ b â†’ h (âŸ¦ p âŸ§â‚‚ âŸ¨$âŸ© b) â‰ˆâ‚ h (âŸ¦ q âŸ§â‚‚ âŸ¨$âŸ© b)
  Î¾ b = begin
         h (âŸ¦ p âŸ§â‚‚ âŸ¨$âŸ© b)   â‰ˆâŸ¨ comm-hom-term hh p b âŸ©
         âŸ¦ p âŸ§â‚ âŸ¨$âŸ© (h âˆ˜ b) â‰ˆâŸ¨ Apq (h âˆ˜ b) âŸ©
         âŸ¦ q âŸ§â‚ âŸ¨$âŸ© (h âˆ˜ b) â‰ˆË˜âŸ¨ comm-hom-term hh q b âŸ©
         h (âŸ¦ q âŸ§â‚‚ âŸ¨$âŸ© b)   âˆ
  goal : âŸ¦ p âŸ§â‚‚ âŸ¨$âŸ© b â‰ˆ âŸ¦ q âŸ§â‚‚ âŸ¨$âŸ© b
  goal = âˆ¥ Bâ‰¤A âˆ¥ (Î¾ b)


\end{code}

Next, identities modeled by a class of algebras is also modeled by all subalgebras of the class.  In other terms, every term equation `p â‰ˆ q` that is satisfied by all `ğ‘¨ âˆˆ ğ’¦` is also satisfied by every subalgebra of a member of ğ’¦.

\begin{code}
module _ {X : Type Ï‡}{ğ‘¨ : SetoidAlgebra Î± Ïáµƒ}
         (p q : Term X) where
 open SetoidAlgebra ğ‘¨ using () renaming (Domain to A )
 open Environment ğ‘¨ using () renaming ( âŸ¦_âŸ§ to âŸ¦_âŸ§â‚ )
 open Setoid A using ( ) renaming ( _â‰ˆ_ to _â‰ˆâ‚_ )

 open SetoidReasoning A

 âŠ§-S-class-invar : {ğ’¦ : Pred (SetoidAlgebra Î± Ïáµƒ) â„“}
  â†’                (ğ’¦ âŠ« p â‰ˆ q) â†’ (ğ‘© : SubalgebrasOfClass ğ’¦ {Î²}{Ïáµ‡})
  â†’                âˆ£ ğ‘© âˆ£ âŠ§ p â‰ˆ q
 âŠ§-S-class-invar Kpq (ğ‘© , ğ‘¨ , kA , Bâ‰¤A) = âŠ§-S-invar ğ‘© p q (Kpq kA) Bâ‰¤A

\end{code}



#### <a id="product-invariance">Product invariance of âŠ§</a>

An identity satisfied by all algebras in an indexed collection is also satisfied by the product of algebras in that collection.

\begin{code}

module _ {I : Type â„“}(ğ’œ : I â†’ SetoidAlgebra Î± Ïáµƒ){X : Type Ï‡} where

 open SetoidAlgebra (â¨… ğ’œ) using () renaming ( Domain to â¨…A )
 open Setoid â¨…A using ( _â‰ˆ_ )
 open Environment (â¨… ğ’œ) using () renaming ( âŸ¦_âŸ§ to âŸ¦_âŸ§â‚ )
 open Environment using ( âŸ¦_âŸ§ )
 open SetoidReasoning â¨…A
 âŠ§-P-invar : (p q : Term X) â†’ (âˆ€ i â†’ ğ’œ i âŠ§ p â‰ˆ q) â†’ â¨… ğ’œ âŠ§ p â‰ˆ q
 âŠ§-P-invar p q ğ’œpq a = goal
  where
  Î¾ : (Î» i â†’ (âŸ¦ ğ’œ i âŸ§ p) âŸ¨$âŸ© (Î» x â†’ (a x) i)) â‰ˆ (Î» i â†’ (âŸ¦ ğ’œ i âŸ§ q) âŸ¨$âŸ© (Î» x â†’ (a x) i))
  Î¾ = Î» i â†’ ğ’œpq i (Î» x â†’ (a x) i)
  goal : âŸ¦ p âŸ§â‚ âŸ¨$âŸ© a â‰ˆ âŸ¦ q âŸ§â‚ âŸ¨$âŸ© a
  goal = begin
          âŸ¦ p âŸ§â‚ âŸ¨$âŸ© a â‰ˆâŸ¨ interp-prod ğ’œ p a âŸ©
          (Î» i â†’ (âŸ¦ ğ’œ i âŸ§ p) âŸ¨$âŸ© (Î» x â†’ (a x) i)) â‰ˆâŸ¨ Î¾ âŸ©
          (Î» i â†’ (âŸ¦ ğ’œ i âŸ§ q) âŸ¨$âŸ© (Î» x â†’ (a x) i)) â‰ˆË˜âŸ¨ interp-prod ğ’œ q a âŸ©
          âŸ¦ q âŸ§â‚ âŸ¨$âŸ© a âˆ

\end{code}

An identity satisfied by all algebras in a class is also satisfied by the product of algebras in the class.

\begin{code}

 âŠ§-P-class-invar : (ğ’¦ : Pred (SetoidAlgebra Î± Ïáµƒ)(ov Î±)){p q : Term X}
  â†’                ğ’¦ âŠ« p â‰ˆ q â†’ (âˆ€ i â†’ ğ’œ i âˆˆ ğ’¦) â†’ â¨… ğ’œ âŠ§ p â‰ˆ q

 âŠ§-P-class-invar ğ’¦ {p}{q}Ïƒ Kğ’œ = âŠ§-P-invar p q (Î» i Ï â†’ Ïƒ (Kğ’œ i) Ï)

\end{code}

Another fact that will turn out to be useful is that a product of a collection of algebras models p â‰ˆ q if the lift of each algebra in the collection models p â‰ˆ q.

\begin{code}

 âŠ§-P-lift-invar : (p q : Term X) â†’ (âˆ€ i â†’ Lift-AlgË¡ (ğ’œ i) Î² âŠ§ p â‰ˆ q)  â†’  â¨… ğ’œ âŠ§ p â‰ˆ q
 âŠ§-P-lift-invar p q Î± = âŠ§-P-invar p q Aipq
  where
  Aipq : âˆ€ i â†’ (ğ’œ i) âŠ§ p â‰ˆ q
  Aipq i = âŠ§-lower-invar{ğ‘¨ = (ğ’œ i)} p q (Î± i)

\end{code}



#### <a id="homomorphisc-invariance">Homomorphic invariance of âŠ§</a>

If an algebra ğ‘¨ models an identity p â‰ˆ q, then the pair (p , q) belongs to the kernel of every homomorphism Ï† : hom (ğ‘» X) ğ‘¨ from the term algebra to ğ‘¨; that is, every homomorphism from ğ‘» X to ğ‘¨ maps p and q to the same element of ğ‘¨.

 \begin{code}

module _ {X : Type Ï‡}{ğ‘¨ : SetoidAlgebra Î± Ïáµƒ}(Ï†h : hom (ğ‘» X) ğ‘¨) where
 open  SetoidAlgebra ğ‘¨ using () renaming ( Domain to A )
 open Setoid A using ( _â‰ˆ_ )
 open SetoidReasoning A
 open Environment ğ‘¨ using () renaming ( âŸ¦_âŸ§ to âŸ¦_âŸ§â‚ )
 open Environment using ( âŸ¦_âŸ§ )
 private Ï† = _âŸ¨$âŸ©_ âˆ£ Ï†h âˆ£

 âŠ§-H-invar : {p q : Term X} â†’ ğ‘¨ âŠ§ p â‰ˆ q â†’ Ï† p â‰ˆ Ï† q
 âŠ§-H-invar {p}{q} Î² =
  begin
   Ï† p                   â‰ˆâŸ¨ cong âˆ£ Ï†h âˆ£ (term-agreement p) âŸ©
   Ï† ((âŸ¦ ğ‘» X âŸ§ p) âŸ¨$âŸ© â„Š) â‰ˆâŸ¨ comm-hom-term Ï†h p â„Š âŸ©
   âŸ¦ p âŸ§â‚ âŸ¨$âŸ© (Ï† âˆ˜ â„Š)    â‰ˆâŸ¨ Î² (Ï† âˆ˜ â„Š) âŸ©
   âŸ¦ q âŸ§â‚ âŸ¨$âŸ© (Ï† âˆ˜ â„Š)    â‰ˆË˜âŸ¨ comm-hom-term Ï†h q â„Š âŸ©
   Ï† ((âŸ¦ ğ‘» X âŸ§ q) âŸ¨$âŸ© â„Š) â‰ˆË˜âŸ¨ cong âˆ£ Ï†h âˆ£ (term-agreement q) âŸ©
   Ï† q                   âˆ

\end{code}

---------------------------------

<span style="float:left;">[â† Varieties.Func.Closure](Varieties.Func.Closure.html)</span>
<span style="float:right;">[Varieties.Func.Preservation â†’](Varieties.Func.Preservation.html)</span>

{% include UALib.Links.md %}
