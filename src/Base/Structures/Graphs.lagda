---
layout: default
title : "Base.Structures.Graphs module"
date : "2021-06-22"
author: "agda-algebras development team"
---

### <a id="graph-structures">Graph Structures</a>

This is the [Base.Structures.Graphs][] module of the [Agda Universal Algebra Library][].

N.B. This module differs from 0Graphs.lagda in that this module is universe polymorphic; i.e., we do not restrict universe levels (to, e.g., â„“â‚€). This complicates some things; e.g., we must use lift and lower in some places (cf. [Base/Structures/Graphs0.lagda][]).

**Definition** (Graph of a structure). Let ğ‘¨ be an (ğ‘…,ğ¹)-structure (relations from ğ‘… and operations from ğ¹).
The *graph* of ğ‘¨ is the structure Gr ğ‘¨ with the same domain as ğ‘¨ with relations from ğ‘… and together with a (k+1)-ary relation symbol G ğ‘“ for each ğ‘“ âˆˆ ğ¹ of arity k, which is interpreted in Gr ğ‘¨ as all tuples (t , y) âˆˆ AáµâºÂ¹ such that ğ‘“ t â‰¡ y. (See also Definition 2 of https://arxiv.org/pdf/2010.04958v2.pdf)


\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

module Base.Structures.Graphs where

-- imports from Agda and the Agda Standard Library -------------------------------------------
open import Agda.Primitive using ( _âŠ”_ ; lsuc ) renaming ( Set to Type ; lzero  to â„“â‚€ )
open import Data.Product   using ( _,_ ; Î£-syntax ; _Ã—_ )
open import Data.Sum.Base  using ( _âŠ_ ) renaming ( injâ‚ to inl ; injâ‚‚ to inr )
open import Data.Unit.Base using ( âŠ¤ ; tt )
open import Level          using ( Level ; Lift ; lift ; lower )
open import Function.Base  using ( _âˆ˜_  )
open import Relation.Binary.PropositionalEquality
                           using ( _â‰¡_ ; refl ; module â‰¡-Reasoning ; cong ; sym )

-- Imports from the Agda Universal Algebra Library ---------------------------------------------
open import Base.Overture.Preliminaries     using ( âˆ£_âˆ£ ; _â‰ˆ_ ; âˆ¥_âˆ¥ ; _âˆ™_ ; lowerâˆ¼lift ; liftâˆ¼lower )
open import Base.Relations.Continuous       using ( Rel )
open import Base.Structures.Basic           using ( signature ; structure )
open import Base.Structures.Homs            using ( hom ; ğ’¾ğ’¹ ; âˆ˜-hom ; ğ“ğ’¾ğ’»ğ“‰ ; ğ“â„´ğ“Œâ„¯ğ“‡ ; is-hom-rel; is-hom-op)
open import Examples.Structures.Signatures  using ( Sâˆ… )

open signature
open structure
open _âŠ_


Gr-sig : signature â„“â‚€ â„“â‚€ â†’ signature â„“â‚€ â„“â‚€ â†’ signature â„“â‚€ â„“â‚€

Gr-sig ğ¹ ğ‘… = record { symbol = symbol ğ‘… âŠ symbol ğ¹
                    ; arity  = ar }
 where
 ar : symbol ğ‘… âŠ symbol ğ¹ â†’ Type _
 ar (inl ğ‘Ÿ) = (arity ğ‘…) ğ‘Ÿ
 ar (inr ğ‘“) = (arity ğ¹) ğ‘“ âŠ âŠ¤

private variable
 ğ¹ ğ‘… : signature â„“â‚€ â„“â‚€
 Î± Ï : Level

Gr : âˆ€{Î± Ï} â†’ structure ğ¹ ğ‘… {Î±} {Ï} â†’ structure Sâˆ… (Gr-sig ğ¹ ğ‘…) {Î±} {Î± âŠ” Ï}
Gr {ğ¹}{ğ‘…}{Î±}{Ï} ğ‘¨ = record { carrier = carrier ğ‘¨ ; op = Î» () ; rel = split }
  where
  split : (s : symbol ğ‘… âŠ symbol ğ¹) â†’ Rel (carrier ğ‘¨) (arity (Gr-sig ğ¹ ğ‘…) s) {Î± âŠ” Ï}
  split (inl ğ‘Ÿ) arg = Lift Î± (rel ğ‘¨ ğ‘Ÿ arg)
  split (inr ğ‘“) args = Lift Ï (op ğ‘¨ ğ‘“ (args âˆ˜ inl) â‰¡ args (inr tt))


open â‰¡-Reasoning

private variable
 Ïáµƒ Î² Ïáµ‡ : Level

module _ {ğ‘¨ : structure ğ¹ ğ‘… {Î±} {Ïáµƒ}}
         {ğ‘© : structure ğ¹ ğ‘… {Î²} {Ïáµ‡}} where

 homâ†’Grhom : hom ğ‘¨ ğ‘© â†’ hom (Gr ğ‘¨) (Gr ğ‘©)
 homâ†’Grhom (h , hhom) = h , (i , ii)
  where
  i : is-hom-rel (Gr ğ‘¨) (Gr ğ‘©) h
  i (inl ğ‘Ÿ) a x = lift (âˆ£ hhom âˆ£ ğ‘Ÿ a (lower x))
  i (inr ğ‘“) a x = lift goal
   where
   homop : h (op ğ‘¨ ğ‘“ (a âˆ˜ inl)) â‰¡ op ğ‘© ğ‘“ (h âˆ˜ (a âˆ˜ inl))
   homop = âˆ¥ hhom âˆ¥ ğ‘“ (a âˆ˜ inl)

   goal : op ğ‘© ğ‘“ (h âˆ˜ (a âˆ˜ inl)) â‰¡ h (a (inr tt))
   goal = op ğ‘© ğ‘“ (h âˆ˜ (a âˆ˜ inl)) â‰¡âŸ¨ sym homop âŸ©
          h (op ğ‘¨ ğ‘“ (a âˆ˜ inl))   â‰¡âŸ¨ cong h (lower x) âŸ©
          h (a (inr tt))         âˆ

  ii : is-hom-op (Gr ğ‘¨) (Gr ğ‘©) h
  ii = Î» ()

 Grhomâ†’hom : hom (Gr ğ‘¨) (Gr ğ‘©) â†’ hom ğ‘¨ ğ‘©
 Grhomâ†’hom (h , hhom) = h , (i , ii)
  where
  i : is-hom-rel ğ‘¨ ğ‘© h
  i R a x = lower (âˆ£ hhom âˆ£ (inl R) a (lift x))
  ii : is-hom-op ğ‘¨ ğ‘© h
  ii f a = goal -- goal
   where
   split : arity ğ¹ f âŠ âŠ¤ â†’ carrier ğ‘¨
   split (inl x) = a x
   split (inr y) = op ğ‘¨ f a
   goal : h (op ğ‘¨ f a) â‰¡ op ğ‘© f (Î» x â†’ h (a x))
   goal = sym (lower (âˆ£ hhom âˆ£ (inr f) split (lift refl)))

\end{code}

--------------------------------

<span style="float:left;">[â† Base.Structures.Basic](Base.Structures.Basic.html)</span>
<span style="float:right;">[Base.Structures.Graphs0 â†’](Base.Structures.Graphs0.html)</span>

{% include UALib.Links.md %}
