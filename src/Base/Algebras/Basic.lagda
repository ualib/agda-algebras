---
layout: default
title : "Base.Algebras.Basic module (Agda Universal Algebra Library)"
date : "2021-04-23"
author: "agda-algebras development team"
---

### <a id="basic-definitions">Basic definitions</a>

This is the [Base.Algebras.Basic][] module of the [Agda Universal Algebra Library][].

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

module Base.Algebras.Basic where

-- Imports from the Agda (Builtin) and the Agda Standard Library -----------------------
open import Agda.Primitive  using ( _âŠ”_ ; lsuc ) renaming ( Set to  Type ; lzero to â„“â‚€ )
open import Data.Product    using ( _,_ ; _Ã—_ ; Î£-syntax )
open import Level           using ( Level )
open import Relation.Binary using ( IsEquivalence ) renaming ( Rel to BinRel )
open import Relation.Unary  using ( _âˆˆ_ ; Pred )


-- Imports from the Agda Universal Algebra Library -------------------------------------
open import Base.Overture.Preliminaries using ( âˆ£_âˆ£ ; âˆ¥_âˆ¥ )
open import Base.Relations.Discrete     using ( Op ; _|:_ ; _|:pred_ )
open import Base.Relations.Continuous   using ( Rel ; compatible-Rel ; REL ; compatible-REL )

private variable Î± Î² Ï : Level

variable ğ“ ğ“¥ : Level

\end{code}

The variables `ğ“` and `ğ“¥` are not private since, as mentioned earlier, throughout the [agda-algebras](https://github.com/ualib/agda-algebras) library `ğ“` denotes the universe level of *operation symbol* types, while `ğ“¥` denotes the universe level of *arity* types.


#### <a id="signatures-of-an-algebra">Signatures of an algebra</a>

In [model theory](https://en.wikipedia.org/wiki/Model_theory), the *signature* `ğ‘† = (ğ¶, ğ¹, ğ‘…, Ï)` of a structure consists of three (possibly empty) sets `ğ¶`, `ğ¹`, and `ğ‘…`---called *constant symbols*, *function symbols*, and *relation symbols*, respectively---along with a function `Ï : ğ¶ + ğ¹ + ğ‘… â†’ ğ‘` that assigns an *arity* to each symbol. Often (but not always) `ğ‘ = â„•`, the natural numbers.

As our focus here is universal algebra, we are more concerned with the restricted notion of an *algebraic signature* (or *signature* for algebraic structures), by which we mean a pair `ğ‘† = (ğ¹, Ï)` consisting of a collection `ğ¹` of *operation symbols* and an *arity function* `Ï : ğ¹ â†’ ğ‘` that maps each operation symbol to its arity; here, ğ‘ denotes the *arity type*. Heuristically, the arity `Ï ğ‘“` of an operation symbol `ğ‘“ âˆˆ ğ¹` may be thought of as the "number of arguments" that `ğ‘“` takes as "input".

If the arity of `ğ‘“` is `n`, then we call `ğ‘“` an `n`-*ary* operation symbol.  In case `n` is 0 (or 1 or 2 or 3, respectively) we call the function *nullary* (or *unary* or *binary* or *ternary*, respectively).

If `A` is a set and `ğ‘“` is a (`Ï ğ‘“`)-ary operation on `A`, we often indicate this by writing `ğ‘“ : A`<sup>Ï ğ‘“</sup> `â†’ A`. On the other hand, the arguments of such an operation form a (`Ï ğ‘“`)-tuple, say, `(a 0, a 1, â€¦, a (Ïf-1))`, which may be viewed as the graph of the function `a : Ïğ‘“ â†’ A`. When the codomain of `Ï` is `â„•`, we may view `Ï ğ‘“` as the finite set `{0, 1, â€¦, Ïğ‘“ - 1}`. Thus, by identifying the `Ïğ‘“`-th power `A`<sup>Ï ğ‘“</sup> with the type `Ï ğ‘“ â†’ A` of functions from `{0, 1, â€¦, Ïğ‘“ - 1}` to `A`, we identify the function type `A`<sup>Ï f</sup> `â†’ A` with the function (or "functional") type `(Ïğ‘“ â†’ A) â†’ A`.

**Example**. Suppose `ğ‘” : (m â†’ A) â†’ A` is an `m`-ary operation on `A`, and `a : m â†’ A` is an `m`-tuple on `A`. Then `ğ‘” a` may be viewed as `ğ‘” (a 0, a 1, â€¦, a (m-1))`, which has type `A`. Suppose further that `ğ‘“ : (Ïğ‘“ â†’ B) â†’ B` is a `Ïğ‘“`-ary operation on `B`, let `a : Ïğ‘“ â†’ A` be a `Ïğ‘“`-tuple on `A`, and let `h : A â†’ B` be a function.  Then the following typing judgments obtain: `h âˆ˜ a : Ïğ‘“ â†’ B` and we `ğ‘“ (h âˆ˜ a) : B`.

#### <a id="signature-type">Signature type</a>

In the [agda-algebras](https://github.com/ualib/agda-algebras) library we represent the *signature* of an algebraic structure using the following type.

\begin{code}

Signature : (ğ“ ğ“¥ : Level) â†’ Type (lsuc (ğ“ âŠ” ğ“¥))
Signature ğ“ ğ“¥ = Î£[ F âˆˆ Type ğ“ ] (F â†’ Type ğ“¥)


Level-of-Signature : {ğ“ ğ“¥ : Level} â†’ Signature ğ“ ğ“¥ â†’ Level
Level-of-Signature {ğ“}{ğ“¥} _ = lsuc (ğ“ âŠ” ğ“¥)

\end{code}


In the
[Overture](https://ualib.github.io/agda-algebras/Overture.html)
module of the [agda-algebras](https://github.com/ualib/agda-algebras) library, special syntax is defined for the first and second projections---namely, `âˆ£_âˆ£` and `âˆ¥_âˆ¥`, resp. Consequently, if `ğ‘† : Signature ğ“ ğ“¥` is a signature, then

* `âˆ£ ğ‘† âˆ£` denotes the set of operation symbols, and
* `âˆ¥ ğ‘† âˆ¥` denotes the arity function.

If `ğ‘“ : âˆ£ ğ‘† âˆ£` is an operation symbol in the signature `ğ‘†`, then `âˆ¥ ğ‘† âˆ¥ ğ‘“` is the arity of `ğ‘“`.



#### <a id="algebras">Algebras</a>

Our first goal is to develop a working vocabulary and formal library for classical (single-sorted, set-based) universal algebra.  In this section we define the main objects of study.  An *algebraic structure* (or *algebra*) in the signature `ğ‘† = (ğ¹, Ï)` is denoted by `ğ‘¨ = (A, F`<sup>`ğ‘¨`</sup>`)` and consists of

* `A` := a *nonempty* set (or type), called the *domain* (or *carrier* or *universe*) of the algebra;
* `F`<sup>`ğ‘¨`</sup> := `{ f`<sup>`ğ‘¨`</sup>` âˆ£ f âˆˆ F, f`<sup>`ğ‘¨`</sup>` : (Ïğ‘“ â†’ A) â†’ A }`, a collection of *operations* on `ğ´`;
* a (potentially empty) collection of *identities* satisfied by elements and operations of `ğ´`.

Note that to each operation symbol `ğ‘“ âˆˆ ğ¹` corresponds an operation `ğ‘“`<sup>`ğ‘¨`</sup> on `ğ´` of arity `Ïğ‘“`; we call such `ğ‘“`<sup>`ğ‘¨`</sup> the *interpretation* of the symbol `ğ‘“` in the algebra `ğ‘¨`. We call an algebra in the signature `ğ‘†` an `ğ‘†`-*algebra*.  An algebra is called *finite* if it has a finite domain, and is called *trivial* if its universe is a singleton.  Given two algebras `ğ‘¨` and `ğ‘©`, we say that `ğ‘©` is a *reduct* of `ğ‘¨` if both algebras have the same domain and `ğ‘©` can be obtained from `ğ‘¨` by simply removing some of the operations.

Recall, we defined the type `Signature ğ“ ğ“¥` above as the dependent pair type `Î£ F ê‰ Type ğ“ , (F â†’ Type ğ“¥)`, and the type `Op` of operation symbols is the function type `Op I A = (I â†’ A) â†’ A` (see [Base.Relations.Discrete][]).

For a fixed signature `ğ‘† : Signature ğ“ ğ“¥` and universe level `Î±`, we define the *type of algebras in the signature* `ğ‘†` (or *type of* `ğ‘†`-*algebras*) *with domain type* `Type Î±` as follows.

\begin{code}

Algebra : (Î± : Level)(ğ‘† : Signature ğ“ ğ“¥) â†’ Type (ğ“ âŠ” ğ“¥ âŠ” lsuc Î±)
Algebra Î± ğ‘† = Î£[ A âˆˆ Type Î± ]                   -- the domain
              âˆ€ (f : âˆ£ ğ‘† âˆ£) â†’ Op A (âˆ¥ ğ‘† âˆ¥ f)    -- the basic operations

\end{code}

It would be more precise to refer to inhabitants of this type as âˆ-*algebras* because their domains can be of arbitrary type and need not be truncated at some level and, in particular, need to be a set. (See [Base.Equality.Truncation][].)

We might take this opportunity to define the type of 0-*algebras*, that is, algebras whose domains are sets, which is probably closer to what most of us think of when doing informal universal algebra.  However, in the [agda-algebras](https://github.com/ualib/agda-algebras) library we will only need to know that the domains of certain algebras are sets in a few places, so it seems preferable to work with general (âˆ-)algebras throughout and then explicitly postulate additional axioms (e.g., [uniquness of identity proofs](https://ualib.github.io/agda-algebras/Equality.Truncation.html#uniqueness-of-identity-proofs) if and only if necessary.


#### <a id="algebras-as-record-types">Algebras as record types</a>

A popular way to represent algebraic structures in type theory is with record types.  The Sigma type defined above provides an equivalent alternative that we happen to prefer and we use it throughout the library, both for consistency and because of its direct connection to the existential quantifier of logic. Recall that the type `Î£ x ê‰ X , P x` represents the proposition, "there exists `x` in `X` such that `P x` holds;" in symbols, `âˆƒ x âˆˆ X , P x`.  Indeed, an inhabitant of `Î£ x ê‰ X , P x` is a pair `(x , p)` such that `x` inhabits `X` and `p` is a proof of `P x`. In other terms, the pair `(x , p)` is a witness and proof of the proposition `âˆƒ x âˆˆ X , P x`.

Nonetheless, for those who prefer to represent algebraic structures in type theory using records, we offer the following definition (which is equivalent to the Sigma type formulation).

\begin{code}

record algebra (Î± : Level) (ğ‘† : Signature ğ“ ğ“¥) : Type(lsuc(ğ“ âŠ” ğ“¥ âŠ” Î±)) where
 constructor mkalg
 field
  carrier : Type Î±
  opsymbol : (f : âˆ£ ğ‘† âˆ£) â†’ ((âˆ¥ ğ‘† âˆ¥ f) â†’ carrier) â†’ carrier

\end{code}

This representation of algebras as inhabitants of a record type is equivalent to the representation using Sigma types in the sense that a bi-implication between the two representations is obvious.

\begin{code}

module _ {ğ‘† : Signature ğ“ ğ“¥} where

 open algebra

 algebraâ†’Algebra : algebra Î± ğ‘† â†’ Algebra Î± ğ‘†
 algebraâ†’Algebra ğ‘¨ = (carrier ğ‘¨ , opsymbol ğ‘¨)

 Algebraâ†’algebra : Algebra Î± ğ‘† â†’ algebra Î± ğ‘†
 Algebraâ†’algebra ğ‘¨ = mkalg âˆ£ ğ‘¨ âˆ£ âˆ¥ ğ‘¨ âˆ¥

\end{code}


#### <a id="operation-interpretation-syntax">Operation interpretation syntax</a>

We now define a convenient shorthand for the interpretation of an operation symbol. This looks more similar to the standard notation one finds in the literature as compared to the double bar notation we started with, so we will use this new notation almost exclusively in the remaining modules of the [agda-algebras][] library.

\begin{code}

 _Ì‚_ : (ğ‘“ : âˆ£ ğ‘† âˆ£)(ğ‘¨ : Algebra Î± ğ‘†) â†’ (âˆ¥ ğ‘† âˆ¥ ğ‘“  â†’  âˆ£ ğ‘¨ âˆ£) â†’ âˆ£ ğ‘¨ âˆ£

 ğ‘“ Ì‚ ğ‘¨ = Î» ğ‘ â†’ (âˆ¥ ğ‘¨ âˆ¥ ğ‘“) ğ‘

\end{code}

So, if `ğ‘“ : âˆ£ ğ‘† âˆ£` is an operation symbol in the signature `ğ‘†`, and if `ğ‘ : âˆ¥ ğ‘† âˆ¥ ğ‘“ â†’ âˆ£ ğ‘¨ âˆ£` is a tuple of the appropriate arity, then `(ğ‘“ Ì‚ ğ‘¨) ğ‘` denotes the operation `ğ‘“` interpreted in `ğ‘¨` and evaluated at `ğ‘`.



#### <a id="the-universe-level-of-an-algebra">The universe level of an algebra</a>

Occasionally we will be given an algebra and we just need to know the universe level of its domain. The following utility function provides this.

\begin{code}

Level-of-Alg : {Î± ğ“ ğ“¥ : Level}{ğ‘† : Signature ğ“ ğ“¥} â†’ Algebra Î± ğ‘† â†’ Level
Level-of-Alg {Î± = Î±}{ğ“}{ğ“¥} _ = ğ“ âŠ” ğ“¥ âŠ” lsuc Î±

Level-of-Carrier : {Î± ğ“ ğ“¥  : Level}{ğ‘† : Signature ğ“ ğ“¥} â†’ Algebra Î± ğ‘† â†’ Level
Level-of-Carrier {Î± = Î±} _ = Î±

\end{code}


#### <a id="lifts-of-algebras">Level lifting algebra types</a>

Recall, in the [section on level lifting and lowering](Overture.Lifts.html#level-lifting-and-lowering), we described the difficulties one may encounter when working with a noncumulative universe hierarchy. We made a promise to provide some domain-specific level lifting and level lowering methods. Here we fulfill this promise by supplying a couple of bespoke tools designed specifically for our operation and algebra types.

\begin{code}


open Level

Lift-alg-op : {I : Type ğ“¥} {A : Type Î±} â†’ Op A I â†’ (Î² : Level) â†’ Op (Lift Î² A) I
Lift-alg-op f Î² = Î» x â†’ lift (f (Î» i â†’ lower (x i)))

Lift-Alg : {ğ‘† : Signature ğ“ ğ“¥} â†’ Algebra Î± ğ‘† â†’ (Î² : Level) â†’ Algebra (Î± âŠ” Î²) ğ‘†
Lift-Alg {ğ‘† = ğ‘†} ğ‘¨ Î² = Lift Î² âˆ£ ğ‘¨ âˆ£ , (Î» (ğ‘“ : âˆ£ ğ‘† âˆ£) â†’ Lift-alg-op (ğ‘“ Ì‚ ğ‘¨) Î²)


open algebra

Lift-algebra : {ğ‘† : Signature ğ“ ğ“¥} â†’ algebra Î± ğ‘† â†’ (Î² : Level) â†’ algebra (Î± âŠ” Î²) ğ‘†
Lift-algebra {ğ‘† = ğ‘†} ğ‘¨ Î² = mkalg (Lift Î² (carrier ğ‘¨)) (Î» (f : âˆ£ ğ‘† âˆ£) â†’ Lift-alg-op ((opsymbol ğ‘¨) f) Î²)

\end{code}

What makes the `Lift-Alg` type so useful for resolving type level errors involving algebras is the nice properties it possesses.  Indeed, the [agda-algebras][] library contains formal proofs of the following facts.

+ [`Lift-Alg` is a homomorphism](Base.Homomorphisms.Basic.html#exmples-of-homomorphisms) (see [Base.Homomorphisms.Basic][])
+ [`Lift-Alg` is an algebraic invariant](Base.Homomorphisms.Isomorphisms.html#lift-is-an-algebraic-invariant") (see [Base.Homomorphisms.Isomorphisms][])
+ [`Lift-Alg` of a subalgebra is a subalgebra](Base.Subalgebras.Subalgebras.html#lifts-of-subalgebras) (see [Base.Subalgebras.Subalgebras][])
+ [`Lift-Alg` preserves identities](Base.Varieties.EquationalLogic.html#lift-invariance)) (see [Base.Varieties.EquationalLogic][])


#### <a id="compatibility-of-binary-relations">Compatibility of binary relations</a>

We now define the function `compatible` so that, if `ğ‘¨` denotes an algebra and `R` a binary relation, then `compatible ğ‘¨ R` will represent the assertion that `R` is *compatible* with all basic operations of `ğ‘¨`. The formal definition is immediate since all the work is done by the relation `|:`, which we defined above (see [Base.Relations.Discrete][]).

\begin{code}

compatible : {ğ‘† : Signature ğ“ ğ“¥}(ğ‘¨ : Algebra Î± ğ‘†) â†’ BinRel âˆ£ ğ‘¨ âˆ£ Ï â†’ Type (ğ“ âŠ” ğ“¥ âŠ” Î± âŠ” Ï)
compatible  ğ‘¨ R = âˆ€ ğ‘“ â†’ (ğ‘“ Ì‚ ğ‘¨) |: R

compatible-pred : {ğ‘† : Signature ğ“ ğ“¥}(ğ‘¨ : Algebra Î± ğ‘†) â†’ Pred (âˆ£ ğ‘¨ âˆ£ Ã— âˆ£ ğ‘¨ âˆ£)Ï â†’ Type (ğ“ âŠ” ğ“¥ âŠ” Î± âŠ” Ï)
compatible-pred  ğ‘¨ P = âˆ€ ğ‘“ â†’ (ğ‘“ Ì‚ ğ‘¨) |:pred P

\end{code}

Recall, the `|:` type was defined in [Base.Relations.Discrete][] module.


#### <a id="compatibility-of-continuous-relations">Compatibility of continuous relations</a>

In the [Base.Relations.Continuous][] module, we defined a function called `compatible-Rel` to represent the assertion that a given continuous relation is compatible with a given operation. With that, it is easy to define a function, which we call `compatible-Rel-alg`, representing compatibility of a continuous relation with all operations of an algebra.  Similarly, we define the analogous `compatible-REL-alg` function for the (even more general) type of *dependent relations*.

\begin{code}

module _ {I : Type ğ“¥} {ğ‘† : Signature ğ“ ğ“¥} where

 compatible-Rel-alg : (ğ‘¨ : Algebra Î± ğ‘†) â†’ Rel âˆ£ ğ‘¨ âˆ£ I{Ï} â†’ Type(ğ“ âŠ” Î± âŠ” ğ“¥ âŠ” Ï)
 compatible-Rel-alg ğ‘¨ R = âˆ€ (ğ‘“ : âˆ£ ğ‘† âˆ£ ) â†’  compatible-Rel (ğ‘“ Ì‚ ğ‘¨) R

 compatible-REL-alg : (ğ’œ : I â†’ Algebra Î± ğ‘†) â†’ REL I (Î» i â†’ âˆ£ ğ’œ  i âˆ£) {Ï} â†’ Type(ğ“ âŠ” Î± âŠ” ğ“¥ âŠ” Ï)
 compatible-REL-alg ğ’œ R = âˆ€ ( ğ‘“ : âˆ£ ğ‘† âˆ£ ) â†’  compatible-REL (Î» i â†’ ğ‘“ Ì‚ (ğ’œ i)) R

\end{code}

-------------------------------------

<span style="float:left;">[â†‘ Base.Algebras](Base.Algebras.html)</span>
<span style="float:right;">[Base.Algebras.Products â†’](Base.Algebras.Products.html)</span>

{% include UALib.Links.md %}
