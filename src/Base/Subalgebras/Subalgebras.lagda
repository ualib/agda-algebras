---
layout: default
title : "Base.Subalgebras.Subalgebras module (The Agda Universal Algebra Library)"
date : "2021-01-14"
author: "agda-algebras development team"
---

### <a id="subalgebras">Subalgebras</a>

The [Base.Subalgebras.Subalgebras][] module of the [Agda Universal Algebra Library][] defines the `Subalgebra` type, representing the subalgebra of a given algebra, as well as the collection of all subalgebras of a given class of algebras.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Base.Algebras.Basic using (ğ“ ; ğ“¥ ; Signature )

module Base.Subalgebras.Subalgebras {ğ‘† : Signature ğ“ ğ“¥} where

-- imports from Agda and the Agda Standard Library ------------------------------------
open import Agda.Primitive   using ( _âŠ”_ ; lsuc ; Level ) renaming ( Set to Type )
open import Data.Product     using ( _,_ ; Î£-syntax ; _Ã—_ ) renaming ( projâ‚‚ to snd )
open import Relation.Unary   using ( Pred ; _âˆˆ_ )

-- Imports from the Agda Universal Algebra Library ------------------------------------
open import Base.Overture.Preliminaries             using ( âˆ£_âˆ£ ; âˆ¥_âˆ¥ )
open import Base.Overture.Injective                 using ( IsInjective )
open import Base.Equality.Welldefined               using ( swelldef )
open import Base.Equality.Truncation                using ( is-set ; blk-uip )
open import Base.Equality.Extensionality            using ( pred-ext )
open import Base.Algebras.Basic                     using ( Algebra )
open import Base.Algebras.Products          {ğ‘† = ğ‘†} using ( ov )
open import Base.Homomorphisms.Basic        {ğ‘† = ğ‘†} using ( hom )
open import Base.Homomorphisms.Kernels      {ğ‘† = ğ‘†} using ( kercon ; ker[_â‡’_]_â†¾_ )
open import Base.Homomorphisms.Noether      {ğ‘† = ğ‘†} using ( FirstHomTheorem|Set )
open import Base.Homomorphisms.Isomorphisms {ğ‘† = ğ‘†} using ( _â‰…_ )
open import Base.Terms.Basic                {ğ‘† = ğ‘†} using ( ğ‘» ; Term )
private variable Î± Î² Î³ ğ“§ : Level
\end{code}


#### <a id="subalgebra-type">Subalgebra type</a>

Given algebras `ğ‘¨ : Algebra Î± ğ‘†` and `ğ‘© : Algebra ğ“¦ ğ‘†`, we say that `ğ‘©` is a *subalgebra* of `ğ‘¨` just in case `ğ‘©` can be *homomorphically embedded* in `ğ‘¨`; that is, there exists a map `h : âˆ£ ğ‘© âˆ£ â†’ âˆ£ ğ‘¨ âˆ£` that is both a homomorphism and an embedding.

\begin{code}

_â‰¤_  -- (alias for subalgebra relation))
 _IsSubalgebraOf_ : Algebra Î± ğ‘† â†’ Algebra Î² ğ‘† â†’ Type _
ğ‘¨ IsSubalgebraOf ğ‘© = Î£[ h âˆˆ hom ğ‘¨ ğ‘© ] IsInjective âˆ£ h âˆ£

_â‰¥_  -- (alias for supalgebra (aka overalgebra))
 _IsSupalgebraOf_ : Algebra Î± ğ‘† â†’ Algebra Î² ğ‘† â†’ Type _
ğ‘¨ IsSupalgebraOf ğ‘© = Î£[ h âˆˆ hom ğ‘© ğ‘¨ ] IsInjective âˆ£ h âˆ£

-- Syntactic sugar for sub/sup-algebra relations.
ğ‘¨ â‰¤ ğ‘© = ğ‘¨ IsSubalgebraOf ğ‘©
ğ‘¨ â‰¥ ğ‘© = ğ‘¨ IsSupalgebraOf ğ‘©

-- From now on we use `ğ‘¨ â‰¤ ğ‘©` to express the assertion that `ğ‘¨` is a subalgebra of `ğ‘©`.
record SubalgebraOf : Type (ov (Î± âŠ” Î²)) where
 field
  algebra : Algebra Î± ğ‘†
  subalgebra : Algebra Î² ğ‘†
  issubalgebra : subalgebra â‰¤ algebra


Subalgebra : Algebra Î± ğ‘† â†’ {Î² : Level} â†’ Type _
Subalgebra  ğ‘¨ {Î²} = Î£[ ğ‘© âˆˆ (Algebra Î² ğ‘†) ] ğ‘© â‰¤ ğ‘¨

\end{code}


Note the order of the arguments.  The universe `Î²` comes first because in certain situations we must explicitly specify this universe, whereas we can almost always leave the universe `Î±` implicit. (See, for example, the definition of `_IsSubalgebraOfClass_` below.)


#### <a id="consequences-of-first-homomorphism-theorem">Consequences of First Homomorphism Theorem</a>

We take this opportunity to prove an important lemma that makes use of the `IsSubalgebraOf` type defined above.  It is the following: If `ğ‘¨` and `ğ‘©` are `ğ‘†`-algebras and `h : hom ğ‘¨ ğ‘©` a homomorphism from `ğ‘¨` to `ğ‘©`, then the quotient `ğ‘¨ â•± ker h` is (isomorphic to) a subalgebra of `ğ‘©`.  This is an easy corollary of the First Homomorphism Theorem proved in the [Base.Homomorphisms.Noether][] module.

\begin{code}

module _ (ğ‘¨ : Algebra Î± ğ‘†)(ğ‘© : Algebra Î² ğ‘†)(h : hom ğ‘¨ ğ‘©)
         -- extensionality assumptions:
         (pe : pred-ext Î± Î²)(fe : swelldef ğ“¥ Î²)

         -- truncation assumptions:
         (Bset : is-set âˆ£ ğ‘© âˆ£)
         (buip : blk-uip âˆ£ ğ‘¨ âˆ£ âˆ£ kercon fe {ğ‘©} h âˆ£)
         where

 FirstHomCorollary|Set : (ker[ ğ‘¨ â‡’ ğ‘© ] h â†¾ fe) IsSubalgebraOf ğ‘©
 FirstHomCorollary|Set = Ï•hom , Ï•inj
  where
   hh = FirstHomTheorem|Set ğ‘¨ ğ‘© h pe fe Bset buip
   Ï•hom : hom (ker[ ğ‘¨ â‡’ ğ‘© ] h â†¾ fe) ğ‘©
   Ï•hom = âˆ£ hh âˆ£

   Ï•inj : IsInjective âˆ£ Ï•hom âˆ£
   Ï•inj = âˆ£ snd âˆ¥ hh âˆ¥ âˆ£

\end{code}

If we apply the foregoing theorem to the special case in which the `ğ‘¨` is term algebra `ğ‘» X`, we obtain the following result which will be useful later.

\begin{code}

module _ (X : Type ğ“§)(ğ‘© : Algebra Î² ğ‘†)(h : hom (ğ‘» X) ğ‘©)
         -- extensionality assumptions:
         (pe : pred-ext (ov ğ“§) Î²)(fe : swelldef ğ“¥ Î²)

         -- truncation assumptions:
         (Bset : is-set âˆ£ ğ‘© âˆ£)
         (buip : blk-uip (Term X) âˆ£ kercon fe {ğ‘©} h âˆ£)
         where

 free-quot-subalg : (ker[ ğ‘» X â‡’ ğ‘© ] h â†¾ fe) IsSubalgebraOf ğ‘©
 free-quot-subalg = FirstHomCorollary|Set{Î± = ov ğ“§}(ğ‘» X) ğ‘© h pe fe Bset buip

\end{code}

#### <a id="subalgebras-of-a-class">Subalgebras of a class</a>

One of our goals is to formally express and prove properties of classes of algebraic structures.  Fixing a signature `ğ‘†` and a universe `Î±`, we represent classes of `ğ‘†`-algebras with domains of type `Type Î±` as predicates over the `Algebra Î± ğ‘†` type. In the syntax of the [agda-algebras](https://github.com/ualib/agda-algebras) library, such predicates inhabit the type `Pred (Algebra Î± ğ‘†) Î³`, for some universe Î³.

Suppose `ğ’¦ : Pred (Algebra Î± ğ‘†) Î³` denotes a class of `ğ‘†`-algebras and `ğ‘© : Algebra Î² ğ‘†` denotes an arbitrary `ğ‘†`-algebra. Then we might wish to consider the assertion that `ğ‘©` is a subalgebra of an algebra in the class `ğ’¦`.  The next type we define allows us to express this assertion as `ğ‘© IsSubalgebraOfClass ğ’¦`.

\begin{code}

module _ {Î± Î² : Level} where

 _IsSubalgebraOfClass_ : Algebra Î² ğ‘† â†’ Pred (Algebra Î± ğ‘†) Î³ â†’ Type (Î³ âŠ” ov (Î± âŠ” Î²))
 ğ‘© IsSubalgebraOfClass ğ’¦ = Î£[ ğ‘¨ âˆˆ Algebra Î± ğ‘† ] Î£[ sa âˆˆ Subalgebra ğ‘¨ {Î²} ] ((ğ‘¨ âˆˆ ğ’¦) Ã— (ğ‘© â‰… âˆ£ sa âˆ£))

\end{code}

Using this type, we express the collection of all subalgebras of algebras in a class by the type `SubalgebraOfClass`, which we now define.

\begin{code}

 SubalgebraOfClass : Pred (Algebra Î± ğ‘†)(ov Î±) â†’ Type (ov (Î± âŠ” Î²))
 SubalgebraOfClass ğ’¦ = Î£[ ğ‘© âˆˆ Algebra Î² ğ‘† ] ğ‘© IsSubalgebraOfClass ğ’¦

\end{code}

---------------------------------

<span style="float:left;">[â† Base.Subalgebras.Subuniverses](Base.Subalgebras.Subuniverses.html)</span>
<span style="float:right;">[Base.Subalgebras.Properties â†’](Base.Subalgebras.Properties.html)</span>

{% include UALib.Links.md %}
