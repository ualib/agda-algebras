---
layout: default
title : "Base.Relations.Discrete module (The Agda Universal Algebra Library)"
date : "2021-02-28"
author: "the agda-algebras development team"
---

### <a id="discrete-relations">Discrete Relations</a>

This is the [Base.Relations.Discrete][] module of the [Agda Universal Algebra Library][].

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

module Base.Relations.Discrete where

-- Imports from Agda and the Agda Standard Library ----------------------------------------------
open import Agda.Primitive               using ( _âŠ”_ ; lsuc ) renaming ( Set to Type )
open import Data.Product                 using ( _,_ ; _Ã—_ )
open import Function.Base                using ( _âˆ˜_ )
open import Level                        using ( Level ; Lift )
open import Relation.Binary              using ( IsEquivalence ; _â‡’_ ; _=[_]â‡’_ )
                                      renaming ( REL to BinREL ; Rel to BinRel )
open import Relation.Binary.Definitions  using ( Reflexive ; Transitive )
open import Relation.Unary               using ( _âˆˆ_; Pred )
open import Relation.Binary.PropositionalEquality using ( _â‰¡_ )

-- Imports from agda-algebras -------------------------------------------------------------------
open import Base.Overture.Preliminaries using (_â‰ˆ_ ; Î -syntax)

private variable Î± Î² Ï ğ“¥ : Level
\end{code}

Here is a function that is useful for defining poitwise equality of functions wrt a given equality
(see, e.g., the definition of `_â‰ˆÌ‡_` in the [Residuation.Properties][] module).

\begin{code}

PointWise : {A : Type Î±}{B : Type Î² }
            (_â‰‹_ : BinRel B Ï) â†’ BinRel (A â†’ B) _
PointWise {A = A}{B} _â‰‹_ = Î» (f g : A â†’ B) â†’ âˆ€ x â†’ f x â‰‹ g x

depPointWise : {A : Type Î±}{B : A â†’ Type Î² }
               (_â‰‹_ : {Î³ : Level}{C : Type Î³} â†’ BinRel C Ï)
 â†’             BinRel ((a : A) â†’ B a) _
depPointWise {A = A}{B} _â‰‹_ = Î» (f g : (a : A) â†’ B a) â†’ âˆ€ x â†’ f x â‰‹ g x

\end{code}

Here is useful notation for asserting that the image of a function (the first argument)
is contained in a predicate, the second argument (a "subset" of the codomain).

\begin{code}

Im_âŠ†_ : {A : Type Î±}{B : Type Î²} â†’ (A â†’ B) â†’ Pred B Ï â†’ Type (Î± âŠ” Ï)
Im f âŠ† S = âˆ€ x â†’ f x âˆˆ S

\end{code}


#### <a id="operation-symbols-unary-relations-binary-relations">Operation symbols, unary relations, binary relations</a>

The unary relation (or "predicate") type is imported from Relation.Unary of the std lib.

```agda
Pred : âˆ€ {a} â†’ Set a â†’ (â„“ : Level) â†’ Set (a âŠ” suc â„“)
Pred A â„“ = A â†’ Set â„“
```
Sometimes it is useful to obtain the underlying type of a predicate.

\begin{code}

PredType : {A : Type Î±} â†’ Pred A Ï â†’ Type Î±
PredType {A = A} _ = A

\end{code}

The binary relation types are called `Rel` and `REL` in the standard library, but we
will call them `BinRel` and `BinREL` and reserve the names `Rel` and `REL` for the more
general types of relations we define below and in the Base.Relations.Continuous module.

The heterogeneous binary relation type is imported from the standard library and renamed `BinREL`.

```agda
BinREL : âˆ€ {â„“} (A B : Type â„“) (â„“' : Level) â†’ Type (â„“-max â„“ (â„“-suc â„“'))
BinREL A B â„“' = A â†’ B â†’ Type â„“'
```

The homogeneous binary relation type is imported from the standard
library and renamed BinRel.

```agda
BinRel : âˆ€{â„“} â†’ Type â„“ â†’ (â„“' : Level) â†’ Type (â„“ âŠ” lsuc â„“')
BinRel A â„“' = REL A A â„“'
```

\begin{code}

Level-of-Rel : {A : Type Î±}{â„“ : Level} â†’ BinRel A â„“ â†’ Level
Level-of-Rel {A = A}{â„“} _ = â„“

\end{code}


#### <a id="kernels">Kernels</a>

The *kernel* of `f : A â†’ B` is defined informally by `{(x , y) âˆˆ A Ã— A : f x = f y}`.
This can be represented in type theory and Agda in a number of ways, each of which
may be useful in a particular context. For example, we could define the kernel
to be an inhabitant of a (binary) relation type, or a (unary) predicate type.

\begin{code}

module _ {A : Type Î±}{B : Type Î²} where

 ker : (A â†’ B) â†’ BinRel A Î²
 ker g x y = g x â‰¡ g y

 kerRel : {Ï : Level} â†’ BinRel B Ï â†’ (A â†’ B) â†’ BinRel A Ï
 kerRel _â‰ˆ_ g x y = g x â‰ˆ g y

 kernelRel : {Ï : Level} â†’ BinRel B Ï â†’ (A â†’ B) â†’ Pred (A Ã— A) Ï
 kernelRel _â‰ˆ_ g (x , y) = g x â‰ˆ g y

 open IsEquivalence

 kerRelOfEquiv : {Ï : Level}{R : BinRel B Ï} â†’ IsEquivalence R â†’ (h : A â†’ B) â†’ IsEquivalence (kerRel R h)
 kerRelOfEquiv eqR h = record { refl = refl eqR ; sym = sym eqR ; trans = trans eqR }

 kerlift : (A â†’ B) â†’ (Ï : Level) â†’ BinRel A (Î² âŠ” Ï)
 kerlift g Ï x y = Lift Ï (g x â‰¡ g y)

 ker' : (A â†’ B) â†’ (I : Type ğ“¥) â†’ BinRel (I â†’ A) (Î² âŠ” ğ“¥)
 ker' g I x y = g âˆ˜ x â‰¡ g âˆ˜ y

 kernel : (A â†’ B) â†’ Pred (A Ã— A) Î²
 kernel g (x , y) = g x â‰¡ g y


-- The *identity relation* (equivalently, the kernel of a 1-to-1 function)
0[_] : (A : Type Î±) â†’ {Ï : Level} â†’ BinRel A (Î± âŠ” Ï)
0[ A ] {Ï} = Î» x y â†’ Lift Ï (x â‰¡ y)

module _ {A : Type (Î± âŠ” Ï)} where

 -- Subset containment relation for binary realtions
 _âŠ‘_ : BinRel A Ï â†’ BinRel A Ï â†’ Type (Î± âŠ” Ï)
 P âŠ‘ Q = âˆ€ x y â†’ P x y â†’ Q x y

 âŠ‘-refl : Reflexive _âŠ‘_
 âŠ‘-refl = Î» _ _ z â†’ z

 âŠ‘-trans : Transitive _âŠ‘_
 âŠ‘-trans PâŠ‘Q QâŠ‘R x y Pxy = QâŠ‘R x y (PâŠ‘Q x y Pxy)

\end{code}


### <a id="operation-type-and-compatibility">Operation type and compatibility</a>

**Notation**. For consistency and readability, we reserve two universe variables for
special purposes.  The first of these is ğ“ which shall be reserved for types that
represent *operation symbols*. The second is ğ“¥ which we
reserve for types representing *arities* of relations or operations.

In the next subsection, we define types that are useful for asserting and proving
facts about *compatibility* of *operations* with relations, but first we need a
general type with which to represent operations.  Here is the definition, which we
justify below.

The type `Op` encodes the arity of an operation as an arbitrary type `I : Type ğ“¥`,
which gives us a very general way to represent an operation as a function type with
domain `I â†’ A` (the type of "tuples") and codomain `A`. For example, the `I`-*ary
projection operations* on `A` are represented as inhabitants of the type `Op I A` as
follows.

\begin{code}

-- The type of operations on A of arity I
Op : Type Î± â†’ Type ğ“¥ â†’ Type (Î± âŠ” ğ“¥)
Op A I = (I â†’ A) â†’ A

-- Example (projections)
Ï€ : {I : Type ğ“¥} {A : Type Î± } â†’ I â†’ Op A I
Ï€ i x = x i

-- return the arity of a given operation symbol

arity[_] : {I : Type ğ“¥} {A : Type Î± } â†’ Op A I â†’ Type ğ“¥
arity[_] {I = I} f = I

-- lift a binary relation to the corresponding `I`-ary relation.

eval-rel : {A : Type Î±}{I : Type ğ“¥} â†’ BinRel A Ï â†’ BinRel (I â†’ A) (ğ“¥ âŠ” Ï)
eval-rel R u v = âˆ€ i â†’ R (u i) (v i)

eval-pred : {A : Type Î±}{I : Type ğ“¥} â†’ Pred (A Ã— A) Ï â†’ BinRel (I â†’ A) (ğ“¥ âŠ” Ï)
eval-pred P u v = âˆ€ i â†’ (u i , v i) âˆˆ P

\end{code}

If `f : Op I` and `R : Rel A Î²`, then we say `f` and `R` are *compatible* just in case `âˆ€ u v : I â†’ A`, `Î  i ê‰ I , R (u i) (v i)  â†’  R (f u) (f v)`.

\begin{code}

_preserves_ : {A : Type Î±}{I : Type ğ“¥} â†’ Op A I â†’ BinRel A Ï â†’ Type (Î± âŠ” ğ“¥ âŠ” Ï)
f preserves R  = âˆ€ u v â†’ (eval-rel R) u v â†’ R (f u) (f v)

--shorthand notation for preserves
_|:_ : {A : Type Î±}{I : Type ğ“¥} â†’ Op A I â†’ BinRel A Ï â†’ Type (Î± âŠ” ğ“¥ âŠ” Ï)
f |: R  = (eval-rel R) =[ f ]â‡’ R

-- predicate version of the compatibility relation
_preserves-pred_ : {A : Type Î±}{I : Type ğ“¥} â†’ Op A I â†’ Pred ( A Ã— A ) Ï â†’ Type (Î± âŠ” ğ“¥ âŠ” Ï)
f preserves-pred P  = âˆ€ u v â†’ (eval-pred P) u v â†’ (f u , f v) âˆˆ P

_|:pred_ : {A : Type Î±}{I : Type ğ“¥} â†’ Op A I â†’ Pred (A Ã— A) Ï â†’ Type (Î± âŠ” ğ“¥ âŠ” Ï)
f |:pred P  = (eval-pred P) =[ f ]â‡’ Î» x y â†’ (x , y) âˆˆ P


-- The two types just defined are logically equivalent.
module _ {A : Type Î±}{I : Type ğ“¥}{f : Op A I}{R : BinRel A Ï} where
 compatibility-agreement : f preserves R â†’ f |: R
 compatibility-agreement c {x}{y} Rxy = c x y Rxy
 compatibility-agreement' : f |: R â†’ f preserves R
 compatibility-agreement' c = Î» u v x â†’ c x

\end{code}

--------------------------------------

<span style="float:left;">[â†‘ Base.Relations](Base.Relations.html)</span>
<span style="float:right;">[Base.Relations.Continuous â†’](Base.Relations.Continuous.html)</span>

{% include UALib.Links.md %}
