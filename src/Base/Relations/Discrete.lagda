---
layout: default
title : "Base.Relations.Discrete module (The Agda Universal Algebra Library)"
date : "2021-02-28"
author: "the agda-algebras development team"
---

### <a id="discrete-relations">Discrete Relations</a>

This is the [Base.Relations.Discrete][] module of the [Agda Universal Algebra Library][].

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

module Base.Relations.Discrete where

-- Imports from Agda and the Agda Standard Library ----------------------------------------------
open import Agda.Primitive               using () renaming ( Set to Type )
open import Data.Product                 using ( _,_ ; _Ã—_ )
open import Function.Base                using ( _âˆ˜_ )
open import Level                        using ( _âŠ”_ ; Level ; Lift )
open import Relation.Binary              using ( IsEquivalence ; _â‡’_ ; _=[_]â‡’_ )
                                      renaming ( REL to BinREL ; Rel to BinRel )
open import Relation.Binary.Definitions  using ( Reflexive ; Transitive )
open import Relation.Unary               using ( _âˆˆ_; Pred )
open import Relation.Binary.PropositionalEquality using ( _â‰¡_ )

-- Imports from agda-algebras -------------------------------------------------------------------
open import Overture using (_â‰ˆ_ ; Î -syntax ; Op)

private variable a b Ï ğ“¥ : Level
\end{code}

We begin with a definition that is useful for defining poitwise "equality" of functions
with respect to a given "equality" relation (see also the definition of `_â‰ˆÌ‡_` in the [Base.Adjunction.Residuation][] module).

\begin{code}

module _ {A : Type a} where

 PointWise : {B : Type b} (_â‰‹_ : BinRel B Ï) â†’ BinRel (A â†’ B) _
 PointWise {B = B} _â‰‹_ = Î» (f g : A â†’ B) â†’ âˆ€ x â†’ f x â‰‹ g x

\end{code}

Thus, given a binary relation `â‰‹` on â€µB`, and a pair of functions `f, g : A â†’ B`,
we have `f (Pointwise _â‰‹_) g` provided `âˆ€ x â†’ f x â‰‹ g x`.

Here is the analogous definition for dependent functions.

\begin{code}

 depPointWise :  {B : A â†’ Type b }
                 (_â‰‹_ : {Î³ : Level}{C : Type Î³} â†’ BinRel C Ï)
  â†’              BinRel ((a : A) â†’ B a) _
 depPointWise {B = B} _â‰‹_ = Î» (f g : (a : A) â†’ B a) â†’ âˆ€ x â†’ f x â‰‹ g x

\end{code}

Next we define a type that is useful for asserting that the image of a function
is contained in a particular "subset" (predicate) of the codomain.

\begin{code}

 Im_âŠ†_ : {B : Type b} â†’ (A â†’ B) â†’ Pred B Ï â†’ Type (a âŠ” Ï)
 Im f âŠ† S = âˆ€ x â†’ f x âˆˆ S

\end{code}


#### <a id="operation-symbols-unary-relations-binary-relations">Operation symbols, unary relations, binary relations</a>

The unary relation (or "predicate") type is imported from Relation.Unary of the [Agda Standard Library][].

```agda
Pred : âˆ€ {a} â†’ Type a â†’ (â„“ : Level) â†’ Type (a âŠ” suc â„“)
Pred A â„“ = A â†’ Type â„“
```
We represent "sets" as inhabitants of such predicate types.

(In the definition of `Pred` above, we replaced `Set` with `Type` for consistency with our notation.)

Sometimes it is useful to obtain the underlying type (`A`) over which the predicates in `Pred A â„“` (the "subsets" of `A`) are defined.

\begin{code}

 PredType : Pred A Ï â†’ Type a
 PredType _ = A

\end{code}

The binary relation types are called `Rel` and `REL` in the standard library, but we
will call them `BinRel` and `BinREL` and reserve the names `Rel` and `REL` for the relation
types we define below and in the [Base.Relations.Continuous][] module.

We import the "heterogeneous" binary relation type from the standard library and renamed `BinREL`.

```agda
BinREL : âˆ€ {â„“} (A B : Type â„“) (â„“' : Level) â†’ Type (â„“-max â„“ (â„“-suc â„“'))
BinREL A B â„“' = A â†’ B â†’ Type â„“'
```

A special case, the homogeneous binary relation type is also imported and renamed `BinRel`.

```agda
BinRel : âˆ€{â„“} â†’ Type â„“ â†’ (â„“' : Level) â†’ Type (â„“ âŠ” lsuc â„“')
BinRel A â„“' = REL A A â„“'
```

Occasionally it is useful to extract the universe level over which a binary relation is defined.

\begin{code}

 Level-of-Rel : {â„“ : Level} â†’ BinRel A â„“ â†’ Level
 Level-of-Rel {â„“} _ = â„“
\end{code}


#### <a id="kernels">Kernels</a>

The *kernel* of `f : A â†’ B` is defined informally by `{(x , y) âˆˆ A Ã— A : f x = f y}`.
This can be represented in type theory and Agda in a number of ways, each of which
may be useful in a particular context. For example, we could define the kernel
to be an inhabitant of a (binary) relation type, or a (unary) predicate type.

\begin{code}

module _ {A : Type a}{B : Type b} where

 ker : (A â†’ B) â†’ BinRel A b
 ker g x y = g x â‰¡ g y

 kerRel : {Ï : Level} â†’ BinRel B Ï â†’ (A â†’ B) â†’ BinRel A Ï
 kerRel _â‰ˆ_ g x y = g x â‰ˆ g y

 kernelRel : {Ï : Level} â†’ BinRel B Ï â†’ (A â†’ B) â†’ Pred (A Ã— A) Ï
 kernelRel _â‰ˆ_ g (x , y) = g x â‰ˆ g y

 open IsEquivalence

 kerRelOfEquiv :  {Ï : Level}{R : BinRel B Ï}
  â†’               IsEquivalence R â†’ (h : A â†’ B) â†’ IsEquivalence (kerRel R h)

 kerRelOfEquiv eqR h = record  { refl = refl eqR
                               ; sym = sym eqR
                               ; trans = trans eqR
                               }

 kerlift : (A â†’ B) â†’ (Ï : Level) â†’ BinRel A (b âŠ” Ï)
 kerlift g Ï x y = Lift Ï (g x â‰¡ g y)

 ker' : (A â†’ B) â†’ (I : Type ğ“¥) â†’ BinRel (I â†’ A) (b âŠ” ğ“¥)
 ker' g I x y = g âˆ˜ x â‰¡ g âˆ˜ y

 kernel : (A â†’ B) â†’ Pred (A Ã— A) b
 kernel g (x , y) = g x â‰¡ g y

-- The *identity relation* (equivalently, the kernel of a 1-to-1 function)
0[_] : (A : Type a) â†’ {Ï : Level} â†’ BinRel A (a âŠ” Ï)
0[ A ] {Ï} = Î» x y â†’ Lift Ï (x â‰¡ y)

module _ {A : Type (a âŠ” Ï)} where

 -- Subset containment relation for binary realtions
 _âŠ‘_ : BinRel A Ï â†’ BinRel A Ï â†’ Type (a âŠ” Ï)
 P âŠ‘ Q = âˆ€ x y â†’ P x y â†’ Q x y

 âŠ‘-refl : Reflexive _âŠ‘_
 âŠ‘-refl = Î» _ _ z â†’ z

 âŠ‘-trans : Transitive _âŠ‘_
 âŠ‘-trans PâŠ‘Q QâŠ‘R x y Pxy = QâŠ‘R x y (PâŠ‘Q x y Pxy)
\end{code}


### <a id="compatibility-of-operations-and-relations">Compatibility of operations and relations</a>

Recall, from the [Overture.Signatures][] and [Overture.Operations][] modules which established
our convention of reserving the sybmols `ğ“` and `ğ“¥` for types that
represent operation symbols and arities, respectively.

In the present subsection, we define types that are useful for asserting and proving
facts about *compatibility* of operations and relations

\begin{code}

-- lift a binary relation to the corresponding `I`-ary relation.

eval-rel : {A : Type a}{I : Type ğ“¥} â†’ BinRel A Ï â†’ BinRel (I â†’ A) (ğ“¥ âŠ” Ï)
eval-rel R u v = âˆ€ i â†’ R (u i) (v i)

eval-pred : {A : Type a}{I : Type ğ“¥} â†’ Pred (A Ã— A) Ï â†’ BinRel (I â†’ A) (ğ“¥ âŠ” Ï)
eval-pred P u v = âˆ€ i â†’ (u i , v i) âˆˆ P

\end{code}

If `f : Op I` and `R : Rel A b`, then we say `f` and `R` are *compatible* just in case `âˆ€ u v : I â†’ A`, `Î  i ê‰ I , R (u i) (v i)  â†’  R (f u) (f v)`.

\begin{code}

_preserves_ : {A : Type a}{I : Type ğ“¥} â†’ Op A I â†’ BinRel A Ï â†’ Type (a âŠ” ğ“¥ âŠ” Ï)
f preserves R  = âˆ€ u v â†’ (eval-rel R) u v â†’ R (f u) (f v)

--shorthand notation for preserves
_|:_ : {A : Type a}{I : Type ğ“¥} â†’ Op A I â†’ BinRel A Ï â†’ Type (a âŠ” ğ“¥ âŠ” Ï)
f |: R  = (eval-rel R) =[ f ]â‡’ R

-- predicate version of the compatibility relation
_preserves-pred_ : {A : Type a}{I : Type ğ“¥} â†’ Op A I â†’ Pred ( A Ã— A ) Ï â†’ Type (a âŠ” ğ“¥ âŠ” Ï)
f preserves-pred P  = âˆ€ u v â†’ (eval-pred P) u v â†’ (f u , f v) âˆˆ P

_|:pred_ : {A : Type a}{I : Type ğ“¥} â†’ Op A I â†’ Pred (A Ã— A) Ï â†’ Type (a âŠ” ğ“¥ âŠ” Ï)
f |:pred P  = (eval-pred P) =[ f ]â‡’ Î» x y â†’ (x , y) âˆˆ P


-- The two types just defined are logically equivalent.
module _ {A : Type a}{I : Type ğ“¥}{f : Op A I}{R : BinRel A Ï} where
 compatibility-agreement : f preserves R â†’ f |: R
 compatibility-agreement c {x}{y} Rxy = c x y Rxy
 compatibility-agreement' : f |: R â†’ f preserves R
 compatibility-agreement' c = Î» u v x â†’ c x
\end{code}

--------------------------------------

<span style="float:left;">[â†‘ Base.Relations](Base.Relations.html)</span>
<span style="float:right;">[Base.Relations.Continuous â†’](Base.Relations.Continuous.html)</span>

{% include UALib.Links.md %}
