---
layout: default
title : "Base.Overture.Preliminaries module"
date : "2021-01-13"
author: "the agda-algebras development team"
---

### <a id="preliminaries">Preliminaries</a>

This is the [Base.Overture.Preliminaries][] module of the [agda-algebras][] library.

#### <a id="logical-foundations">Logical foundations</a>

(See also the Equality module of the [agda-algebras][] library.)

An Agda program typically begins by setting some options and by importing types from existing Agda libraries. Options are specified with the `OPTIONS` *pragma* and control the way Agda behaves by, for example, specifying the logical axioms and deduction rules we wish to assume when the program is type-checked to verify its correctness. Every Agda program in [agda-algebras](https://github.com/ualib/agda-algebras) begins with the following line.
\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

\end{code}
These options control certain foundational assumptions that Agda makes when type-checking the program to verify its correctness.

* `--without-K` disables [Streicher's K axiom](https://ncatlab.org/nlab/show/axiom+K+%28type+theory%29) ; see also the [section on axiom K](https://agda.readthedocs.io/en/v2.6.1/language/without-k.html) in the [Agda Language Reference Manual](https://agda.readthedocs.io/en/v2.6.1.3/language).

* `--exact-split` makes Agda accept only those definitions that behave like so-called *judgmental* equalities.  [MartÃ­n EscardÃ³](https://www.cs.bham.ac.uk/~mhe) explains this by saying it "makes sure that pattern matching corresponds to Martin-LÃ¶f eliminators;" see also the [Pattern matching and equality section](https://agda.readthedocs.io/en/v2.6.1/tools/command-line-options.html#pattern-matching-and-equality) of the [Agda Tools](https://agda.readthedocs.io/en/v2.6.1.3/tools/) documentation.

* `safe` ensures that nothing is postulated outright---every non-MLTT axiom has to be an explicit assumption (e.g., an argument to a function or module); see also [this section](https://agda.readthedocs.io/en/v2.6.1/tools/command-line-options.html#cmdoption-safe) of the [Agda Tools](https://agda.readthedocs.io/en/v2.6.1.3/tools/) documentation and the [Safe Agda section](https://agda.readthedocs.io/en/v2.6.1/language/safe-agda.html#safe-agda) of the [Agda Language Reference](https://agda.readthedocs.io/en/v2.6.1.3/language).

Note that if we wish to type-check a file that imports another file that still has some unmet proof obligations, we must replace the `--safe` flag with `--allow-unsolved-metas`, but this is never done in (publicly released versions of) the [agda-algebras](https://github.com/ualib/agda-algebras) library.


#### <a id="agda-modules">Agda Modules</a>

The `OPTIONS` pragma is usually followed by the start of a module.  For example, the [Base.Overture.Preliminaries][] module begins with the following line, and then a list of imports of things used in the module.
\begin{code}

module Base.Overture.Preliminaries where

-- Imports from Agda and the Agda Standard Library -----------------------------------------------
open import Agda.Primitive   using ( _âŠ”_ ; lsuc ) renaming ( Set to  Type ; lzero to  â„“â‚€ )
open import Data.Product     using ( _,_ ; âˆƒ ; Î£-syntax ; _Ã—_ ) renaming ( projâ‚ to fst ; projâ‚‚ to snd )
open import Function.Base    using ( _âˆ˜_ ; id )
open import Level            using ( Level ; Lift ; lift ; lower )
open import Relation.Binary  using ( Decidable )
open import Relation.Binary.Structures using ( IsEquivalence ; IsPartialOrder )
open import Relation.Binary.PropositionalEquality using ( _â‰¡_ ; refl ; sym ; trans )
open import Relation.Nullary using ( Dec ; yes ; no ; Irrelevant )
private variable Î± Î² : Level

â„“â‚ : Level
â„“â‚ = lsuc â„“â‚€

-- the two element type
data ğŸš : Type â„“â‚€ where  -- We could use Bool instead.
 ğŸ : ğŸš ;  ğŸ : ğŸš

-- the three element type
data ğŸ› : Type â„“â‚€ where
 ğŸ : ğŸ› ;  ğŸ : ğŸ› ;  ğŸ : ğŸ›
\end{code}

#### <a id="projection-notation">Projection notation</a>

The definition of `Î£` (and thus, of `Ã—`) includes the fields `projâ‚` and `projâ‚‚` representing the first and second projections out of the product.  However, we prefer the shorter names `fst` and `snd`.  Sometimes we prefer to denote these projections by `âˆ£_âˆ£` and `âˆ¥_âˆ¥`, respectively. We define these alternative notations for projections out of pairs as follows.

\begin{code}

module _ {A : Type Î± }{B : A â†’ Type Î²} where

 âˆ£_âˆ£ : Î£[ x âˆˆ A ] B x â†’ A
 âˆ£_âˆ£ = fst

 âˆ¥_âˆ¥ : (z : Î£[ a âˆˆ A ] B a) â†’ B âˆ£ z âˆ£
 âˆ¥_âˆ¥ = snd

 infix  40 âˆ£_âˆ£

\end{code}

Here we put the definitions inside an *anonymous module*, which starts with the `module` keyword followed by an underscore (instead of a module name). The purpose is simply to move the postulated typing judgments---the "parameters" of the module (e.g., `A : Type Î±`)---out of the way so they don't obfuscate the definitions inside the module.

Let's define some useful syntactic sugar that will make it easier to apply symmetry and transitivity of `â‰¡` in proofs.

\begin{code}

_â»Â¹ : {A : Type Î±} {x y : A} â†’ x â‰¡ y â†’ y â‰¡ x
p â»Â¹ = sym p

infix  40 _â»Â¹

\end{code}

If we have a proof `p : x â‰¡ y`, and we need a proof of `y â‰¡ x`, then instead of `sym p` we can use the more intuitive `p â»Â¹`. Similarly, the following syntactic sugar makes abundant appeals to transitivity easier to stomach.

\begin{code}

_âˆ™_ : {A : Type Î±}{x y z : A} â†’ x â‰¡ y â†’ y â‰¡ z â†’ x â‰¡ z
p âˆ™ q = trans p q

ğ‘–ğ‘‘ : (A : Type Î± ) â†’ A â†’ A
ğ‘–ğ‘‘ A = Î» x â†’ x

infixl 30 _âˆ™_
\end{code}

#### <a id="sigma-types">Sigma types</a>

\begin{code}

infix 2 âˆƒ-syntax

âˆƒ-syntax : âˆ€ {A : Type Î±} â†’ (A â†’ Type Î²) â†’ Set (Î± âŠ” Î²)
âˆƒ-syntax = âˆƒ

syntax âˆƒ-syntax (Î» x â†’ B) = âˆƒ[ x âˆˆ A ] B

\end{code}




#### <a id="pi-types">Pi types</a>

The dependent function type is traditionally denoted with an uppercase pi symbol and typically expressed as `Î (x : A) B x`, or something similar.  In Agda syntax, one writes `(x : A) â†’ B x` for this dependent function type, but we can define syntax that is closer to standard notation as follows.
\begin{code}

Î  : {A : Type Î± } (B : A â†’ Type Î² ) â†’ Type (Î± âŠ” Î²)
Î  {A = A} B = (x : A) â†’ B x

Î -syntax : (A : Type Î±)(B : A â†’ Type Î²) â†’ Type (Î± âŠ” Î²)
Î -syntax A B = Î  B

syntax Î -syntax A (Î» x â†’ B) = Î [ x âˆˆ A ] B
infix 6 Î -syntax

\end{code}
In the modules that follow, we will see many examples of this syntax in action.


#### <a id="agdas-universe-hierarchy">Agda's universe hierarchy</a>

The hierarchy of universes in Agda is structured as follows:
```agda

Type Î± : Type (lsuc Î±) ,   Type (lsuc Î±) : Type (lsuc (lsuc Î±)) , etc.

```
and so on. This means that the universe `Type Î±` has type `Type(lsuc Î±)`, and  `Type(lsuc Î±)` has type `Type(lsuc (lsuc Î±))`, and so on.  It is important to note, however, this does *not* imply that  `Type Î± : Type(lsuc(lsuc Î±))`. In other words, Agda's universe hierarchy is *non-cumulative*. This makes it possible to treat universe levels more precisely, which is nice. On the other hand, a non-cumulative hierarchy can sometimes make for a non-fun proof assistant. Specifically, in certain situations, the non-cumulativity makes it unduly difficult to convince Agda that a program or proof is correct.


#### <a id="lifting-and-lowering">Lifting and lowering</a>

Here we describe a general `Lift` type that help us overcome the technical issue described in the previous subsection.  In the [Lifts of algebras section](Base.Algebras.Basic.html#lifts-of-algebras) of the [Base.Algebras.Basic][] module we will define a couple domain-specific lifting types which have certain properties that make them useful for resolving universe level problems when working with algebra types.

Let us be more concrete about what is at issue here by considering a typical example. Agda will often complain with errors like the following:
```
Birkhoff.lagda:498,20-23
Î± != ğ“ âŠ” ğ“¥ âŠ” (lsuc Î±) when checking that the expression... has type...
```
This error message means that Agda encountered the universe level `lsuc Î±`, on line 498 (columns 20--23) of the file `Birkhoff.lagda`, but was expecting a type at level `ğ“ âŠ” ğ“¥ âŠ” lsuc Î±` instead.

The general `Lift` record type that we now describe makes such problems easier to deal with. It takes a type inhabiting some universe and embeds it into a higher universe and, apart from syntax and notation, it is equivalent to the `Lift` type one finds in the `Level` module of the [Agda Standard Library][].
```agda
record Lift {ğ“¦ Î± : Level} (A : Set Î±) : Set (Î± âŠ” ğ“¦) where
```
```agda
    constructor lift
```
```agda
    field lower : A
```
The point of having a ramified hierarchy of universes is to avoid Russell's paradox, and this would be subverted if we were to lower the universe of a type that wasn't previously lifted.  However, we can prove that if an application of `lower` is immediately followed by an application of `lift`, then the result is the identity transformation. Similarly, `lift` followed by `lower` is the identity.
\begin{code}

liftâˆ¼lower : {A : Type Î±} â†’ lift âˆ˜ lower â‰¡ ğ‘–ğ‘‘ (Lift Î² A)
liftâˆ¼lower = refl

lowerâˆ¼lift : {A : Type Î±} â†’ (lower {Î±}{Î²}) âˆ˜ lift â‰¡ ğ‘–ğ‘‘ A
lowerâˆ¼lift = refl

\end{code}
The proofs are trivial. Nonetheless, we'll come across some holes these lemmas can fill.


#### <a id="pointwise-equality-of-dependent-functions">Pointwise equality of dependent functions</a>

We conclude this module with a definition that conveniently represents te assertion that two functions are (extensionally) the same in the sense that they produce the same output when given the same input.  (We will have more to say about this notion of equality in the [Base.Equality.Extensionality][] module.)
\begin{code}

module _ {Î± : Level}{A : Type Î±}{Î² : Level}{B : A â†’ Type Î² } where

 _â‰ˆ_ :  (f g : (a : A) â†’ B a) â†’ Type (Î± âŠ” Î²)
 f â‰ˆ g = âˆ€ x â†’ f x â‰¡ g x

 infix 8 _â‰ˆ_

 â‰ˆIsEquivalence : IsEquivalence _â‰ˆ_
 IsEquivalence.refl â‰ˆIsEquivalence = Î» _ â†’ refl
 IsEquivalence.sym â‰ˆIsEquivalence {f}{g} fâ‰ˆg = Î» x â†’ sym (fâ‰ˆg x)
 IsEquivalence.trans â‰ˆIsEquivalence {f}{g}{h} fâ‰ˆg gâ‰ˆh = Î» x â†’ trans (fâ‰ˆg x) (gâ‰ˆh x)

\end{code}
The following is convenient for proving two pairs of a product type are equal using the fact that their
respective components are equal.
\begin{code}

â‰¡-by-parts : {A : Type Î±}{B : Type Î²}{u v : A Ã— B} â†’ fst u â‰¡ fst v â†’ snd u â‰¡ snd v â†’ u â‰¡ v
â‰¡-by-parts refl refl = refl

\end{code}
Lastly, we will use the following type (instead of `subst`) to transport equality proofs.

\begin{code}

transport : {A : Type Î± } (B : A â†’ Type Î²) {x y : A} â†’ x â‰¡ y â†’ B x â†’ B y
transport B refl = id

\end{code}

------------------------------

<span style="float:left;">[â†‘ Base.Overture](Base.Overture.html)</span>
<span style="float:right;">[Base.Overture.Inverses â†’](Base.Overture.Inverses.html)</span>

{% include UALib.Links.md %}


