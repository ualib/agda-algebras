---
layout: default
title : "Base.Terms.Operations module (The Agda Universal Algebra Library)"
date : "2021-01-14"
author: "agda-algebras development team"
---

### <a id="term-operations">Term Operations</a>

This section presents the [Base.Terms.Operations][] module of the [Agda Universal Algebra Library][].

Here we define *term operations* which are simply terms interpreted in a particular algebra, and we prove some compatibility properties of term operations.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Base.Algebras.Basic

module Base.Terms.Operations {ğ‘† : Signature ğ“ ğ“¥} where

-- Imports from Agda and the Agda Standard Library ---------------------
open import Agda.Primitive using ( _âŠ”_ ; lsuc ; Level ) renaming ( Set to Type )
open import Axiom.Extensionality.Propositional
                           using () renaming (Extensionality to funext)
open import Data.Product   using ( _,_ ; Î£-syntax ; Î£ )
open import Function.Base  using ( _âˆ˜_ )
open import Relation.Binary.PropositionalEquality
                           using ( _â‰¡_ ; refl ; module â‰¡-Reasoning ; sym ; cong )

-- Imports from Agda Universal Algebra Library ----------------------------------------------
open import Base.Overture.Preliminaries       using ( _âˆ™_ ; _â»Â¹ ; âˆ£_âˆ£ ; âˆ¥_âˆ¥ ; Î  ; Î -syntax ; _â‰ˆ_ )
open import Base.Relations.Discrete           using ( _|:_ )
open import Base.Equality.Welldefined         using ( swelldef )
open import Base.Algebras.Products    {ğ‘† = ğ‘†} using ( ov ; â¨… )
open import Base.Algebras.Congruences {ğ‘† = ğ‘†} using ( Con ; IsCongruence)
open import Base.Homomorphisms.Basic  {ğ‘† = ğ‘†} using ( hom )
open import Base.Terms.Basic          {ğ‘† = ğ‘†} using ( Term ; ğ‘» )
open import Base.Terms.Properties     {ğ‘† = ğ‘†} using ( free-lift )

open Term

private variable Î± Î² Î³ Ï Ï‡ : Level

\end{code}

When we interpret a term in an algebra we call the resulting function a *term operation*.  Given a term `p` and an algebra `ğ‘¨`, we denote by `ğ‘¨ âŸ¦ p âŸ§` the *interpretation* of `p` in `ğ‘¨`.  This is defined inductively as follows.

1. If `p` is a variable symbol `x : X` and if `a : X â†’ âˆ£ ğ‘¨ âˆ£` is a tuple of elements of `âˆ£ ğ‘¨ âˆ£`, then `ğ‘¨ âŸ¦ p âŸ§ a := a x`.

2. If `p = f t`, where `f : âˆ£ ğ‘† âˆ£` is an operation symbol, if `t : âˆ¥ ğ‘† âˆ¥ f â†’ ğ‘» X` is a tuple of terms, and if `a : X â†’ âˆ£ ğ‘¨ âˆ£` is a tuple from `ğ‘¨`, then we define `ğ‘¨ âŸ¦ p âŸ§ a = ğ‘¨ âŸ¦ f t âŸ§ a := (f Ì‚ ğ‘¨) (Î» i â†’ ğ‘¨ âŸ¦ t i âŸ§ a)`.

Thus the interpretation of a term is defined by induction on the structure of the term, and the definition is formally implemented in the [agda-algebras](https://github.com/ualib/agda-algebras) library as follows.

\begin{code}

_âŸ¦_âŸ§ : (ğ‘¨ : Algebra Î± ğ‘†){X : Type Ï‡ } â†’ Term X â†’ (X â†’ âˆ£ ğ‘¨ âˆ£) â†’ âˆ£ ğ‘¨ âˆ£
ğ‘¨ âŸ¦ â„Š x âŸ§ = Î» Î· â†’ Î· x
ğ‘¨ âŸ¦ node f t âŸ§ = Î» Î· â†’ (f Ì‚ ğ‘¨) (Î» i â†’ (ğ‘¨ âŸ¦ t i âŸ§) Î·)

\end{code}

It turns out that the intepretation of a term is the same as the `free-lift` (modulo argument order and assuming function extensionality).

\begin{code}

free-lift-interp : swelldef ğ“¥ Î± â†’ (ğ‘¨ : Algebra Î± ğ‘†){X : Type Ï‡ }(Î· : X â†’ âˆ£ ğ‘¨ âˆ£)(p : Term X)
 â†’                 (ğ‘¨ âŸ¦ p âŸ§) Î· â‰¡ (free-lift ğ‘¨ Î·) p

free-lift-interp _ ğ‘¨ Î· (â„Š x) = refl
free-lift-interp wd ğ‘¨ Î· (node f t) = wd (f Ì‚ ğ‘¨) (Î» z â†’ (ğ‘¨ âŸ¦ t z âŸ§) Î·)
                                       ((free-lift ğ‘¨ Î·) âˆ˜ t)((free-lift-interp wd ğ‘¨ Î·) âˆ˜ t)

\end{code}

If the algebra in question happens to be `ğ‘» X`, then we expect that `âˆ€ s` we have `(ğ‘» X)âŸ¦ p âŸ§ s â‰¡ p s`. But what is `(ğ‘» X)âŸ¦ p âŸ§ s` exactly? By definition, it depends on the form of `p` as follows:

* if `p = â„Š x`, then `(ğ‘» X)âŸ¦ p âŸ§ s := (ğ‘» X)âŸ¦ â„Š x âŸ§ s â‰¡ s x`

* if `p = node f t`, then `(ğ‘» X)âŸ¦ p âŸ§ s := (ğ‘» X)âŸ¦ node f t âŸ§ s = (f Ì‚ ğ‘» X) Î» i â†’ (ğ‘» X)âŸ¦ t i âŸ§ s`

Now, assume `Ï• : hom ğ‘» ğ‘¨`. Then by `comm-hom-term`, we have `âˆ£ Ï• âˆ£ (ğ‘» X)âŸ¦ p âŸ§ s = ğ‘¨ âŸ¦ p âŸ§ âˆ£ Ï• âˆ£ âˆ˜ s`.

* if `p = â„Š x` (and `t : X â†’ âˆ£ ğ‘» X âˆ£`), then

  `âˆ£ Ï• âˆ£ p â‰¡ âˆ£ Ï• âˆ£ (â„Š x) â‰¡ âˆ£ Ï• âˆ£ (Î» t â†’ h t) â‰¡ Î» t â†’ (âˆ£ Ï• âˆ£ âˆ˜ t) x`

* if `p = node f t`, then

   âˆ£ Ï• âˆ£ p â‰¡ âˆ£ Ï• âˆ£ (ğ‘» X)âŸ¦ p âŸ§ s = (ğ‘» X)âŸ¦ node f t âŸ§ s = (f Ì‚ ğ‘» X) Î» i â†’ (ğ‘» X)âŸ¦ t i âŸ§ s

We claim that for all `p : Term X` there exists `q : Term X` and `t : X â†’ âˆ£ ğ‘» X âˆ£` such that `p â‰¡ (ğ‘» X)âŸ¦ q âŸ§ t`. We prove this fact as follows.

\begin{code}

term-interp : {X : Type Ï‡} (f : âˆ£ ğ‘† âˆ£){s t : âˆ¥ ğ‘† âˆ¥ f â†’ Term X} â†’ s â‰¡ t â†’ node f s â‰¡ (f Ì‚ ğ‘» X) t
term-interp f {s}{t} st = cong (node f) st

term-interp' : swelldef ğ“¥ (ov Ï‡) â†’ {X : Type Ï‡} (f : âˆ£ ğ‘† âˆ£){s t : âˆ¥ ğ‘† âˆ¥ f â†’ Term X}
 â†’             (âˆ€ i â†’ s i â‰¡ t i) â†’ node f s â‰¡ (f Ì‚ ğ‘» X) t
term-interp' wd f {s}{t} st = wd (node f) s t st

term-gen : swelldef ğ“¥ (ov Ï‡) â†’ {X : Type Ï‡}(p : âˆ£ ğ‘» X âˆ£) â†’ Î£[ q âˆˆ âˆ£ ğ‘» X âˆ£ ] p â‰¡ (ğ‘» X âŸ¦ q âŸ§) â„Š
term-gen _ (â„Š x) = (â„Š x) , refl
term-gen wd (node f t) = (node f (Î» i â†’ âˆ£ term-gen wd (t i) âˆ£)) ,
                         term-interp' wd f Î» i â†’ âˆ¥ term-gen wd (t i) âˆ¥

term-gen-agreement : (wd : swelldef ğ“¥ (ov Ï‡)){X : Type Ï‡}(p : âˆ£ ğ‘» X âˆ£) â†’ (ğ‘» X âŸ¦ p âŸ§) â„Š â‰¡ (ğ‘» X âŸ¦ âˆ£ term-gen wd p âˆ£ âŸ§) â„Š
term-gen-agreement _ (â„Š x) = refl
term-gen-agreement wd {X} (node f t) = wd (f Ì‚ ğ‘» X) (Î» x â†’ (ğ‘» X âŸ¦ t x âŸ§) â„Š)
                                          (Î» x â†’ (ğ‘» X âŸ¦ âˆ£ term-gen wd (t x) âˆ£ âŸ§) â„Š) Î» i â†’ term-gen-agreement wd (t i)

term-agreement : swelldef ğ“¥ (ov Ï‡) â†’ {X : Type Ï‡}(p : âˆ£ ğ‘» X âˆ£) â†’ p â‰¡  (ğ‘» X âŸ¦ p âŸ§) â„Š
term-agreement wd {X} p = âˆ¥ term-gen wd p âˆ¥ âˆ™ (term-gen-agreement wd p)â»Â¹


\end{code}



#### <a id="interpretation-of-terms-in-product-algebras">Interpretation of terms in product algebras</a>

\begin{code}

module _ (wd : swelldef ğ“¥ (Î² âŠ” Î±)){X : Type Ï‡ }{I : Type Î²} where

 interp-prod : (p : Term X)(ğ’œ : I â†’ Algebra Î± ğ‘†)(a : X â†’ Î [ i âˆˆ I ] âˆ£ ğ’œ i âˆ£)
  â†’            (â¨… ğ’œ âŸ¦ p âŸ§) a â‰¡ Î» i â†’ (ğ’œ i âŸ¦ p âŸ§)(Î» x â†’ (a x) i)

 interp-prod (â„Š _) ğ’œ a = refl
 interp-prod (node f t) ğ’œ a = wd ((f Ì‚ â¨… ğ’œ)) u v IH
  where
  u : âˆ€ x â†’ âˆ£ â¨… ğ’œ âˆ£
  u = Î» x â†’ (â¨… ğ’œ âŸ¦ t x âŸ§) a
  v : âˆ€ x i â†’ âˆ£ ğ’œ i âˆ£
  v = Î» x i â†’ (ğ’œ i âŸ¦ t x âŸ§)(Î» j â†’ a j i)
  IH : âˆ€ i â†’ u i â‰¡ v i
  IH = Î» x â†’ interp-prod (t x) ğ’œ a

 interp-prod2 : funext (Î± âŠ” Î² âŠ” Ï‡) (Î± âŠ” Î²) â†’ (p : Term X)(ğ’œ : I â†’ Algebra Î± ğ‘†)
  â†’             â¨… ğ’œ âŸ¦ p âŸ§ â‰¡ (Î» a i â†’ (ğ’œ i âŸ¦ p âŸ§) Î» x â†’ a x i)
 interp-prod2 _ (â„Š xâ‚) ğ’œ = refl
 interp-prod2 fe (node f t) ğ’œ = fe Î» a â†’ wd (f Ì‚ â¨… ğ’œ)(u a) (v a) (IH a)
  where
  u : âˆ€ a x â†’ âˆ£ â¨… ğ’œ âˆ£
  u a = Î» x â†’ (â¨… ğ’œ âŸ¦ t x âŸ§) a
  v : âˆ€ (a : X â†’ âˆ£ â¨… ğ’œ âˆ£) â†’ âˆ€ x i â†’ âˆ£ ğ’œ i âˆ£
  v a = Î» x i â†’ (ğ’œ i âŸ¦ t x âŸ§)(Î» z â†’ (a z) i)
  IH : âˆ€ a x â†’ (â¨… ğ’œ âŸ¦ t x âŸ§) a â‰¡ Î» i â†’ (ğ’œ i âŸ¦ t x âŸ§)(Î» z â†’ (a z) i)
  IH a = Î» x â†’ interp-prod (t x) ğ’œ a

\end{code}


#### <a id="compatibility-of-terms">Compatibility of terms</a>

We now prove two important facts about term operations.  The first of these, which is used very often in the sequel, asserts that every term commutes with every homomorphism.

\begin{code}

open â‰¡-Reasoning

comm-hom-term : swelldef ğ“¥ Î² â†’ {ğ‘¨ : Algebra Î± ğ‘†} (ğ‘© : Algebra Î² ğ‘†)
                (h : hom ğ‘¨ ğ‘©){X : Type Ï‡}(t : Term X) (a : X â†’ âˆ£ ğ‘¨ âˆ£)
                -----------------------------------------
  â†’             âˆ£ h âˆ£ ((ğ‘¨ âŸ¦ t âŸ§) a) â‰¡ (ğ‘© âŸ¦ t âŸ§) (âˆ£ h âˆ£ âˆ˜ a)

comm-hom-term _ ğ‘© h (â„Š x) a = refl
comm-hom-term wd {ğ‘¨} ğ‘© h (node f t) a = âˆ£ h âˆ£((f Ì‚ ğ‘¨) Î» i â†’  (ğ‘¨ âŸ¦ t i âŸ§) a)    â‰¡âŸ¨ i  âŸ©
                                         (f Ì‚ ğ‘©)(Î» i â†’  âˆ£ h âˆ£ ((ğ‘¨ âŸ¦ t i âŸ§) a))  â‰¡âŸ¨ ii âŸ©
                                         (f Ì‚ ğ‘©)(Î» r â†’ (ğ‘© âŸ¦ t r âŸ§) (âˆ£ h âˆ£ âˆ˜ a)) âˆ
 where i  = âˆ¥ h âˆ¥ f Î» r â†’ (ğ‘¨ âŸ¦ t r âŸ§) a
       ii = wd (f Ì‚ ğ‘©) (Î» iâ‚ â†’ âˆ£ h âˆ£ ((ğ‘¨ âŸ¦ t iâ‚ âŸ§) a))
                       (Î» r â†’ (ğ‘© âŸ¦ t r âŸ§) (Î» x â†’ âˆ£ h âˆ£ (a x)))
                       Î» j â†’ comm-hom-term wd ğ‘© h (t j) a

\end{code}

To conclude this module, we prove that every term is compatible with every congruence relation. That is, if `t : Term X` and `Î¸ : Con ğ‘¨`, then `a Î¸ b â†’ t(a) Î¸ t(b)`. (Recall, the compatibility relation `|:` was defined in [Relations.Discrete][].)

\begin{code}


module _ {Î± Î² : Level}{X : Type Î±} where

 open IsCongruence

 _âˆ£:_ : {ğ‘¨ : Algebra Î± ğ‘†}(t : Term X)(Î¸ : Con{Î±}{Î²} ğ‘¨) â†’ (ğ‘¨ âŸ¦ t âŸ§) |: âˆ£ Î¸ âˆ£
 ((â„Š x) âˆ£: Î¸) p = p x
 ((node f t) âˆ£: Î¸) p = (is-compatible âˆ¥ Î¸ âˆ¥) f Î» x â†’ ((t x) âˆ£: Î¸) p

\end{code}

**WARNING!** The compatibility relation for terms `âˆ£:` is typed as \|:, whereas the compatibility type for functions `|:` (defined in the [Base.Relations.Discrete][] module) is typed as `|:`.



#### <a id="substitution">Substitution</a>

A substitution from `Y` to `X` is simply a function from `Y` to `X`, and the application of a substitution is represented as follows.

\begin{code}

_[_] : {Ï‡ : Level}{X Y : Type Ï‡} â†’ Term Y â†’ (Y â†’ X) â†’ Term X
(â„Š y) [ Ïƒ ] = â„Š (Ïƒ y)
(node f t)  [ Ïƒ ] = node f Î» i â†’ t i [ Ïƒ ]

\end{code}

Alternatively, we may want a substitution that replaces each variable symbol in `Y`, not with an element of `X`, but with a term from `Term X`.

\begin{code}

-- Substerm X Y, an inhabitant of which replaces each variable symbol in Y with a term from Term X.
Substerm : (X Y : Type Ï‡) â†’ Type (ov Ï‡)
Substerm X Y = (y : Y) â†’ Term X

-- Application of a Substerm.
_[_]t : {X Y : Type Ï‡ } â†’ Term Y â†’ Substerm X Y â†’ Term X
(â„Š y) [ Ïƒ ]t = Ïƒ y
(node f t) [ Ïƒ ]t = node f (Î» z â†’ (t z) [ Ïƒ ]t )

\end{code}

Next we prove the important Substitution Theorem which asserts that an identity `p â‰ˆ q` holds in an algebra `ğ‘¨` iff it holds in `ğ‘¨` after applying any substitution.

\begin{code}


subst-lemma : swelldef ğ“¥ Î± â†’ {X Y : Type Ï‡ }(p : Term Y)(Ïƒ : Y â†’ X)(ğ‘¨ : Algebra Î± ğ‘†)(Î· : X â†’ âˆ£ ğ‘¨ âˆ£)
 â†’            (ğ‘¨ âŸ¦ p [ Ïƒ ] âŸ§) Î· â‰¡ (ğ‘¨ âŸ¦ p âŸ§) (Î· âˆ˜ Ïƒ)
subst-lemma _ (â„Š x) Ïƒ ğ‘¨ Î· = refl
subst-lemma wd (node f t) Ïƒ ğ‘¨ Î· = wd (f Ì‚ ğ‘¨) (Î» i â†’ (ğ‘¨ âŸ¦ (t i) [ Ïƒ ] âŸ§) Î·)
                                             (Î» i â†’ (ğ‘¨ âŸ¦ t i âŸ§) (Î· âˆ˜ Ïƒ))
                                             (Î» i â†’ subst-lemma wd (t i) Ïƒ ğ‘¨ Î·)

subst-theorem : swelldef ğ“¥ Î± â†’ {X Y : Type Ï‡ }
                (p q : Term Y)(Ïƒ : Y â†’ X)(ğ‘¨ : Algebra Î± ğ‘†)
 â†’              ğ‘¨ âŸ¦ p âŸ§ â‰ˆ ğ‘¨ âŸ¦ q âŸ§ â†’ ğ‘¨ âŸ¦ p [ Ïƒ ] âŸ§ â‰ˆ ğ‘¨ âŸ¦ q [ Ïƒ ] âŸ§

subst-theorem wd p q Ïƒ ğ‘¨ Apq Î· = (ğ‘¨ âŸ¦ p [ Ïƒ ] âŸ§) Î· â‰¡âŸ¨ subst-lemma wd p Ïƒ ğ‘¨ Î· âŸ©
                                 (ğ‘¨ âŸ¦ p âŸ§) (Î· âˆ˜ Ïƒ) â‰¡âŸ¨ Apq (Î· âˆ˜ Ïƒ) âŸ©
                                 (ğ‘¨ âŸ¦ q âŸ§) (Î· âˆ˜ Ïƒ) â‰¡âŸ¨ sym (subst-lemma wd q Ïƒ ğ‘¨ Î·) âŸ©
                                 (ğ‘¨ âŸ¦ q [ Ïƒ ] âŸ§) Î· âˆ

\end{code}

----------------------------------

<span style="float:left;">[â† Base.Terms.Properties](Base.Terms.Properties.html)</span>
<span style="float:right;">[Base.Subalgebras â†’](Base.Subalgebras.html)</span>

{% include UALib.Links.md %}
