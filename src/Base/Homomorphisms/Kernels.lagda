---
layout: default
title : "Base.Homomorphisms.Kernels module (The Agda Universal Algebra Library)"
date : "2021-09-08"
author: "agda-algebras development team"
---

### <a id="kernels-of-homomorphisms">Kernels of Homomorphisms</a>

This is the [Base.Homomorphisms.Kernels] module of the [Agda Universal Algebra Library][].

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Base.Algebras.Basic

module Base.Homomorphisms.Kernels {ğ‘† : Signature ğ“ ğ“¥} where

-- Imports from Agda and the Agda Standard Library --------------------------------
open import Agda.Primitive using ( _âŠ”_ ; lsuc )
open import Data.Product   using ( _,_ )
open import Function.Base  using ( _âˆ˜_ )
open import Level          using ( Level )
open import Relation.Binary.PropositionalEquality
                           using ( _â‰¡_ ; module â‰¡-Reasoning ; refl )

-- -- Imports from the Agda Universal Algebras Library --------------------------------
open import Base.Overture.Preliminaries       using ( âˆ£_âˆ£ ; âˆ¥_âˆ¥ ; _â»Â¹ )
open import Base.Overture.Inverses            using ( Image_âˆ‹_ )
open import Base.Overture.Surjective          using ( IsSurjective )
open import Base.Equality.Welldefined         using ( swelldef )
open import Base.Relations.Discrete           using ( ker )
open import Base.Relations.Quotients          using ( ker-IsEquivalence ; âŸª_âŸ« ; mkblk )
open import Base.Algebras.Congruences {ğ‘† = ğ‘†} using ( Con ; mkcon ; _â•±_ ; IsCongruence ; /-â‰¡ )
open import Base.Homomorphisms.Basic  {ğ‘† = ğ‘†} using ( hom ; epi ; epiâ†’hom )

private variable Î± Î² : Level

\end{code}


#### <a id="kernels-of-homomorphisms">Kernels of homomorphisms</a>

The kernel of a homomorphism is a congruence relation and conversely for every congruence relation Î¸, there exists a homomorphism with kernel Î¸ (namely, that canonical projection onto the quotient modulo Î¸).

\begin{code}

module _ {ğ‘¨ : Algebra Î± ğ‘†} where
 open â‰¡-Reasoning
 homker-comp : swelldef ğ“¥ Î² â†’ {ğ‘© : Algebra Î² ğ‘†}(h : hom ğ‘¨ ğ‘©) â†’ compatible ğ‘¨ (ker âˆ£ h âˆ£)
 homker-comp wd {ğ‘©} h f {u}{v} kuv = âˆ£ h âˆ£((f Ì‚ ğ‘¨) u)   â‰¡âŸ¨ âˆ¥ h âˆ¥ f u âŸ©
                                     (f Ì‚ ğ‘©)(âˆ£ h âˆ£ âˆ˜ u) â‰¡âŸ¨ wd(f Ì‚ ğ‘©)(âˆ£ h âˆ£ âˆ˜ u)(âˆ£ h âˆ£ âˆ˜ v)kuv âŸ©
                                     (f Ì‚ ğ‘©)(âˆ£ h âˆ£ âˆ˜ v) â‰¡âŸ¨ (âˆ¥ h âˆ¥ f v)â»Â¹ âŸ©
                                     âˆ£ h âˆ£((f Ì‚ ğ‘¨) v)   âˆ


\end{code}

(Notice, it is here that the `swelldef` postulate comes into play, and because it is needed to prove `homker-comp`, it is postulated by all the lemmas below that depend upon `homker-comp`.)

It is convenient to define a function that takes a homomorphism and constructs a congruence from its kernel.  We call this function `kercon`.

\begin{code}

 kercon : swelldef ğ“¥ Î² â†’ {ğ‘© : Algebra Î² ğ‘†} â†’ hom ğ‘¨ ğ‘© â†’ Con{Î±}{Î²} ğ‘¨
 kercon wd {ğ‘©} h = ker âˆ£ h âˆ£ , mkcon (ker-IsEquivalence âˆ£ h âˆ£)(homker-comp wd {ğ‘©} h)

\end{code}

With this congruence we construct the corresponding quotient, along with some syntactic sugar to denote it.

\begin{code}

 kerquo : swelldef ğ“¥ Î² â†’ {ğ‘© : Algebra Î² ğ‘†} â†’ hom ğ‘¨ ğ‘© â†’ Algebra (Î± âŠ” lsuc Î²) ğ‘†
 kerquo wd {ğ‘©} h = ğ‘¨ â•± (kercon wd {ğ‘©} h)


ker[_â‡’_]_â†¾_ : (ğ‘¨ : Algebra Î± ğ‘†)(ğ‘© : Algebra Î² ğ‘†) â†’ hom ğ‘¨ ğ‘© â†’ swelldef ğ“¥ Î² â†’ Algebra (Î± âŠ” lsuc Î²) ğ‘†
ker[ ğ‘¨ â‡’ ğ‘© ] h â†¾ wd = kerquo wd {ğ‘©} h

\end{code}

Thus, given `h : hom ğ‘¨ ğ‘©`, we can construct the quotient of `ğ‘¨` modulo the kernel of `h`, and the syntax for this quotient in the [agda-algebras](https://github.com/ualib/agda-algebras) library is `ğ‘¨ [ ğ‘© ]/ker h â†¾ fe`.



#### <a id="the-canonical-projection">The canonical projection</a>

Given an algebra `ğ‘¨` and a congruence `Î¸`, the *canonical projection* is a map from `ğ‘¨` onto `ğ‘¨ â•± Î¸` that is constructed, and proved epimorphic, as follows.

\begin{code}

module _ {Î± Î² : Level}{ğ‘¨ : Algebra Î± ğ‘†} where
 Ï€epi : (Î¸ : Con{Î±}{Î²} ğ‘¨) â†’ epi ğ‘¨ (ğ‘¨ â•± Î¸)
 Ï€epi Î¸ = (Î» a â†’ âŸª a âŸ«) , (Î» _ _ â†’ refl) , cÏ€-is-epic  where
  cÏ€-is-epic : IsSurjective (Î» a â†’ âŸª a âŸ«)
  cÏ€-is-epic (C , mkblk a refl ) =  Image_âˆ‹_.eq a refl

\end{code}

In may happen that we don't care about the surjectivity of `Ï€epi`, in which case would might prefer to work with the *homomorphic reduct* of `Ï€epi`. This is obtained by applying `epi-to-hom`, like so.

\begin{code}

 Ï€hom : (Î¸ : Con{Î±}{Î²} ğ‘¨) â†’ hom ğ‘¨ (ğ‘¨ â•± Î¸)
 Ï€hom Î¸ = epiâ†’hom (ğ‘¨ â•± Î¸) (Ï€epi Î¸)

\end{code}


We combine the foregoing to define a function that takes ğ‘†-algebras `ğ‘¨` and `ğ‘©`, and a homomorphism `h : hom ğ‘¨ ğ‘©` and returns the canonical epimorphism from `ğ‘¨` onto `ğ‘¨ [ ğ‘© ]/ker h`. (Recall, the latter is the special notation we defined above for the quotient of `ğ‘¨` modulo the kernel of `h`.)

\begin{code}

 Ï€ker : (wd : swelldef ğ“¥ Î²){ğ‘© : Algebra Î² ğ‘†}(h : hom ğ‘¨ ğ‘©) â†’ epi ğ‘¨ (ker[ ğ‘¨ â‡’ ğ‘© ] h â†¾ wd)
 Ï€ker wd {ğ‘©} h = Ï€epi (kercon wd {ğ‘©} h)

\end{code}

The kernel of the canonical projection of `ğ‘¨` onto `ğ‘¨ / Î¸` is equal to `Î¸`, but since equality of inhabitants of certain types (like `Congruence` or `Rel`) can be a tricky business, we settle for proving the containment `ğ‘¨ / Î¸ âŠ† Î¸`. Of the two containments, this is the easier one to prove; luckily it is also the one we need later.

\begin{code}

 open IsCongruence

 ker-in-con : {wd : swelldef ğ“¥ (Î± âŠ” lsuc Î²)}(Î¸ : Con ğ‘¨)
  â†’           âˆ€ {x}{y} â†’ âˆ£ kercon wd {ğ‘¨ â•± Î¸} (Ï€hom Î¸) âˆ£ x y â†’  âˆ£ Î¸ âˆ£ x y

 ker-in-con Î¸ hyp = /-â‰¡ Î¸ hyp

\end{code}

---------------------------------

<span style="float:left;">[â† Base.Homomorphisms.Properties](Base.Homomorphisms.Properties.html)</span>
<span style="float:right;">[Base.Homomorphisms.Products â†’](Base.Homomorphisms.Products.html)</span>

{% include UALib.Links.md %}
