---
layout: default
title : "Base.Varieties.Properties module (The Agda Universal Algebra Library)"
date : "2021-06-24"
author: "agda-algebras development team"
---

### <a id="properties-of-the-models-relation">Properties of the models relation</a>

We prove some closure and invariance properties of the relation `âŠ§`.  In particular, we prove the following facts (which are needed, for example, in the proof the Birkhoff HSP Theorem).

* [Algebraic invariance](#algebraic-invariance). `âŠ§` is an *algebraic invariant* (stable under isomorphism).

* [Subalgebraic invariance](#subalgebraic-invariance). Identities modeled by a class of algebras are also modeled by all subalgebras of algebras in the class.

* [Product invariance](#product-invariance). Identities modeled by a class of algebras are also modeled by all products of algebras in the class.


\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Base.Algebras.Basic using ( ğ“ ; ğ“¥ ; Signature )

module Base.Varieties.Properties {ğ‘† : Signature ğ“ ğ“¥} where

-- Imports from Agda and the Agda Standard Library -------------------------------------------
open import Agda.Primitive using ( _âŠ”_ ; lsuc ; Level )   renaming ( Set to Type ; lzero to  â„“â‚€ )
open import Axiom.Extensionality.Propositional using ()   renaming ( Extensionality to funext )
open import Data.Product   using ( _,_ ; Î£-syntax ; _Ã—_ ) renaming ( projâ‚ to fst ; projâ‚‚ to snd )
open import Function.Base  using ( _âˆ˜_ )
open import Relation.Unary using ( Pred ; _âˆˆ_ ; _âŠ†_ ; â‹‚ )
open import Relation.Binary.PropositionalEquality using ( _â‰¡_ ; refl ; module â‰¡-Reasoning ; cong )

-- Imports from the Agda Universal Algebra Library ---------------------------------------------
open import Base.Overture.Preliminaries               using ( âˆ£_âˆ£ ; âˆ¥_âˆ¥ ; _â»Â¹ )
open import Base.Overture.Injective                   using ( IsInjective ; âˆ˜-injective )
open import Base.Equality.Welldefined                 using ( SwellDef )
open import Base.Equality.Extensionality              using ( DFunExt )
open import Base.Algebras.Basic                       using ( Algebra ; Lift-Alg )
open import Base.Algebras.Products           {ğ‘† = ğ‘†}  using ( ov ; â¨… )
open import Base.Homomorphisms.Basic         {ğ‘† = ğ‘†}  using ( hom )
open import Base.Homomorphisms.Properties    {ğ‘† = ğ‘†}  using ( âˆ˜-hom )
open import Base.Homomorphisms.Isomorphisms  {ğ‘† = ğ‘†}  using ( _â‰…_ ; mkiso ; Lift-â‰… ; â‰…-sym ; â‰…-trans )
open import Base.Terms.Basic                 {ğ‘† = ğ‘†}  using ( Term ; ğ‘» )
open import Base.Terms.Properties            {ğ‘† = ğ‘†}  using ( lift-hom )
open import Base.Terms.Operations            {ğ‘† = ğ‘†}  using ( _âŸ¦_âŸ§ ; comm-hom-term ; interp-prod )
                                                      using ( term-agreement )
open import Base.Subalgebras.Subalgebras     {ğ‘† = ğ‘†}  using ( _â‰¤_ ; SubalgebraOfClass )
open import Base.Subalgebras.Properties      {ğ‘† = ğ‘†}  using ( isoâ†’injective )
open import Base.Varieties.EquationalLogic   {ğ‘† = ğ‘†}  using ( _âŠ§_â‰ˆ_ ; _âŠ«_â‰ˆ_ )

\end{code}


#### <a id="algebraic-invariance-of-models">Algebraic invariance of âŠ§</a>

The binary relation âŠ§ would be practically useless if it were not an *algebraic invariant* (i.e., invariant under isomorphism).

\begin{code}

open Term
open â‰¡-Reasoning
open _â‰…_

module _ (wd : SwellDef){Î± Î² Ï‡ : Level}{X : Type Ï‡}{ğ‘¨ : Algebra Î± ğ‘†}
         (ğ‘© : Algebra Î² ğ‘†)(p q : Term X) where

 âŠ§-I-invar : ğ‘¨ âŠ§ p â‰ˆ q  â†’  ğ‘¨ â‰… ğ‘©  â†’  ğ‘© âŠ§ p â‰ˆ q

 âŠ§-I-invar Apq (mkiso f g fâˆ¼g gâˆ¼f) x =
  (ğ‘© âŸ¦ p âŸ§) x                      â‰¡âŸ¨ wd Ï‡ Î² (ğ‘© âŸ¦ p âŸ§) x (âˆ£ f âˆ£ âˆ˜ âˆ£ g âˆ£ âˆ˜ x) (Î» i â†’ ( fâˆ¼g (x i))â»Â¹) âŸ©
  (ğ‘© âŸ¦ p âŸ§) ((âˆ£ f âˆ£ âˆ˜ âˆ£ g âˆ£) âˆ˜ x)  â‰¡âŸ¨ (comm-hom-term (wd ğ“¥ Î²) ğ‘© f p (âˆ£ g âˆ£ âˆ˜ x))â»Â¹ âŸ©
  âˆ£ f âˆ£ ((ğ‘¨ âŸ¦ p âŸ§) (âˆ£ g âˆ£ âˆ˜ x))    â‰¡âŸ¨ cong âˆ£ f âˆ£ (Apq (âˆ£ g âˆ£ âˆ˜ x))  âŸ©
  âˆ£ f âˆ£ ((ğ‘¨ âŸ¦ q âŸ§) (âˆ£ g âˆ£ âˆ˜ x))    â‰¡âŸ¨ comm-hom-term (wd ğ“¥ Î²) ğ‘© f q (âˆ£ g âˆ£ âˆ˜ x) âŸ©
  (ğ‘© âŸ¦ q âŸ§) ((âˆ£ f âˆ£ âˆ˜ âˆ£ g âˆ£) âˆ˜  x) â‰¡âŸ¨ wd Ï‡ Î² (ğ‘© âŸ¦ q âŸ§) (âˆ£ f âˆ£ âˆ˜ âˆ£ g âˆ£ âˆ˜ x) x (Î» i â†’ ( fâˆ¼g (x i))) âŸ©
  (ğ‘© âŸ¦ q âŸ§) x                      âˆ

\end{code}


 As the proof makes clear, we show ğ‘© âŠ§ p â‰ˆ q by showing that `ğ‘© âŸ¦ p âŸ§ â‰¡ ğ‘© âŸ¦ q âŸ§ holds *extensionally*, that is, `âˆ€ x, ğ‘© âŸ¦ p âŸ§ x â‰¡ ğ‘© âŸ¦q âŸ§ x`.

#### <a id="lift-invariance">Lift-invariance of âŠ§</a>
The âŠ§ relation is also invariant under the algebraic lift and lower operations.

\begin{code}

module _ (wd : SwellDef){Î± Î² Ï‡ : Level}{X : Type Ï‡}{ğ‘¨ : Algebra Î± ğ‘†} where

 âŠ§-Lift-invar : (p q : Term X) â†’ ğ‘¨ âŠ§ p â‰ˆ q â†’ Lift-Alg ğ‘¨ Î² âŠ§ p â‰ˆ q
 âŠ§-Lift-invar p q Apq = âŠ§-I-invar wd (Lift-Alg ğ‘¨ _) p q Apq Lift-â‰…

 âŠ§-lower-invar : (p q : Term X) â†’ Lift-Alg ğ‘¨ Î² âŠ§ p â‰ˆ q  â†’  ğ‘¨ âŠ§ p â‰ˆ q
 âŠ§-lower-invar p q lApq = âŠ§-I-invar wd ğ‘¨ p q lApq (â‰…-sym Lift-â‰…)

\end{code}



#### <a id="subalgebraic-invariance">Subalgebraic invariance of âŠ§</a>

Identities modeled by an algebra `ğ‘¨` are also modeled by every subalgebra of `ğ‘¨`, which fact can be formalized as follows.

\begin{code}

module _ (wd : SwellDef){Ï‡ : Level}{ğ“¤ ğ“¦ : Level}{X : Type Ï‡} where

 âŠ§-S-invar : {ğ‘¨ : Algebra ğ“¤ ğ‘†}(ğ‘© : Algebra ğ“¦ ğ‘†){p q : Term X}
  â†’          ğ‘¨ âŠ§ p â‰ˆ q  â†’  ğ‘© â‰¤ ğ‘¨  â†’  ğ‘© âŠ§ p â‰ˆ q
 âŠ§-S-invar {ğ‘¨} ğ‘© {p}{q} Apq Bâ‰¤A b = (âˆ¥ Bâ‰¤A âˆ¥) (Î¾ b)
  where
  h : hom ğ‘© ğ‘¨
  h = âˆ£ Bâ‰¤A âˆ£

  Î¾ : âˆ€ b â†’ âˆ£ h âˆ£ ((ğ‘© âŸ¦ p âŸ§) b) â‰¡ âˆ£ h âˆ£ ((ğ‘© âŸ¦ q âŸ§) b)
  Î¾ b = âˆ£ h âˆ£((ğ‘© âŸ¦ p âŸ§) b)   â‰¡âŸ¨ comm-hom-term (wd ğ“¥ ğ“¤) ğ‘¨ h p b âŸ©
        (ğ‘¨ âŸ¦ p âŸ§)(âˆ£ h âˆ£ âˆ˜ b) â‰¡âŸ¨ Apq (âˆ£ h âˆ£ âˆ˜ b) âŸ©
        (ğ‘¨ âŸ¦ q âŸ§)(âˆ£ h âˆ£ âˆ˜ b) â‰¡âŸ¨ (comm-hom-term (wd ğ“¥ ğ“¤) ğ‘¨ h q b)â»Â¹ âŸ©
        âˆ£ h âˆ£((ğ‘© âŸ¦ q âŸ§) b)   âˆ

\end{code}

Next, identities modeled by a class of algebras is also modeled by all subalgebras of the class.  In other terms, every term equation `p â‰ˆ q` that is satisfied by all `ğ‘¨ âˆˆ ğ’¦` is also satisfied by every subalgebra of a member of ğ’¦.

 \begin{code}

 âŠ§-S-class-invar : {ğ’¦ : Pred (Algebra ğ“¤ ğ‘†)(ov ğ“¤)}(p q : Term X)
  â†’                ğ’¦ âŠ« p â‰ˆ q â†’ (ğ‘© : SubalgebraOfClass ğ’¦) â†’ âˆ£ ğ‘© âˆ£ âŠ§ p â‰ˆ q
 âŠ§-S-class-invar p q Kpq (ğ‘© , ğ‘¨ , SA , (ka , Bâ‰…SA)) = âŠ§-S-invar ğ‘© {p}{q}((Kpq ka)) (h , hinj)
  where
  h : hom ğ‘© ğ‘¨
  h = âˆ˜-hom ğ‘© ğ‘¨ (to Bâ‰…SA) âˆ£ snd SA âˆ£
  hinj : IsInjective âˆ£ h âˆ£
  hinj = âˆ˜-injective (isoâ†’injective Bâ‰…SA) âˆ¥ snd SA âˆ¥

\end{code}



#### <a id="product-invariance">Product invariance of âŠ§</a>

An identity satisfied by all algebras in an indexed collection is also satisfied by the product of algebras in that collection.

 \begin{code}

module _ (fe : DFunExt)(wd : SwellDef){Î± Î² Ï‡ : Level}{I : Type Î²}(ğ’œ : I â†’ Algebra Î± ğ‘†){X : Type Ï‡} where

 âŠ§-P-invar : (p q : Term X) â†’ (âˆ€ i â†’ ğ’œ i âŠ§ p â‰ˆ q) â†’ â¨… ğ’œ âŠ§ p â‰ˆ q
 âŠ§-P-invar p q ğ’œpq a = goal
  where
  -- This is where function extensionality is used.
  Î¾ : (Î» i â†’ (ğ’œ i âŸ¦ p âŸ§) (Î» x â†’ (a x) i)) â‰¡ (Î» i â†’ (ğ’œ i âŸ¦ q âŸ§)  (Î» x â†’ (a x) i))
  Î¾ = fe Î² Î± Î» i â†’ ğ’œpq i (Î» x â†’ (a x) i)

  goal : (â¨… ğ’œ âŸ¦ p âŸ§) a  â‰¡  (â¨… ğ’œ âŸ¦ q âŸ§) a
  goal = (â¨… ğ’œ âŸ¦ p âŸ§) a                      â‰¡âŸ¨ interp-prod (wd ğ“¥ (Î± âŠ” Î²)) p ğ’œ a âŸ©
      (Î» i â†’ (ğ’œ i âŸ¦ p âŸ§)(Î» x â†’ (a x)i))  â‰¡âŸ¨ Î¾ âŸ©
      (Î» i â†’ (ğ’œ i âŸ¦ q âŸ§)(Î» x â†’ (a x)i))  â‰¡âŸ¨ (interp-prod (wd ğ“¥ (Î± âŠ” Î²)) q ğ’œ a)â»Â¹ âŸ©
      (â¨… ğ’œ âŸ¦ q âŸ§) a                      âˆ

\end{code}

An identity satisfied by all algebras in a class is also satisfied by the product of algebras in the class.

\begin{code}

 âŠ§-P-class-invar : (ğ’¦ : Pred (Algebra Î± ğ‘†)(ov Î±)){p q : Term X}
  â†’                ğ’¦ âŠ« p â‰ˆ q â†’ (âˆ€ i â†’ ğ’œ i âˆˆ ğ’¦) â†’ â¨… ğ’œ âŠ§ p â‰ˆ q

 âŠ§-P-class-invar ğ’¦ {p}{q}Ïƒ Kğ’œ = âŠ§-P-invar p q Î» i â†’ Ïƒ (Kğ’œ i)

\end{code}

Another fact that will turn out to be useful is that a product of a collection of algebras models p â‰ˆ q if the lift of each algebra in the collection models p â‰ˆ q.

\begin{code}

 âŠ§-P-lift-invar : (p q : Term X) â†’ (âˆ€ i â†’ Lift-Alg (ğ’œ i) Î² âŠ§ p â‰ˆ q)  â†’  â¨… ğ’œ âŠ§ p â‰ˆ q
 âŠ§-P-lift-invar p q Î± = âŠ§-P-invar p q Aipq
  where
  Aipq : âˆ€ i â†’ (ğ’œ i) âŠ§ p â‰ˆ q
  Aipq i = âŠ§-lower-invar wd p q (Î± i) --  (â‰…-sym Lift-â‰…)

\end{code}



#### <a id="homomorphisc-invariance">Homomorphic invariance of âŠ§</a>

If an algebra ğ‘¨ models an identity p â‰ˆ q, then the pair (p , q) belongs to the kernel of every homomorphism Ï† : hom (ğ‘» X) ğ‘¨ from the term algebra to ğ‘¨; that is, every homomorphism from ğ‘» X to ğ‘¨ maps p and q to the same element of ğ‘¨.

 \begin{code}

module _ (wd : SwellDef){Î± Ï‡ : Level}{X : Type Ï‡}{ğ‘¨ : Algebra Î± ğ‘†} where

 âŠ§-H-invar : {p q : Term X}(Ï† : hom (ğ‘» X) ğ‘¨) â†’ ğ‘¨ âŠ§ p â‰ˆ q  â†’  âˆ£ Ï† âˆ£ p â‰¡ âˆ£ Ï† âˆ£ q

 âŠ§-H-invar {p}{q}Ï† Î² = âˆ£ Ï† âˆ£ p               â‰¡âŸ¨ cong âˆ£ Ï† âˆ£(term-agreement(wd ğ“¥ (ov Ï‡)) p)âŸ©
                       âˆ£ Ï† âˆ£((ğ‘» X âŸ¦ p âŸ§) â„Š)  â‰¡âŸ¨ comm-hom-term (wd ğ“¥ Î±) ğ‘¨ Ï† p â„Š âŸ©
                       (ğ‘¨ âŸ¦ p âŸ§) (âˆ£ Ï† âˆ£ âˆ˜ â„Š) â‰¡âŸ¨ Î² (âˆ£ Ï† âˆ£ âˆ˜ â„Š ) âŸ©
                       (ğ‘¨ âŸ¦ q âŸ§) (âˆ£ Ï† âˆ£ âˆ˜ â„Š) â‰¡âŸ¨ (comm-hom-term (wd ğ“¥ Î±)  ğ‘¨ Ï† q â„Š )â»Â¹ âŸ©
                       âˆ£ Ï† âˆ£ ((ğ‘» X âŸ¦ q âŸ§) â„Š) â‰¡âŸ¨(cong âˆ£ Ï† âˆ£ (term-agreement (wd ğ“¥ (ov Ï‡)) q))â»Â¹ âŸ©
                       âˆ£ Ï† âˆ£ q               âˆ

\end{code}

More generally, an identity is satisfied by all algebras in a class if and only if that identity is invariant under all homomorphisms from the term algebra `ğ‘» X` into algebras of the class. More precisely, if `ğ’¦` is a class of `ğ‘†`-algebras and `ğ‘`, `ğ‘` terms in the language of `ğ‘†`, then,
```
  ğ’¦ âŠ§ p â‰ˆ q  â‡”  âˆ€ ğ‘¨ âˆˆ ğ’¦,  âˆ€ Ï† : hom (ğ‘» X) ğ‘¨,  Ï† âˆ˜ (ğ‘» X)âŸ¦ p âŸ§ = Ï† âˆ˜ (ğ‘» X)âŸ¦ q âŸ§.
```
\begin{code}

module _ (wd : SwellDef){Î± Ï‡ : Level}{X : Type Ï‡}{ğ’¦ : Pred (Algebra Î± ğ‘†)(ov Î±)}  where

 -- â‡’ (the "only if" direction)
 âŠ§-H-class-invar : {p q : Term X}
  â†’                ğ’¦ âŠ« p â‰ˆ q â†’ âˆ€ ğ‘¨ Ï† â†’ ğ‘¨ âˆˆ ğ’¦ â†’ âˆ€ a â†’ âˆ£ Ï† âˆ£ ((ğ‘» X âŸ¦ p âŸ§) a) â‰¡ âˆ£ Ï† âˆ£ ((ğ‘» X âŸ¦ q âŸ§) a)
 âŠ§-H-class-invar {p = p}{q} Ïƒ ğ‘¨ Ï† ka a = Î¾
  where
   Î¾ : âˆ£ Ï† âˆ£ ((ğ‘» X âŸ¦ p âŸ§) a) â‰¡ âˆ£ Ï† âˆ£ ((ğ‘» X âŸ¦ q âŸ§) a)
   Î¾ = âˆ£ Ï† âˆ£ ((ğ‘» X âŸ¦ p âŸ§) a)  â‰¡âŸ¨ comm-hom-term (wd ğ“¥ Î±) ğ‘¨ Ï† p a âŸ©
         (ğ‘¨ âŸ¦ p âŸ§)(âˆ£ Ï† âˆ£ âˆ˜ a)   â‰¡âŸ¨ (Ïƒ ka) (âˆ£ Ï† âˆ£ âˆ˜ a) âŸ©
         (ğ‘¨ âŸ¦ q âŸ§)(âˆ£ Ï† âˆ£ âˆ˜ a)   â‰¡âŸ¨ (comm-hom-term (wd ğ“¥ Î±) ğ‘¨ Ï† q a)â»Â¹ âŸ©
         âˆ£ Ï† âˆ£ ((ğ‘» X âŸ¦ q âŸ§) a)  âˆ

 -- â‡ (the "if" direction)
 âŠ§-H-class-coinvar : {p q : Term X}
  â†’  (âˆ€ ğ‘¨ Ï† â†’ ğ‘¨ âˆˆ ğ’¦ â†’ âˆ€ a â†’ âˆ£ Ï† âˆ£ ((ğ‘» X âŸ¦ p âŸ§) a) â‰¡ âˆ£ Ï† âˆ£ ((ğ‘» X âŸ¦ q âŸ§) a)) â†’ ğ’¦ âŠ« p â‰ˆ q

 âŠ§-H-class-coinvar {p}{q} Î² {ğ‘¨} ka = goal
  where
  Ï† : (a : X â†’ âˆ£ ğ‘¨ âˆ£) â†’ hom (ğ‘» X) ğ‘¨
  Ï† a = lift-hom ğ‘¨ a

  goal : ğ‘¨ âŠ§ p â‰ˆ q
  goal a = (ğ‘¨ âŸ¦ p âŸ§)(âˆ£ Ï† a âˆ£ âˆ˜ â„Š)     â‰¡âŸ¨(comm-hom-term (wd ğ“¥ Î±) ğ‘¨ (Ï† a) p â„Š)â»Â¹ âŸ©
           (âˆ£ Ï† a âˆ£ âˆ˜ (ğ‘» X âŸ¦ p âŸ§)) â„Š  â‰¡âŸ¨ Î² ğ‘¨ (Ï† a) ka â„Š âŸ©
           (âˆ£ Ï† a âˆ£ âˆ˜ (ğ‘» X âŸ¦ q âŸ§)) â„Š  â‰¡âŸ¨ (comm-hom-term (wd ğ“¥ Î±) ğ‘¨ (Ï† a) q â„Š) âŸ©
           (ğ‘¨ âŸ¦ q âŸ§)(âˆ£ Ï† a âˆ£ âˆ˜ â„Š)     âˆ

\end{code}

---------------------------------

<span style="float:left;">[â† Base.Varieties.Closure](Base.Varieties.Closure.html)</span>
<span style="float:right;">[Base.Varieties.Preservation â†’](Base.Varieties.Preservation.html)</span>

{% include UALib.Links.md %}
