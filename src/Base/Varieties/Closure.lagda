---
layout: default
title : "Base.Varieties.Closure  module (The Agda Universal Algebra Library)"
date : "2021-01-14"
author: "agda-algebras development team"
---

### <a id="closure-operators">Closure Operators</a>

Fix a signature `ğ‘†`, let `ğ’¦` be a class of `ğ‘†`-algebras, and define

* `H ğ’¦` = algebras isomorphic to a homomorphic image of a member of `ğ’¦`;
* `S ğ’¦` = algebras isomorphic to a subalgebra of a member of `ğ’¦`;
* `P ğ’¦` = algebras isomorphic to a product of members of `ğ’¦`.

A straight-forward verification confirms that H, S, and P are *closure operators* (expansive, monotone, and idempotent).  A class ğ’¦ of ğ‘†-algebras is said to be *closed under the taking of homomorphic images* provided `H ğ’¦ âŠ† ğ’¦`. Similarly, ğ’¦ is *closed under the taking of subalgebras* (resp., *arbitrary products*) provided `S ğ’¦ âŠ† ğ’¦` (resp., `P ğ’¦ âŠ† ğ’¦`). The operators H, S, and P can be composed with one another repeatedly, forming yet more closure operators.

An algebra is a homomorphic image (resp., subalgebra; resp., product) of every algebra to which it is isomorphic. Thus, the class `H ğ’¦` (resp., `S ğ’¦`; resp., `P ğ’¦`) is closed under isomorphism.

A *variety* is a class of algebras, in the same signature, that is closed under the taking of homomorphic images, subalgebras, and arbitrary products.  To represent varieties we define inductive types for the closure operators `H`, `S`, and `P` that are composable.  Separately, we define an inductive type `V` which simultaneously represents closure under all three operators, `H`, `S`, and `P`.


\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Base.Algebras.Basic using ( ğ“ ; ğ“¥ ; Signature )

module Base.Varieties.Closure {ğ‘† : Signature ğ“ ğ“¥} where

-- Imports from Agda and the Agda Standard Library ---------------------------------------
open import Axiom.Extensionality.Propositional
                            using () renaming ( Extensionality to funext )
open import Agda.Primitive  using ( _âŠ”_ ; lsuc ) renaming ( Set to Type )
open import Data.Product    using ( _,_ ; Î£-syntax ) renaming ( projâ‚ to fst ; projâ‚‚ to snd )
open import Level           using ( Level ;  Lift )
open import Relation.Unary  using ( Pred  ; _âˆˆ_ ; _âŠ†_ )


-- Imports from the Agda Universal Algebra Library ---------------------------------------
open import Base.Overture.Preliminaries                    using ( âˆ£_âˆ£ ; âˆ¥_âˆ¥ )
open import Base.Algebras.Basic                            using ( Algebra ; Lift-Alg )
open import Base.Algebras.Products                {ğ‘† = ğ‘†}  using ( ov ; â¨… )
open import Base.Homomorphisms.Isomorphisms       {ğ‘† = ğ‘†}  using ( _â‰…_ ; â‰…-sym ; Lift-â‰… ; â‰…-trans ; â‰…-refl )
                                                           using ( Lift-Alg-iso ; Lift-Alg-â¨…â‰… )
                                                           using (  Lift-Alg-assoc )
open import Base.Homomorphisms.HomomorphicImages  {ğ‘† = ğ‘†}  using ( HomImages ; _IsHomImageOf_ )
                                                           using ( Lift-Alg-hom-image )
open import Base.Subalgebras.Subalgebras          {ğ‘† = ğ‘†}  using (_â‰¤_ ; _IsSubalgebraOfClass_ ; Subalgebra )
open import Base.Subalgebras.Properties           {ğ‘† = ğ‘†}  using ( â‰¤-refl ; â‰…-RESP-â‰¤ ; â‰¤-RESP-â‰… ; â‰¤-trans )
                                                           using ( Lift-â‰¤-Lift )

\end{code}

#### <a id="the-inductive-type-h">The Inductive Type H</a>

We define the inductive type `H` to represent classes of algebras that include all homomorphic images of algebras in the class; i.e., classes that are closed under the taking of homomorphic images.

\begin{code}

data H {Î± Î² : Level} (ğ’¦ : Pred (Algebra Î± ğ‘†) (ov Î±)) : Pred (Algebra (Î± âŠ” Î²) ğ‘†) (ov(Î± âŠ” Î²))
 where
 hbase : {ğ‘¨ : Algebra Î± ğ‘†} â†’ ğ‘¨ âˆˆ ğ’¦ â†’ Lift-Alg ğ‘¨ Î² âˆˆ H ğ’¦
 hhimg : {ğ‘¨ ğ‘© : Algebra (Î± âŠ” Î²) ğ‘†} â†’ ğ‘¨ âˆˆ H {Î±} {Î²} ğ’¦ â†’ ((ğ‘© , _) : HomImages ğ‘¨) â†’ ğ‘© âˆˆ H ğ’¦

\end{code}

#### <a id="the-inductive-type-s">The Inductive Type S</a>

Here we define the inductive type `S` to represent classes of algebras closed under the taking of subalgebras.

\begin{code}

data S {Î± Î² : Level}(ğ’¦ : Pred(Algebra Î± ğ‘†)(ov Î±)) : Pred(Algebra(Î± âŠ” Î²)ğ‘†)(ov(Î± âŠ” Î²))
 where
 sbase : {ğ‘¨ : Algebra Î± ğ‘†} â†’ ğ‘¨ âˆˆ ğ’¦ â†’ Lift-Alg ğ‘¨ Î² âˆˆ S ğ’¦
 slift : {ğ‘¨ : Algebra Î± ğ‘†} â†’ ğ‘¨ âˆˆ S{Î±}{Î±} ğ’¦ â†’ Lift-Alg ğ‘¨ Î² âˆˆ S ğ’¦
 ssub  : {ğ‘¨ : Algebra Î± ğ‘†}{ğ‘© : Algebra _ ğ‘†} â†’ ğ‘¨ âˆˆ S{Î±}{Î±} ğ’¦ â†’ ğ‘© â‰¤ ğ‘¨ â†’ ğ‘© âˆˆ S ğ’¦
 siso  : {ğ‘¨ : Algebra Î± ğ‘†}{ğ‘© : Algebra _ ğ‘†} â†’ ğ‘¨ âˆˆ S{Î±}{Î±} ğ’¦ â†’ ğ‘¨ â‰… ğ‘© â†’ ğ‘© âˆˆ S ğ’¦

\end{code}

#### <a id="the-inductive-types-p">The Inductive Type P </a>

Here we define the inductive type `P` to represent classes of algebras closed under the taking of arbitrary products.

\begin{code}

data P {Î± Î² : Level}(ğ’¦ : Pred(Algebra Î± ğ‘†)(ov Î±)) : Pred(Algebra(Î± âŠ” Î²)ğ‘†)(ov(Î± âŠ” Î²))
 where
 pbase  : {ğ‘¨ : Algebra Î± ğ‘†} â†’ ğ‘¨ âˆˆ ğ’¦ â†’ Lift-Alg ğ‘¨ Î² âˆˆ P ğ’¦
 pliftu : {ğ‘¨ : Algebra Î± ğ‘†} â†’ ğ‘¨ âˆˆ P{Î±}{Î±} ğ’¦ â†’ Lift-Alg ğ‘¨ Î² âˆˆ P ğ’¦
 pliftw : {ğ‘¨ : Algebra (Î± âŠ” Î²) ğ‘†} â†’ ğ‘¨ âˆˆ P{Î±}{Î²} ğ’¦ â†’ Lift-Alg ğ‘¨ (Î± âŠ” Î²) âˆˆ P ğ’¦
 produ  : {I : Type Î² }{ğ’œ : I â†’ Algebra Î± ğ‘†} â†’ (âˆ€ i â†’ (ğ’œ i) âˆˆ P{Î±}{Î±} ğ’¦) â†’ â¨… ğ’œ âˆˆ P ğ’¦
 prodw  : {I : Type Î² }{ğ’œ : I â†’ Algebra _ ğ‘†} â†’ (âˆ€ i â†’ (ğ’œ i) âˆˆ P{Î±}{Î²} ğ’¦) â†’ â¨… ğ’œ âˆˆ P ğ’¦
 pisow  : {ğ‘¨ ğ‘© : Algebra _ ğ‘†} â†’ ğ‘¨ âˆˆ P{Î±}{Î²} ğ’¦ â†’ ğ‘¨ â‰… ğ‘© â†’ ğ‘© âˆˆ P ğ’¦

\end{code}


#### <a id="the-inductive-types-v">The Inductive Types V</a>

A class ğ’¦ of ğ‘†-algebras is called a *variety* if it is closed under each of the closure operators H, S, and P introduced elsewhere; the corresponding closure operator is often denoted ğ•, but we will denote it by `V`.

We now define `V` as an inductive type which is crafted to contain all the parts of H, S and P, under different names.

\begin{code}

data V {Î± Î² : Level}(ğ’¦ : Pred(Algebra Î± ğ‘†)(ov Î±)) : Pred(Algebra(Î± âŠ” Î²)ğ‘†)(ov(Î± âŠ” Î²))
 where
 vbase  : {ğ‘¨ : Algebra Î± ğ‘†} â†’ ğ‘¨ âˆˆ ğ’¦ â†’ Lift-Alg ğ‘¨ Î² âˆˆ V ğ’¦
 vlift  : {ğ‘¨ : Algebra Î± ğ‘†} â†’ ğ‘¨ âˆˆ V{Î±}{Î±} ğ’¦ â†’ Lift-Alg ğ‘¨ Î² âˆˆ V ğ’¦
 vliftw : {ğ‘¨ : Algebra _ ğ‘†} â†’ ğ‘¨ âˆˆ V{Î±}{Î²} ğ’¦ â†’ Lift-Alg ğ‘¨ (Î± âŠ” Î²) âˆˆ V ğ’¦
 vhimg  : {ğ‘¨ ğ‘© : Algebra _ ğ‘†} â†’ ğ‘¨ âˆˆ V{Î±}{Î²} ğ’¦ â†’ ((ğ‘© , _) : HomImages ğ‘¨) â†’ ğ‘© âˆˆ V ğ’¦
 vssubw : {ğ‘¨ ğ‘© : Algebra _ ğ‘†} â†’ ğ‘¨ âˆˆ V{Î±}{Î²} ğ’¦ â†’ ğ‘© â‰¤ ğ‘¨ â†’ ğ‘© âˆˆ V ğ’¦
 vprodu : {I : Type Î²}{ğ’œ : I â†’ Algebra Î± ğ‘†} â†’ (âˆ€ i â†’ (ğ’œ i) âˆˆ V{Î±}{Î±} ğ’¦) â†’ â¨… ğ’œ âˆˆ V ğ’¦
 vprodw : {I : Type Î²}{ğ’œ : I â†’ Algebra _ ğ‘†} â†’ (âˆ€ i â†’ (ğ’œ i) âˆˆ V{Î±}{Î²} ğ’¦) â†’ â¨… ğ’œ âˆˆ V ğ’¦
 visou  : {ğ‘¨ : Algebra Î± ğ‘†}{ğ‘© : Algebra _ ğ‘†} â†’ ğ‘¨ âˆˆ V{Î±}{Î±} ğ’¦ â†’ ğ‘¨ â‰… ğ‘© â†’ ğ‘© âˆˆ V ğ’¦
 visow  : {ğ‘¨ ğ‘© : Algebra _ ğ‘†} â†’ ğ‘¨ âˆˆ V{Î±}{Î²} ğ’¦ â†’ ğ‘¨ â‰… ğ‘© â†’ ğ‘© âˆˆ V ğ’¦

\end{code}

Thus, if ğ’¦ is a class of ğ‘†-algebras, then the *variety generated by* ğ’¦ is denoted by `V ğ’¦` and defined to be the smallest class that contains ğ’¦ and is closed under `H`, `S`, and `P`.

With the closure operator V representing closure under HSP, we represent formally what it means to be a variety of algebras as follows.

\begin{code}

is-variety : {Î± : Level}(ğ’± : Pred (Algebra Î± ğ‘†)(ov Î±)) â†’ Type(ov Î±)
is-variety{Î±} ğ’± = V{Î±}{Î±} ğ’± âŠ† ğ’±

variety : (Î± : Level) â†’ Type(lsuc (ğ“ âŠ” ğ“¥ âŠ” (lsuc Î±)))
variety Î± = Î£[ ğ’± âˆˆ (Pred (Algebra Î± ğ‘†)(ov Î±)) ] is-variety ğ’±

\end{code}


#### <a id="closure-properties-of-S">Closure properties of S</a>

`S` is a closure operator.  The facts that S is idempotent and expansive won't be needed, so we omit these, but we will make use of monotonicity of S.  Here is the proof of the latter.

\begin{code}

S-mono : {Î± Î² : Level}{ğ’¦ ğ’¦' : Pred (Algebra Î± ğ‘†)(ov Î±)}
 â†’       ğ’¦ âŠ† ğ’¦' â†’ S{Î±}{Î²} ğ’¦ âŠ† S{Î±}{Î²} ğ’¦'

S-mono kk (sbase x)            = sbase (kk x)
S-mono kk (slift{ğ‘¨} x)         = slift (S-mono kk x)
S-mono kk (ssub{ğ‘¨}{ğ‘©} sA Bâ‰¤A)  = ssub (S-mono kk sA) Bâ‰¤A
S-mono kk (siso x xâ‚)          = siso (S-mono kk x) xâ‚

\end{code}

We sometimes want to go back and forth between our two representations of subalgebras of algebras in a class. The tools `subalgebraâ†’S` and `Sâ†’subalgebra` are made for that purpose.

\begin{code}

module _ {Î± Î² : Level}{ğ’¦ : Pred (Algebra Î± ğ‘†)(ov Î±)} where

 subalgebraâ†’S : {ğ‘© : Algebra (Î± âŠ” Î²) ğ‘†} â†’ ğ‘© IsSubalgebraOfClass ğ’¦ â†’ ğ‘© âˆˆ S{Î±}{Î²} ğ’¦

 subalgebraâ†’S {ğ‘©} (ğ‘¨ , ((ğ‘ª , Câ‰¤A) , KA , Bâ‰…C)) = ssub sA Bâ‰¤A
  where
   Bâ‰¤A : ğ‘© â‰¤ ğ‘¨
   Bâ‰¤A = â‰…-RESP-â‰¤ {ğ‘ª = ğ‘¨} Bâ‰…C Câ‰¤A

   slAu : Lift-Alg ğ‘¨ Î± âˆˆ S{Î±}{Î±} ğ’¦
   slAu = sbase KA

   sA : ğ‘¨ âˆˆ S{Î±}{Î±} ğ’¦
   sA = siso slAu (â‰…-sym Lift-â‰…)


module _ {Î± : Level}{ğ’¦ : Pred (Algebra Î± ğ‘†)(ov Î±)} where

 Sâ†’subalgebra : {ğ‘© : Algebra Î± ğ‘†} â†’ ğ‘© âˆˆ S{Î±}{Î±} ğ’¦  â†’  ğ‘© IsSubalgebraOfClass ğ’¦

 Sâ†’subalgebra (sbase{ğ‘©} x) =  ğ‘© , ((ğ‘© , (â‰¤-refl â‰…-refl)) , x , â‰…-sym Lift-â‰…)
 Sâ†’subalgebra (slift{ğ‘©} x) = âˆ£ BS âˆ£ , SA , âˆ£ snd âˆ¥ BS âˆ¥ âˆ£ , â‰…-trans (â‰…-sym Lift-â‰…) Bâ‰…SA
  where
   BS : ğ‘© IsSubalgebraOfClass ğ’¦
   BS = Sâ†’subalgebra x
   SA : Subalgebra âˆ£ BS âˆ£
   SA = fst âˆ¥ BS âˆ¥
   Bâ‰…SA : ğ‘© â‰… âˆ£ SA âˆ£
   Bâ‰…SA = âˆ¥ snd âˆ¥ BS âˆ¥ âˆ¥

 Sâ†’subalgebra {ğ‘©} (ssub{ğ‘¨} sA Bâ‰¤A) = âˆ£ AS âˆ£ , (ğ‘© , Bâ‰¤AS) , âˆ£ snd âˆ¥ AS âˆ¥ âˆ£ , â‰…-refl
  where
   AS : ğ‘¨ IsSubalgebraOfClass ğ’¦
   AS = Sâ†’subalgebra sA
   SA : Subalgebra âˆ£ AS âˆ£
   SA = fst âˆ¥ AS âˆ¥
   Bâ‰¤SA : ğ‘© â‰¤ âˆ£ SA âˆ£
   Bâ‰¤SA = â‰¤-RESP-â‰… Bâ‰¤A (âˆ¥ snd âˆ¥ AS âˆ¥ âˆ¥)
   Bâ‰¤AS : ğ‘© â‰¤ âˆ£ AS âˆ£
   Bâ‰¤AS = â‰¤-trans ğ‘© âˆ£ AS âˆ£ Bâ‰¤SA âˆ¥ SA âˆ¥

 Sâ†’subalgebra {ğ‘©} (siso{ğ‘¨} sA Aâ‰…B) = âˆ£ AS âˆ£ , SA ,  âˆ£ snd âˆ¥ AS âˆ¥ âˆ£ , (â‰…-trans (â‰…-sym Aâ‰…B) Aâ‰…SA)
  where
   AS : ğ‘¨ IsSubalgebraOfClass ğ’¦
   AS = Sâ†’subalgebra sA
   SA : Subalgebra âˆ£ AS âˆ£
   SA = fst âˆ¥ AS âˆ¥
   Aâ‰…SA : ğ‘¨ â‰… âˆ£ SA âˆ£
   Aâ‰…SA = snd âˆ¥ snd AS âˆ¥

\end{code}


#### <a id="closure-properties-of-P">Closure properties of P</a>

`P` is a closure operator.  This is proved by checking that `P` is *monotone*, *expansive*, and *idempotent*. The meaning of these terms will be clear from the definitions of the types that follow.

\begin{code}

P-mono : {Î± Î² : Level}{ğ’¦ ğ’¦' : Pred(Algebra Î± ğ‘†)(ov Î±)}
 â†’       ğ’¦ âŠ† ğ’¦' â†’ P{Î±}{Î²} ğ’¦ âŠ† P{Î±}{Î²} ğ’¦'

P-mono kk' (pbase x)    = pbase (kk' x)
P-mono kk' (pliftu x)   = pliftu (P-mono kk' x)
P-mono kk' (pliftw x)   = pliftw (P-mono kk' x)
P-mono kk' (produ x)    = produ (Î» i â†’ P-mono kk' (x i))
P-mono kk' (prodw x)    = prodw (Î» i â†’ P-mono kk' (x i))
P-mono kk' (pisow x xâ‚) = pisow (P-mono kk' x) xâ‚


P-expa : {Î± : Level}{ğ’¦ : Pred (Algebra Î± ğ‘†)(ov Î±)} â†’ ğ’¦ âŠ† P{Î±}{Î±} ğ’¦
P-expa{Î±}{ğ’¦} {ğ‘¨} KA =  pisow {ğ‘© = ğ‘¨} (pbase KA) (â‰…-sym Lift-â‰…)


module _ {Î± Î² : Level} where

 P-idemp : {ğ’¦ : Pred (Algebra Î± ğ‘†)(ov Î±)}
  â†’        P{Î± âŠ” Î²}{Î± âŠ” Î²} (P{Î±}{Î± âŠ” Î²} ğ’¦) âŠ† P{Î±}{Î± âŠ” Î²} ğ’¦

 P-idemp (pbase x)    = pliftw x
 P-idemp (pliftu x)   = pliftw (P-idemp x)
 P-idemp (pliftw x)   = pliftw (P-idemp x)
 P-idemp (produ x)    = prodw (Î» i â†’ P-idemp (x i))
 P-idemp (prodw x)    = prodw (Î» i â†’ P-idemp (x i))
 P-idemp (pisow x xâ‚) = pisow (P-idemp x) xâ‚

\end{code}

Next we observe that lifting to a higher universe does not break the property of being a subalgebra of an algebra of a class.  In other words, if we lift a subalgebra of an algebra in a class, the result is still a subalgebra of an algebra in the class.

\begin{code}
module _ {Î± Î² : Level}{ğ’¦ : Pred (Algebra Î± ğ‘†)(ov Î±)} where

 Lift-Alg-subP : {ğ‘© : Algebra Î² ğ‘†}
  â†’              ğ‘© IsSubalgebraOfClass (P{Î±}{Î²} ğ’¦)
  â†’              (Lift-Alg ğ‘© Î±) IsSubalgebraOfClass (P{Î±}{Î²} ğ’¦)

 Lift-Alg-subP (ğ‘¨ , (ğ‘ª , Câ‰¤A) , pA , Bâ‰…C ) = lA , (lC , lCâ‰¤lA) , plA , (Lift-Alg-iso Bâ‰…C)
   where
   lA lC : Algebra (Î± âŠ” Î²)  ğ‘†
   lA = Lift-Alg ğ‘¨ (Î± âŠ” Î²)
   lC = Lift-Alg ğ‘ª Î±

   lCâ‰¤lA : lC â‰¤ lA
   lCâ‰¤lA = Lift-â‰¤-Lift Î± {ğ‘¨} (Î± âŠ” Î²) Câ‰¤A
   plA : lA âˆˆ P{Î±}{Î²} ğ’¦
   plA = pliftw pA

 Lift-Alg-subP' : {ğ‘¨ : Algebra Î± ğ‘†}
  â†’                ğ‘¨ IsSubalgebraOfClass (P{Î±}{Î±} ğ’¦)
  â†’               (Lift-Alg ğ‘¨ Î²) IsSubalgebraOfClass (P{Î±}{Î²} ğ’¦)

 Lift-Alg-subP' (ğ‘© , (ğ‘ª , Câ‰¤B) , pB , Aâ‰…C ) = lB , (lC , lCâ‰¤lB) , plB , (Lift-Alg-iso Aâ‰…C)
   where
   lB lC : Algebra (Î± âŠ” Î²)  ğ‘†
   lB = Lift-Alg ğ‘© Î²
   lC = Lift-Alg ğ‘ª Î²

   lCâ‰¤lB : lC â‰¤ lB
   lCâ‰¤lB = Lift-â‰¤-Lift Î² {ğ‘©} Î² Câ‰¤B
   plB : lB âˆˆ P{Î±}{Î²} ğ’¦
   plB = pliftu pB

\end{code}


#### <a id="V-is-closed-under-lift">V is closed under lift</a>

As mentioned earlier, a technical hurdle that must be overcome when formalizing proofs in Agda is the proper handling of universe levels. In particular, in the proof of the Birkhoff's theorem, for example, we will need to know that if an algebra ğ‘¨ belongs to the variety V ğ’¦, then so does the lift of ğ‘¨.  Let us get the tedious proof of this technical lemma out of the way.

\begin{code}

open Level

module Vlift {Î± : Level} {feâ‚€ : funext (ov Î±) Î±}
             {feâ‚ : funext ((ov Î±) âŠ” (lsuc (ov Î±))) (lsuc (ov Î±))}
             {feâ‚‚ : funext (ov Î±) (ov Î±)}
             {ğ’¦ : Pred (Algebra Î± ğ‘†)(ov Î±)} where

 VlA : {ğ‘¨ : Algebra (ov Î±) ğ‘†} â†’ ğ‘¨ âˆˆ V{Î±}{ov Î±} ğ’¦
  â†’    Lift-Alg ğ‘¨ (lsuc (ov Î±)) âˆˆ V{Î±}{lsuc (ov Î±)} ğ’¦
 VlA (vbase{ğ‘¨} x) = visow (vbase x) (Lift-Alg-assoc _ _ {ğ‘¨})
 VlA (vlift{ğ‘¨} x) = visow (vlift x) (Lift-Alg-assoc _ _ {ğ‘¨})
 VlA (vliftw{ğ‘¨} x) = visow (VlA x) (Lift-Alg-assoc _ _ {ğ‘¨})

 VlA (vhimg{ğ‘¨}{ğ‘©} x hB) = vhimg {ğ‘© = Lift-Alg ğ‘© (lsuc (ov Î±))} (VlA x) (lC , lChi)
  where
  lC : Algebra (lsuc (ov(Î±))) ğ‘†
  lC = Lift-Alg âˆ£ hB âˆ£ (lsuc (ov(Î±)))
  lChi : lC IsHomImageOf _
  lChi = (Lift-Alg-hom-image (lsuc (ov(Î±))) {âˆ£ hB âˆ£} (lsuc (ov(Î±))) âˆ¥ hB âˆ¥)

 VlA (vssubw{ğ‘¨}{ğ‘©} x Bâ‰¤A) = vssubw (VlA x) (Lift-â‰¤-Lift  (lsuc (ov(Î±))) {ğ‘¨}  (lsuc (ov(Î±))) Bâ‰¤A)
 VlA (vprodu{I}{ğ’œ} x) = visow (vprodw vlA) (â‰…-sym Bâ‰…A)
  where
  ğ‘° : Type (lsuc (ov Î±))
  ğ‘° = Lift (lsuc (ov Î±)) I

  lA : ğ‘° â†’ Algebra (lsuc (ov Î±)) ğ‘†
  lA i = Lift-Alg (ğ’œ (lower i)) (lsuc (ov Î±))

  vlA : âˆ€ i â†’ (lA i) âˆˆ V{Î±}{lsuc (ov Î±)} ğ’¦
  vlA i = vlift (x (lower i))

  iso-components : âˆ€ i â†’ ğ’œ i â‰… lA (lift i)
  iso-components i = Lift-â‰…

  Bâ‰…A : Lift-Alg (â¨… ğ’œ) (lsuc (ov Î±)) â‰… â¨… lA
  Bâ‰…A = Lift-Alg-â¨…â‰…  {fizw = feâ‚}{fiu = feâ‚€} iso-components


 VlA (vprodw{I}{ğ’œ} x) = visow (vprodw vlA) (â‰…-sym Bâ‰…A)
  where
  ğ‘° : Type (lsuc (ov Î±))
  ğ‘° = Lift (lsuc (ov Î±)) I

  lA : ğ‘° â†’ Algebra (lsuc (ov Î±)) ğ‘†
  lA i = Lift-Alg (ğ’œ (lower i)) (lsuc (ov Î±))

  vlA : âˆ€ i â†’ (lA i) âˆˆ V{Î±}{lsuc (ov Î±)} ğ’¦
  vlA i = VlA (x (lower i))

  iso-components : âˆ€ i â†’ ğ’œ i â‰… lA (lift i)
  iso-components i = Lift-â‰…

  Bâ‰…A : Lift-Alg (â¨… ğ’œ) (lsuc (ov Î±)) â‰… â¨… lA
  Bâ‰…A = Lift-Alg-â¨…â‰… {fizw = feâ‚}{fiu = feâ‚‚} iso-components

 VlA (visou{ğ‘¨}{ğ‘©} x Aâ‰…B) = visow (vlift x) (Lift-Alg-iso Aâ‰…B)
 VlA (visow{ğ‘¨}{ğ‘©} x Aâ‰…B) = visow (VlA x) (Lift-Alg-iso Aâ‰…B)

\end{code}

---------------------------------

<span style="float:left;">[â† Base.Varieties.EquationalLogic](Base.Varieties.EquationalLogic.html)</span>
<span style="float:right;">[Base.Varieties.Properties â†’](Base.Varieties.Properties.html)</span>

{% include UALib.Links.md %}
