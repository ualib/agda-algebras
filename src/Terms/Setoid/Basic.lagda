---
layout: default
title : Terms.Setoid.Basic module (The Agda Universal Algebra Library)
date : 2021-07-17
author: [agda-algebras development team][]
---

### <a id="basic-definitions">Basic Definitions</a>

This is the [Terms.Setoid.Basic][] module of the [Agda Universal Algebra Library][].

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Algebras.Basic using ( ğ“ ; ğ“¥ ; Signature )

module Terms.Setoid.Basic {ğ‘† : Signature ğ“ ğ“¥} where

-- imports from Agda and the Agda Standard Library -------------------------------------
open import Agda.Primitive         using ( Level ; _âŠ”_ ; lsuc ) renaming ( Set to Type )
open import Data.Empty.Polymorphic using ( âŠ¥ )
open import Data.Product           using ( _,_ )
open import Data.Sum.Base          using ( _âŠ_ ) renaming ( injâ‚ to inl ; injâ‚‚ to inr )
open import Function.Bundles       using ( Func )
open import Level                  using ( Level ; Lift )
open import Relation.Binary        using ( Setoid ; IsEquivalence )
open import Relation.Binary.Definitions
                                   using ( Reflexive ; Symmetric ; Transitive )
open import Relation.Binary.PropositionalEquality
                                   using ( _â‰¡_ ; sym ; trans ; refl )

-- Imports from the Agda Universal Algebra Library --------------------
open import Overture.Preliminaries        using ( âˆ£_âˆ£ ; âˆ¥_âˆ¥ )
open import Algebras.Setoid.Basic {ğ‘† = ğ‘†} using ( SetoidAlgebra ; ov )
open import Terms.Basic           {ğ‘† = ğ‘†} using ( Term ) public
open Term


private variable
 Ï‡ Î± â„“ : Level
 Î“ Î” : Type Ï‡

\end{code}


#### <a id="equality-of-terms">Equality of Terms</a>

We take a different approach here, using Setoids instead of quotient types.
That is, we will define the collection of terms in a signature as a setoid
with a particular equality-of-terms relation, which we must define.
Ultimately we will use this to define the (absolutely free) term algebra
as a SetoidAlgebra whose carrier is the setoid of terms.

\begin{code}

module _ {X : Type Ï‡ } where

 -- Equality of terms as an inductive datatype
 data _â‰_ : Term X â†’ Term X â†’ Type (ov Ï‡) where
  refl : {x y : X} â†’ x â‰¡ y â†’ (â„Š x) â‰ (â„Š y)
  genl : âˆ€ {f : âˆ£ ğ‘† âˆ£}{s t : âˆ¥ ğ‘† âˆ¥ f â†’ Term X} â†’ (âˆ€ i â†’ (s i) â‰ (t i)) â†’ (node f s) â‰ (node f t)

 -- Equality of terms is an equivalence relation
 open Level
 â‰-isRefl : Reflexive _â‰_
 â‰-isRefl {â„Š x} = refl refl
 â‰-isRefl {node f t} = genl (Î» i â†’ â‰-isRefl)

 â‰-isSym : Symmetric _â‰_
 â‰-isSym {.(â„Š _)} {.(â„Š _)} (refl x) = refl (sym x)
 â‰-isSym {.(node _ _)} {.(node _ _)} (genl x) = genl (Î» i â†’ â‰-isSym (x i))

 â‰-isTrans : Transitive _â‰_
 â‰-isTrans {.(â„Š _)} {.(â„Š _)} {.(â„Š _)} (refl x) (refl y) = refl (trans x y)
 â‰-isTrans {.(node _ _)} {.(node _ _)} {.(node _ _)} (genl x) (genl y) = genl (Î» i â†’ â‰-isTrans (x i) (y i))

 â‰-isEquiv : IsEquivalence _â‰_
 â‰-isEquiv = record { refl = â‰-isRefl ; sym = â‰-isSym ; trans = â‰-isTrans }

TermSetoid : (X : Type Ï‡) â†’ Setoid (ov Ï‡) (ov Ï‡)
TermSetoid X = record { Carrier = Term X ; _â‰ˆ_ = _â‰_ ; isEquivalence = â‰-isEquiv }

module _ where

 open SetoidAlgebra
 open Func renaming ( f to _<$>_ )

 -- The Term SetoidAlgebra
 ğ‘» : (X : Type Ï‡) â†’ SetoidAlgebra (ov Ï‡) (ov Ï‡)
 Domain (ğ‘» X) = TermSetoid X
 Interp (ğ‘» X) <$> (f , ts) = node f ts
 cong (Interp (ğ‘» X)) {f , ss} {.f , ts} (refl , ssâ‰ˆts) = genl ssâ‰ˆts

\end{code}


#### <a id="interpretation-of-terms-in-setoid-algebras">Interpretation of Terms in Setoid Algebras</a>

The approach to terms and their interpretation in this module was inspired by
[Andreas Abel's formal proof of Birkhoff's completeness theorem](http://www.cse.chalmers.se/~abela/agda/MultiSortedAlgebra.pdf).

To obtain terms with free variables, we add nullary operations, each representing a variable.
These are covered in the std lib FreeMonad module, albeit with the restriction that the sets of
operation symbols and variable symbols have the same size.

\begin{code}

Ops : Type Ï‡ â†’ Signature (ğ“ âŠ” Ï‡) ğ“¥
Ops X = ((âˆ£ ğ‘† âˆ£ âŠ X) , ar)
 where
 ar : âˆ£ ğ‘† âˆ£ âŠ X â†’ Type _
 ar (inl f) = âˆ¥ ğ‘† âˆ¥ f
 ar (inr x) = âŠ¥             -- Add a nullary operation symbol for each variable symbol.


-- Parallel substitutions. A substitution from Î” to Î“ holds a term in Î“ for each variable in Î”.
Sub : (Î“ Î” : Type Ï‡) â†’ Type (ov Ï‡)
Sub Î“ Î” = (x : Î”) â†’ Term Î“


-- Application of a substitution.
_[_] : (t : Term Î”) (Ïƒ : Sub Î“ Î”) â†’ Term Î“
(â„Š x) [ Ïƒ ] = Ïƒ x
(node f ts) [ Ïƒ ] = node f (Î» i â†’ ts i [ Ïƒ ])


-- Interpretation of terms in a model.
module Environment (M : SetoidAlgebra Î± â„“) where

 open SetoidAlgebra M

 open Setoid        renaming ( refl  to  reflS
                             ; sym   to  symS
                             ; trans to  transS)

 -- Equality in M's interpretation of its sort.
 _â‰ƒ_ : Carrier Domain â†’ Carrier Domain â†’ Type â„“
 _â‰ƒ_ = Domain ._â‰ˆ_
 infix -1 _â‰ƒ_

 -- An environment for Î“ maps each variable `x : Î“` to an element of M.
 -- Equality of environments is defined pointwise.
 Env : Type Ï‡ â†’ Setoid _ _
 Env Î“ = record { Carrier = (x : Î“) â†’ Carrier Domain

                ; _â‰ˆ_ = Î» Ï Ï' â†’ (x : Î“) â†’ Ï x â‰ƒ Ï' x

                ; isEquivalence =
                   record { refl = Î» _ â†’ reflS Domain
                          ; sym = Î» h x â†’ symS Domain (h x)
                          ; trans = Î» g h x â†’ transS Domain (g x) (h x)
                          }
                }



 -- Interpretation of terms is iteration on the W-type.
 -- The standard library offers `iter' (on sets), but we need this to be a Func (on setoids).
 open Func renaming ( f to _<$>_ )

 âŸ¦_âŸ§ : {Î“ : Type Ï‡}(t : Term Î“) â†’ Func (Env Î“) Domain

 âŸ¦ â„Š x âŸ§ <$> Ï = Ï x
 âŸ¦ â„Š x âŸ§ .cong Ïâ‚â‰¡Ïâ‚‚ = Ïâ‚â‰¡Ïâ‚‚ x

 âŸ¦ node f args âŸ§ <$> Ï = Interp <$> (f , Î» i â†’ âŸ¦ args i âŸ§ <$> Ï)
 âŸ¦ node f args âŸ§ .cong  Ïâ‚â‰¡Ïâ‚‚  =  cong Interp (refl , Î» i â†’ cong âŸ¦ args i âŸ§ Ïâ‚â‰¡Ïâ‚‚)


 -- An equality between two terms holds in a model
 -- if the two terms are equal under all valuations of their free variables.
 Equal : âˆ€ {Î“ : Type Ï‡} (p q : Term Î“) â†’ Type _
 Equal p q = âˆ€ (Ï : Env _ .Carrier) â†’  âŸ¦ p âŸ§ <$> Ï â‰ƒ âŸ¦ q âŸ§ <$> Ï


 -- Equal is an equivalence relation.
 isEquiv : {Î“ : Type Ï‡} â†’ IsEquivalence (Equal {Î“ = Î“})

 isEquiv = record { refl  =         Î» Ï â†’ reflS  Domain
                  ; sym   =     Î» x=y Ï â†’ symS   Domain (x=y Ï)
                  ; trans = Î» i=j j=k Ï â†’ transS Domain (i=j Ï) (j=k Ï)
                  }

 -- Evaluation of a substitution gives an environment.
 âŸ¦_âŸ§s : {Î“ Î” : Type Ï‡} â†’ Sub Î“ Î” â†’ Carrier (Env Î“) â†’ Carrier (Env Î”)
 âŸ¦ Ïƒ âŸ§s Ï x = âŸ¦ Ïƒ x âŸ§ <$> Ï


 -- Substitution lemma: âŸ¦t[Ïƒ]âŸ§Ï â‰ƒ âŸ¦tâŸ§âŸ¦ÏƒâŸ§Ï
 substitution : {Î“ Î” : Type Ï‡} â†’ (t : Term Î”) (Ïƒ : Sub Î“ Î”) (Ï : Env Î“ .Carrier)
  â†’             âŸ¦ t [ Ïƒ ] âŸ§ <$> Ï  â‰ƒ  âŸ¦ t âŸ§ <$> (âŸ¦ Ïƒ âŸ§s Ï)

 substitution (â„Š x) Ïƒ Ï = reflS Domain
 substitution (node f ts) Ïƒ Ï = cong Interp (refl , Î» i â†’ substitution (ts i) Ïƒ Ï)

\end{code}

--------------------------------

[â†‘ Terms.Setoid](Terms.Setoid.html)
<span style="float:right;">[Terms.Setoid.Properties â†’](Terms.Setoid.Properties.html)</span>

{% include UALib.Links.md %}

[agda-algebras development team]: https://github.com/ualib/agda-algebras#the-agda-algebras-development-team
