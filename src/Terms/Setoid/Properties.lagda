---
layout: default
title : Terms.Setoid.Properties module (The Agda Universal Algebra Library)
date : 2021-08-24
author: [agda-algebras development team][]
---

### <a id="properties-of-the-setoid-based-term-algebra">Properties of the Setoid-based Term Algebra</a>

This is the [Terms.Setoid.Properties][] module of the [Agda Universal Algebra Library][].


\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Algebras.Basic using ( ğ“ ; ğ“¥ ; Signature )

module Terms.Setoid.Properties {ğ‘† : Signature ğ“ ğ“¥} where

-- Imports from Agda and the Agda Standard Library ---------------------
open import Axiom.Extensionality.Propositional
                                   using () renaming (Extensionality to funext)
open import Agda.Primitive         using ( Level ; _âŠ”_ ; lsuc ) renaming ( Set to Type )
open import Data.Product           using ( _,_ ; Î£-syntax ) renaming (projâ‚‚ to snd)
open import Function.Base          using ( _âˆ˜_ )
open import Function.Bundles using ( Func )
open import Data.Empty.Polymorphic using ( âŠ¥ )
open import Relation.Binary        using ( IsEquivalence ; Setoid )
open import Relation.Binary.Definitions
                                   using (Reflexive ; Symmetric ; Transitive )
open import Relation.Binary.PropositionalEquality
                                   using ( _â‰¡_ ; refl ; module â‰¡-Reasoning ; cong )

-- Imports from the Agda Universal Algebra Library ----------------------------------------
open import Overture.Preliminaries             using ( _â»Â¹ ; ğ‘–ğ‘‘ ; âˆ£_âˆ£ ; âˆ¥_âˆ¥ ; transport )
open import Overture.Inverses                  using ( IsSurjective ; Inv ; InvIsInv ; Image_âˆ‹_ ; eq )
open import Algebras.Setoid.Basic      {ğ‘† = ğ‘†} using ( SetoidAlgebra ; _Ì‚_ ; ov ; ğ•Œ[_] )
open import Homomorphisms.Setoid.Basic {ğ‘† = ğ‘†} using ( hom )
open import Terms.Setoid.Basic         {ğ‘† = ğ‘†} using ( Term ; ğ‘» ; _â‰_ )
open Term

private variable Î± Ï Ï‡ : Level

\end{code}


#### <a id="the-universal-property">The universal property</a>

The term algebra `ğ‘» X` is *absolutely free* (or *universal*, or *initial*) for algebras in the signature `ğ‘†`. That is, for every ğ‘†-algebra `ğ‘¨`, the following hold.

1. Every function from `ğ‘‹` to `âˆ£ ğ‘¨ âˆ£` lifts to a homomorphism from `ğ‘» X` to `ğ‘¨`.
2. The homomorphism that exists by item 1 is unique.

We now prove this in [Agda][], starting with the fact that every map from `X` to `âˆ£ ğ‘¨ âˆ£` lifts to a map from `âˆ£ ğ‘» X âˆ£` to `âˆ£ ğ‘¨ âˆ£` in a natural way, by induction on the structure of the given term.

\begin{code}

private variable X : Type Ï‡
open Setoid using (Carrier)
open SetoidAlgebra

free-lift : (ğ‘¨ : SetoidAlgebra Î± Ï)(h : X â†’ ğ•Œ[ ğ‘¨ ]) â†’ ğ•Œ[ ğ‘» X ] â†’ ğ•Œ[ ğ‘¨ ]
free-lift _ h (â„Š x) = h x
free-lift ğ‘¨ h (node f t) = (f Ì‚ ğ‘¨) (Î» i â†’ free-lift ğ‘¨ h (t i))

\end{code}

Naturally, at the base step of the induction, when the term has the form `generator`
x, the free lift of `h` agrees with `h`.  For the inductive step, when the
given term has the form `node f t`, the free lift is defined as
follows: Assuming (the induction hypothesis) that we know the image of each
subterm `t i` under the free lift of `h`, define the free lift at the
full term by applying `f Ì‚ ğ‘¨` to the images of the subterms.

The free lift so defined is a homomorphism by construction. Indeed, here is the trivial proof.

\begin{code}

lift-hom : (ğ‘¨ : SetoidAlgebra Î± Ï) â†’ (X â†’ ğ•Œ[ ğ‘¨ ]) â†’ hom (ğ‘» X) ğ‘¨
lift-hom ğ‘¨ h = free-lift ğ‘¨ h , Î» f a â†’ cong (f Ì‚ ğ‘¨) refl

\end{code}

Finally, we prove that the homomorphism is unique.  Recall, when we proved this in the module [Terms.Properties][], we needed function extensionality. Here, by using setoid equality, we can omit the `swelldef` hypothesis used to prove `free-unique` in the [Terms.Properties][] module.

\begin{code}

module _ {ğ‘¨ : SetoidAlgebra Î± Ï} where
 open SetoidAlgebra
 open Setoid
 open Func renaming   ( f to _<$>_ ; cong to â‰ˆcong )

 private
  A = ğ•Œ[ ğ‘¨ ]
  _â‰ˆA_ = (Domain ğ‘¨) ._â‰ˆ_

 free-unique : (g h : hom (ğ‘» X) ğ‘¨)
  â†’            (âˆ€ x â†’ âˆ£ g âˆ£ (â„Š x) â‰ˆA âˆ£ h âˆ£ (â„Š x))
               ------------------------------------------
  â†’            âˆ€ (t : Term X) â†’  âˆ£ g âˆ£ t â‰ˆA âˆ£ h âˆ£ t

 free-unique _ _ p (â„Š x) = p x

 free-unique g h p (node f t) =
  trans (Domain ğ‘¨) (trans (Domain ğ‘¨) geq lem3) (sym (Domain ğ‘¨) heq)
  where
  lem2 : âˆ€ i â†’ (âˆ£ g âˆ£ âˆ˜ t) i â‰ˆA (âˆ£ h âˆ£ âˆ˜ t) i
  lem2 i = free-unique g h p (t i)

  lem3 : (f Ì‚ ğ‘¨)(âˆ£ g âˆ£ âˆ˜ t) â‰ˆA (f Ì‚ ğ‘¨)(âˆ£ h âˆ£ âˆ˜ t)
  lem3 = â‰ˆcong (Interp ğ‘¨) (_â‰¡_.refl , lem2)

  geq : âˆ£ g âˆ£ (node f t) â‰ˆA (f Ì‚ ğ‘¨)(âˆ£ g âˆ£ âˆ˜ t)
  geq = ==â‡’ ğ‘¨ (âˆ¥ g âˆ¥ f t)

  heq : âˆ£ h âˆ£ (node f t) â‰ˆA (f Ì‚ ğ‘¨)(âˆ£ h âˆ£ âˆ˜ t)
  heq = ==â‡’ ğ‘¨ (âˆ¥ h âˆ¥ f t)

\end{code}

Let's account for what we have proved thus far about the term algebra.  If we postulate a type `X : Type Ï‡` (representing an arbitrary collection of variable symbols) such that for each `ğ‘†`-algebra `ğ‘¨` there is a map from `X` to the domain of `ğ‘¨`, then it follows that for every `ğ‘†`-algebra `ğ‘¨` there is a homomorphism from `ğ‘» X` to `âˆ£ ğ‘¨ âˆ£` that "agrees with the original map on `X`," by which we mean that for all `x : X` the lift evaluated at `â„Š x` is equal to the original function evaluated at `x`.

If we further assume that each of the mappings from `X` to `âˆ£ ğ‘¨ âˆ£` is *surjective*, then the homomorphisms constructed with `free-lift` and `lift-hom` are *epimorphisms*, as we now prove.

\begin{code}

lift-of-epi-is-epi : (ğ‘¨ : SetoidAlgebra Î± Ï){hâ‚€ : X â†’ ğ•Œ[ ğ‘¨ ]}
 â†’                   IsSurjective hâ‚€ â†’ IsSurjective âˆ£ lift-hom ğ‘¨ hâ‚€ âˆ£

lift-of-epi-is-epi ğ‘¨ {hâ‚€} hE y = Goal
 where
 hâ‚€â»Â¹y = Inv hâ‚€ (hE y)

 Î· : y â‰¡ âˆ£ lift-hom ğ‘¨ hâ‚€ âˆ£ (â„Š hâ‚€â»Â¹y)
 Î· = (InvIsInv hâ‚€ (hE y))â»Â¹

 Goal : Image âˆ£ lift-hom ğ‘¨ hâ‚€ âˆ£ âˆ‹ y
 Goal = eq (â„Š hâ‚€â»Â¹y) Î·

\end{code}

The `lift-hom` and `lift-of-epi-is-epi` types will be called to action when such epimorphisms are needed later (e.g., in the [Varieties][] module).


------------------------------

<span style="float:left;">[â† Terms.Setoid.Basic](Terms.Setoid.Basic.html)</span>
<span style="float:right;">[Subalgebras â†’](Subalgebras.html)</span>

{% include UALib.Links.md %}

[the agda-algebras development team]: https://github.com/ualib/agda-algebras#the-agda-algebras-development-team

