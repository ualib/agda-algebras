---
layout: default
title : "Setoid.Varieties.Properties module (The Agda Universal Algebra Library)"
date : "2021-09-24"
author: "agda-algebras development team"
---

### <a id="properties-of-the-models-relation">Properties of the models relation for setoid algebras</a>

We prove some closure and invariance properties of the relation `âŠ§`.  In particular, we prove the following facts (which are needed, for example, in the proof the Birkhoff HSP Theorem).

* [Algebraic invariance](#algebraic-invariance). `âŠ§` is an *algebraic invariant* (stable under isomorphism).

* [Subalgebraic invariance](#subalgebraic-invariance). Identities modeled by a class of algebras are also modeled by all subalgebras of algebras in the class.

* [Product invariance](#product-invariance). Identities modeled by a class of algebras are also modeled by all products of algebras in the class.


\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Base.Algebras.Basic using ( ğ“ ; ğ“¥ ; Signature )

module Setoid.Varieties.Properties {ğ‘† : Signature ğ“ ğ“¥} where

-- Imports from Agda and the Agda Standard Library -------------------------------------------
open import Agda.Primitive    using ( Level ; _âŠ”_ ) renaming ( Set to Type )
open import Data.Product      using ( _,_ )
open import Function.Base     using ( _âˆ˜_ )
open import Function.Bundles  using ( Func )
open import Relation.Binary   using ( Setoid )
open import Relation.Unary    using ( Pred ; _âˆˆ_ )
import Relation.Binary.Reasoning.Setoid as SetoidReasoning

-- Imports from the Agda Universal Algebra Library ---------------------------------------------
open import Base.Overture.Preliminaries                     using ( âˆ£_âˆ£ ; âˆ¥_âˆ¥ )
open import Base.Terms.Basic                        {ğ‘† = ğ‘†} using ( Term ; â„Š )
open import Setoid.Overture.Inverses                        using ( InvIsInverseÊ³ )
open import Setoid.Overture.Surjective                      using ( SurjInv )
open import Setoid.Algebras.Basic                   {ğ‘† = ğ‘†} using ( Algebra ; Lift-AlgË¡ ; ov ; ğ•Œ[_] ; ğ”»[_] )
open import Setoid.Algebras.Products                {ğ‘† = ğ‘†} using ( â¨… )
open import Setoid.Homomorphisms.Basic              {ğ‘† = ğ‘†} using ( hom )
open import Setoid.Homomorphisms.Isomorphisms       {ğ‘† = ğ‘†} using ( _â‰…_ ; mkiso ; Lift-â‰…Ë¡ ; â‰…-sym )
open import Setoid.Homomorphisms.HomomorphicImages  {ğ‘† = ğ‘†} using ( _IsHomImageOf_ )
open import Setoid.Terms.Basic                      {ğ‘† = ğ‘†} using ( ğ‘» ; module Environment )
open import Setoid.Terms.Operations                 {ğ‘† = ğ‘†} using ( comm-hom-term ; interp-prod )
                                                            using ( term-agreement )
open import Setoid.Subalgebras.Subalgebras          {ğ‘† = ğ‘†} using ( _â‰¤_ ; SubalgebrasOfClass )
open import Setoid.Varieties.SoundAndComplete       {ğ‘† = ğ‘†} using ( _âŠ§_ ; _âŠ¨_ ; _âŠ«_ ; Eq ; _â‰ˆÌ‡_ )
                                                            using ( lhs ; rhs ; _âŠ¢_â–¹_â‰ˆ_ )

private variable
 Î± Ïáµƒ Î² Ïáµ‡ Ï‡ â„“ : Level

open Func using ( cong ) renaming (f to _âŸ¨$âŸ©_ )
open Algebra using ( Domain )

\end{code}


#### <a id="algebraic-invariance-of-models">Algebraic invariance of âŠ§</a>

The binary relation âŠ§ would be practically useless if it were not an *algebraic invariant* (i.e., invariant under isomorphism).

\begin{code}


module _ {X : Type Ï‡}{ğ‘¨ : Algebra Î± Ïáµƒ}
         (ğ‘© : Algebra Î² Ïáµ‡)(p q : Term X) where
 open Environment ğ‘¨     using () renaming ( âŸ¦_âŸ§   to âŸ¦_âŸ§â‚ )
 open Environment ğ‘©     using () renaming ( âŸ¦_âŸ§   to âŸ¦_âŸ§â‚‚ )
 open Setoid (Domain ğ‘¨) using () renaming ( _â‰ˆ_   to _â‰ˆâ‚_ )
 open Setoid (Domain ğ‘©) using ( _â‰ˆ_ ; sym )
 open SetoidReasoning (Domain ğ‘©)

 âŠ§-I-invar : ğ‘¨ âŠ§ (p â‰ˆÌ‡ q)  â†’  ğ‘¨ â‰… ğ‘©  â†’  ğ‘© âŠ§ (p â‰ˆÌ‡ q)
 âŠ§-I-invar Apq (mkiso fh gh fâˆ¼g gâˆ¼f) Ï =
  begin
   âŸ¦ p âŸ§â‚‚ âŸ¨$âŸ© Ï             â‰ˆË˜âŸ¨ cong âŸ¦ p âŸ§â‚‚ (Î» x â†’ fâˆ¼g (Ï x)) âŸ©
   âŸ¦ p âŸ§â‚‚ âŸ¨$âŸ© (f âˆ˜ (g âˆ˜ Ï)) â‰ˆË˜âŸ¨ comm-hom-term fh p (g âˆ˜ Ï) âŸ©
   f (âŸ¦ p âŸ§â‚ âŸ¨$âŸ© (g âˆ˜ Ï))   â‰ˆâŸ¨ cong âˆ£ fh âˆ£ (Apq (g âˆ˜ Ï)) âŸ©
   f (âŸ¦ q âŸ§â‚ âŸ¨$âŸ© (g âˆ˜ Ï))   â‰ˆâŸ¨ comm-hom-term fh q (g âˆ˜ Ï) âŸ©
   âŸ¦ q âŸ§â‚‚ âŸ¨$âŸ© (f âˆ˜ (g âˆ˜ Ï)) â‰ˆâŸ¨ cong âŸ¦ q âŸ§â‚‚ (Î» x â†’ fâˆ¼g (Ï x)) âŸ©
   âŸ¦ q âŸ§â‚‚ âŸ¨$âŸ© Ï             âˆ
  where private f = _âŸ¨$âŸ©_ âˆ£ fh âˆ£ ; g = _âŸ¨$âŸ©_ âˆ£ gh âˆ£

\end{code}


 As the proof makes clear, we show ğ‘© âŠ§ p â‰ˆ q by showing that `ğ‘© âŸ¦ p âŸ§ â‰¡ ğ‘© âŸ¦ q âŸ§ holds *extensionally*, that is, `âˆ€ x, ğ‘© âŸ¦ p âŸ§ x â‰¡ ğ‘© âŸ¦q âŸ§ x`.

#### <a id="lift-invariance">Lift-invariance of âŠ§</a>
The âŠ§ relation is also invariant under the algebraic lift and lower operations.

\begin{code}

-- module _ (wd : SwellDef){Î± Î² Ï‡ : Level}{X : Type Ï‡}{ğ‘¨ : Algebra Î± Ïáµƒ} where
module _ {X : Type Ï‡}{ğ‘¨ : Algebra Î± Ïáµƒ} where

 âŠ§-Lift-invar : (p q : Term X) â†’ ğ‘¨ âŠ§ (p â‰ˆÌ‡ q) â†’ Lift-AlgË¡ ğ‘¨ Î² âŠ§ (p â‰ˆÌ‡ q)
 âŠ§-Lift-invar p q Apq = âŠ§-I-invar (Lift-AlgË¡ ğ‘¨ _) p q Apq Lift-â‰…Ë¡

 âŠ§-lower-invar : (p q : Term X) â†’ Lift-AlgË¡ ğ‘¨ Î² âŠ§ (p â‰ˆÌ‡ q)  â†’  ğ‘¨ âŠ§ (p â‰ˆÌ‡ q)
 âŠ§-lower-invar p q lApq = âŠ§-I-invar ğ‘¨ p q lApq (â‰…-sym Lift-â‰…Ë¡)

\end{code}



#### <a id="homomorphic-invariance">Homomorphic invariance of âŠ§</a>
Identities modeled by an algebra `ğ‘¨` are also modeled by every homomorphic image of `ğ‘¨`, which fact can be formalized as follows.

\begin{code}

module _ {X : Type Ï‡}{ğ‘¨ : Algebra Î± Ïáµƒ}{ğ‘© : Algebra Î² Ïáµ‡}{p q : Term X} where

 âŠ§-H-invar : ğ‘¨ âŠ§ (p â‰ˆÌ‡ q) â†’ ğ‘© IsHomImageOf ğ‘¨ â†’ ğ‘© âŠ§ (p â‰ˆÌ‡ q)
 âŠ§-H-invar Apq (Ï†h , Ï†E) Ï =
  begin
       âŸ¦ p âŸ§   âŸ¨$âŸ©               Ï    â‰ˆË˜âŸ¨  cong âŸ¦ p âŸ§(Î» _ â†’ InvIsInverseÊ³ Ï†E)  âŸ©
       âŸ¦ p âŸ§   âŸ¨$âŸ© (Ï† âˆ˜  Ï†â»Â¹  âˆ˜  Ï)   â‰ˆË˜âŸ¨  comm-hom-term Ï†h p (Ï†â»Â¹ âˆ˜ Ï)        âŸ©
   Ï†(  âŸ¦ p âŸ§á´¬  âŸ¨$âŸ© (     Ï†â»Â¹  âˆ˜  Ï))  â‰ˆâŸ¨   cong âˆ£ Ï†h âˆ£ (Apq (Ï†â»Â¹ âˆ˜ Ï))         âŸ©
   Ï†(  âŸ¦ q âŸ§á´¬  âŸ¨$âŸ© (     Ï†â»Â¹  âˆ˜  Ï))  â‰ˆâŸ¨   comm-hom-term Ï†h q (Ï†â»Â¹ âˆ˜ Ï)        âŸ©
       âŸ¦ q âŸ§   âŸ¨$âŸ© (Ï† âˆ˜  Ï†â»Â¹  âˆ˜  Ï)   â‰ˆâŸ¨   cong âŸ¦ q âŸ§(Î» _ â†’ InvIsInverseÊ³ Ï†E)  âŸ©
       âŸ¦ q âŸ§   âŸ¨$âŸ©               Ï    âˆ
  where
  Ï†â»Â¹ : ğ•Œ[ ğ‘© ] â†’ ğ•Œ[ ğ‘¨ ]
  Ï†â»Â¹ = SurjInv âˆ£ Ï†h âˆ£ Ï†E
  private Ï† = (_âŸ¨$âŸ©_ âˆ£ Ï†h âˆ£)
  open Environment ğ‘¨  using () renaming ( âŸ¦_âŸ§ to âŸ¦_âŸ§á´¬)
  open Environment ğ‘©  using ( âŸ¦_âŸ§ )
  open SetoidReasoning ğ”»[ ğ‘© ]
\end{code}


#### <a id="subalgebraic-invariance">Subalgebraic invariance of âŠ§</a>
Identities modeled by an algebra `ğ‘¨` are also modeled by every subalgebra of `ğ‘¨`, which fact can be formalized as follows.

\begin{code}

module _ {X : Type Ï‡}{p q : Term X}{ğ‘¨ : Algebra Î± Ïáµƒ}
         {ğ‘© : Algebra Î² Ïáµ‡} where
 open Environment ğ‘¨ using () renaming ( âŸ¦_âŸ§ to âŸ¦_âŸ§â‚ )
 open Environment ğ‘© using () renaming ( âŸ¦_âŸ§ to âŸ¦_âŸ§â‚‚ )
 open Setoid (Domain ğ‘¨) using ( _â‰ˆ_ )
 open Setoid (Domain ğ‘©) using () renaming ( _â‰ˆ_ to _â‰ˆâ‚‚_ )
 open SetoidReasoning (Domain ğ‘¨)


 âŠ§-S-invar : ğ‘¨ âŠ§ (p â‰ˆÌ‡ q) â†’  ğ‘© â‰¤ ğ‘¨  â†’  ğ‘© âŠ§ (p â‰ˆÌ‡ q)
 âŠ§-S-invar Apq Bâ‰¤A b = goal
  where
  hh : hom ğ‘© ğ‘¨
  hh = âˆ£ Bâ‰¤A âˆ£
  h = _âŸ¨$âŸ©_ âˆ£ hh âˆ£
  Î¾ : âˆ€ b â†’ h (âŸ¦ p âŸ§â‚‚ âŸ¨$âŸ© b) â‰ˆ h (âŸ¦ q âŸ§â‚‚ âŸ¨$âŸ© b)
  Î¾ b = begin
         h (âŸ¦ p âŸ§â‚‚ âŸ¨$âŸ© b)   â‰ˆâŸ¨ comm-hom-term hh p b âŸ©
         âŸ¦ p âŸ§â‚ âŸ¨$âŸ© (h âˆ˜ b) â‰ˆâŸ¨ Apq (h âˆ˜ b) âŸ©
         âŸ¦ q âŸ§â‚ âŸ¨$âŸ© (h âˆ˜ b) â‰ˆË˜âŸ¨ comm-hom-term hh q b âŸ©
         h (âŸ¦ q âŸ§â‚‚ âŸ¨$âŸ© b)   âˆ
  goal : âŸ¦ p âŸ§â‚‚ âŸ¨$âŸ© b â‰ˆâ‚‚ âŸ¦ q âŸ§â‚‚ âŸ¨$âŸ© b
  goal = âˆ¥ Bâ‰¤A âˆ¥ (Î¾ b)


\end{code}

Next, identities modeled by a class of algebras is also modeled by all subalgebras of the class.  In other terms, every term equation `(p â‰ˆÌ‡ q)` that is satisfied by all `ğ‘¨ âˆˆ ğ’¦` is also satisfied by every subalgebra of a member of ğ’¦.

\begin{code}
module _ {X : Type Ï‡}{p q : Term X} where

 âŠ§-S-class-invar : {ğ’¦ : Pred (Algebra Î± Ïáµƒ) â„“}
  â†’                (ğ’¦ âŠ« (p â‰ˆÌ‡ q)) â†’ ((ğ‘© , _) : SubalgebrasOfClass ğ’¦ {Î²}{Ïáµ‡})
  â†’                ğ‘© âŠ§ (p â‰ˆÌ‡ q)
 âŠ§-S-class-invar Kpq (ğ‘© , ğ‘¨ , kA , Bâ‰¤A) = âŠ§-S-invar{p = p}{q} (Kpq ğ‘¨ kA) Bâ‰¤A

\end{code}



#### <a id="product-invariance">Product invariance of âŠ§</a>

An identity satisfied by all algebras in an indexed collection is also satisfied by the product of algebras in that collection.

\begin{code}

module _ {X : Type Ï‡}{p q : Term X}{I : Type â„“}(ğ’œ : I â†’ Algebra Î± Ïáµƒ) where

 âŠ§-P-invar : (âˆ€ i â†’ ğ’œ i âŠ§ (p â‰ˆÌ‡ q)) â†’ â¨… ğ’œ âŠ§ (p â‰ˆÌ‡ q)
 âŠ§-P-invar ğ’œpq a = goal
  where
  open Algebra (â¨… ğ’œ) using () renaming ( Domain to â¨…A )
  open Environment   (â¨… ğ’œ) using () renaming ( âŸ¦_âŸ§ to âŸ¦_âŸ§â‚ )
  open Environment using ( âŸ¦_âŸ§ )
  open Setoid â¨…A   using ( _â‰ˆ_ )
  open SetoidReasoning â¨…A

  Î¾ : (Î» i â†’ (âŸ¦ ğ’œ i âŸ§ p) âŸ¨$âŸ© (Î» x â†’ (a x) i)) â‰ˆ (Î» i â†’ (âŸ¦ ğ’œ i âŸ§ q) âŸ¨$âŸ© (Î» x â†’ (a x) i))
  Î¾ = Î» i â†’ ğ’œpq i (Î» x â†’ (a x) i)
  goal : âŸ¦ p âŸ§â‚ âŸ¨$âŸ© a â‰ˆ âŸ¦ q âŸ§â‚ âŸ¨$âŸ© a
  goal = begin
          âŸ¦ p âŸ§â‚ âŸ¨$âŸ© a â‰ˆâŸ¨ interp-prod ğ’œ p a âŸ©
          (Î» i â†’ (âŸ¦ ğ’œ i âŸ§ p) âŸ¨$âŸ© (Î» x â†’ (a x) i)) â‰ˆâŸ¨ Î¾ âŸ©
          (Î» i â†’ (âŸ¦ ğ’œ i âŸ§ q) âŸ¨$âŸ© (Î» x â†’ (a x) i)) â‰ˆË˜âŸ¨ interp-prod ğ’œ q a âŸ©
          âŸ¦ q âŸ§â‚ âŸ¨$âŸ© a âˆ

\end{code}

An identity satisfied by all algebras in a class is also satisfied by the product of algebras in the class.

\begin{code}

 âŠ§-P-class-invar : (ğ’¦ : Pred (Algebra Î± Ïáµƒ)(ov Î±))
  â†’                ğ’¦ âŠ« (p â‰ˆÌ‡ q) â†’ (âˆ€ i â†’ ğ’œ i âˆˆ ğ’¦) â†’ â¨… ğ’œ âŠ§ (p â‰ˆÌ‡ q)

 âŠ§-P-class-invar ğ’¦ Ïƒ Kğ’œ = âŠ§-P-invar (Î» i Ï â†’ Ïƒ (ğ’œ i) (Kğ’œ i) Ï)

\end{code}

Another fact that will turn out to be useful is that a product of a collection of algebras models (p â‰ˆÌ‡ q) if the lift of each algebra in the collection models (p â‰ˆÌ‡ q).

\begin{code}

 âŠ§-P-lift-invar : (âˆ€ i â†’ Lift-AlgË¡ (ğ’œ i) Î² âŠ§ (p â‰ˆÌ‡ q))  â†’  â¨… ğ’œ âŠ§ (p â‰ˆÌ‡ q)
 âŠ§-P-lift-invar Î± = âŠ§-P-invar Aipq
  where
  Aipq : âˆ€ i â†’ (ğ’œ i) âŠ§ (p â‰ˆÌ‡ q)
  Aipq i = âŠ§-lower-invar{ğ‘¨ = (ğ’œ i)} p q (Î± i)

\end{code}



#### <a id="homomorphisc-invariance">Homomorphic invariance of âŠ§</a>

If an algebra ğ‘¨ models an identity (p â‰ˆÌ‡ q), then the pair (p , q) belongs to the kernel of every homomorphism Ï† : hom (ğ‘» X) ğ‘¨ from the term algebra to ğ‘¨; that is, every homomorphism from ğ‘» X to ğ‘¨ maps p and q to the same element of ğ‘¨.

 \begin{code}

module _ {X : Type Ï‡}{p q : Term X}{ğ‘¨ : Algebra Î± Ïáµƒ}(Ï†h : hom (ğ‘» X) ğ‘¨) where
 open Setoid (Domain ğ‘¨) using ( _â‰ˆ_ )
 private Ï† = _âŸ¨$âŸ©_ âˆ£ Ï†h âˆ£

 âŠ§-H-ker : ğ‘¨ âŠ§ (p â‰ˆÌ‡ q) â†’ Ï† p â‰ˆ Ï† q
 âŠ§-H-ker Î² =
  begin
   Ï† p                â‰ˆâŸ¨ cong âˆ£ Ï†h âˆ£ (term-agreement p)âŸ©
   Ï† (âŸ¦ p âŸ§ âŸ¨$âŸ© â„Š)    â‰ˆâŸ¨ comm-hom-term Ï†h p â„Š âŸ©
   âŸ¦ p âŸ§â‚‚ âŸ¨$âŸ© (Ï† âˆ˜ â„Š) â‰ˆâŸ¨ Î² (Ï† âˆ˜ â„Š) âŸ©
   âŸ¦ q âŸ§â‚‚ âŸ¨$âŸ© (Ï† âˆ˜ â„Š) â‰ˆË˜âŸ¨ comm-hom-term Ï†h q â„Š âŸ©
   Ï† (âŸ¦ q âŸ§ âŸ¨$âŸ© â„Š)    â‰ˆË˜âŸ¨ cong âˆ£ Ï†h âˆ£ (term-agreement q)âŸ©
   Ï† q                âˆ

  where
  open SetoidReasoning (Domain ğ‘¨)
  open Environment ğ‘¨ using () renaming ( âŸ¦_âŸ§ to âŸ¦_âŸ§â‚‚ )
  open Environment (ğ‘» X) using ( âŸ¦_âŸ§ )

\end{code}

---------------------------------

<span style="float:left;">[â† Setoid.Varieties.Closure](Setoid.Varieties.Closure.html)</span>
<span style="float:right;">[Setoid.Varieties.Preservation â†’](Setoid.Varieties.Preservation.html)</span>

{% include UALib.Links.md %}
