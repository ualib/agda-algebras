---
layout: default
title : "Setoid.Varieties.HSP module (Agda Universal Algebra Library)"
date : "2021-10-16"
author: "agda-algebras development team"
---

#### <a id="the-hsp-theorem">The HSP Theorem</a>

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Base.Algebras.Basic using ( ğ“ ; ğ“¥ ; Signature )

module Setoid.Varieties.HSP {ğ‘† : Signature ğ“ ğ“¥} where

-- Imports from Agda and the Agda Standard Library ------------------------------------------------
open import Agda.Primitive    using ( lsuc )            renaming ( Set to Type )
open import Data.Product      using ( _,_ ; Î£-syntax )  renaming ( projâ‚ to fst ; projâ‚‚ to snd ; _Ã—_  to _âˆ§_ )
open import Function.Bundles  using ()                  renaming ( Func to _âŸ¶_ )
open import Level
open import Relation.Binary   using ( Setoid )
open import Relation.Unary    using ( Pred ; _âˆˆ_ ; _âŠ†_ )

-- -- Imports from the Agda Universal Algebra Library ---------------------------------------------------
open import Base.Overture.Preliminaries                     using ( âˆ£_âˆ£ ; âˆ¥_âˆ¥ )
open import Setoid.Relations.Discrete                       using ( fkerPred )
open import Setoid.Algebras.Basic                   {ğ‘† = ğ‘†} using ( Algebra ; ov ; Lift-Alg )
open import Setoid.Algebras.Products                {ğ‘† = ğ‘†} using ( â¨… )
open import Setoid.Homomorphisms.Basic              {ğ‘† = ğ‘†} using ( hom ; mon ; IsMon ; IsHom )
                                                            using ( epi ; epiâ†’ontohom )
open import Setoid.Homomorphisms.Products           {ğ‘† = ğ‘†} using ( â¨…-hom-co )
open import Setoid.Homomorphisms.Factor             {ğ‘† = ğ‘†} using ( HomFactor )
open import Setoid.Homomorphisms.Isomorphisms       {ğ‘† = ğ‘†} using ( â‰…-refl )
open import Setoid.Homomorphisms.HomomorphicImages  {ğ‘† = ğ‘†} using ( _IsHomImageOf_ )
open import Setoid.Subalgebras.Subalgebras          {ğ‘† = ğ‘†} using ( _â‰¤_ ; monâ†’â‰¤ )
open import Setoid.Terms.Basic                      {ğ‘† = ğ‘†} using ( module Environment ; ğ‘» )
open import Setoid.Terms.Properties                 {ğ‘† = ğ‘†} using ( lift-hom ; free-lift )
open import Setoid.Terms.Operations                 {ğ‘† = ğ‘†} using ( free-lift-interp )
open import Setoid.Varieties.SoundAndComplete       {ğ‘† = ğ‘†} using ( module FreeAlgebra ; _âŠ«_ ; _â‰ˆÌ‡_ )
                                                            using ( _âŠ¢_â–¹_â‰ˆ_ ; Mod ; Th )
open import Setoid.Varieties.Closure                {ğ‘† = ğ‘†} using ( S ; V ; P ; S-idem ; V-â‰…-lc )
open import Setoid.Varieties.Preservation           {ğ‘† = ğ‘†} using ( S-id2 ; PSâŠ†SP )
open import Setoid.Varieties.FreeAlgebras           {ğ‘† = ğ‘†} using ( module FreeHom ; ğ”½-ModTh-epi-lift )

open _âŸ¶_          using () renaming ( f to _âŸ¨$âŸ©_ )
open Setoid       using ( Carrier )
open Algebra      using ( Domain )
open Environment  using ( Env )

module _ {Î± Ïáµƒ â„“ : Level}
         (ğ’¦ : Pred(Algebra Î± Ïáµƒ) (Î± âŠ” Ïáµƒ âŠ” ov â„“))
         {X : Type (Î± âŠ” Ïáµƒ âŠ” â„“)} where

 private Î¹ = ov(Î± âŠ” Ïáµƒ âŠ” â„“)

 open FreeHom (Î± âŠ” Ïáµƒ âŠ” â„“) {Î±}{Ïáµƒ}{â„“}{ğ’¦}
 open FreeAlgebra {Î¹ = Î¹}{I = â„} â„° using ( ğ”½[_] )

\end{code}

We want to pair each `(ğ‘¨ , p)` (where p : ğ‘¨ âˆˆ S ğ’¦) with an environment
`Ï : X â†’ âˆ£ ğ‘¨ âˆ£` so that we can quantify over all algebras *and* all
assignments of values in the domain `âˆ£ ğ‘¨ âˆ£` to variables in `X`.

\begin{code}

 â„‘âº : Type Î¹
 â„‘âº = Î£[ ğ‘¨ âˆˆ (Algebra Î± Ïáµƒ) ] (ğ‘¨ âˆˆ S â„“ ğ’¦) âˆ§ (Carrier (Env ğ‘¨ X))

 ğ”„âº : â„‘âº â†’ Algebra Î± Ïáµƒ
 ğ”„âº i = âˆ£ i âˆ£

 â„­ : Algebra Î¹ Î¹
 â„­ = â¨… ğ”„âº

\end{code}

Next we define a useful type, `skEqual`, which we use to represent a term identity `p â‰ˆ q` for any
given `i = (ğ‘¨ , sA , Ï)` (where `ğ‘¨` is an algebra, `sA : ğ‘¨ âˆˆ S ğ’¦` is a proof that `ğ‘¨` belongs
to `S ğ’¦`, and `Ï` is a mapping from `X` to the domain of `ğ‘¨`). Then we prove `AllEqualâŠ†kerğ”½` which
asserts that if the identity `p â‰ˆ q` holds in all `ğ‘¨ âˆˆ S ğ’¦` (for all environments), then `p â‰ˆ q`
holds in the relatively free algebra `ğ”½[ X ]`; equivalently, the pair `(p , q)` belongs to the
kernel of the natural homomorphism from `ğ‘» X` onto `ğ”½[ X ]`. We will use this fact below to prove
that there is a monomorphism from `ğ”½[ X ]` into `â„­`, and thus `ğ”½[ X ]` is a subalgebra of â„­,
so belongs to `S (P ğ’¦)`.

\begin{code}

 skEqual : (i : â„‘âº) â†’ âˆ€{p q} â†’ Type Ïáµƒ
 skEqual i {p}{q} = âŸ¦ p âŸ§ âŸ¨$âŸ© snd âˆ¥ i âˆ¥ â‰ˆ âŸ¦ q âŸ§ âŸ¨$âŸ© snd âˆ¥ i âˆ¥
  where
  open Setoid (Domain (ğ”„âº i)) using ( _â‰ˆ_ )
  open Environment (ğ”„âº i) using ( âŸ¦_âŸ§ )

 AllEqualâŠ†kerğ”½ : âˆ€ {p q}
  â†’              (âˆ€ i â†’ skEqual i {p}{q}) â†’ (p , q) âˆˆ fkerPred âˆ£ homğ”½[ X ] âˆ£
 AllEqualâŠ†kerğ”½ {p} {q} x = Goal
  where
  open Algebra ğ”½[ X ] using () renaming ( Domain to F ; Interp to InterpF )
  open Setoid F using () renaming ( _â‰ˆ_  to _â‰ˆFâ‰ˆ_ ; refl to reflF )
  Sğ’¦âŠ«pq : S{Î² = Î±}{Ïáµƒ} â„“ ğ’¦ âŠ« (p â‰ˆÌ‡ q)
  Sğ’¦âŠ«pq ğ‘¨ sA Ï = x (ğ‘¨ , sA , Ï)
  Goal : p â‰ˆFâ‰ˆ q
  Goal = ğ’¦âŠ«â†’â„°âŠ¢ (S-id2{â„“ = â„“}{p = p}{q} Sğ’¦âŠ«pq)

 homâ„­ : hom (ğ‘» X) â„­
 homâ„­ = â¨…-hom-co ğ”„âº h
  where
  h : âˆ€ i â†’ hom (ğ‘» X) (ğ”„âº i)
  h i = lift-hom (snd âˆ¥ i âˆ¥)

 open Algebra ğ”½[ X ] using () renaming ( Domain to F ; Interp to InterpF )
 open Setoid F using () renaming (refl to reflF ; _â‰ˆ_ to _â‰ˆFâ‰ˆ_ ; Carrier to âˆ£Fâˆ£)


 kerğ”½âŠ†kerâ„­ : fkerPred âˆ£ homğ”½[ X ] âˆ£ âŠ† fkerPred âˆ£ homâ„­ âˆ£
 kerğ”½âŠ†kerâ„­ {p , q} pKq (ğ‘¨ , sA , Ï) = Goal
  where
  open Setoid (Domain ğ‘¨) using ( _â‰ˆ_ ; sym ; trans )
  open Environment ğ‘¨ using ( âŸ¦_âŸ§ )
  fl : âˆ€ t â†’ âŸ¦ t âŸ§ âŸ¨$âŸ© Ï â‰ˆ free-lift Ï t
  fl t = free-lift-interp {ğ‘¨ = ğ‘¨} Ï t
  subgoal : âŸ¦ p âŸ§ âŸ¨$âŸ© Ï â‰ˆ âŸ¦ q âŸ§ âŸ¨$âŸ© Ï
  subgoal = kerğ”½âŠ†Equal{ğ‘¨ = ğ‘¨}{sA} pKq Ï
  Goal : (free-lift{ğ‘¨ = ğ‘¨} Ï p) â‰ˆ (free-lift{ğ‘¨ = ğ‘¨} Ï q)
  Goal = trans (sym (fl p)) (trans subgoal (fl q))


 homğ”½â„­ : hom ğ”½[ X ] â„­
 homğ”½â„­ = âˆ£ HomFactor â„­ homâ„­ homğ”½[ X ] kerğ”½âŠ†kerâ„­ homğ”½[ X ]-is-epic âˆ£

 open Environment â„­

 kerâ„­âŠ†kerğ”½ : âˆ€{p q} â†’ (p , q) âˆˆ fkerPred âˆ£ homâ„­ âˆ£ â†’ (p , q) âˆˆ fkerPred âˆ£ homğ”½[ X ] âˆ£
 kerâ„­âŠ†kerğ”½ {p}{q} pKq = EâŠ¢pq
  where
  pqEqual : âˆ€ i â†’ skEqual i {p}{q}
  pqEqual i = goal
   where
   open Environment (ğ”„âº i) using () renaming ( âŸ¦_âŸ§ to âŸ¦_âŸ§áµ¢ )
   open Setoid (Domain (ğ”„âº i)) using ( _â‰ˆ_ ; sym ; trans )
   goal : âŸ¦ p âŸ§áµ¢ âŸ¨$âŸ© snd âˆ¥ i âˆ¥ â‰ˆ âŸ¦ q âŸ§áµ¢ âŸ¨$âŸ© snd âˆ¥ i âˆ¥
   goal = trans (free-lift-interp{ğ‘¨ = âˆ£ i âˆ£}(snd âˆ¥ i âˆ¥) p)
                 (trans (pKq i)(sym (free-lift-interp{ğ‘¨ = âˆ£ i âˆ£} (snd âˆ¥ i âˆ¥) q)))
  EâŠ¢pq : â„° âŠ¢ X â–¹ p â‰ˆ q
  EâŠ¢pq = AllEqualâŠ†kerğ”½ pqEqual


 monğ”½â„­ : mon ğ”½[ X ] â„­
 monğ”½â„­ = âˆ£ homğ”½â„­ âˆ£ , isMon
  where
  open IsMon
  open IsHom
  isMon : IsMon ğ”½[ X ] â„­ âˆ£ homğ”½â„­ âˆ£
  isHom isMon = âˆ¥ homğ”½â„­ âˆ¥
  isInjective isMon {p} {q} Ï†pq = kerâ„­âŠ†kerğ”½ Ï†pq

\end{code}

Now that we have proved the existence of a monomorphism from `ğ”½[ X ]` to `â„­` we are in a position
to prove that `ğ”½[ X ]` is a subalgebra of â„­, so belongs to `S (P ğ’¦)`.  In fact, we will show
that `ğ”½[ X ]` is a subalgebra of the *lift* of `â„­`, denoted `â„“â„­`.

\begin{code}

 ğ”½â‰¤â„­ : ğ”½[ X ] â‰¤ â„­
 ğ”½â‰¤â„­ = monâ†’â‰¤ monğ”½â„­

 SPğ”½ : ğ”½[ X ] âˆˆ S Î¹ (P â„“ Î¹ ğ’¦)
 SPğ”½ = S-idem SSPğ”½
  where
  PSâ„­ : â„­ âˆˆ P (Î± âŠ” Ïáµƒ âŠ” â„“) Î¹ (S â„“ ğ’¦)
  PSâ„­ = â„‘âº , (ğ”„âº , ((Î» i â†’ fst âˆ¥ i âˆ¥) , â‰…-refl))

  SPâ„­ : â„­ âˆˆ S Î¹ (P â„“ Î¹ ğ’¦)
  SPâ„­ = PSâŠ†SP {â„“ = â„“} PSâ„­

  SSPğ”½ : ğ”½[ X ] âˆˆ S Î¹ (S Î¹ (P â„“ Î¹ ğ’¦))
  SSPğ”½ = â„­ , (SPâ„­ , ğ”½â‰¤â„­)

\end{code}

#### <a id="proof-of-the-hsp-theorem">Proof of the HSP theorem</a>

Finally, we are in a position to prove Birkhoff's celebrated variety theorem.

\begin{code}

module _ {Î± Ïáµƒ â„“ : Level}
         {ğ’¦ : Pred(Algebra Î± Ïáµƒ) (Î± âŠ” Ïáµƒ âŠ” ov â„“)} where
 private
  Î¹ = ov(Î± âŠ” Ïáµƒ âŠ” â„“)

 open FreeHom (Î± âŠ” Ïáµƒ âŠ” â„“) {Î±}{Ïáµƒ}{â„“}{ğ’¦}
 open FreeAlgebra {Î¹ = Î¹}{I = â„} â„° using ( ğ”½[_] )

 Birkhoff : âˆ€ ğ‘¨ â†’ ğ‘¨ âˆˆ Mod (Th (V â„“ Î¹ ğ’¦)) â†’ ğ‘¨ âˆˆ V â„“ Î¹ ğ’¦
 Birkhoff ğ‘¨ ModThA = V-â‰…-lc{Î±}{Ïáµƒ}{â„“} ğ’¦ ğ‘¨ VlA
  where
  open Setoid (Domain ğ‘¨) using () renaming ( Carrier to âˆ£Aâˆ£ )
  spğ”½A : ğ”½[ âˆ£Aâˆ£ ] âˆˆ S{Î¹} Î¹ (P â„“ Î¹ ğ’¦)
  spğ”½A = SPğ”½{â„“ = â„“} ğ’¦

  epiğ”½lA : epi ğ”½[ âˆ£Aâˆ£ ] (Lift-Alg ğ‘¨ Î¹ Î¹)
  epiğ”½lA = ğ”½-ModTh-epi-lift{â„“ = â„“} (Î» {p q} â†’ ModThA{p = p}{q})

  lAimgğ”½A : Lift-Alg ğ‘¨ Î¹ Î¹ IsHomImageOf ğ”½[ âˆ£Aâˆ£ ]
  lAimgğ”½A = epiâ†’ontohom ğ”½[ âˆ£Aâˆ£ ] (Lift-Alg ğ‘¨ Î¹ Î¹) epiğ”½lA

  VlA : Lift-Alg ğ‘¨ Î¹ Î¹ âˆˆ V â„“ Î¹ ğ’¦
  VlA = ğ”½[ âˆ£Aâˆ£ ] , spğ”½A , lAimgğ”½A

\end{code}

The converse inclusion, `V ğ’¦ âŠ† Mod (Th (V ğ’¦))`, is a simple consequence of the
fact that `Mod Th` is a closure operator. Nonetheless, completeness demands
that we formalize this inclusion as well, however trivial the proof.

\begin{code}

 module _ {ğ‘¨ : Algebra Î± Ïáµƒ} where
  open Setoid (Domain ğ‘¨) using () renaming ( Carrier to âˆ£Aâˆ£ )

  Birkhoff-converse : ğ‘¨ âˆˆ V{Î±}{Ïáµƒ}{Î±}{Ïáµƒ}{Î±}{Ïáµƒ} â„“ Î¹ ğ’¦ â†’ ğ‘¨ âˆˆ Mod{X = âˆ£Aâˆ£} (Th (V â„“ Î¹ ğ’¦))
  Birkhoff-converse vA pThq = pThq ğ‘¨ vA

\end{code}

We have thus proved that every variety is an equational class.

Readers familiar with the classical formulation of the Birkhoff HSP theorem as an
"if and only if" assertion might worry that the proof is still incomplete. However,
recall that in the [Setoid.Varieties.Preservation][] module we proved the following
identity preservation lemma:

`V-id1 : ğ’¦ âŠ« p â‰ˆÌ‡ q â†’ V ğ’¦ âŠ« p â‰ˆÌ‡ q`

Thus, if `ğ’¦` is an equational class---that is, if ğ’¦ is the class of algebras
satisfying all identities in some set---then `V ğ’¦` âŠ† ğ’¦`.  On the other hand, we
proved that `V` is expansive in the [Setoid.Varieties.Closure][] module:

`V-expa : ğ’¦ âŠ† V ğ’¦`

so `ğ’¦` (= `V ğ’¦` = `HSP ğ’¦`) is a variety.

Taken together, `V-id1` and `V-expa` constitute formal proof that every equational
class is a variety.

This completes the formal proof of Birkhoff's variety theorem.

--------------------------------

<span style="float:left;">[â† Setoid.Varieties.FreeAlgebras](Setoid.Varieties.FreeAlgebras.html)</span>
<span style="float:right;">[Top â†‘](index.html)</span>

{% include UALib.Links.md %}

