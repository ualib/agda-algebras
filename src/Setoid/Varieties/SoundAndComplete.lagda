---
layout: default
title : "Setoid.Varieties.SoundAndComplete module (The Agda Universal Algebra Library)"
date : "2021-01-14"
author: "agda-algebras development team"
---

#### <a id="entailment-derivation-rules-soundness-and-completeness">Entailment, derivation rules, soundness and completeness</a>

This is the [Setoid.Varieties.SoundAndComplete][] module of the [Agda Universal Algebra Library][].

This module is based on [Andreas Abel's Agda formalization of Birkhoff's completeness theorem](http://www.cse.chalmers.se/~abela/agda/MultiSortedAlgebra.pdf).

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Base.Algebras.Basic using ( ğ“ ; ğ“¥ ; Signature )

module Setoid.Varieties.SoundAndComplete {ğ‘† : Signature ğ“ ğ“¥} where

-- imports from Agda and the Agda Standard Library -------------------------------------------
open import Agda.Primitive   using ( _âŠ”_ ; lsuc ; Level ) renaming ( Set to Type )
open import Data.Product     using ( _,_ ; Î£-syntax ; _Ã—_ ) renaming ( projâ‚ to fst ; projâ‚‚ to snd )
open import Function.Base    using ( _âˆ˜_ ; flip ; id )
open import Function.Bundles using () renaming ( Func to _âŸ¶_ )
open import Relation.Binary  using ( Setoid ; IsEquivalence )
open import Relation.Unary   using ( Pred ; _âˆˆ_ )
open import Relation.Binary.PropositionalEquality using ( _â‰¡_ ; refl )
import Relation.Binary.Reasoning.Setoid as SetoidReasoning

-- Imports from the Agda Universal Algebra Library ---------------------------------------------
open import Base.Overture.Preliminaries     using ( âˆ£_âˆ£ )
open import Base.Terms.Basic       {ğ‘† = ğ‘†}  using ( Term )
open import Setoid.Algebras.Basic  {ğ‘† = ğ‘†}  using ( Algebra ; ov ; âŸ¨_âŸ© )
open import Setoid.Terms.Basic     {ğ‘† = ğ‘†}  using ( module Environment ; Sub ; _[_] )

open Setoid  using ( Carrier ; _â‰ˆ_ ; isEquivalence )
open _âŸ¶_     renaming ( f to _âŸ¨$âŸ©_ )
open Term

private variable
 Ï‡ Î± Ïáµƒ Î¹ â„“ : Level
 X Î“ Î” : Type Ï‡
 f     : âˆ£ ğ‘† âˆ£
 I : Type Î¹

-- Equations
-- An equation is a pair (s , t) of terms in the same context.
record Eq : Type (ov Ï‡) where
 constructor _â‰ˆÌ‡_
 field
  {cxt} : Type Ï‡
  lhs   : Term cxt
  rhs   : Term cxt

open Eq public

-- Equation p â‰ˆÌ‡ q holding in algebra M. (type \~~\^. to get â‰ˆÌ‡; type \models to get âŠ§)
_âŠ§_ : (ğ‘¨ : Algebra Î± Ïáµƒ)(term-identity : Eq{Ï‡}) â†’ Type _
ğ‘¨ âŠ§ (p â‰ˆÌ‡ q) = Equal p q where open Environment ğ‘¨

_âŠ«_ : Pred (Algebra Î± Ïáµƒ) â„“ â†’ Eq{Ï‡} â†’ Type (â„“ âŠ” Ï‡ âŠ” ov(Î± âŠ” Ïáµƒ))
ğ’¦ âŠ« eq = âˆ€ ğ‘¨ â†’ ğ’¦ ğ‘¨ â†’ ğ‘¨ âŠ§ eq                    -- (type \||= to get âŠ«)

-- An I-indexed set of equations inhabits the type I â†’ Eq.

-- For such `â„° : I â†’ Eq`...

-- ...`ğ‘¨ âŠ¨ â„°` is the assertion that the algebra ğ‘¨ models every equation in â„°.
_âŠ¨_ : (ğ‘¨ : Algebra Î± Ïáµƒ) â†’ (I â†’ Eq{Ï‡}) â†’ Type _
ğ‘¨ âŠ¨ â„° = âˆ€ i â†’ Equal (lhs (â„° i))(rhs (â„° i)) where open Environment ğ‘¨  --   (type \|= to get âŠ¨)

-- ...`ğ’¦ âˆ¥â‰ˆ â„°` is the assertion that every algebra in ğ’¦ models every equation in â„°.
_âˆ¥â‰ˆ_ : Pred (Algebra Î± Ïáµƒ) â„“ â†’ (I â†’ Eq{Ï‡}) â†’ Type _
ğ’¦ âˆ¥â‰ˆ â„° = âˆ€ i â†’ ğ’¦ âŠ« â„° i

-- ...`Mod â„°` is the class of algebras that model every equation in â„°.
ModTuple : (I â†’ Eq{Ï‡}) â†’ Pred(Algebra Î± Ïáµƒ) _
ModTuple â„° = _âŠ¨ â„°

module _ {Î± Ïáµƒ â„“ : Level} where

 Mod : Pred(Term X Ã— Term X) â„“ â†’ Pred (Algebra Î± Ïáµƒ) _ -- (ğ“ âŠ” ğ“¥ âŠ” lsuc Ï‡ âŠ” â„“ âŠ” Î± âŠ” Ïáµƒ)
 Mod â„° ğ‘¨ = âˆ€ {p q} â†’ (p , q) âˆˆ â„° â†’ Equal p q where open Environment ğ‘¨

 Th : Pred (Algebra Î± Ïáµƒ) â„“ â†’ Pred(Term X Ã— Term X) _ -- (â„“ âŠ” Ï‡ âŠ” ov (Î± âŠ” Ïáµƒ))
 Th ğ’¦ = Î» (p , q) â†’ ğ’¦ âŠ« (p â‰ˆÌ‡ q)

 â„‘Th : Pred(Term X Ã— Term X) (â„“ âŠ” Ï‡ âŠ” ov (Î± âŠ” Ïáµƒ)) â†’ Type _ -- (â„“ âŠ” ov (Î± âŠ” Ïáµƒ âŠ” Ï‡))
 â„‘Th P = Î£[ p âˆˆ (Term _ Ã— Term _) ] p âˆˆ P

 module _ {Ï‡ : Level}{X : Type Ï‡} where

  ThTuple : (ğ’¦ : Pred (Algebra Î± Ïáµƒ) â„“) â†’ â„‘Th{Ï‡ = Ï‡} (Th{X = X} ğ’¦) â†’ Eq{Ï‡}
  ThTuple ğ’¦ = Î» i â†’ fst âˆ£ i âˆ£ â‰ˆÌ‡ snd âˆ£ i âˆ£

module _ {Î±}{Ïáµƒ}{Î¹}{I : Type Î¹} where
 -- An entailment E âŠƒ eq holds iff it holds in all models of E.
 _âŠƒ_ : (E : I â†’ Eq{Ï‡}) (eq : Eq{Ï‡}) â†’ Type _
 E âŠƒ eq = (M : Algebra Î± Ïáµƒ) â†’ M âŠ¨ E â†’ M âŠ§ eq

\end{code}


##### <a id="derivations-in-a-context">Derivations in a context</a>

(Based on [Andreas Abel's Agda formalization of Birkhoff's completeness theorem](http://www.cse.chalmers.se/~abela/agda/MultiSortedAlgebra.pdf).)

\begin{code}

module _ {Ï‡ Î¹ : Level} where

 data _âŠ¢_â–¹_â‰ˆ_ {I : Type Î¹}(E : I â†’ Eq) : (X : Type Ï‡)(p q : Term X) â†’ Type (Î¹ âŠ” ov Ï‡) where
  hyp   : âˆ€ i â†’ let p â‰ˆÌ‡ q = E i in E âŠ¢ _ â–¹ p â‰ˆ q
  app   : âˆ€ {ps qs} â†’ (âˆ€ i â†’ E âŠ¢ Î“ â–¹ ps i â‰ˆ qs i) â†’ E âŠ¢ Î“ â–¹ (node f ps) â‰ˆ (node f qs)
  sub   : âˆ€ {p q} â†’ E âŠ¢ Î” â–¹ p â‰ˆ q â†’ âˆ€ (Ïƒ : Sub Î“ Î”) â†’ E âŠ¢ Î“ â–¹ (p [ Ïƒ ]) â‰ˆ (q [ Ïƒ ])

  refl  : âˆ€ {p}              â†’ E âŠ¢ Î“ â–¹ p â‰ˆ p
  sym   : âˆ€ {p q : Term Î“}   â†’ E âŠ¢ Î“ â–¹ p â‰ˆ q â†’ E âŠ¢ Î“ â–¹ q â‰ˆ p
  trans : âˆ€ {p q r : Term Î“} â†’ E âŠ¢ Î“ â–¹ p â‰ˆ q â†’ E âŠ¢ Î“ â–¹ q â‰ˆ r â†’ E âŠ¢ Î“ â–¹ p â‰ˆ r

 âŠ¢â–¹â‰ˆIsEquiv : {I : Type Î¹}{E : I â†’ Eq} â†’ IsEquivalence (E âŠ¢ Î“ â–¹_â‰ˆ_)
 âŠ¢â–¹â‰ˆIsEquiv = record { refl = refl ; sym = sym ; trans = trans }

\end{code}



##### <a id="soundness-of-the-inference-rules">Soundness of the inference rules</a>

(Based on [Andreas Abel's Agda formalization of Birkhoff's completeness theorem](see: http://www.cse.chalmers.se/~abela/agda/MultiSortedAlgebra.pdf).)

\begin{code}

module Soundness {Ï‡ Î± Î¹ : Level}{I : Type Î¹} (E : I â†’ Eq{Ï‡})
                 (ğ‘¨ : Algebra Î± Ïáµƒ)     -- We assume an algebra M
                 (V : ğ‘¨ âŠ¨ E)         -- that models all equations in E.
                 where
 open Algebra ğ‘¨ using ( Interp ) renaming (Domain to A)

 -- In any model M that satisfies the equations E, derived equality is actual equality.
 open SetoidReasoning A

 open Environment ğ‘¨ renaming ( âŸ¦_âŸ§s to âŸª_âŸ« )
 open IsEquivalence renaming ( refl to reflâ‰ˆ ; sym to  symâ‰ˆ ; trans to transâ‰ˆ )

 sound : âˆ€ {p q} â†’ E âŠ¢ X â–¹ p â‰ˆ q â†’ ğ‘¨ âŠ§ (p â‰ˆÌ‡ q)
 sound (hyp i)                      = V i
 sound (app {f = f} es) Ï           = Interp .cong (refl , Î» i â†’ sound (es i) Ï)
 sound (sub {p = p} {q} Epq Ïƒ) Ï    =
  begin
   âŸ¦ p [ Ïƒ ] âŸ§ âŸ¨$âŸ©       Ï â‰ˆâŸ¨ substitution p Ïƒ Ï âŸ©
   âŸ¦ p       âŸ§ âŸ¨$âŸ© âŸª Ïƒ âŸ« Ï â‰ˆâŸ¨ sound Epq (âŸª Ïƒ âŸ« Ï)  âŸ©
   âŸ¦ q       âŸ§ âŸ¨$âŸ© âŸª Ïƒ âŸ« Ï â‰ˆË˜âŸ¨ substitution  q Ïƒ Ï âŸ©
   âŸ¦ q [ Ïƒ ] âŸ§ âŸ¨$âŸ©       Ï âˆ

 sound (refl {p = p})               = reflâ‰ˆ  isEquiv {x = p}
 sound (sym {p = p} {q} Epq)        = symâ‰ˆ   isEquiv {x = p}{q}   (sound Epq)
 sound (trans{p = p}{q}{r} Epq Eqr) = transâ‰ˆ isEquiv {i = p}{q}{r}(sound Epq)(sound Eqr)

\end{code}

The deductive closure of a set E is the set of equations modeled by all models of E; that is, Th Mod E.

The soundness proof above shows âˆ€ X â†’ E âŠ¢ X â–¹ p â‰ˆ q â†’ (p , q) âˆˆ Th Mod â„°.
That is,  âˆ€ X â†’ E âŠ¢ X â–¹ p â‰ˆ q â†’ Mod E âŠ« p â‰ˆ q.

The converse is Birkhoff's completeness theorem: if Mod E âŠ« p â‰ˆ q, then E âŠ¢ X â–¹ p â‰ˆ q.

We will prove that result next.



##### <a id="birkhoffs-completeness-theorem">Birkhoff's completeness theorem</a>

The proof proceeds by constructing a relatively free algebra consisting of term
quotiented by derivable equality E âŠ¢ X â–¹ _â‰ˆ_.  It then suffices to prove
that this model satisfies all equations in $E$.

(Based on [Andreas Abel's Agda formalization of Birkhoff's completeness theorem](http://www.cse.chalmers.se/~abela/agda/MultiSortedAlgebra.pdf).)

We denote by `ğ”½[ X ]` the *relatively free algebra* over `X` (relative to `E`), which is defined as `Term X` modulo `E âŠ¢ X â–¹ _â‰ˆ_`.  This algebra `ğ”½[ X ]` is "free" or "initial" in the variety of algebras satisfying the identities in `E` in the sense that it satisfies the following universal property: for each algebra `ğ‘¨`, if `ğ‘¨ âŠ§ E`, then there is a unique homomorphism from `ğ”½[ X ]` to `ğ‘¨`.

\begin{code}

module FreeAlgebra {Ï‡ : Level}{Î¹ : Level}{I : Type Î¹}(E : I â†’ Eq) where
 open Algebra

 -- Domain of the relatively free algebra.
 FreeDomain : Type Ï‡ â†’ Setoid _ _
 FreeDomain X = record { Carrier       = Term X
                       ; _â‰ˆ_           = E âŠ¢ X â–¹_â‰ˆ_
                       ; isEquivalence = âŠ¢â–¹â‰ˆIsEquiv
                       }

 -- The interpretation of an operation is simply the operation itself.
 -- This works since E âŠ¢ X â–¹_â‰ˆ_ is a congruence.
 FreeInterp : âˆ€ {X} â†’ (âŸ¨ ğ‘† âŸ© (FreeDomain X)) âŸ¶ (FreeDomain X)
 FreeInterp âŸ¨$âŸ© (f , ts) = node f ts
 cong FreeInterp (refl , h) = app h


 -- The relatively free algebra
 ğ”½[_] : Type Ï‡ â†’ Algebra (ov Ï‡) (Î¹ âŠ” ov Ï‡)
 Domain ğ”½[ X ] = FreeDomain X
 Interp ğ”½[ X ] = FreeInterp


 -- The identity substitution Ïƒâ‚€ maps variables to themselves.
 Ïƒâ‚€ : {X : Type Ï‡} â†’ Sub X X
 Ïƒâ‚€ x = â„Š x

 -- Ïƒâ‚€ acts indeed as identity.
 identity : (t : Term X) â†’ E âŠ¢ X â–¹ t [ Ïƒâ‚€ ] â‰ˆ t
 identity (â„Š x) = refl
 identity (node f ts) = app (identity âˆ˜ ts)

\end{code}

Evaluation in the term model is substitution `E âŠ¢ X â–¹ âŸ¦tâŸ§Ïƒ â‰ˆ t[Ïƒ]`. (This would
hold "on the nose" if we had function extensionality.)

(We put this and the next two lemmas into their own submodules to emphasize
the fact that these results are independent of the chosen variable symbol
type `X` (or `Î”`, or `Î“`), which is an arbitrary inhabitant of `Type Ï‡`.)
\begin{code}

 module _ {X : Type Ï‡} where
  open Environment ğ”½[ X ]
  evaluation : (t : Term Î”) (Ïƒ : Sub X Î”) â†’ E âŠ¢ X â–¹ (âŸ¦ t âŸ§ âŸ¨$âŸ© Ïƒ) â‰ˆ (t [ Ïƒ ])
  evaluation (â„Š x) Ïƒ = refl
  evaluation (node f ts) Ïƒ = app (flip (evaluation âˆ˜ ts) Ïƒ)


 module _ {Î” : Type Ï‡} where
  -- The term model satisfies all the equations it started out with.
  satisfies : ğ”½[ Î” ] âŠ¨ E
  satisfies i Ïƒ =
   begin
    âŸ¦ p âŸ§ âŸ¨$âŸ© Ïƒ  â‰ˆâŸ¨ evaluation p Ïƒ âŸ©
    p [ Ïƒ ]      â‰ˆâŸ¨ sub (hyp i) Ïƒ âŸ©
    q [ Ïƒ ]      â‰ˆË˜âŸ¨ evaluation q Ïƒ âŸ©
    âŸ¦ q âŸ§ âŸ¨$âŸ© Ïƒ  âˆ
    where
    open Environment ğ”½[ Î” ]
    open SetoidReasoning (Domain ğ”½[ Î” ]) ; p = lhs (E i) ; q = rhs (E i)

\end{code}

We are finally ready to formally state and prove Birkhoff's Completeness Theorem, which asserts that every valid consequence is derivable.

\begin{code}

 module _ {Î“ : Type Ï‡} where

  completeness : âˆ€ p q â†’ ModTuple E âŠ« (p â‰ˆÌ‡ q) â†’ E âŠ¢ Î“ â–¹ p â‰ˆ q
  completeness p q V =
   begin
    p              â‰ˆË˜âŸ¨ identity p âŸ©
    p [ Ïƒâ‚€ ]       â‰ˆË˜âŸ¨ evaluation p Ïƒâ‚€ âŸ©
    âŸ¦ p âŸ§ âŸ¨$âŸ© Ïƒâ‚€   â‰ˆâŸ¨ V ğ”½[ Î“ ] satisfies Ïƒâ‚€ âŸ©
    âŸ¦ q âŸ§ âŸ¨$âŸ© Ïƒâ‚€   â‰ˆâŸ¨ evaluation q Ïƒâ‚€ âŸ©
    q [ Ïƒâ‚€ ]       â‰ˆâŸ¨ identity q âŸ©
    q              âˆ
   where
   open Environment ğ”½[ Î“ ]
   open SetoidReasoning (Domain ğ”½[ Î“ ])

\end{code}

--------------------------------

<span style="float:left;">[â†‘ Setoid.Varieties](Setoid.Varieties.html)</span>
<span style="float:right;">[Setoid.Varieties.Closure â†’](Setoid.Varieties.Closure.html)</span>

{% include UALib.Links.md %}
