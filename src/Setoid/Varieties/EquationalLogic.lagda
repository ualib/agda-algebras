---
layout: default
title : "Setoid.Varieties.EquationalLogic module (The Agda Universal Algebra Library)"
date : "2021-09-24"
author: "agda-algebras development team"
---

### <a id="varieties-model-theory-and-equational-logic">Equational Logic and varieties of setoid algebras</a>

This is the [Setoid.Varieties.EquationalLogic][] module of the [Agda Universal Algebra Library][] where the binary "models" relation âŠ§, relating algebras (or classes of algebras) to the identities that they satisfy, is defined.

Let ğ‘† be a signature. By an *identity* or *equation* in ğ‘† we mean an ordered pair of terms, written ğ‘ â‰ˆ ğ‘, from the term algebra ğ‘» X. If ğ‘¨ is an ğ‘†-algebra we say that ğ‘¨ *satisfies* ğ‘ â‰ˆ ğ‘ provided ğ‘ Ì‡ ğ‘¨ â‰¡ ğ‘ Ì‡ ğ‘¨ holds. In this situation, we write ğ‘¨ âŠ§ ğ‘ â‰ˆ ğ‘ and say that ğ‘¨ *models* the identity ğ‘ â‰ˆ q. If ğ’¦ is a class of algebras, all of the same signature, we write ğ’¦ âŠ§ p â‰ˆ q if, for every ğ‘¨ âˆˆ ğ’¦, ğ‘¨ âŠ§ ğ‘ â‰ˆ ğ‘.

Because a class of structures has a different type than a single structure, we must use a slightly different syntax to avoid overloading the relations âŠ§ and â‰ˆ. As a reasonable alternative to what we would normally express informally as ğ’¦ âŠ§ ğ‘ â‰ˆ ğ‘, we have settled on ğ’¦ âŠ« p â‰ˆ q to denote this relation.  To reiterate, if ğ’¦ is a class of ğ‘†-algebras, we write ğ’¦ âŠ« ğ‘ â‰ˆ ğ‘ if every ğ‘¨ âˆˆ ğ’¦ satisfies ğ‘¨ âŠ§ ğ‘ â‰ˆ ğ‘.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Base.Algebras.Basic using ( ğ“ ; ğ“¥ ; Signature )

module Setoid.Varieties.EquationalLogic {ğ‘† : Signature ğ“ ğ“¥} where

-- Imports from Agda and the Agda Standard Library ----------------
open import Agda.Primitive    using     ( _âŠ”_ ;  lsuc ; Level )
                              renaming  ( Set to Type )
open import Data.Product      using     ( _Ã—_ ; _,_ ; Î£-syntax)
                              renaming  ( projâ‚ to fst ; projâ‚‚ to snd )
open import Function.Bundles  using     ()
                              renaming  ( Func to _âŸ¶_ )
open import Relation.Binary   using     ( Setoid )
open import Relation.Unary    using     ( Pred ; _âˆˆ_ )

-- Imports from the Agda Universal Algebra Library ----------------
open import Setoid.Algebras.Basic  {ğ‘† = ğ‘†} using ( Algebra ; ov )
open import Base.Terms.Basic       {ğ‘† = ğ‘†} using ( Term )
open import Setoid.Terms.Basic     {ğ‘† = ğ‘†} using ( ğ‘» ; module Environment )
private variable
 Ï‡ Î± Ïáµƒ â„“ Î¹ : Level

\end{code}


#### <a id="the-models-relation">The models relation</a>

We define the binary "models" relation `âŠ§` using infix syntax so that we may
write, e.g., `ğ‘¨ âŠ§ p â‰ˆ q` or `ğ’¦ âŠ« p â‰ˆ q`, relating algebras (or classes of
algebras) to the identities that they satisfy. We also prove a couple of useful
facts about âŠ§.  More will be proved about âŠ§ in the next module,
Varieties.EquationalLogic.

\begin{code}

open _âŸ¶_ using () renaming ( f to _âŸ¨$âŸ©_ )

module _  {X : Type Ï‡} where

 open Setoid using () renaming (Carrier to âˆ£_âˆ£ )
 open Algebra using ( Domain )

 _âŠ§_â‰ˆ_ : Algebra Î± Ïáµƒ â†’ Term X â†’ Term X â†’ Type _
 ğ‘¨ âŠ§ p â‰ˆ q = âˆ€ (Ï : âˆ£ Env X âˆ£) â†’ âŸ¦ p âŸ§ âŸ¨$âŸ© Ï â‰ˆ âŸ¦ q âŸ§ âŸ¨$âŸ© Ï
  where
  open Setoid ( Domain ğ‘¨ ) using ( _â‰ˆ_ )
  open Environment ğ‘¨ using ( Env ; âŸ¦_âŸ§ )

 _âŠ«_â‰ˆ_ : Pred(Algebra Î± Ïáµƒ) â„“ â†’ Term X â†’ Term X â†’ Type (Ï‡ âŠ” â„“ âŠ” ov(Î± âŠ” Ïáµƒ))
 ğ’¦ âŠ« p â‰ˆ q = {ğ‘¨ : Algebra _ _} â†’ ğ’¦ ğ‘¨ â†’ ğ‘¨ âŠ§ p â‰ˆ q

\end{code}

(**Unicode tip**. Type \models to get `âŠ§` ; type \||= to get `âŠ«`.)

The expression `ğ‘¨ âŠ§ p â‰ˆ q` represents the assertion that the identity `p â‰ˆ q`
holds when interpreted in the algebra `ğ‘¨` for any environment Ï; syntactically, we write
this interpretation as `âŸ¦ p âŸ§ Ï â‰ˆ âŸ¦ q âŸ§ Ï`. (Recall, and environment is simply an
assignment of values in the domain to variable symbols).


#### <a id="equational-theories-and-models">Equational theories and models over setoids</a>

If ğ’¦ denotes a class of structures, then `Th ğ’¦` represents the set of identities
modeled by the members of ğ’¦.

\begin{code}

 Th : Pred (Algebra Î± Ïáµƒ) â„“ â†’ Pred(Term X Ã— Term X) (Ï‡ âŠ” â„“ âŠ” ov(Î± âŠ” Ïáµƒ))
 Th ğ’¦ = Î» (p , q) â†’ ğ’¦ âŠ« p â‰ˆ q

Th' : {Ï‡ Î± : Level}{X : Type Ï‡} â†’ Pred (Algebra Î± Î±) (ov Î±)
 â†’    Pred(Term X Ã— Term X) (Ï‡ âŠ” ov Î±)
Th' ğ’¦ = Î» (p , q) â†’ ğ’¦ âŠ« p â‰ˆ q

\end{code}

Perhaps we want to represent Th ğ’¦ as an indexed collection.  We do so
essentially by taking `Th ğ’¦` itself to be the index set, as shown below.

\begin{code}

module _ {X : Type Ï‡}{ğ’¦ : Pred (Algebra Î± Ïáµƒ) (ov Î±)} where

 â„ : Type (ov(Î± âŠ” Ïáµƒ âŠ” Ï‡))
 â„ = Î£[ (p , q) âˆˆ (Term X Ã— Term X) ] ğ’¦ âŠ« p â‰ˆ q

 â„° : â„ â†’ Term X Ã— Term X
 â„° ((p , q) , _) = (p , q)

\end{code}

If `â„°` denotes a set of identities, then `Mod â„°` is the class of structures
satisfying the identities in `â„°`.

\begin{code}

 Mod : Pred(Term X Ã— Term X) (ov Î±) â†’ Pred(Algebra Î± Ïáµƒ) (Ïáµƒ âŠ” ov(Î± âŠ” Ï‡))
 Mod â„° = Î» ğ‘¨ â†’ âˆ€ p q â†’ (p , q) âˆˆ â„° â†’ ğ‘¨ âŠ§ p â‰ˆ q

\end{code}

It is sometimes more convenient to have a "tupled" version of the previous definition, which we denote by `Modáµ—` and define as follows.

\begin{code}

 Modáµ— : {I : Type Î¹} â†’ (I â†’ Term X Ã— Term X) â†’ {Î± : Level} â†’ Pred(Algebra Î± Ïáµƒ) (Ï‡ âŠ” Ïáµƒ âŠ” Î¹ âŠ” Î±)
 Modáµ— â„° = Î» ğ‘¨ â†’ âˆ€ i â†’ ğ‘¨ âŠ§ fst (â„° i) â‰ˆ snd (â„° i)

\end{code}


-------------------------------------

<span style="float:left;">[â†‘ Setoid.Varieties](Setoid.Varieties.html)</span>
<span style="float:right;">[Setoid.Varieties.SoundAndComplete â†’](Setoid.Varieties.SoundAndComplete.html)</span>

{% include UALib.Links.md %}
