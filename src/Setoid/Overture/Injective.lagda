---
layout: default
title : "Setoid.Overture.Injective module"
date : "2021-09-13"
author: "the agda-algebras development team"
---

### <a id="injective-functions-on-setoids">Injective functions on setoids</a>

This is the [Setoid.Overture.Injective][] module of the [agda-algebras][] library.

We say that a function `f : A â†’ B` from one setoid (A , â‰ˆâ‚€) to another (B , â‰ˆâ‚) is *injective* (or *monic*) provided the following implications hold:  âˆ€ aâ‚€ aâ‚ if f âŸ¨$âŸ© aâ‚€ â‰ˆâ‚ f âŸ¨$âŸ© aâ‚, then aâ‚€ â‰ˆâ‚€ aâ‚.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Relation.Binary using ( Setoid )

module Setoid.Overture.Injective where


-- Imports from Agda and the Agda Standard Library -------------
open import Agda.Primitive        using ( _âŠ”_ ; Level )  renaming ( Set to Type )
open import Function.Bundles      using ( Injection )    renaming ( Func to _âŸ¶_ )
open import Function.Base         using ( _âˆ˜_ ; id )
open import Relation.Binary.Core  using ( _Preserves_âŸ¶_ )
open import Relation.Binary       using ( Rel )
import Function.Definitions as FD

-- Imports from agda-algebras -----------------------------------------------
open import Setoid.Overture.Preliminaries  using ( ğ‘–ğ‘‘ )  renaming ( _âˆ˜_ to _âŸ¨âˆ˜âŸ©_ )
open import Setoid.Overture.Inverses       using ( Image_âˆ‹_ ; Inv )

private variable
 Î± Î² Î³ Ïáµƒ Ïáµ‡ Ïá¶œ â„“â‚ â„“â‚‚ â„“â‚ƒ : Level

\end{code}

A function `f : A âŸ¶ B` from one setoid `(A , â‰ˆâ‚€)` to another
`(B , â‰ˆâ‚)` is called *injective* provided `âˆ€ aâ‚€ aâ‚`, if `f âŸ¨$âŸ© aâ‚€ â‰ˆâ‚ f âŸ¨$âŸ©
aâ‚`, then `aâ‚€ â‰ˆâ‚€ aâ‚`.  The [Agda Standard Library][] defines a type representing
injective functions on bare types and we use this type (called `Injective`) to
define our own type representing the property of being an injective function on
setoids (called `IsInjective`).

\begin{code}

module _ {ğ‘¨ : Setoid Î± Ïáµƒ}{ğ‘© : Setoid Î² Ïáµ‡} where

 open Setoid ğ‘¨  using ()               renaming (Carrier to A; _â‰ˆ_ to _â‰ˆâ‚_)
 open Setoid ğ‘©  using ( trans ; sym )  renaming (Carrier to B; _â‰ˆ_ to _â‰ˆâ‚‚_)

 open Injection {From = ğ‘¨}{To = ğ‘©} using ( function ; injective )  renaming (f to _âŸ¨$âŸ©_)
 open _âŸ¶_ {a = Î±}{Ïáµƒ}{Î²}{Ïáµ‡}{From = ğ‘¨}{To = ğ‘©}                    renaming (f to _âŸ¨$âŸ©_ )
 open FD _â‰ˆâ‚_ _â‰ˆâ‚‚_

 IsInjective : (ğ‘¨ âŸ¶ ğ‘©) â†’  Type (Î± âŠ” Ïáµƒ âŠ” Ïáµ‡)
 IsInjective f = Injective (_âŸ¨$âŸ©_ f)

 open Image_âˆ‹_

 -- Inverse of an injective function preserves setoid equalities
 LeftInvPreservesâ‰ˆ : (F : Injection ğ‘¨ ğ‘©)
                     {bâ‚€ bâ‚ : B}(u : Image (function F) âˆ‹ bâ‚€)(v : Image (function F) âˆ‹ bâ‚)
  â†’                  bâ‚€ â‰ˆâ‚‚ bâ‚ â†’ (Inv (function F) u) â‰ˆâ‚ (Inv (function F) v)

 LeftInvPreservesâ‰ˆ F {bâ‚€}{bâ‚} (eq aâ‚€ xâ‚€) (eq aâ‚ xâ‚) bb = Goal
  where
  faâ‚€â‰ˆfaâ‚ : (F âŸ¨$âŸ© aâ‚€) â‰ˆâ‚‚ (F âŸ¨$âŸ© aâ‚)
  faâ‚€â‰ˆfaâ‚ = trans (sym xâ‚€) (trans bb xâ‚)
  Goal : aâ‚€ â‰ˆâ‚ aâ‚
  Goal = injective F faâ‚€â‰ˆfaâ‚

\end{code}

Proving that the composition of injective functions is again injective
is simply a matter of composing the two assumed witnesses to injectivity.
(Note that here we are viewing the maps as functions on the underlying carriers
of the setoids; an alternative for setoid functions, called `âˆ˜-injective`, is proved below.)

\begin{code}

module compose {A : Type Î±}(_â‰ˆâ‚_ : Rel A Ïáµƒ)
               {B : Type Î²}(_â‰ˆâ‚‚_ : Rel B Ïáµ‡)
               {C : Type Î³}(_â‰ˆâ‚ƒ_ : Rel C Ïá¶œ) where

 open FD {A = A} {B} _â‰ˆâ‚_ _â‰ˆâ‚‚_ using () renaming ( Injective to InjectiveAB )
 open FD {A = B} {C} _â‰ˆâ‚‚_ _â‰ˆâ‚ƒ_ using () renaming ( Injective to InjectiveBC )
 open FD {A = A} {C} _â‰ˆâ‚_ _â‰ˆâ‚ƒ_ using () renaming ( Injective to InjectiveAC )

 âˆ˜-injective-bare : {f : A â†’ B}{g : B â†’ C} â†’ InjectiveAB f â†’ InjectiveBC g â†’ InjectiveAC (g âˆ˜ f)
 âˆ˜-injective-bare finj ginj = finj âˆ˜ ginj

\end{code}

The three lines that begin `open FD` illustrate one of the technical consequences
of the precision demanded in formal proofs. In the statements of the
`âˆ˜-injective-func` lemma, we must distinguish the (distinct) notions of injectivity, one
for each domain-codomain pair of setoids, and we do this with the `open FD`
lines which give each instance of injectivity a different name.

\begin{code}

module _ {ğ‘¨ : Setoid Î± Ïáµƒ}{ğ‘© : Setoid Î² Ïáµ‡} {ğ‘ª : Setoid Î³ Ïá¶œ} where
 open Setoid ğ‘¨   using () renaming ( Carrier to A ; _â‰ˆ_ to _â‰ˆâ‚_ )
 open Setoid ğ‘©   using () renaming ( Carrier to B )
 open Setoid ğ‘ª   using () renaming ( Carrier to C ; _â‰ˆ_ to _â‰ˆâ‚ƒ_)
 open Injection  using () renaming ( function to fun )

 âˆ˜-injective : (f : ğ‘¨ âŸ¶ ğ‘©)(g : ğ‘© âŸ¶ ğ‘ª)
  â†’            IsInjective f â†’ IsInjective g â†’ IsInjective (g âŸ¨âˆ˜âŸ© f)
 âˆ˜-injective _ _ finj ginj = finj âˆ˜ ginj

 âˆ˜-injection : Injection ğ‘¨ ğ‘© â†’ Injection ğ‘© ğ‘ª â†’ Injection ğ‘¨ ğ‘ª
 âˆ˜-injection fi gi = record { f = Î» x â†’ apg (apf x)
                            ; cong = conggf
                            ; injective = âˆ˜-injective (fun fi) (fun gi) (injective fi) (injective gi)
                            }
  where
  open Injection
  apf : A â†’ B
  apf = f fi
  apg : B â†’ C
  apg = f gi
  conggf : (Î» x â†’ apg (apf x)) Preserves _â‰ˆâ‚_ âŸ¶ _â‰ˆâ‚ƒ_
  conggf {x}{y} xâ‰ˆy = cong gi (cong fi xâ‰ˆy)


id-is-injective : {ğ‘¨ : Setoid Î± Ïáµƒ} â†’ IsInjective{ğ‘¨ = ğ‘¨}{ğ‘¨} ğ‘–ğ‘‘
id-is-injective = id

\end{code}

--------------------------------------

<span style="float:left;">[â† Setoid.Overture.Inverses](Setoid.Overture.Inverses.html)</span>
<span style="float:right;">[Setoid.Overture.Surjective â†’](Setoid.Overture.Surjective.html)</span>

{% include UALib.Links.md %}

