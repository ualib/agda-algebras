---
layout: default
title : "Setoid.Algebras.Basic module (Agda Universal Algebra Library)"
date : "2021-04-23"
author: "agda-algebras development team"
---

#### <a id="basic-definitions">Basic definitions for algebras over setoids</a>

This is the [Setoid.Algebras.Basic][] module of the [Agda Universal Algebra Library][].

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Base.Algebras.Basic using (ğ“ ; ğ“¥ ; Signature )

module Setoid.Algebras.Basic {ğ‘† : Signature ğ“ ğ“¥} where

-- Imports from the Agda and the Agda Standard Library --------------------
open import Agda.Primitive   using ( _âŠ”_ ; lsuc ) renaming ( Set to Type )
open import Data.Product     using ( _,_ ; _Ã—_ ; Î£-syntax )
open import Function         using ( _âˆ˜_ )
open import Function.Bundles using ( Func )
open import Level            using ( Level )
open import Relation.Binary  using ( Setoid ; IsEquivalence )
open import Relation.Binary.PropositionalEquality as â‰¡
                             using ( _â‰¡_ ; refl )

-- Imports from the Agda Universal Algebra Library ----------------------
open import Base.Overture.Preliminaries using ( âˆ¥_âˆ¥ ; âˆ£_âˆ£ )

private variable
 Î± Ï Î¹ : Level

ov : Level â†’ Level
ov Î± = ğ“ âŠ” ğ“¥ âŠ” lsuc Î±
\end{code}


#### <a id="setoid-algebras">Setoid Algebras</a>

Here we define algebras over a setoid, instead of a mere type with no equivalence on it.

(This approach is inspired by the one taken, e.g., by Andreas Abel in his formalization Birkhoff's completeness theorem; a [pdf is available here](http://www.cse.chalmers.se/~abela/agda/MultiSortedAlgebra.pdf).)

First we define an operator that translates an ordinary signature into a signature over a setoid domain.

\begin{code}

open Setoid using    (_â‰ˆ_ ; Carrier )
            renaming ( refl  to reflS
                     ; sym   to symS
                     ; trans to transS
                     ; isEquivalence to isEqv )
open Func renaming   ( f to _âŸ¨$âŸ©_ ; cong to â‰ˆcong )


EqArgs : {ğ‘† : Signature ğ“ ğ“¥}{Î¾ : Setoid Î± Ï}
 â†’       âˆ€ {f g} â†’ f â‰¡ g â†’ (âˆ¥ ğ‘† âˆ¥ f â†’ Carrier Î¾) â†’ (âˆ¥ ğ‘† âˆ¥ g â†’ Carrier Î¾) â†’ Type (ğ“¥ âŠ” Ï)
EqArgs {Î¾ = Î¾} refl u v = âˆ€ i â†’ (_â‰ˆ_ Î¾) (u i) (v i)



âŸ¨_âŸ© : Signature ğ“ ğ“¥ â†’ Setoid Î± Ï â†’ Setoid _ _

Carrier (âŸ¨ ğ‘† âŸ© Î¾) = Î£[ f âˆˆ âˆ£ ğ‘† âˆ£ ] ((âˆ¥ ğ‘† âˆ¥ f) â†’ Î¾ .Carrier)
_â‰ˆ_ (âŸ¨ ğ‘† âŸ© Î¾) (f , u) (g , v) = Î£[ eqv âˆˆ f â‰¡ g ] EqArgs{Î¾ = Î¾} eqv u v

IsEquivalence.refl  (isEqv (âŸ¨ ğ‘† âŸ© Î¾))                     = refl , Î» _ â†’ reflS  Î¾
IsEquivalence.sym   (isEqv (âŸ¨ ğ‘† âŸ© Î¾))(refl , g)           = refl , Î» i â†’ symS   Î¾ (g i)
IsEquivalence.trans (isEqv (âŸ¨ ğ‘† âŸ© Î¾))(refl , g)(refl , h) = refl , Î» i â†’ transS Î¾ (g i) (h i)

\end{code}


A setoid algebra is just like an algebra but we require that all basic operations
of the algebra respect the underlying setoid equality. The `Func` record packs a
function (f, aka apply, aka _âŸ¨$âŸ©_) with a proof (cong) that the function respects
equality.

\begin{code}

record Algebra Î± Ï : Type (ğ“ âŠ” ğ“¥ âŠ” lsuc (Î± âŠ” Ï)) where
 field
  Domain : Setoid Î± Ï
  Interp : Func (âŸ¨ ğ‘† âŸ© Domain) Domain
   --      ^^^^^^^^^^^^^^^^^^^^^^^ is a record type with two fields:
   --       1. a function  f : Carrier (âŸ¨ ğ‘† âŸ© Domain)  â†’ Carrier Domain
   --       2. a proof cong : f Preserves _â‰ˆâ‚_ âŸ¶ _â‰ˆâ‚‚_ (that f preserves the setoid equalities)
 -- Actually, we already have the following: (it's called "reflexive"; see Structures.IsEquivalence)
 â‰¡â†’â‰ˆ : âˆ€{x}{y} â†’ x â‰¡ y â†’ (_â‰ˆ_ Domain) x y
 â‰¡â†’â‰ˆ refl = Setoid.refl Domain

\end{code}

It should be clear that the two types `Algebroid` and `Algebra` are equivalent. (We tend to use the latter throughout most of the [agda-algebras][] library.)

\begin{code}

open Algebra

-- Forgetful Functor
ğ•Œ[_] : Algebra Î± Ï â†’  Type Î±
ğ•Œ[ ğ‘¨ ] = Carrier (Domain ğ‘¨)

ğ”»[_] : Algebra Î± Ï â†’  Setoid Î± Ï
ğ”»[ ğ‘¨ ] = Domain ğ‘¨

-- The universe level of a Algebra

Level-of-Alg : {Î± Ï ğ“ ğ“¥ : Level}{ğ‘† : Signature ğ“ ğ“¥} â†’ Algebra Î± Ï â†’ Level
Level-of-Alg {Î± = Î±}{Ï}{ğ“}{ğ“¥} _ = ğ“ âŠ” ğ“¥ âŠ” lsuc (Î± âŠ” Ï)

Level-of-Carrier : {Î± Ï ğ“ ğ“¥  : Level}{ğ‘† : Signature ğ“ ğ“¥} â†’ Algebra Î± Ï â†’ Level
Level-of-Carrier {Î± = Î±} _ = Î±


open Algebra

_Ì‚_ : (f : âˆ£ ğ‘† âˆ£)(ğ‘¨ : Algebra Î± Ï) â†’ (âˆ¥ ğ‘† âˆ¥ f  â†’  ğ•Œ[ ğ‘¨ ]) â†’ ğ•Œ[ ğ‘¨ ]

f Ì‚ ğ‘¨ = Î» a â†’ (Interp ğ‘¨) âŸ¨$âŸ© (f , a)

\end{code}


#### <a id="level-lifting-setoid-algebra-types">Level lifting setoid algebra types</a>

\begin{code}

module _ (ğ‘¨ : Algebra Î± Ï) where

 open Algebra ğ‘¨ using ( Interp ) renaming ( Domain to A )
 open Setoid A using (sym ; trans ) renaming ( Carrier to âˆ£Aâˆ£ ; _â‰ˆ_ to _â‰ˆâ‚_ ; refl to reflâ‚ )

 open Level


 Lift-AlgË¡ : (â„“ : Level) â†’ Algebra (Î± âŠ” â„“) Ï

 Domain (Lift-AlgË¡ â„“) = record { Carrier = Lift â„“ âˆ£Aâˆ£
                                  ; _â‰ˆ_ = Î» x y â†’ lower x â‰ˆâ‚ lower y
                                  ; isEquivalence = record { refl = reflâ‚
                                                           ; sym = sym
                                                           ; trans = trans
                                                           }
                                  }

 Interp (Lift-AlgË¡ â„“) âŸ¨$âŸ© (f , la) = lift ((f Ì‚ ğ‘¨) (lower âˆ˜ la))
 â‰ˆcong (Interp (Lift-AlgË¡ â„“)) (refl , la=lb) = â‰ˆcong (Interp ğ‘¨) ((refl , la=lb))


 Lift-AlgÊ³ : (â„“ : Level) â†’ Algebra Î± (Ï âŠ” â„“)

 Domain (Lift-AlgÊ³ â„“) =
  record { Carrier = âˆ£Aâˆ£
         ; _â‰ˆ_ = Î» x y â†’ Lift â„“ (x â‰ˆâ‚ y)
         ; isEquivalence = record { refl = lift reflâ‚
                                  ; sym = Î» x â†’ lift (sym (lower x))
                                  ; trans = Î» x y â†’ lift (trans (lower x) (lower y))  }
                                  }

 Interp (Lift-AlgÊ³ â„“ ) âŸ¨$âŸ© (f , la) = (f Ì‚ ğ‘¨) la
 â‰ˆcong (Interp (Lift-AlgÊ³ â„“)) (refl , laâ‰¡lb) = lift (â‰ˆcong (Interp ğ‘¨) (â‰¡.refl , Î» i â†’ lower (laâ‰¡lb i)))

Lift-Alg : (ğ‘¨ : Algebra Î± Ï)(â„“â‚€ â„“â‚ : Level) â†’ Algebra (Î± âŠ” â„“â‚€) (Ï âŠ” â„“â‚)
Lift-Alg ğ‘¨ â„“â‚€ â„“â‚ = Lift-AlgÊ³ (Lift-AlgË¡ ğ‘¨ â„“â‚€) â„“â‚

\end{code}


--------------------------------

<span style="float:left;">[â†‘ Setoid.Algebras](Setoid.Algebras.html)</span>
<span style="float:right;">[Setoid.Algebras.Products â†’](Setoid.Algebras.Products.html)</span>

{% include UALib.Links.md %}
