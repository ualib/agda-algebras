---
layout: default
title : "Setoid.Terms.Basic module (The Agda Universal Algebra Library)"
date : "2021-09-18"
author: "agda-algebras development team"
---

#### <a id="basic-definitions">Basic definitions</a>

This is the [Setoid.Terms.Basic][] module of the [Agda Universal Algebra Library][].

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Base.Algebras.Basic using ( ğ“ ; ğ“¥ ; Signature )

module Setoid.Terms.Basic {ğ‘† : Signature ğ“ ğ“¥} where

-- imports from Agda and the Agda Standard Library -------------------------------------
open import Agda.Primitive         using ( _âŠ”_ ; lsuc ) renaming ( Set to Type )
open import Data.Empty.Polymorphic using ( âŠ¥ )
open import Data.Product           using ( _,_ )
open import Data.Sum.Base          using ( _âŠ_ ) renaming ( injâ‚ to inl ; injâ‚‚ to inr )
open import Function.Bundles       using ( ) renaming ( Func to _âŸ¶_ )
open import Level                  using ( Level ; Lift )
open import Relation.Binary        using ( Setoid ; IsEquivalence )
open import Relation.Binary.Definitions using ( Reflexive ; Symmetric ; Transitive )
open import Relation.Binary.PropositionalEquality as â‰¡ using ( _â‰¡_ )

-- Imports from the Agda Universal Algebra Library ------------------------------------
open import Base.Overture.Preliminaries      using ( âˆ¥_âˆ¥ )
open import Setoid.Algebras.Basic {ğ‘† = ğ‘†} using ( Algebra ; ov ; _Ì‚_)
open import Base.Terms.Basic         {ğ‘† = ğ‘†} using ( Term ; â„Š ; node )
open _âŸ¶_ renaming ( f to _âŸ¨$âŸ©_ )

private variable
 Ï‡ Î± â„“ : Level
 X Y : Type Ï‡

\end{code}


##### <a id="equality-of-terms">Equality of terms</a>

We take a different approach here, using Setoids instead of quotient types.
That is, we will define the collection of terms in a signature as a setoid
with a particular equality-of-terms relation, which we must define.
Ultimately we will use this to define the (absolutely free) term algebra
as a Algebra whose carrier is the setoid of terms.

\begin{code}

module _ {X : Type Ï‡ } where

 -- Equality of terms as an inductive datatype
 data _â‰_ : Term X â†’ Term X â†’ Type (ov Ï‡) where
  rfl : {x y : X} â†’ x â‰¡ y â†’ (â„Š x) â‰ (â„Š y)
  gnl : âˆ€ {f}{s t : âˆ¥ ğ‘† âˆ¥ f â†’ Term X} â†’ (âˆ€ i â†’ (s i) â‰ (t i)) â†’ (node f s) â‰ (node f t)

 -- Equality of terms is an equivalence relation
 open Level
 â‰-isRefl : Reflexive _â‰_
 â‰-isRefl {â„Š _} = rfl â‰¡.refl
 â‰-isRefl {node _ _} = gnl (Î» _ â†’ â‰-isRefl)

 â‰-isSym : Symmetric _â‰_
 â‰-isSym (rfl x) = rfl (â‰¡.sym x)
 â‰-isSym (gnl x) = gnl (Î» i â†’ â‰-isSym (x i))

 â‰-isTrans : Transitive _â‰_
 â‰-isTrans (rfl x) (rfl y) = rfl (â‰¡.trans x y)
 â‰-isTrans (gnl x) (gnl y) = gnl (Î» i â†’ â‰-isTrans (x i) (y i))

 â‰-isEquiv : IsEquivalence _â‰_
 â‰-isEquiv = record { refl = â‰-isRefl ; sym = â‰-isSym ; trans = â‰-isTrans }

TermSetoid : (X : Type Ï‡) â†’ Setoid (ov Ï‡) (ov Ï‡)
TermSetoid X = record { Carrier = Term X ; _â‰ˆ_ = _â‰_ ; isEquivalence = â‰-isEquiv }

module _ where

 open Algebra

 -- The Term Algebra
 ğ‘» : (X : Type Ï‡) â†’ Algebra (ov Ï‡) (ov Ï‡)
 Domain (ğ‘» X) = TermSetoid X
 Interp (ğ‘» X) âŸ¨$âŸ© (f , ts) = node f ts
 cong (Interp (ğ‘» X)) (â‰¡.refl , ssâ‰ts) = gnl ssâ‰ts

\end{code}


##### <a id="interpretation-of-terms-in-setoid-algebras">Interpretation of terms in setoid algebras</a>

The approach to terms and their interpretation in this module was inspired by
[Andreas Abel's formal proof of Birkhoff's completeness theorem](http://www.cse.chalmers.se/~abela/agda/MultiSortedAlgebra.pdf).

A substitution from `Î”` to `Î“` associates a term in `Î“` with each variable in `Î”`.

\begin{code}

-- Parallel substitutions.
Sub : Type Ï‡ â†’ Type Ï‡ â†’ Type (ov Ï‡)
Sub X Y = (y : Y) â†’ Term X

-- Application of a substitution.
_[_] : (t : Term Y) (Ïƒ : Sub X Y) â†’ Term X
(â„Š x) [ Ïƒ ] = Ïƒ x
(node f ts) [ Ïƒ ] = node f (Î» i â†’ ts i [ Ïƒ ])

\end{code}

An environment for `Î“` maps each variable `x : Î“` to an element of `A`, and equality of environments is defined pointwise.

\begin{code}

module Environment (ğ‘¨ : Algebra Î± â„“) where
 open Algebra ğ‘¨ using ( Interp ) renaming ( Domain to A )
 open Setoid A using ( refl ; sym ; trans ) renaming ( _â‰ˆ_ to _â‰ˆâ‚_ ; Carrier to âˆ£Aâˆ£ )

 Env : Type Ï‡ â†’ Setoid _ _
 Env X = record { Carrier = X â†’ âˆ£Aâˆ£
                ; _â‰ˆ_ = Î» Ï Ï' â†’ (x : X) â†’ Ï x â‰ˆâ‚ Ï' x
                ; isEquivalence =
                   record { refl = Î» _ â†’ refl
                          ; sym = Î» h x â†’ sym (h x)
                          ; trans = Î» g h x â†’ trans (g x) (h x)
                          }
                }

 open Algebra using ( Domain ) renaming ( Interp to interpretation )

 EnvAlgebra : Type Ï‡ â†’ Algebra (Î± âŠ” Ï‡) (â„“ âŠ” Ï‡)
 Domain (EnvAlgebra X) = Env X
 (interpretation (EnvAlgebra X) âŸ¨$âŸ© (f , aÏ•)) x = (f Ì‚ ğ‘¨) (Î» i â†’ aÏ• i x)
 cong (interpretation (EnvAlgebra X)) {f , a} {.f , b} (â‰¡.refl , aibi) x = cong Interp (â‰¡.refl , (Î» i â†’ aibi i x))

\end{code}

Interpretation of terms is iteration on the W-type. The standard library offers `iter' (on sets), but we need this to be a setoid function.

\begin{code}

 âŸ¦_âŸ§ : {X : Type Ï‡}(t : Term X) â†’ (Env X) âŸ¶ A
 âŸ¦ â„Š x âŸ§ âŸ¨$âŸ© Ï = Ï x
 âŸ¦ node f args âŸ§ âŸ¨$âŸ© Ï = Interp âŸ¨$âŸ© (f , Î» i â†’ âŸ¦ args i âŸ§ âŸ¨$âŸ© Ï)
 cong âŸ¦ â„Š x âŸ§ uâ‰ˆv = uâ‰ˆv x
 cong âŸ¦ node f args âŸ§ xâ‰ˆy = cong Interp (â‰¡.refl , Î» i â†’ cong âŸ¦ args i âŸ§ xâ‰ˆy )

 open Setoid using () renaming ( Carrier to âˆ£_âˆ£ )

 -- An equality between two terms holds in a model
 -- if the two terms are equal under all valuations of their free variables.
 Equal : âˆ€ {X : Type Ï‡} (s t : Term X) â†’ Type _
 Equal {X = X} s t = âˆ€ (Ï : âˆ£ Env X âˆ£) â†’  âŸ¦ s âŸ§ âŸ¨$âŸ© Ï â‰ˆâ‚ âŸ¦ t âŸ§ âŸ¨$âŸ© Ï

 â‰â†’Equal : {X : Type Ï‡}(s t : Term X) â†’ s â‰ t â†’ Equal s t
 â‰â†’Equal .(â„Š _) .(â„Š _) (rfl â‰¡.refl) = Î» _ â†’ refl
 â‰â†’Equal (node _ s)(node _ t)(gnl x) =
  Î» Ï â†’ cong Interp (â‰¡.refl , Î» i â†’ â‰â†’Equal(s i)(t i)(x i)Ï )

 -- Equal is an equivalence relation.
 isEquiv : {Î“ : Type Ï‡} â†’ IsEquivalence (Equal {X = Î“})
 IsEquivalence.refl  isEquiv = Î» _ â†’ refl
 IsEquivalence.sym   isEquiv = Î» x=y Ï â†’ sym (x=y Ï)
 IsEquivalence.trans isEquiv = Î» ij jk Ï â†’ trans (ij Ï) (jk Ï)

 -- Evaluation of a substitution gives an environment.
 âŸ¦_âŸ§s : {X Y : Type Ï‡} â†’ Sub X Y â†’ âˆ£ Env X âˆ£ â†’ âˆ£ Env Y âˆ£
 âŸ¦ Ïƒ âŸ§s Ï x = âŸ¦ Ïƒ x âŸ§ âŸ¨$âŸ© Ï

 -- Substitution lemma: âŸ¦t[Ïƒ]âŸ§Ï â‰ƒ âŸ¦tâŸ§âŸ¦ÏƒâŸ§Ï
 substitution : {X Y : Type Ï‡} â†’ (t : Term Y) (Ïƒ : Sub X Y) (Ï : âˆ£ Env X âˆ£ )
  â†’             âŸ¦ t [ Ïƒ ] âŸ§ âŸ¨$âŸ© Ï  â‰ˆâ‚  âŸ¦ t âŸ§ âŸ¨$âŸ© (âŸ¦ Ïƒ âŸ§s Ï)

 substitution (â„Š x) Ïƒ Ï = refl
 substitution (node f ts) Ïƒ Ï = cong Interp (â‰¡.refl , Î» i â†’ substitution (ts i) Ïƒ Ï)

\end{code}

--------------------------------

<span style="float:left;">[â†‘ Setoid.Terms](Setoid.Terms.html)</span>
<span style="float:right;">[Setoid.Terms.Properties â†’](Setoid.Terms.Properties.html)</span>

{% include UALib.Links.md %}
