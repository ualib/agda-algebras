---
layout: default
title : "Setoid.Functions.Surjective module"
date : "2021-09-13"
author: "the agda-algebras development team"
---

### <a id="surjective-functions-on-setoids">Surjective functions on setoids</a>

This is the [Setoid.Functions.Surjective][] module of the [agda-algebras][] library.

A *surjective function* from a setoid `ğ‘¨ = (A, â‰ˆâ‚€)` to a setoid `ğ‘© = (B, â‰ˆâ‚)` is a function `f : ğ‘¨ âŸ¶ ğ‘©` such that for all `b : B` there exists `a : A` such that `(f âŸ¨$âŸ© a) â‰ˆâ‚ b`.  In other words, the range and codomain of `f` agree.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

module Setoid.Functions.Surjective where

-- Imports from Agda and the Agda Standard Library --------------------------
open import Agda.Primitive   using () renaming ( Set to Type )
open import Data.Product     using ( _,_ ; Î£-syntax ) renaming (projâ‚ to fst ; projâ‚‚ to snd)
open import Function         using ( Surjection ; IsSurjection ; _$_ ; _âˆ˜_ )
                             renaming ( Func to _âŸ¶_ )
open import Level            using ( _âŠ”_ ; Level )
open import Relation.Binary  using ( Setoid ; IsEquivalence )

open import Function.Construct.Composition renaming ( isSurjection to isOnto )
 using ()

import Function.Definitions as FD

-- Imports from agda-algebras -----------------------------------------------
open import Overture                   using ( âˆ£_âˆ£ ; âˆ¥_âˆ¥ ; âˆƒ-syntax ; transport )
open import Setoid.Functions.Basic     using ( _âŠ™_ )
open import Setoid.Functions.Inverses  using ( Img_âˆ‹_ ; Image_âˆ‹_ ; Inv ; InvIsInverseÊ³ )


private variable
 Î± Ïáµƒ Î² Ïáµ‡ Î³ Ïá¶œ : Level

open Image_âˆ‹_

module _ {ğ‘¨ : Setoid Î± Ïáµƒ}{ğ‘© : Setoid Î² Ïáµ‡} where

 open Setoid ğ‘¨  renaming (Carrier to A; _â‰ˆ_ to _â‰ˆâ‚_; isEquivalence to isEqA ) using ()
 open Setoid ğ‘©  renaming (Carrier to B; _â‰ˆ_ to _â‰ˆâ‚‚_; isEquivalence to isEqB )
                using ( trans ; sym )

 open Surjection {a = Î±}{Ïáµƒ}{Î²}{Ïáµ‡}{From = ğ‘¨}{To = ğ‘©}  renaming (to to _âŸ¨$âŸ©_)
 open _âŸ¶_ {a = Î±}{Ïáµƒ}{Î²}{Ïáµ‡}{From = ğ‘¨}{To = ğ‘©}         renaming (to to _âŸ¨$âŸ©_ )
 open FD

 isSurj : (A â†’ B) â†’ Type (Î± âŠ” Î² âŠ” Ïáµ‡)
 isSurj f = âˆ€ {y} â†’ Img_âˆ‹_ {ğ‘¨ = ğ‘¨}{ğ‘© = ğ‘©} f y

 IsSurjective : (ğ‘¨ âŸ¶ ğ‘©) â†’ Type (Î± âŠ” Î² âŠ” Ïáµ‡)
 IsSurjective F = âˆ€ {y} â†’ Image F âˆ‹ y

 isSurjâ†’IsSurjective : (F : ğ‘¨ âŸ¶ ğ‘©) â†’ isSurj (_âŸ¨$âŸ©_ F) â†’ IsSurjective F
 isSurjâ†’IsSurjective F isSurjF {y} = hyp isSurjF
  where
  hyp : Img (_âŸ¨$âŸ©_ F) âˆ‹ y â†’ Image F âˆ‹ y
  hyp (Img_âˆ‹_.eq a x) = eq a x

 open Image_âˆ‹_

 SurjectionIsSurjective : (Surjection ğ‘¨ ğ‘©) â†’ Î£[ g âˆˆ (ğ‘¨ âŸ¶ ğ‘©) ] (IsSurjective g)
 SurjectionIsSurjective s = g , gE
  where
  g : ğ‘¨ âŸ¶ ğ‘©
  g = (record { to = _âŸ¨$âŸ©_ s ; cong = cong s })
  gE : IsSurjective g
  gE {y} = eq âˆ£ (surjective s) y âˆ£ (sym (snd (surjective s y) (IsEquivalence.refl isEqA)))

 SurjectionIsSurjection : (Surjection ğ‘¨ ğ‘©) â†’ Î£[ g âˆˆ (ğ‘¨ âŸ¶ ğ‘©) ] (IsSurjection _â‰ˆâ‚_ _â‰ˆâ‚‚_ (_âŸ¨$âŸ©_ g))
 SurjectionIsSurjection s = g , gE
  where
  g : ğ‘¨ âŸ¶ ğ‘©
  g = record { to = _âŸ¨$âŸ©_ s ; cong = cong s }

  gE : IsSurjection _â‰ˆâ‚_ _â‰ˆâ‚‚_ (_âŸ¨$âŸ©_ g)
  gE .IsSurjection.isCongruent = record  { cong = cong g
                                         ; isEquivalenceâ‚ = isEqA
                                         ; isEquivalenceâ‚‚ = isEqB
                                         }
  gE .IsSurjection.surjective y = âˆ£ (surjective s) y âˆ£ , âˆ¥ (surjective s) y âˆ¥

\end{code}

With the next definition we represent a *right-inverse* of a surjective setoid function.

\begin{code}

 SurjInv : (f : ğ‘¨ âŸ¶ ğ‘©) â†’ IsSurjective f â†’ B â†’ A
 SurjInv f fE b = Inv f (fE {b})

\end{code}

Thus, a right-inverse of `f` is obtained by applying `Inv` to `f` and a proof of `IsSurjective f`.  Next we prove that this does indeed give the right-inverse.

\begin{code}

 SurjInvIsInverseÊ³ :  (f : ğ‘¨ âŸ¶ ğ‘©)(fE : IsSurjective f)
  â†’                   âˆ€ {b} â†’ f âŸ¨$âŸ© (SurjInv f fE) b â‰ˆâ‚‚ b

 SurjInvIsInverseÊ³ f fE = InvIsInverseÊ³ fE

\end{code}

Next, we prove composition laws for epics.

\begin{code}

module _ {ğ‘¨ : Setoid Î± Ïáµƒ}{ğ‘© : Setoid Î² Ïáµ‡}{ğ‘ª : Setoid Î³ Ïá¶œ} where

 open Setoid ğ‘¨  using ()               renaming (Carrier to A; _â‰ˆ_ to _â‰ˆâ‚_)
 open Setoid ğ‘©  using ( trans ; sym )  renaming (Carrier to B; _â‰ˆ_ to _â‰ˆâ‚‚_)
 open Setoid ğ‘ª  using ()               renaming (Carrier to C; _â‰ˆ_ to _â‰ˆâ‚ƒ_)
 open Surjection  renaming (to to _âŸ¨$âŸ©_)
 open _âŸ¶_         renaming (to to _âŸ¨$âŸ©_ )
 open FD


 âŠ™-IsSurjective :  {G : ğ‘¨ âŸ¶ ğ‘ª}{H : ğ‘ª âŸ¶ ğ‘©}
  â†’                IsSurjective G â†’ IsSurjective H â†’ IsSurjective (H âŠ™ G)

 âŠ™-IsSurjective {G} {H} gE hE {y} = Goal
  where
  mp : Image H âˆ‹ y â†’ Image H âŠ™ G âˆ‹ y
  mp (eq c p) = Î· gE
   where
   Î· : Image G âˆ‹ c â†’ Image H âŠ™ G âˆ‹ y
   Î· (eq a q) = eq a $ trans p $ cong H q

  Goal : Image H âŠ™ G âˆ‹ y
  Goal = mp hE


 âˆ˜-epic : Surjection ğ‘¨ ğ‘ª â†’ Surjection ğ‘ª ğ‘© â†’ Surjection ğ‘¨ ğ‘©
 Surjection.to           (âˆ˜-epic g h) = h âŸ¨$âŸ©_ âˆ˜ g âŸ¨$âŸ©_
 Surjection.cong        (âˆ˜-epic g h) = cong h âˆ˜ cong g
 Surjection.surjective  (âˆ˜-epic g h) = surjective $ isOnto  âˆ¥ SurjectionIsSurjection g âˆ¥
                                                            âˆ¥ SurjectionIsSurjection h âˆ¥
  where open IsSurjection


 epic-factor :  (f : ğ‘¨ âŸ¶ ğ‘©)(g : ğ‘¨ âŸ¶ ğ‘ª)(h : ğ‘ª âŸ¶ ğ‘©)
  â†’             IsSurjective f â†’ (âˆ€ i â†’ (f âŸ¨$âŸ© i) â‰ˆâ‚‚ ((h âŠ™ g) âŸ¨$âŸ© i)) â†’ IsSurjective h

 epic-factor f g h fE compId {y} = Goal
  where
   finv : B â†’ A
   finv = SurjInv f fE

   Î¶ : y â‰ˆâ‚‚ (f âŸ¨$âŸ© (finv y))
   Î¶ = sym $ SurjInvIsInverseÊ³ f fE

   Î· : y â‰ˆâ‚‚ ((h âŠ™ g) âŸ¨$âŸ© (finv y))
   Î· = trans Î¶ $ compId $ finv y

   Goal : Image h âˆ‹ y
   Goal = eq (g âŸ¨$âŸ© (finv y)) Î·
\end{code}


--------------------------------------

<span style="float:left;">[â† Setoid.Functions.Injective](Setoid.Functions.Injective.html)</span>
<span style="float:right;">[Setoid.Functions.Bijective â†’](Setoid.Functions.Bijective.html)</span>

{% include UALib.Links.md %}

