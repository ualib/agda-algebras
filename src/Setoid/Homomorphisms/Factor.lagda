---
layout: default
title : "Setoid.Homomorphisms.Factor module (The Agda Universal Algebra Library)"
date : "2021-09-13"
author: "agda-algebras development team"
---

#### <a id="factoring-homomorphisms-of-setoidalgebra">Factoring Homomorphism of Algebras</a>

This is the [Setoid.Homomorphisms.Factor][] module of the [Agda Universal Algebra Library][].

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Base.Algebras.Basic using (ğ“ ; ğ“¥ ; Signature )

module Setoid.Homomorphisms.Factor {ğ‘† : Signature ğ“ ğ“¥} where

-- Imports from Agda and the Agda Standard Library -------------------------------------------------
open import Data.Product     using ( _,_ ; Î£-syntax )  renaming ( projâ‚ to fst ; projâ‚‚ to snd )
open import Function         using ( _âˆ˜_ )             renaming ( Func to _âŸ¶_ )
open import Level            using ( Level )
open import Relation.Binary  using ( Setoid )
open import Relation.Unary   using ( _âŠ†_ )
open import Relation.Binary.PropositionalEquality as â‰¡ using ()
import Relation.Binary.Reasoning.Setoid as SetoidReasoning

-- Imports from the Agda Universal Algebra Library ------------------------------------------------
open import Base.Overture.Preliminaries          using ( âˆ£_âˆ£ ; âˆ¥_âˆ¥ )
open import Setoid.Overture.Inverses             using ( Image_âˆ‹_ )
open import Setoid.Overture.Surjective           using ( IsSurjective ; SurjInv )
                                                 using ( SurjInvIsInverseÊ³ ; epic-factor )
open import Base.Relations.Discrete                   using ( kernelRel )
open import Setoid.Algebras.Basic       {ğ‘† = ğ‘†}  using ( Algebra ; ğ•Œ[_] ; _Ì‚_ )
open import Setoid.Homomorphisms.Basic  {ğ‘† = ğ‘†}  using ( hom ; IsHom ; compatible-map ; epi ; IsEpi)

private variable
 Î± Ïáµƒ Î² Ïáµ‡ Î³ Ïá¶œ : Level

\end{code}

If `g : hom ğ‘¨ ğ‘©`, `h : hom ğ‘¨ ğ‘ª`, `h` is surjective, and `ker h âŠ† ker g`, then there exists `Ï† : hom ğ‘ª ğ‘©` such that `g = Ï† âˆ˜ h` so the following diagram commutes:

```
ğ‘¨ --- h -->> ğ‘ª
 \         .
  \       .
   g     Ï†
    \   .
     \ .
      V
      ğ‘©
```

We will prove this in case h is both surjective and injective.

\begin{code}

module _ {ğ‘¨ : Algebra Î± Ïáµƒ}
         (ğ‘© : Algebra Î² Ïáµ‡)
         {ğ‘ª : Algebra Î³ Ïá¶œ}
         (gh : hom ğ‘¨ ğ‘©)(hh : hom ğ‘¨ ğ‘ª) where

 open Algebra ğ‘© using () renaming (Domain to B )
 open Algebra ğ‘ª using ( Interp ) renaming (Domain to C )
 open Setoid B using () renaming ( _â‰ˆ_ to _â‰ˆâ‚‚_ ; sym to symâ‚‚ )
 open Setoid C using ( trans ) renaming ( _â‰ˆ_ to _â‰ˆâ‚ƒ_ ; sym to symâ‚ƒ )
 open SetoidReasoning B
 open _âŸ¶_ using ( cong ) renaming (f to _âŸ¨$âŸ©_ )

 private
  gfunc = âˆ£ gh âˆ£
  hfunc = âˆ£ hh âˆ£
  g = _âŸ¨$âŸ©_ gfunc
  h = _âŸ¨$âŸ©_ hfunc

 open IsHom
 open Image_âˆ‹_

 HomFactor : kernelRel _â‰ˆâ‚ƒ_ h âŠ† kernelRel _â‰ˆâ‚‚_ g â†’ IsSurjective hfunc
              ---------------------------------------------------------
  â†’           Î£[ Ï† âˆˆ hom ğ‘ª ğ‘© ] âˆ€ a â†’ (g a) â‰ˆâ‚‚ âˆ£ Ï† âˆ£ âŸ¨$âŸ© (h a)

 HomFactor Khg hE = (Ï†map , Ï†hom) , gÏ†h
  where
  kerpres : âˆ€ aâ‚€ aâ‚ â†’ h aâ‚€ â‰ˆâ‚ƒ h aâ‚ â†’ g aâ‚€ â‰ˆâ‚‚ g aâ‚
  kerpres aâ‚€ aâ‚ hyp = Khg hyp

  hâ»Â¹ : ğ•Œ[ ğ‘ª ] â†’ ğ•Œ[ ğ‘¨ ]
  hâ»Â¹ = SurjInv hfunc hE

  Î· : âˆ€ {c} â†’ h (hâ»Â¹ c) â‰ˆâ‚ƒ c
  Î· = SurjInvIsInverseÊ³ hfunc hE

  Î¾ : âˆ€ {a} â†’ h a â‰ˆâ‚ƒ h (hâ»Â¹ (h a))
  Î¾ = symâ‚ƒ Î·

  Î¶ : âˆ€{x y} â†’ x â‰ˆâ‚ƒ y â†’ h (hâ»Â¹ x) â‰ˆâ‚ƒ h (hâ»Â¹ y)
  Î¶ xy = trans Î· (trans xy (symâ‚ƒ Î·))


  Ï†map : C âŸ¶ B
  _âŸ¨$âŸ©_ Ï†map = g âˆ˜ hâ»Â¹
  cong Ï†map = Khg âˆ˜ Î¶

  gÏ†h : (a : ğ•Œ[ ğ‘¨ ]) â†’ g a â‰ˆâ‚‚ Ï†map âŸ¨$âŸ© (h a)
  gÏ†h a = Khg Î¾


  open _âŸ¶_ Ï†map using () renaming (cong to Ï†cong)
  Ï†comp : compatible-map ğ‘ª ğ‘© Ï†map
  Ï†comp {f}{c} =
   begin
    Ï†map âŸ¨$âŸ© ((f Ì‚ ğ‘ª) c)              â‰ˆË˜âŸ¨ Ï†cong (cong Interp (â‰¡.refl , (Î» _ â†’ Î·))) âŸ©
    g (hâ»Â¹ ((f Ì‚ ğ‘ª)(h âˆ˜ (hâ»Â¹ âˆ˜ c)))) â‰ˆË˜âŸ¨ Ï†cong (compatible âˆ¥ hh âˆ¥) âŸ©
    g (hâ»Â¹ (h ((f Ì‚ ğ‘¨)(hâ»Â¹ âˆ˜ c))))   â‰ˆË˜âŸ¨ gÏ†h ((f Ì‚ ğ‘¨)(hâ»Â¹ âˆ˜ c)) âŸ©
    g ((f Ì‚ ğ‘¨)(hâ»Â¹ âˆ˜ c))             â‰ˆâŸ¨ compatible âˆ¥ gh âˆ¥ âŸ©
    (f Ì‚ ğ‘©)(g âˆ˜ (hâ»Â¹ âˆ˜ c))           âˆ

  Ï†hom : IsHom ğ‘ª ğ‘© Ï†map
  compatible Ï†hom = Ï†comp

\end{code}


If, in addition, `g` is surjective, then so will be the factor `Ï†`.

\begin{code}


 HomFactorEpi : kernelRel _â‰ˆâ‚ƒ_ h âŠ† kernelRel _â‰ˆâ‚‚_ g
  â†’             IsSurjective hfunc â†’ IsSurjective gfunc
                -------------------------------------------------
  â†’             Î£[ Ï† âˆˆ epi ğ‘ª ğ‘© ] âˆ€ a â†’ (g a) â‰ˆâ‚‚ âˆ£ Ï† âˆ£ âŸ¨$âŸ© (h a)

 HomFactorEpi Khg hE gE = (Ï†map , Ï†epi) , gÏ†h
  where
  homfactor : Î£[ Ï† âˆˆ hom ğ‘ª ğ‘© ] âˆ€ a â†’ (g a) â‰ˆâ‚‚ âˆ£ Ï† âˆ£ âŸ¨$âŸ© (h a)
  homfactor = HomFactor Khg hE

  Ï†map : C âŸ¶ B
  Ï†map = fst âˆ£ homfactor âˆ£

  gÏ†h : (a : ğ•Œ[ ğ‘¨ ]) â†’ g a â‰ˆâ‚‚ Ï†map âŸ¨$âŸ© (h a)
  gÏ†h = snd homfactor -- Khg Î¾

  Ï†hom : IsHom ğ‘ª ğ‘© Ï†map
  Ï†hom = snd âˆ£ homfactor âˆ£

  Ï†epi : IsEpi ğ‘ª ğ‘© Ï†map
  Ï†epi = record { isHom = Ï†hom
                ; isSurjective = epic-factor gfunc hfunc Ï†map gE gÏ†h }

\end{code}

--------------------------------

<span style="float:left;">[â† Setoid.Homomorphisms.Noether](Setoid.Homomorphisms.Noether.html)</span>
<span style="float:right;">[Setoid.Homomorphisms.Isomorphisms â†’](Setoid.Homomorphisms.Isomorphisms.html)</span>

{% include UALib.Links.md %}

