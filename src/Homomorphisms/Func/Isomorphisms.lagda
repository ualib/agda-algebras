---
layout: default
title : "Homomorphisms.Func.Isomoprhisms module (The Agda Universal Algebra Library)"
date : "2021-09-15"
author: "agda-algebras development team"
---

#### <a id="isomorphisms-of-setoid-algebras">Isomorphisms of setoid algebras</a>

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Algebras.Basic using ( ğ“ ; ğ“¥ ; Signature )

module Homomorphisms.Func.Isomorphisms {ğ‘† : Signature ğ“ ğ“¥}  where

-- Imports from Agda (builtin/primitive) and the Agda Standard Library ---------------------
open import Agda.Primitive              using ( _âŠ”_ ; lsuc ; Level ) renaming ( Set to Type )
open import Data.Product                using ( _,_ )
open import Function                    using ( Func )
open import Level                       using ( Level ; Lift ; lift ; lower )
open import Relation.Binary             using ( Setoid )
open import Relation.Binary.Definitions using ( Reflexive ; Sym ; Trans )

-- Imports from the Agda Universal Algebra Library -----------------------------------------
open import Overture.Preliminaries                using ( âˆ£_âˆ£ ; âˆ¥_âˆ¥ )
open import Overture.Func.Preliminaries           using ( _âŸ¶_ ; _âˆ˜_ )
open import Overture.Func.Injective               using ( IsInjective )
open import Algebras.Func.Basic           {ğ‘† = ğ‘†} using ( SetoidAlgebra ; Lift-Alg ; Lift-AlgË¡ ; Lift-AlgÊ³ )
open import Algebras.Func.Products        {ğ‘† = ğ‘†} using ( â¨… )
open import Homomorphisms.Func.Basic      {ğ‘† = ğ‘†} using ( hom ; IsHom )
open import Homomorphisms.Func.Properties {ğ‘† = ğ‘†} using ( ğ’¾ğ’¹ ; âˆ˜-hom ; ToLiftË¡ ; FromLiftË¡
                                                        ; ToFromLiftË¡ ; FromToLiftË¡ ; ToLiftÊ³
                                                        ; FromLiftÊ³ ; ToFromLiftÊ³ ; FromToLiftÊ³ )

\end{code}

Recall, `f ~ g` means f and g are *extensionally* (or pointwise) equal; i.e., `âˆ€ x, f x â‰¡ g x`. We use this notion of equality of functions in the following definition of *isomorphism*.

We could define this using Sigma types, like this.

```agda
_â‰…_ : {Î± Î² : Level}(ğ‘¨ : Algebra Î± ğ‘†)(ğ‘© : SetoidAlgebra Î² Ïáµ‡) â†’ Type(ğ“ âŠ” ğ“¥ âŠ” Î± âŠ” Î²)
ğ‘¨ â‰… ğ‘© =  Î£[ f âˆˆ (hom ğ‘¨ ğ‘©)] Î£[ g âˆˆ hom ğ‘© ğ‘¨ ] ((âˆ£ f âˆ£ âˆ˜ âˆ£ g âˆ£ â‰ˆ âˆ£ ğ’¾ğ’¹ ğ‘© âˆ£) Ã— (âˆ£ g âˆ£ âˆ˜ âˆ£ f âˆ£ â‰ˆ âˆ£ ğ’¾ğ’¹ ğ‘¨ âˆ£))
```

However, with four components, an equivalent record type is easier to work with.

\begin{code}

private variable
 Î± Ïáµƒ Î² Ïáµ‡ Î³ Ïá¶œ Î¹ : Level


open Func using ( cong ) renaming ( f to _âŸ¨$âŸ©_ )
open SetoidAlgebra using ( Domain )

module _ (ğ‘¨ : SetoidAlgebra Î± Ïáµƒ) (ğ‘© : SetoidAlgebra Î² Ïáµ‡) where
 open Setoid (Domain ğ‘¨) using () renaming ( _â‰ˆ_ to _â‰ˆâ‚_ )
 open Setoid (Domain ğ‘©) using () renaming ( _â‰ˆ_ to _â‰ˆâ‚‚_ )

 record _â‰…_ : Type (ğ“ âŠ” ğ“¥ âŠ” Î± âŠ” Î² âŠ” Ïáµƒ âŠ” Ïáµ‡ ) where
  constructor mkiso
  field
   to : hom ğ‘¨ ğ‘©
   from : hom ğ‘© ğ‘¨
   toâˆ¼from : âˆ€ b â†’ (âˆ£ to âˆ£ âŸ¨$âŸ© (âˆ£ from âˆ£ âŸ¨$âŸ© b)) â‰ˆâ‚‚ b
   fromâˆ¼to : âˆ€ a â†’ (âˆ£ from âˆ£ âŸ¨$âŸ© (âˆ£ to âˆ£ âŸ¨$âŸ© a)) â‰ˆâ‚ a

\end{code}

That is, two structures are *isomorphic* provided there are homomorphisms going back and forth between them which compose to the identity map.


#### <a id="properties-of-isomorphisms-of-setoid-algebras">Properties of isomorphism of setoid algebras</a>

\begin{code}

open _â‰…_

â‰…-refl : Reflexive (_â‰…_ {Î±}{Ïáµƒ})
â‰…-refl {Î±}{Ïáµƒ}{ğ‘¨} = mkiso ğ’¾ğ’¹ ğ’¾ğ’¹ (Î» b â†’ refl) Î» a â†’ refl
 where open Setoid (Domain ğ‘¨) using ( refl )

â‰…-sym : Sym (_â‰…_{Î²}{Ïáµ‡}) (_â‰…_{Î±}{Ïáµƒ})
â‰…-sym Ï† = mkiso (from Ï†) (to Ï†) (fromâˆ¼to Ï†) (toâˆ¼from Ï†)

â‰…-trans : Trans (_â‰…_ {Î±}{Ïáµƒ})(_â‰…_{Î²}{Ïáµ‡})(_â‰…_{Î±}{Ïáµƒ}{Î³}{Ïá¶œ})
â‰…-trans {Ïá¶œ = Ïá¶œ}{ğ‘¨}{ğ‘©}{ğ‘ª} ab bc = mkiso f g Ï„ Î½
  where
  open Setoid (Domain ğ‘¨) using () renaming ( _â‰ˆ_ to _â‰ˆâ‚_ ; trans to transâ‚ )
  open Setoid (Domain ğ‘ª) using () renaming ( _â‰ˆ_ to _â‰ˆâ‚ƒ_ ; trans to transâ‚ƒ )

  f : hom ğ‘¨ ğ‘ª
  f = âˆ˜-hom (to ab) (to bc)
  g : hom ğ‘ª ğ‘¨
  g = âˆ˜-hom (from bc) (from ab)

  Ï„ : âˆ€ b â†’ (âˆ£ f âˆ£ âŸ¨$âŸ© (âˆ£ g âˆ£ âŸ¨$âŸ© b)) â‰ˆâ‚ƒ b
  Ï„ b = transâ‚ƒ (cong âˆ£ to bc âˆ£ (toâˆ¼from ab (âˆ£ from bc âˆ£ âŸ¨$âŸ© b))) (toâˆ¼from bc b)

  Î½ : âˆ€ a â†’ (âˆ£ g âˆ£ âŸ¨$âŸ© (âˆ£ f âˆ£ âŸ¨$âŸ© a)) â‰ˆâ‚ a
  Î½ a = transâ‚ (cong âˆ£ from ab âˆ£ (fromâˆ¼to bc (âˆ£ to ab âˆ£ âŸ¨$âŸ© a))) (fromâˆ¼to ab a)



module _ {ğ‘¨ : SetoidAlgebra Î± Ïáµƒ}{ğ‘© : SetoidAlgebra Î² Ïáµ‡} where
 open Setoid (Domain ğ‘¨) using ( _â‰ˆ_ ; sym ; trans )

 -- The "to" map of an isomorphism is injective.
 â‰…toInjective : (Ï† : ğ‘¨ â‰… ğ‘©) â†’ IsInjective âˆ£ to Ï† âˆ£
 â‰…toInjective (mkiso (f , _) (g , _) _ gâˆ¼f){aâ‚€}{aâ‚} fafb = Goal
  where
  lem1 : aâ‚€ â‰ˆ (g âŸ¨$âŸ© (f âŸ¨$âŸ© aâ‚€))
  lem1 = sym (gâˆ¼f aâ‚€)
  lem2 : (g âŸ¨$âŸ© (f âŸ¨$âŸ© aâ‚€)) â‰ˆ (g âŸ¨$âŸ© (f âŸ¨$âŸ© aâ‚))
  lem2 = cong g fafb
  lem3 : (g âŸ¨$âŸ© (f âŸ¨$âŸ© aâ‚)) â‰ˆ aâ‚
  lem3 = gâˆ¼f aâ‚
  Goal : aâ‚€ â‰ˆ aâ‚
  Goal = trans lem1 (trans lem2 lem3)


 -- The "from" map of an isomorphism is injective.

â‰…fromInjective : {ğ‘¨ : SetoidAlgebra Î± Ïáµƒ}{ğ‘© : SetoidAlgebra Î² Ïáµ‡}
                 (Ï† : ğ‘¨ â‰… ğ‘©) â†’ IsInjective âˆ£ from Ï† âˆ£

â‰…fromInjective Ï† = â‰…toInjective (â‰…-sym Ï†)

\end{code}

Fortunately, the lift operation preserves isomorphism (i.e., it's an *algebraic invariant*). As our focus is universal algebra, this is important and is what makes the lift operation a workable solution to the technical problems that arise from the noncumulativity of Agda's universe hierarchy.

\begin{code}

module _ {ğ‘¨ : SetoidAlgebra Î± Ïáµƒ}{â„“ : Level} where
 Lift-â‰…Ë¡ : ğ‘¨ â‰… (Lift-AlgË¡ ğ‘¨ â„“)
 Lift-â‰…Ë¡ = mkiso ToLiftË¡ FromLiftË¡ (ToFromLiftË¡{ğ‘¨ = ğ‘¨}) (FromToLiftË¡{ğ‘¨ = ğ‘¨}{â„“})

 Lift-â‰…Ê³ : ğ‘¨ â‰… (Lift-AlgÊ³ ğ‘¨ â„“)
 Lift-â‰…Ê³ = mkiso ToLiftÊ³ FromLiftÊ³ (ToFromLiftÊ³{ğ‘¨ = ğ‘¨}) (FromToLiftÊ³{ğ‘¨ = ğ‘¨}{â„“})

Lift-â‰… : {ğ‘¨ : SetoidAlgebra Î± Ïáµƒ}{â„“ Ï : Level} â†’ ğ‘¨ â‰… (Lift-Alg ğ‘¨ â„“ Ï)
Lift-â‰… = â‰…-trans Lift-â‰…Ë¡ Lift-â‰…Ê³


module _ {ğ‘¨ : SetoidAlgebra Î± Ïáµƒ}{ğ‘© : SetoidAlgebra Î² Ïáµ‡}{â„“áµƒ â„“áµ‡ : Level} where

 Lift-Alg-isoË¡ : ğ‘¨ â‰… ğ‘© â†’ Lift-AlgË¡ ğ‘¨ â„“áµƒ â‰… Lift-AlgË¡ ğ‘© â„“áµ‡
 Lift-Alg-isoË¡ Aâ‰…B = â‰…-trans (â‰…-trans (â‰…-sym Lift-â‰…Ë¡ ) Aâ‰…B) Lift-â‰…Ë¡

 Lift-Alg-isoÊ³ : ğ‘¨ â‰… ğ‘© â†’  Lift-AlgÊ³ ğ‘¨ â„“áµƒ â‰… Lift-AlgÊ³ ğ‘© â„“áµ‡
 Lift-Alg-isoÊ³ Aâ‰…B = â‰…-trans (â‰…-trans (â‰…-sym Lift-â‰…Ê³ ) Aâ‰…B) Lift-â‰…Ê³


Lift-Alg-iso : {ğ‘¨ : SetoidAlgebra Î± Ïáµƒ}{ğ‘© : SetoidAlgebra Î² Ïáµ‡}{â„“áµƒ ráµƒ â„“áµ‡ ráµ‡ : Level}
 â†’             ğ‘¨ â‰… ğ‘© â†’ Lift-Alg ğ‘¨ â„“áµƒ ráµƒ â‰… Lift-Alg ğ‘© â„“áµ‡ ráµ‡
Lift-Alg-iso {â„“áµ‡ = â„“áµ‡} Aâ‰…B =
  â‰…-trans (Lift-Alg-isoÊ³{â„“áµ‡ = â„“áµ‡}(â‰…-trans (Lift-Alg-isoË¡{â„“áµ‡ = â„“áµ‡} Aâ‰…B) (â‰…-sym Lift-â‰…Ë¡)))
          (Lift-Alg-isoÊ³ Lift-â‰…Ë¡)

\end{code}

The lift is also associative, up to isomorphism at least.

\begin{code}

module _ {ğ‘¨ : SetoidAlgebra Î± Ïáµƒ}{â„“â‚ â„“â‚‚ : Level} where

 Lift-assocË¡ : Lift-AlgË¡ ğ‘¨ (â„“â‚ âŠ” â„“â‚‚) â‰…  Lift-AlgË¡ (Lift-AlgË¡ ğ‘¨ â„“â‚) â„“â‚‚
 Lift-assocË¡ = â‰…-trans (â‰…-trans (â‰…-sym Lift-â‰…Ë¡) Lift-â‰…Ë¡) Lift-â‰…Ë¡

 Lift-assocÊ³ : Lift-AlgÊ³ ğ‘¨ (â„“â‚ âŠ” â„“â‚‚) â‰…  Lift-AlgÊ³ (Lift-AlgÊ³ ğ‘¨ â„“â‚) â„“â‚‚
 Lift-assocÊ³ = â‰…-trans (â‰…-trans (â‰…-sym Lift-â‰…Ê³) Lift-â‰…Ê³) Lift-â‰…Ê³


Lift-assoc : {ğ‘¨ : SetoidAlgebra Î± Ïáµƒ}{â„“ Ï : Level}
 â†’           Lift-Alg ğ‘¨ â„“ Ï â‰…  Lift-AlgÊ³ (Lift-AlgË¡ ğ‘¨ â„“) Ï
Lift-assoc {ğ‘¨ = ğ‘¨}{â„“}{Ï} = â‰…-trans (â‰…-sym Lift-â‰…) (â‰…-trans Lift-â‰…Ë¡ Lift-â‰…Ê³)

\end{code}

Products of isomorphic families of algebras are themselves isomorphic. The proof looks a bit technical, but it is as straightforward as it ought to be.

\begin{code}

module _ {ğ“˜ : Level}{I : Type ğ“˜}
         {ğ’œ : I â†’ SetoidAlgebra Î± Ïáµƒ}
         {â„¬ : I â†’ SetoidAlgebra Î² Ïáµ‡} where


 open SetoidAlgebra (â¨… ğ’œ) using () renaming (Domain to â¨…A )
 open Setoid â¨…A using () renaming ( _â‰ˆ_ to _â‰ˆâ‚_ )

 open SetoidAlgebra (â¨… â„¬) using () renaming (Domain to â¨…B )
 open Setoid â¨…B using () renaming ( _â‰ˆ_ to _â‰ˆâ‚‚_ )

 open IsHom

 â¨…â‰… : (âˆ€ (i : I) â†’ ğ’œ i â‰… â„¬ i) â†’ â¨… ğ’œ â‰… â¨… â„¬

 â¨…â‰… AB = mkiso (Ï• , Ï•hom) (Ïˆ , Ïˆhom) Ï•âˆ¼Ïˆ Ïˆâˆ¼Ï•
  where
   Ï• : â¨…A âŸ¶ â¨…B
   Ï• = record { f = Î» a i â†’ âˆ£ to (AB i) âˆ£ âŸ¨$âŸ© (a i)
              ; cong = Î» a i â†’ cong âˆ£ to (AB i) âˆ£ (a i) }

   Ï•hom : IsHom (â¨… ğ’œ) (â¨… â„¬) Ï•
   Ï•hom = record { compatible = Î» i â†’ compatible âˆ¥ to (AB i) âˆ¥
                 ; preservesâ‰ˆ = Î» xy i â†’ preservesâ‰ˆ âˆ¥ to (AB i) âˆ¥ (xy i) }

   Ïˆ : â¨…B âŸ¶ â¨…A
   Ïˆ = record { f = Î» b i â†’ âˆ£ from (AB i) âˆ£ âŸ¨$âŸ© (b i)
              ; cong = Î» b i â†’ cong âˆ£ from (AB i) âˆ£ (b i) }

   Ïˆhom : IsHom (â¨… â„¬) (â¨… ğ’œ) Ïˆ
   Ïˆhom = record { compatible = Î» i â†’ compatible âˆ¥ from (AB i) âˆ¥
                 ; preservesâ‰ˆ = Î» xy i â†’ preservesâ‰ˆ âˆ¥ from (AB i) âˆ¥ (xy i) }

   Ï•âˆ¼Ïˆ : âˆ€ b â†’ (Ï• âŸ¨$âŸ© (Ïˆ âŸ¨$âŸ© b)) â‰ˆâ‚‚ b
   Ï•âˆ¼Ïˆ b = Î» i â†’ toâˆ¼from (AB i) (b i)

   Ïˆâˆ¼Ï• : âˆ€ a â†’ (Ïˆ âŸ¨$âŸ© (Ï• âŸ¨$âŸ© a)) â‰ˆâ‚ a
   Ïˆâˆ¼Ï• a = Î» i â†’ fromâˆ¼to (AB i)(a i)

\end{code}

A nearly identical proof goes through for isomorphisms of lifted products.

\begin{code}

module _ {ğ“˜ : Level}{I : Type ğ“˜}
         {ğ’œ : I â†’ SetoidAlgebra Î± Ïáµƒ}
         {â„¬ : (Lift Î³ I) â†’ SetoidAlgebra Î² Ïáµ‡} where

 open SetoidAlgebra (â¨… ğ’œ) using () renaming (Domain to â¨…A )
 open Setoid â¨…A using () renaming ( _â‰ˆ_ to _â‰ˆâ‚_ )

 open SetoidAlgebra (â¨… â„¬) using () renaming (Domain to â¨…B )
 open Setoid â¨…B using () renaming ( _â‰ˆ_ to _â‰ˆâ‚‚_ )
 open IsHom


 Lift-Alg-â¨…â‰…Ë¡ : (âˆ€ i â†’ ğ’œ i â‰… â„¬ (lift i)) â†’ Lift-AlgË¡ (â¨… ğ’œ) Î³ â‰… â¨… â„¬

 Lift-Alg-â¨…â‰…Ë¡ AB = Goal
  where
   Ï• : â¨…A âŸ¶ â¨…B
   Ï• = record { f = Î» a i â†’ âˆ£ to (AB (lower i)) âˆ£ âŸ¨$âŸ© (a (lower i))
              ; cong = Î» a i â†’ cong âˆ£ to (AB (lower i)) âˆ£ (a (lower i)) }


   Ï•hom : IsHom (â¨… ğ’œ) (â¨… â„¬) Ï•
   Ï•hom = record { compatible = Î» i â†’ compatible âˆ¥ to (AB (lower i)) âˆ¥
                 ; preservesâ‰ˆ = Î» xy i â†’ preservesâ‰ˆ âˆ¥ to (AB (lower i)) âˆ¥ (xy (lower i)) }

   Ïˆ : â¨…B âŸ¶ â¨…A
   Ïˆ = record { f = Î» b i â†’ âˆ£ from (AB i) âˆ£ âŸ¨$âŸ© (b (lift i))
              ; cong = Î» b i â†’ cong âˆ£ from (AB i) âˆ£ (b (lift i)) }

   Ïˆhom : IsHom (â¨… â„¬) (â¨… ğ’œ) Ïˆ
   Ïˆhom = record { compatible = Î» i â†’ compatible âˆ¥ from (AB i) âˆ¥
                 ; preservesâ‰ˆ = Î» xy i â†’ preservesâ‰ˆ âˆ¥ from (AB i) âˆ¥ (xy (lift i)) }

   Ï•âˆ¼Ïˆ : âˆ€ b â†’ (Ï• âŸ¨$âŸ© (Ïˆ âŸ¨$âŸ© b)) â‰ˆâ‚‚ b
   Ï•âˆ¼Ïˆ b = Î» i â†’ toâˆ¼from (AB (lower i)) (b i)

   Ïˆâˆ¼Ï• : âˆ€ a â†’ (Ïˆ âŸ¨$âŸ© (Ï• âŸ¨$âŸ© a)) â‰ˆâ‚ a
   Ïˆâˆ¼Ï• a = Î» i â†’ fromâˆ¼to (AB i)(a i)

   Aâ‰…B : â¨… ğ’œ â‰… â¨… â„¬
   Aâ‰…B = mkiso (Ï• , Ï•hom) (Ïˆ , Ïˆhom) Ï•âˆ¼Ïˆ Ïˆâˆ¼Ï•

   Goal : Lift-AlgË¡ (â¨… ğ’œ) Î³ â‰… â¨… â„¬
   Goal = â‰…-trans (â‰…-sym Lift-â‰…Ë¡) Aâ‰…B

\end{code}

------------------------------

<span style="float:left;">[â† Homomorphisms.Func.Factor](Homomorphisms.Func.Factor.html)</span>
<span style="float:right;">[Homomorphisms.Func.HomomorphicImages â†’](Homomorphisms.Func.HomomorphicImages.html)</span>

{% include UALib.Links.md %}
