---
layout: default
title : "Homomorphisms.Basic module (The Agda Universal Algebra Library)"
date : "2021-01-13"
author: "agda-algebras development team"
---

### <a id="basic-definitions">Basic Definitions</a>

This section describes the [Homomorphisms.Basic] module of the [Agda Universal Algebra Library][].

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Algebras.Basic

module Homomorphisms.Basic {ğ‘† : Signature ğ“ ğ“¥} where

-- Imports from Agda and the Agda Standard Library --------------------------------
open import Agda.Primitive using ( _âŠ”_ ; lsuc ) renaming ( Set to Type )
open import Axiom.Extensionality.Propositional
                           using () renaming (Extensionality to funext)
open import Data.Product   using ( _,_ ; Î£ ; Î£-syntax ; _Ã—_ ) renaming ( projâ‚ to fst )
open import Function.Base  using ( _âˆ˜_ ; id )
open import Level          using ( Level )
open import Relation.Binary.PropositionalEquality
                           using ( _â‰¡_ ; module â‰¡-Reasoning ; cong ; refl )

-- Imports from the Agda Universal Algebras Library --------------------------------
open import Overture.Preliminaries using ( _â»Â¹ ; âˆ£_âˆ£ ; âˆ¥_âˆ¥)
open import Overture.Inverses      using ( IsInjective ; IsSurjective ; Image_âˆ‹_ )
open import Equality.Welldefined   using ( swelldef )
open import Relations.Discrete     using ( ker )
open import Relations.Quotients    using ( ker-IsEquivalence ; _/_ ; âŸª_âŸ« ; R-block )
open import Algebras.Congruences {ğ‘† = ğ‘†} using ( Con ; IsCongruence ; mkcon ; _â•±_ ; /-â‰¡ )
open import Algebras.Products    {ğ‘† = ğ‘†} using ( â¨… )

private variable Î± Î² Î³ Ï : Level

\end{code}

#### <a id="homomorphisms">Homomorphisms</a>

If `ğ‘¨` and `ğ‘©` are `ğ‘†`-algebras, then a *homomorphism* from `ğ‘¨` to `ğ‘©` is a function `h : âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£` from the domain of `ğ‘¨` to the domain of `ğ‘©` that is *compatible* (or *commutes*) with all of the basic operations of the signature; that is, for all operation symbols `ğ‘“ : âˆ£ ğ‘† âˆ£` and tuples `a : âˆ¥ ğ‘† âˆ¥ ğ‘“ â†’ âˆ£ ğ‘¨ âˆ£` of `ğ‘¨`, the following holds:

`h ((ğ‘“ Ì‚ ğ‘¨) a) â‰¡ (ğ‘“ Ì‚ ğ‘©) (h âˆ˜ a)`.

**Remarks**. Recall, `h âˆ˜ ğ’‚` is the tuple whose i-th component is `h (ğ’‚ i)`. Instead of "homomorphism," we sometimes use the nickname "hom" to refer to such a map.

To formalize this concept, we first define a type representing the assertion that a function `h : âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£` commutes with a single basic operation `ğ‘“`.  With Agda's extremely flexible syntax the defining equation above can be expressed unadulterated.

\begin{code}

module _ (ğ‘¨ : Algebra Î± ğ‘†)(ğ‘© : Algebra Î² ğ‘†) where

 compatible-op-map : âˆ£ ğ‘† âˆ£ â†’ (âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£) â†’ Type(Î± âŠ” ğ“¥ âŠ” Î²)
 compatible-op-map ğ‘“ h = âˆ€ ğ‘ â†’ h ((ğ‘“ Ì‚ ğ‘¨) ğ‘) â‰¡ (ğ‘“ Ì‚ ğ‘©) (h âˆ˜ ğ‘)

\end{code}

Agda infers from the shorthand `âˆ€ ğ‘` that `ğ‘` has type `âˆ¥ ğ‘† âˆ¥ ğ‘“ â†’ âˆ£ ğ‘¨ âˆ£` since it must be a tuple on `âˆ£ ğ‘¨ âˆ£` of "length" `âˆ¥ ğ‘† âˆ¥ ğ‘“` (the arity of `ğ‘“`).

We now define the type `hom ğ‘¨ ğ‘©` of homomorphisms from `ğ‘¨` to `ğ‘©` by first defining the type `is-homomorphism` which represents the property of being a homomorphism.

\begin{code}

 is-homomorphism : (âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£) â†’ Type(ğ“ âŠ” ğ“¥ âŠ” Î± âŠ” Î²)
 is-homomorphism g = âˆ€ ğ‘“  â†’  compatible-op-map ğ‘“ g

 hom : Type(ğ“ âŠ” ğ“¥ âŠ” Î± âŠ” Î²)
 hom = Î£ (âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£) is-homomorphism

\end{code}


#### <a id="homomorphism-composition">Homomorphism composition</a>

The composition of homomorphisms is again a homomorphism.  We formalize this in a number of alternative ways.

\begin{code}

open â‰¡-Reasoning

module _ (ğ‘¨ : Algebra Î± ğ‘†){ğ‘© : Algebra Î² ğ‘†}(ğ‘ª : Algebra Î³ ğ‘†) where

  âˆ˜-hom : hom ğ‘¨ ğ‘©  â†’  hom ğ‘© ğ‘ª  â†’  hom ğ‘¨ ğ‘ª
  âˆ˜-hom (g , ghom) (h , hhom) = h âˆ˜ g , Goal where

   Goal : âˆ€ ğ‘“ a â†’ (h âˆ˜ g)((ğ‘“ Ì‚ ğ‘¨) a) â‰¡ (ğ‘“ Ì‚ ğ‘ª)(h âˆ˜ g âˆ˜ a)
   Goal ğ‘“ a = (h âˆ˜ g)((ğ‘“ Ì‚ ğ‘¨) a)     â‰¡âŸ¨ cong h ( ghom ğ‘“ a ) âŸ©
              h ((ğ‘“ Ì‚ ğ‘©)(g âˆ˜ a))     â‰¡âŸ¨ hhom ğ‘“ ( g âˆ˜ a ) âŸ©
              (ğ‘“ Ì‚ ğ‘ª)(h âˆ˜ g âˆ˜ a)     âˆ


  âˆ˜-is-hom : {f : âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£}{g : âˆ£ ğ‘© âˆ£ â†’ âˆ£ ğ‘ª âˆ£}
   â†’         is-homomorphism ğ‘¨ ğ‘© f â†’ is-homomorphism ğ‘© ğ‘ª g â†’ is-homomorphism ğ‘¨ ğ‘ª (g âˆ˜ f)
  âˆ˜-is-hom {f} {g} fhom ghom = âˆ¥ âˆ˜-hom (f , fhom) (g , ghom) âˆ¥

\end{code}



#### <a id="important-exmples-of-homomorphisms">Important examples of homomorphisms</a>

Let's look at a few important examples of homomorphisms. These examples are actually quite special in that every algebra has such a homomorphism.

We begin with the identity map, which is proved to be (the underlying map of) a homomorphism as follows.

\begin{code}

ğ’¾ğ’¹ : (ğ‘¨ : Algebra Î± ğ‘†) â†’ hom ğ‘¨ ğ‘¨
ğ’¾ğ’¹ _ = id , Î» ğ‘“ ğ‘ â†’ refl

\end{code}

Next, the lifting of an algebra to a higher universe level is, in fact, a homomorphism. Dually, the lowering of a lifted algebra to its original universe level is a homomorphism.

\begin{code}

open Level

ğ“ğ’¾ğ’»ğ“‰ : {Î² : Level}(ğ‘¨ : Algebra Î± ğ‘†) â†’ hom ğ‘¨ (Lift-Alg ğ‘¨ Î²)
ğ“ğ’¾ğ’»ğ“‰ _ = lift , Î» ğ‘“ ğ‘ â†’ refl

ğ“â„´ğ“Œâ„¯ğ“‡ : {Î² : Level}(ğ‘¨ : Algebra Î± ğ‘†) â†’ hom (Lift-Alg ğ‘¨ Î²) ğ‘¨
ğ“â„´ğ“Œâ„¯ğ“‡ _ = lower , Î» ğ‘“ ğ‘ â†’ refl

\end{code}

Finally, a homomorphism from `ğ‘¨` to `ğ‘©` can be lifted to a homomorphism from `Lift-Alg ğ‘¨ â„“áµƒ` to `Lift-Alg ğ‘© â„“áµ‡`.

\begin{code}

Lift-hom : {ğ‘¨ : Algebra Î± ğ‘†}(â„“áµƒ : Level){ğ‘© : Algebra Î² ğ‘†} (â„“áµ‡ : Level)
 â†’         hom ğ‘¨ ğ‘©  â†’  hom (Lift-Alg ğ‘¨ â„“áµƒ) (Lift-Alg ğ‘© â„“áµ‡)

Lift-hom {ğ‘¨ = ğ‘¨} â„“áµƒ {ğ‘©} â„“áµ‡ (f , fhom) = lift âˆ˜ f âˆ˜ lower , Goal
 where
 lABh : is-homomorphism (Lift-Alg ğ‘¨ â„“áµƒ) ğ‘© (f âˆ˜ lower)
 lABh = âˆ˜-is-hom (Lift-Alg ğ‘¨ â„“áµƒ) ğ‘© {lower}{f} (Î» _ _ â†’ refl) fhom

 Goal : is-homomorphism(Lift-Alg ğ‘¨ â„“áµƒ)(Lift-Alg ğ‘© â„“áµ‡) (lift âˆ˜ (f âˆ˜ lower))
 Goal = âˆ˜-is-hom (Lift-Alg ğ‘¨ â„“áµƒ) (Lift-Alg ğ‘© â„“áµ‡){f âˆ˜ lower}{lift} lABh Î» _ _ â†’ refl

\end{code}

We should probably point out that while the lifting and lowering homomorphisms are important for our formal treatment of algebras in type theory, they never arise---in fact, they are not even definable---in classical universal algebra based on set theory.


#### <a id="monomorphisms-and-epimorphisms">Monomorphisms and epimorphisms</a>

A *monomorphism* is an injective homomorphism and an *epimorphism* is a surjective homomorphism. These are represented in the [agda-algebras](https://github.com/ualib/agda-algebras) library by the following types.

\begin{code}

is-monomorphism : (ğ‘¨ : Algebra Î± ğ‘†)(ğ‘© : Algebra Î² ğ‘†) â†’ (âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£) â†’ Type(ğ“ âŠ” ğ“¥ âŠ” Î± âŠ” Î²)
is-monomorphism ğ‘¨ ğ‘© g = is-homomorphism ğ‘¨ ğ‘© g Ã— IsInjective g

mon : Algebra Î± ğ‘† â†’ Algebra Î² ğ‘†  â†’ Type(ğ“ âŠ” ğ“¥ âŠ” Î± âŠ” Î²)
mon ğ‘¨ ğ‘© = Î£[ g âˆˆ (âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£) ] is-monomorphism ğ‘¨ ğ‘© g

is-epimorphism : (ğ‘¨ : Algebra Î± ğ‘†)(ğ‘© : Algebra Î² ğ‘†) â†’ (âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£) â†’ Type(ğ“ âŠ” ğ“¥ âŠ” Î± âŠ” Î²)
is-epimorphism ğ‘¨ ğ‘© g = is-homomorphism ğ‘¨ ğ‘© g Ã— IsSurjective g

epi : Algebra Î± ğ‘† â†’ Algebra Î² ğ‘†  â†’ Type(ğ“ âŠ” ğ“¥ âŠ” Î± âŠ” Î²)
epi ğ‘¨ ğ‘© = Î£[ g âˆˆ (âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£) ] is-epimorphism ğ‘¨ ğ‘© g

\end{code}

It will be convenient to have a function that takes an inhabitant of `mon` (or `epi`) and extracts the homomorphism part, or *hom reduct* (that is, the pair consisting of the map and a proof that the map is a homomorphism).

\begin{code}

mon-to-hom : (ğ‘¨ : Algebra Î± ğ‘†){ğ‘© : Algebra Î² ğ‘†} â†’ mon ğ‘¨ ğ‘© â†’ hom ğ‘¨ ğ‘©
mon-to-hom ğ‘¨ Ï• = âˆ£ Ï• âˆ£ , fst âˆ¥ Ï• âˆ¥

epi-to-hom : {ğ‘¨ : Algebra Î± ğ‘†}(ğ‘© : Algebra Î² ğ‘†) â†’ epi ğ‘¨ ğ‘© â†’ hom ğ‘¨ ğ‘©
epi-to-hom _ Ï• = âˆ£ Ï• âˆ£ , fst âˆ¥ Ï• âˆ¥

\end{code}






#### <a id="kernels-of-homomorphisms">Kernels of homomorphisms</a>

The kernel of a homomorphism is a congruence relation and conversely for every congruence relation Î¸, there exists a homomorphism with kernel Î¸ (namely, that canonical projection onto the quotient modulo Î¸).

\begin{code}

module _ {Î± Î² : Level}{ğ‘¨ : Algebra Î± ğ‘†} where

 homker-comp : swelldef ğ“¥ Î² â†’ {ğ‘© : Algebra Î² ğ‘†}(h : hom ğ‘¨ ğ‘©) â†’ compatible ğ‘¨ (ker âˆ£ h âˆ£)
 homker-comp wd {ğ‘©} h f {u}{v} kuv = âˆ£ h âˆ£((f Ì‚ ğ‘¨) u)   â‰¡âŸ¨ âˆ¥ h âˆ¥ f u âŸ©
                                     (f Ì‚ ğ‘©)(âˆ£ h âˆ£ âˆ˜ u) â‰¡âŸ¨ wd(f Ì‚ ğ‘©)(âˆ£ h âˆ£ âˆ˜ u)(âˆ£ h âˆ£ âˆ˜ v)kuv âŸ©
                                     (f Ì‚ ğ‘©)(âˆ£ h âˆ£ âˆ˜ v) â‰¡âŸ¨ (âˆ¥ h âˆ¥ f v)â»Â¹ âŸ©
                                     âˆ£ h âˆ£((f Ì‚ ğ‘¨) v)   âˆ


\end{code}

(Notice, it is here that the `swelldef` postulate comes into play, and because it is needed to prove `homker-comp`, it is postulated by all the lemmas below that depend upon `homker-comp`.)

It is convenient to define a function that takes a homomorphism and constructs a congruence from its kernel.  We call this function `kercon`.

\begin{code}

 kercon : swelldef ğ“¥ Î² â†’ {ğ‘© : Algebra Î² ğ‘†} â†’ hom ğ‘¨ ğ‘© â†’ Con{Î±}{Î²} ğ‘¨
 kercon wd {ğ‘©} h = ker âˆ£ h âˆ£ , mkcon (ker-IsEquivalence âˆ£ h âˆ£)(homker-comp wd {ğ‘©} h)

\end{code}

With this congruence we construct the corresponding quotient, along with some syntactic sugar to denote it.

\begin{code}

 kerquo : swelldef ğ“¥ Î² â†’ {ğ‘© : Algebra Î² ğ‘†} â†’ hom ğ‘¨ ğ‘© â†’ Algebra (Î± âŠ” lsuc Î²) ğ‘†
 kerquo wd {ğ‘©} h = ğ‘¨ â•± (kercon wd {ğ‘©} h)


ker[_â‡’_]_â†¾_ : (ğ‘¨ : Algebra Î± ğ‘†)(ğ‘© : Algebra Î² ğ‘†) â†’ hom ğ‘¨ ğ‘© â†’ swelldef ğ“¥ Î² â†’ Algebra (Î± âŠ” lsuc Î²) ğ‘†
ker[ ğ‘¨ â‡’ ğ‘© ] h â†¾ wd = kerquo wd {ğ‘©} h

\end{code}

Thus, given `h : hom ğ‘¨ ğ‘©`, we can construct the quotient of `ğ‘¨` modulo the kernel of `h`, and the syntax for this quotient in the [agda-algebras](https://github.com/ualib/agda-algebras) library is `ğ‘¨ [ ğ‘© ]/ker h â†¾ fe`.



#### <a id="the-canonical-projection">The canonical projection</a>

Given an algebra `ğ‘¨` and a congruence `Î¸`, the *canonical projection* is a map from `ğ‘¨` onto `ğ‘¨ â•± Î¸` that is constructed, and proved epimorphic, as follows.

\begin{code}

module _ {Î± Î² : Level}{ğ‘¨ : Algebra Î± ğ‘†} where
 Ï€epi : (Î¸ : Con{Î±}{Î²} ğ‘¨) â†’ epi ğ‘¨ (ğ‘¨ â•± Î¸)
 Ï€epi Î¸ = (Î» a â†’ âŸª a âŸ«) , (Î» _ _ â†’ refl) , cÏ€-is-epic  where
  cÏ€-is-epic : IsSurjective (Î» a â†’ âŸª a âŸ«)
  cÏ€-is-epic (C , R-block a refl ) =  Image_âˆ‹_.eq a refl

\end{code}

In may happen that we don't care about the surjectivity of `Ï€epi`, in which case would might prefer to work with the *homomorphic reduct* of `Ï€epi`. This is obtained by applying `epi-to-hom`, like so.

\begin{code}

 Ï€hom : (Î¸ : Con{Î±}{Î²} ğ‘¨) â†’ hom ğ‘¨ (ğ‘¨ â•± Î¸)
 Ï€hom Î¸ = epi-to-hom (ğ‘¨ â•± Î¸) (Ï€epi Î¸)

\end{code}


We combine the foregoing to define a function that takes ğ‘†-algebras `ğ‘¨` and `ğ‘©`, and a homomorphism `h : hom ğ‘¨ ğ‘©` and returns the canonical epimorphism from `ğ‘¨` onto `ğ‘¨ [ ğ‘© ]/ker h`. (Recall, the latter is the special notation we defined above for the quotient of `ğ‘¨` modulo the kernel of `h`.)

\begin{code}

 Ï€ker : (wd : swelldef ğ“¥ Î²){ğ‘© : Algebra Î² ğ‘†}(h : hom ğ‘¨ ğ‘©) â†’ epi ğ‘¨ (ker[ ğ‘¨ â‡’ ğ‘© ] h â†¾ wd)
 Ï€ker wd {ğ‘©} h = Ï€epi (kercon wd {ğ‘©} h)

\end{code}

The kernel of the canonical projection of `ğ‘¨` onto `ğ‘¨ / Î¸` is equal to `Î¸`, but since equality of inhabitants of certain types (like `Congruence` or `Rel`) can be a tricky business, we settle for proving the containment `ğ‘¨ / Î¸ âŠ† Î¸`. Of the two containments, this is the easier one to prove; luckily it is also the one we need later.

\begin{code}

 open IsCongruence

 ker-in-con : {wd : swelldef ğ“¥ (Î± âŠ” lsuc Î²)}(Î¸ : Con ğ‘¨)
  â†’           âˆ€ {x}{y} â†’ âˆ£ kercon wd {ğ‘¨ â•± Î¸} (Ï€hom Î¸) âˆ£ x y â†’  âˆ£ Î¸ âˆ£ x y

 ker-in-con Î¸ hyp = /-â‰¡ Î¸ hyp

\end{code}



#### <a id="product-homomorphisms">Product homomorphisms</a>

Suppose we have an algebra `ğ‘¨`, a type `I : Type ğ“˜`, and a family `â„¬ : I â†’ Algebra Î² ğ‘†` of algebras.  We sometimes refer to the inhabitants of `I` as *indices*, and call `â„¬` an *indexed family of algebras*.

If in addition we have a family `ğ’½ : (i : I) â†’ hom ğ‘¨ (â„¬ i)` of homomorphisms, then we can construct a homomorphism from `ğ‘¨` to the product `â¨… â„¬` in the natural way.

\begin{code}

module _ {ğ“˜ Î² : Level}{I : Type ğ“˜}(â„¬ : I â†’ Algebra Î² ğ‘†) where

 â¨…-hom-co : funext ğ“˜ Î² â†’ {Î± : Level}(ğ‘¨ : Algebra Î± ğ‘†) â†’ (âˆ€(i : I) â†’ hom ğ‘¨ (â„¬ i)) â†’ hom ğ‘¨ (â¨… â„¬)
 â¨…-hom-co fe ğ‘¨ ğ’½ = (Î» a i â†’ âˆ£ ğ’½ i âˆ£ a) , (Î» ğ‘“ ğ’¶ â†’ fe Î» i â†’ âˆ¥ ğ’½ i âˆ¥ ğ‘“ ğ’¶)

\end{code}

The family `ğ’½` of homomorphisms inhabits the dependent type `Î  i ê‰ I , hom ğ‘¨ (â„¬ i)`.  The syntax we use to represent this type is available to us because of the way `-Î ` is defined in the [Type Topology][] library.  We like this syntax because it is very close to the notation one finds in the standard type theory literature.  However,
we could equally well have used one of the following alternatives, which may be closer to "standard Agda" syntax:

`Î  Î» i â†’ hom ğ‘¨ (â„¬ i)` &nbsp; or &nbsp; `(i : I) â†’ hom ğ‘¨ (â„¬ i)` &nbsp; or &nbsp; `âˆ€ i â†’ hom ğ‘¨ (â„¬ i)`.

The foregoing generalizes easily to the case in which the domain is also a product of a family of algebras. That is, if we are given `ğ’œ : I â†’ Algebra Î± ğ‘† and â„¬ : I â†’ Algebra Î² ğ‘†` (two families of `ğ‘†`-algebras), and `ğ’½ :  Î  i ê‰ I , hom (ğ’œ i)(â„¬ i)` (a family of homomorphisms), then we can construct a homomorphism from `â¨… ğ’œ` to `â¨… â„¬` in the following natural way.

\begin{code}

 â¨…-hom : funext ğ“˜ Î² â†’ {Î± : Level}(ğ’œ : I â†’ Algebra Î± ğ‘†) â†’ (âˆ€ (i : I) â†’ hom (ğ’œ i) (â„¬ i)) â†’ hom (â¨… ğ’œ)(â¨… â„¬)
 â¨…-hom fe ğ’œ ğ’½ = (Î» x i â†’ âˆ£ ğ’½ i âˆ£ (x i)) , (Î» ğ‘“ ğ’¶ â†’ fe Î» i â†’ âˆ¥ ğ’½ i âˆ¥ ğ‘“ (Î» x â†’ ğ’¶ x i))

\end{code}


#### <a id="projections-out-of-products">Projection out of products</a>

Later we will need a proof of the fact that projecting out of a product algebra onto one of its factors is a homomorphism.

\begin{code}

 â¨…-projection-hom : (i : I) â†’ hom (â¨… â„¬) (â„¬ i)
 â¨…-projection-hom = Î» x â†’ (Î» z â†’ z x) , Î» _ _ â†’ refl

\end{code}

We could prove a more general result involving projections onto multiple factors, but so far the single-factor result has sufficed.

---------------------------------

<span style="float:left;">[â†‘ Homomorphisms](Homomorphisms.html)</span>
<span style="float:right;">[Homomorphisms.Noether â†’](Homomorphisms.Noether.html)</span>

{% include UALib.Links.md %}
