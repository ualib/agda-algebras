---
layout: default
title : "Homomorphisms.Setoid.Basic module (Agda Universal Algebra Library)"
date : "2021-07-03"
author: "agda-algebras development team"
---

#### <a id="homomorphisms-of-algebras-over-setoids">Homomorphisms of Algebras over Setoids</a>

This is the [Homomorphisms.Setoid.Basic][] module of the [Agda Universal Algebra Library][].

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Algebras.Basic using (ğ“ ; ğ“¥ ; Signature )

module Homomorphisms.Setoid.Basic {ğ‘† : Signature ğ“ ğ“¥} where

-- Imports from Agda and the Agda Standard Library ------------------------------------------
open import Agda.Primitive    using ( _âŠ”_ ; lsuc ) renaming ( Set to Type )
open import Data.Product      using ( _,_ ; Î£ ; Î£-syntax ; _Ã—_ )
open import Function          using ( _âˆ˜_ ; id )
open import Level             using ( Level ; Lift )
open import Relation.Binary   using ( IsEquivalence )
open import Relation.Unary    using ( _âŠ†_ )
open import Relation.Binary.PropositionalEquality
                              using ( _â‰¡_ ; refl ; module â‰¡-Reasoning ; cong )


-- Imports from the Agda Universal Algebra Library ------------------------------------------
open import Overture.Preliminaries     using ( âˆ£_âˆ£ ; âˆ¥_âˆ¥ ; _â»Â¹ ; _â‰ˆ_)
open import Overture.Inverses          using ( IsInjective ; IsSurjective ; SurjInv
                                             ; SurjInvIsRightInv ; epic-factor )
open import Relations.Discrete         using ( ker ; kernel )
open import Relations.Quotients        using ( ker-IsEquivalence )
open import Foundations.Truncation     using ( is-set ; blk-uip ; is-embedding
                                             ; monic-is-embedding|Set )
open import Foundations.Welldefined    using ( swelldef )
open import Foundations.Extensionality using ( block-ext|uip ; pred-ext )
open import Algebras.Setoid.Basic
                               {ğ‘† = ğ‘†} using ( ğ•Œ[_] ; SetoidAlgebra ; _Ì‚_ ; Lift-Alg )
open import Algebras.Setoid.Congruences
                               {ğ‘† = ğ‘†} using ( _âˆ£â‰ˆ_ ; Con ; IsCongruence ; mkcon ; _â•±_)

private variable
 Î± Î² Î³ Ï Ïáµƒ Ïáµ‡ Ïá¶œ â„“ : Level

module _ (ğ‘¨ : SetoidAlgebra Î± Ïáµƒ) (ğ‘© : SetoidAlgebra Î² Ïáµ‡) where
 compatible-op-map : âˆ£ ğ‘† âˆ£ â†’ (ğ•Œ[ ğ‘¨ ] â†’ ğ•Œ[ ğ‘© ]) â†’ Type _
 compatible-op-map f h = âˆ€ a â†’ h ((f Ì‚ ğ‘¨) a) â‰¡ (f Ì‚ ğ‘©) (h âˆ˜ a)

 -- The property of being a homomorphism.
 is-homomorphism : (ğ•Œ[ ğ‘¨ ] â†’ ğ•Œ[ ğ‘© ]) â†’ Type _
 is-homomorphism h = âˆ€ f  â†’  compatible-op-map f h

 -- The type of homomorphisms from `ğ‘¨` to `ğ‘©`.
 hom : Type _
 hom = Î£ (ğ•Œ[ ğ‘¨ ] â†’ ğ•Œ[ ğ‘© ]) is-homomorphism

\end{code}

##### <a id="composition-of-homs">Composition of homs</a>

\begin{code}

open â‰¡-Reasoning

module _ (ğ‘¨ : SetoidAlgebra Î± Ïáµƒ)  -- (explicit ğ‘¨)
         (ğ‘© : SetoidAlgebra Î² Ïáµ‡)  -- (implicit ğ‘©)
         (ğ‘ª : SetoidAlgebra Î³ Ïá¶œ)  -- (explicit ğ‘ª)
         where

 -- The composition of homomorphisms is again a homomorphism.
 âˆ˜-is-hom : {g : ğ•Œ[ ğ‘¨ ] â†’ ğ•Œ[ ğ‘© ]}{h : ğ•Œ[ ğ‘© ] â†’ ğ•Œ[ ğ‘ª ]}
  â†’         is-homomorphism ğ‘¨ ğ‘© g â†’ is-homomorphism ğ‘© ğ‘ª h
            -------------------------------------------------
  â†’         is-homomorphism ğ‘¨ ğ‘ª (h âˆ˜ g)

 âˆ˜-is-hom {g} {h} ghom hhom ğ‘“ a = (h âˆ˜ g)((ğ‘“ Ì‚ ğ‘¨) a) â‰¡âŸ¨ cong h ( ghom ğ‘“ a ) âŸ©
                                  h ((ğ‘“ Ì‚ ğ‘©)(g âˆ˜ a)) â‰¡âŸ¨ hhom ğ‘“ ( g âˆ˜ a ) âŸ©
                                  (ğ‘“ Ì‚ ğ‘ª)(h âˆ˜ g âˆ˜ a) âˆ

 âˆ˜-hom : hom ğ‘¨ ğ‘©  â†’  hom ğ‘© ğ‘ª  â†’  hom ğ‘¨ ğ‘ª
 âˆ˜-hom (g , ghom) (h , hhom) = h âˆ˜ g , âˆ˜-is-hom {g}{h} ghom hhom 


\end{code}

##### <a id="lifting-and-lowering">Lifting and lowering of homs</a>

First we define the identity homomorphism for setoid algebras and then we prove that the operations of lifting and lowering of a setoid algebra are homomorphisms.

\begin{code}

ğ’¾ğ’¹ :  (ğ‘¨ : SetoidAlgebra Î± Ï) â†’ hom ğ‘¨ ğ‘¨
ğ’¾ğ’¹ _ = id , Î» ğ‘“ a â†’ refl

open Level
ğ“ğ’¾ğ’»ğ“‰ : {ğ‘¨ : SetoidAlgebra Î± Ï} â†’ hom ğ‘¨ (Lift-Alg ğ‘¨ â„“)
ğ“ğ’¾ğ’»ğ“‰ = lift , (Î» ğ‘“ a â†’ refl)

ğ“â„´ğ“Œâ„¯ğ“‡ : {ğ‘¨ : SetoidAlgebra Î± Ï} â†’ hom (Lift-Alg ğ‘¨ â„“) ğ‘¨
ğ“â„´ğ“Œâ„¯ğ“‡ = (lower , Î» ğ‘“ a â†’ refl)

\end{code}

Next we formalize the fact that a homomorphism from `ğ‘¨` to `ğ‘©` can be lifted to a homomorphism from `Lift-Alg ğ‘¨ â„“áµƒ` to `Lift-Alg ğ‘© â„“áµ‡`.

\begin{code}

module _ {ğ‘¨ : SetoidAlgebra Î± Ïáµƒ} {ğ‘© : SetoidAlgebra Î² Ïáµ‡} where
 open Level

 Lift-hom : hom ğ‘¨ ğ‘©  â†’ (â„“áµƒ â„“áµ‡ : Level) â†’  hom (Lift-Alg ğ‘¨ â„“áµƒ) (Lift-Alg ğ‘© â„“áµ‡)
 Lift-hom (f , fhom) â„“áµƒ â„“áµ‡ = lift âˆ˜ f âˆ˜ lower , Goal
  where
  lA lB : SetoidAlgebra _ _
  lA = Lift-Alg ğ‘¨ â„“áµƒ
  lB = Lift-Alg ğ‘© â„“áµ‡

  lABh : is-homomorphism lA ğ‘© (f âˆ˜ lower)
  lABh = âˆ˜-is-hom lA ğ‘¨  ğ‘© {lower}{f} (Î» _ _ â†’ refl) fhom

  Goal : is-homomorphism lA lB (lift âˆ˜ (f âˆ˜ lower))
  Goal = âˆ˜-is-hom lA ğ‘© lB {f âˆ˜ lower}{lift} lABh Î» _ _ â†’ refl

\end{code}

#### <a id="monomorphisms-and-epimorphisms">Monomorphisms and epimorphisms</a>

\begin{code}

module _ (ğ‘¨ : SetoidAlgebra Î± Ïáµƒ) (ğ‘© : SetoidAlgebra Î² Ïáµ‡) where
 private
  A = ğ•Œ[ ğ‘¨ ]  -- carrier of domain of ğ‘¨
  B = ğ•Œ[ ğ‘© ]  -- carrier of domain of ğ‘©

 is-monomorphism : (A â†’ B) â†’ Type _
 is-monomorphism g = is-homomorphism ğ‘¨ ğ‘© g Ã— IsInjective g

 is-epimorphism : (A â†’ B) â†’ Type _
 is-epimorphism g = is-homomorphism ğ‘¨ ğ‘© g Ã— IsSurjective g

record mon (ğ‘¨ : SetoidAlgebra Î± Ïáµƒ)
           (ğ‘© : SetoidAlgebra Î² Ïáµ‡) : Type (ğ“ âŠ” ğ“¥ âŠ” Î± âŠ” Î²) where
 field
  map : ğ•Œ[ ğ‘¨ ] â†’ ğ•Œ[ ğ‘© ]
  is-mon : is-monomorphism ğ‘¨ ğ‘© map

 mon-to-hom : hom ğ‘¨ ğ‘©
 mon-to-hom = map , âˆ£ is-mon âˆ£

record epi (ğ‘¨ : SetoidAlgebra Î± Ïáµƒ)
           (ğ‘© : SetoidAlgebra Î² Ïáµ‡) : Type (ğ“ âŠ” ğ“¥ âŠ” Î± âŠ” Î²) where
 field
  map : ğ•Œ[ ğ‘¨ ] â†’ ğ•Œ[ ğ‘© ]
  is-epi : is-epimorphism ğ‘¨ ğ‘© map

 epi-to-hom : hom ğ‘¨ ğ‘©
 epi-to-hom = map , âˆ£ is-epi âˆ£

\end{code}

#### <a id="kernels-of-homomorphisms">Kernels of homomorphisms</a>

\begin{code}

module _ (ğ‘¨ : SetoidAlgebra Î± Ïáµƒ) (ğ‘© : SetoidAlgebra Î² Ïáµ‡) where
 private
  A = ğ•Œ[ ğ‘¨ ]
  B = ğ•Œ[ ğ‘© ]

 homker-comp : swelldef ğ“¥ Î² â†’ (h : hom ğ‘¨ ğ‘©) â†’ ğ‘¨ âˆ£â‰ˆ (ker âˆ£ h âˆ£)
 homker-comp wd h f {u}{v} kuv = âˆ£ h âˆ£((f Ì‚ ğ‘¨) u)   â‰¡âŸ¨ âˆ¥ h âˆ¥ f u âŸ©
                                 (f Ì‚ ğ‘©)(âˆ£ h âˆ£ âˆ˜ u) â‰¡âŸ¨ wd(f Ì‚ ğ‘©)(âˆ£ h âˆ£ âˆ˜ u)(âˆ£ h âˆ£ âˆ˜ v)kuv âŸ©
                                 (f Ì‚ ğ‘©)(âˆ£ h âˆ£ âˆ˜ v) â‰¡âŸ¨ (âˆ¥ h âˆ¥ f v)â»Â¹ âŸ©
                                 âˆ£ h âˆ£((f Ì‚ ğ‘¨) v)   âˆ

 kercon : swelldef ğ“¥ Î² â†’ hom ğ‘¨ ğ‘© â†’ Con ğ‘¨
 kercon wd h = ker âˆ£ h âˆ£ , mkcon (ker-IsEquivalence âˆ£ h âˆ£) (homker-comp wd h)

 kerquo : swelldef ğ“¥ Î² â†’ hom ğ‘¨ ğ‘© â†’ SetoidAlgebra _ _
 kerquo wd h = ğ‘¨ â•± (kercon wd h)

ker[_â‡’_]_â†¾_ : (ğ‘¨ : SetoidAlgebra Î± Ïáµƒ) (ğ‘© : SetoidAlgebra Î² Ïáµ‡)
 â†’            hom ğ‘¨ ğ‘© â†’ swelldef ğ“¥ Î² â†’ SetoidAlgebra _ _
ker[ ğ‘¨ â‡’ ğ‘© ] h â†¾ wd = kerquo ğ‘¨ ğ‘© wd h

\end{code}

--------------------------------

<span style="float:left;">[â†‘ Homomorphisms.Setoid](Homomorphisms.Setoid.html)</span>
<span style="float:right;">[Homomorphisms.Setoid.Noether â†’](Homomorphisms.Setoid.Noether.html)</span>

{% include UALib.Links.md %}
