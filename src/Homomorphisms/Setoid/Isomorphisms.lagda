---
layout: default
title : "Homomorphisms.Setoid.Isomoprhisms module (The Agda Universal Algebra Library)"
date : "2021-07-11"
author: "agda-algebras development team"
---

#### <a id="isomorphisms-of-setoid-algebras">Isomorphisms of setoid algebras</a>

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Algebras.Basic using ( ğ“ ; ğ“¥ ; Signature )

module Homomorphisms.Setoid.Isomorphisms {ğ‘† : Signature ğ“ ğ“¥}  where

-- Imports from Agda (builtin/primitive) and the Agda Standard Library ---------------------
open import Axiom.Extensionality.Propositional using () renaming (Extensionality to funext )
open import Agda.Primitive              using ( _âŠ”_ ; lsuc ) renaming ( Set to Type )
open import Data.Product                using ( _,_ ; Î£-syntax ; _Ã—_ )
open import Function.Base               using ( _âˆ˜_ )
open import Function.Equality           using ( Î  ; _âŸ¶_ )
open import Level                       using ( Level )
open import Relation.Binary             using ( Setoid ; REL)
open import Relation.Binary.Definitions using ( Reflexive ; Sym ; Trans ; Transitive )
open import Relation.Binary.PropositionalEquality as PE using ( module â‰¡-Reasoning ; _â‰¡_ )

-- Imports from the Agda Universal Algebra Library -----------------------------------------------------
open import Overture.Preliminaries
 using ( âˆ£_âˆ£ ; âˆ¥_âˆ¥ ; _â»Â¹ ; _âˆ™_ ; lowerâˆ¼lift ; liftâˆ¼lower ) renaming ( _â‰ˆ_ to _â‰‹_ )
open import Overture.Setoid.Injective          using ( IsInjective )
open import Algebras.Setoid.Products   {ğ‘† = ğ‘†} using ( â¨… )
open import Algebras.Setoid.Basic      {ğ‘† = ğ‘†} using ( SetoidAlgebra ; ğ•Œ[_] ; _Ì‚_ ; Lift-Alg)
open import Homomorphisms.Setoid.Basic {ğ‘† = ğ‘†} using ( hom ; IsHom )
open import Homomorphisms.Setoid.Properties {ğ‘† = ğ‘†} using ( âˆ˜-hom ; âˆ˜-is-hom ; ğ’¾ğ’¹ ; ğ“ğ’¾ğ’»ğ“‰ ; ğ“â„´ğ“Œâ„¯ğ“‡
                                                          ;  ğ“ğ’¾ğ’»ğ“‰âˆ¼ğ“â„´ğ“Œâ„¯ğ“‡ ; ğ“â„´ğ“Œâ„¯ğ“‡âˆ¼ğ“ğ’¾ğ’»ğ“‰ )

\end{code}

Recall, `f ~ g` means f and g are *extensionally* (or pointwise) equal; i.e., `âˆ€ x, f x â‰¡ g x`. We use this notion of equality of functions in the following definition of *isomorphism*.

We could define this using Sigma types, like this.

```agda
_â‰…_ : {Î± Î² : Level}(ğ‘¨ : Algebra Î± ğ‘†)(ğ‘© : SetoidAlgebra Î² Ïáµ‡) â†’ Type(ğ“ âŠ” ğ“¥ âŠ” Î± âŠ” Î²)
ğ‘¨ â‰… ğ‘© =  Î£[ f âˆˆ (hom ğ‘¨ ğ‘©)] Î£[ g âˆˆ hom ğ‘© ğ‘¨ ] ((âˆ£ f âˆ£ âˆ˜ âˆ£ g âˆ£ â‰ˆ âˆ£ ğ’¾ğ’¹ ğ‘© âˆ£) Ã— (âˆ£ g âˆ£ âˆ˜ âˆ£ f âˆ£ â‰ˆ âˆ£ ğ’¾ğ’¹ ğ‘¨ âˆ£))
```

However, with four components, an equivalent record type is easier to work with.

\begin{code}

private variable
 Î± Ïáµƒ Î² Ïáµ‡ Î³ Ïá¶œ Î¹ : Level

open SetoidAlgebra
open Setoid
open Î 
record _â‰…_ (ğ‘¨ : SetoidAlgebra Î± Ïáµƒ)(ğ‘© : SetoidAlgebra Î² Ïáµ‡) : Type (ğ“ âŠ” ğ“¥ âŠ” Î± âŠ” Î² âŠ” Ïáµƒ âŠ” Ïáµ‡ ) where
 constructor mkiso
 field
  to : hom ğ‘¨ ğ‘©
  from : hom ğ‘© ğ‘¨
  toâˆ¼from : âˆ€ b â†’ (_â‰ˆ_ (Domain ğ‘©)) (âˆ£ to âˆ£ âŸ¨$âŸ© (âˆ£ from âˆ£ âŸ¨$âŸ© b)) b
  fromâˆ¼to : âˆ€ a â†’ (_â‰ˆ_ (Domain ğ‘¨)) (âˆ£ from âˆ£ âŸ¨$âŸ© (âˆ£ to âˆ£ âŸ¨$âŸ© a)) a

open _â‰…_ public

\end{code}

That is, two structures are *isomorphic* provided there are homomorphisms going back and forth between them which compose to the identity map.


#### <a id="properties-of-isomorphisms-of-setoid-algebras">Properties of isomorphism of setoid algebras</a>

\begin{code}

open Setoid

â‰…-refl : Reflexive (_â‰…_ {Î±}{Ïáµƒ})
â‰…-refl {Î±}{Ïáµƒ}{ğ‘¨} = mkiso ğ’¾ğ’¹ ğ’¾ğ’¹ (Î» b â†’ refl (Domain ğ‘¨)) Î» a â†’ refl (Domain ğ‘¨)

â‰…-sym : Sym (_â‰…_{Î²}{Ïáµ‡}) (_â‰…_{Î±}{Ïáµƒ})
â‰…-sym Ï† = mkiso (from Ï†) (to Ï†) (fromâˆ¼to Ï†) (toâˆ¼from Ï†)

â‰…-trans : Trans (_â‰…_ {Î±}{Ïáµƒ})(_â‰…_{Î²}{Ïáµ‡})(_â‰…_{Î±}{Ïáµƒ}{Î³}{Ïá¶œ})
â‰…-trans {Ïá¶œ = Ïá¶œ}{ğ‘¨}{ğ‘©}{ğ‘ª} ab bc = mkiso f g Ï„ Î½
  where
  f : hom ğ‘¨ ğ‘ª
  f = âˆ˜-hom ğ‘¨ ğ‘© ğ‘ª (to ab) (to bc)
  g : hom ğ‘ª ğ‘¨
  g = âˆ˜-hom ğ‘ª ğ‘© ğ‘¨ (from bc) (from ab)

  Ï„ : âˆ€ b â†’ (_â‰ˆ_ (Domain ğ‘ª)) (âˆ£ f âˆ£ âŸ¨$âŸ© (âˆ£ g âˆ£ âŸ¨$âŸ© b)) b
  Ï„ b = trans (Domain ğ‘ª) (cong âˆ£ to bc âˆ£ (toâˆ¼from ab (âˆ£ from bc âˆ£ âŸ¨$âŸ© b))) (toâˆ¼from bc b)

  Î½ : âˆ€ a â†’ (_â‰ˆ_ (Domain ğ‘¨)) (âˆ£ g âˆ£ âŸ¨$âŸ© (âˆ£ f âˆ£ âŸ¨$âŸ© a)) a
  Î½ a = trans (Domain ğ‘¨) (cong âˆ£ from ab âˆ£ (fromâˆ¼to bc (âˆ£ to ab âˆ£ âŸ¨$âŸ© a))) (fromâˆ¼to ab a)

module _ {ğ‘¨ : SetoidAlgebra Î± Ïáµƒ}{ğ‘© : SetoidAlgebra Î² Ïáµ‡} where
 open SetoidAlgebra
 open Setoid
 open Î 

 private
  A = Domain ğ‘¨
  B = Domain ğ‘©
  _â‰ˆAâ‰ˆ_ = _â‰ˆ_ A
  _â‰ˆBâ‰ˆ_ = _â‰ˆ_ B

 -- The "to" map of an isomorphism is injective.
 â‰…toInjective : (Ï† : ğ‘¨ â‰… ğ‘©) â†’ IsInjective âˆ£ to Ï† âˆ£
 â‰…toInjective (mkiso (f , _) (g , _) _ gâˆ¼f){aâ‚€}{aâ‚} fafb = Goal
  where
  lem1 : aâ‚€ â‰ˆAâ‰ˆ (g âŸ¨$âŸ© (f âŸ¨$âŸ© aâ‚€))
  lem1 = sym A (gâˆ¼f aâ‚€)
  lem2 : (g âŸ¨$âŸ© (f âŸ¨$âŸ© aâ‚€)) â‰ˆAâ‰ˆ (g âŸ¨$âŸ© (f âŸ¨$âŸ© aâ‚))
  lem2 = cong g fafb
  lem3 : (g âŸ¨$âŸ© (f âŸ¨$âŸ© aâ‚)) â‰ˆAâ‰ˆ aâ‚
  lem3 = gâˆ¼f aâ‚
  Goal : aâ‚€ â‰ˆAâ‰ˆ aâ‚
  Goal = trans A lem1 (trans A lem2 lem3)


-- The "from" map of an isomorphism is injective.
â‰…fromInjective : {ğ‘¨ : SetoidAlgebra Î± Ïáµƒ}{ğ‘© : SetoidAlgebra Î² Ïáµ‡}
                 (Ï† : ğ‘¨ â‰… ğ‘©) â†’ IsInjective âˆ£ from Ï† âˆ£

â‰…fromInjective Ï† = â‰…toInjective (â‰…-sym Ï†)

\end{code}

Fortunately, the lift operation preserves isomorphism (i.e., it's an *algebraic invariant*). As our focus is universal algebra, this is important and is what makes the lift operation a workable solution to the technical problems that arise from the noncumulativity of Agda's universe hierarchy.

\begin{code}

open Level

Lift-â‰… : {â„“ : Level}{ğ‘¨ : SetoidAlgebra Î± Ïáµƒ} â†’ ğ‘¨ â‰… (Lift-Alg ğ‘¨ â„“)
Lift-â‰… {â„“ = â„“} {ğ‘¨} = mkiso ğ“ğ’¾ğ’»ğ“‰ ğ“â„´ğ“Œâ„¯ğ“‡ (ğ“ğ’¾ğ’»ğ“‰âˆ¼ğ“â„´ğ“Œâ„¯ğ“‡{ğ‘¨ = ğ‘¨}) (ğ“â„´ğ“Œâ„¯ğ“‡âˆ¼ğ“ğ’¾ğ’»ğ“‰{ğ‘¨ = ğ‘¨}{â„“ = â„“})

Lift-Alg-iso : {â„“áµƒ : Level}{ğ‘¨ : SetoidAlgebra Î± Ïáµƒ}
               {â„“áµ‡ : Level}{ğ‘© : SetoidAlgebra Î² Ïáµ‡}
               -----------------------------------------
 â†’             ğ‘¨ â‰… ğ‘© â†’  Lift-Alg ğ‘¨ â„“áµƒ â‰… Lift-Alg ğ‘© â„“áµ‡

Lift-Alg-iso Aâ‰…B = â‰…-trans (â‰…-trans (â‰…-sym Lift-â‰… ) Aâ‰…B) Lift-â‰…

\end{code}

The lift is also associative, up to isomorphism at least.

\begin{code}

Lift-Alg-assoc : (â„“â‚ â„“â‚‚ : Level){ğ‘¨ : SetoidAlgebra Î± Ïáµƒ}
 â†’                     Lift-Alg ğ‘¨ (â„“â‚ âŠ” â„“â‚‚) â‰…  Lift-Alg (Lift-Alg ğ‘¨ â„“â‚) â„“â‚‚

Lift-Alg-assoc _ _ = â‰…-trans (â‰…-trans (â‰…-sym Lift-â‰…) Lift-â‰…) Lift-â‰…

\end{code}

Products of isomorphic families of algebras are themselves isomorphic. The proof looks a bit technical, but it is as straightforward as it ought to be.

\begin{code}

module _ {ğ“˜ : Level}{I : Type ğ“˜}
         {ğ’œ : I â†’ SetoidAlgebra Î± Ïáµƒ}
         {â„¬ : I â†’ SetoidAlgebra Î² Ïáµ‡} where

 open SetoidAlgebra
 open IsHom

 private
  â¨…A = Domain (â¨… ğ’œ)
  â¨…B = Domain (â¨… â„¬)
  _â‰ˆâ¨…Aâ‰ˆ_ = _â‰ˆ_ â¨…A
  _â‰ˆâ¨…Bâ‰ˆ_ = _â‰ˆ_ â¨…B

 â¨…â‰… : (âˆ€ (i : I) â†’ ğ’œ i â‰… â„¬ i) â†’ â¨… ğ’œ â‰… â¨… â„¬

 â¨…â‰… AB = mkiso (Ï• , Ï•hom) (Ïˆ , Ïˆhom) Ï•âˆ¼Ïˆ Ïˆâˆ¼Ï•
  where
   Ï• : â¨…A âŸ¶ â¨…B
   Ï• = record { _âŸ¨$âŸ©_ = Î» a i â†’ âˆ£ to (AB i) âˆ£ âŸ¨$âŸ© (a i)
              ; cong = Î» a i â†’ cong âˆ£ to (AB i) âˆ£ (a i) }

   Ï•hom : IsHom (â¨… ğ’œ) (â¨… â„¬) Ï•
   Ï•hom = record { compatible = Î» f a i â†’ compatible âˆ¥ to (AB i) âˆ¥ f (Î» x â†’ a x i)
                 ; preservesâ‰ˆ = Î» xy i â†’ preservesâ‰ˆ âˆ¥ to (AB i) âˆ¥ (xy i) }

   Ïˆ : â¨…B âŸ¶ â¨…A
   Ïˆ = record { _âŸ¨$âŸ©_ = Î» b i â†’ âˆ£ from (AB i) âˆ£ âŸ¨$âŸ© (b i)
              ; cong = Î» b i â†’ cong âˆ£ from (AB i) âˆ£ (b i) }

   Ïˆhom : IsHom (â¨… â„¬) (â¨… ğ’œ) Ïˆ
   Ïˆhom = record { compatible = Î» f b i â†’ compatible âˆ¥ from (AB i) âˆ¥ f Î» x â†’ b x i
                 ; preservesâ‰ˆ = Î» xy i â†’ preservesâ‰ˆ âˆ¥ from (AB i) âˆ¥ (xy i) }

   Ï•âˆ¼Ïˆ : âˆ€ b â†’ (Ï• âŸ¨$âŸ© (Ïˆ âŸ¨$âŸ© b)) â‰ˆâ¨…Bâ‰ˆ b
   Ï•âˆ¼Ïˆ b = Î» i â†’ toâˆ¼from (AB i) (b i)

   Ïˆâˆ¼Ï• : âˆ€ a â†’ (Ïˆ âŸ¨$âŸ© (Ï• âŸ¨$âŸ© a)) â‰ˆâ¨…Aâ‰ˆ a
   Ïˆâˆ¼Ï• a = Î» i â†’ fromâˆ¼to (AB i)(a i)

\end{code}

A nearly identical proof goes through for isomorphisms of lifted products.

\begin{code}

module _ {ğ“˜ : Level}{I : Type ğ“˜}
         {ğ’œ : I â†’ SetoidAlgebra Î± Ïáµƒ}
         {â„¬ : (Lift Î³ I) â†’ SetoidAlgebra Î² Ïáµ‡} where

 open SetoidAlgebra
 open IsHom

 private
  â¨…A = Domain (â¨… ğ’œ)
  â¨…B = Domain (â¨… â„¬)
  _â‰ˆâ¨…Aâ‰ˆ_ = _â‰ˆ_ â¨…A
  _â‰ˆâ¨…Bâ‰ˆ_ = _â‰ˆ_ â¨…B

 open Level
 Lift-Alg-â¨…â‰… : (âˆ€ i â†’ ğ’œ i â‰… â„¬ (lift i)) â†’ Lift-Alg (â¨… ğ’œ) Î³ â‰… â¨… â„¬

 Lift-Alg-â¨…â‰… AB = Goal
  where
   Ï• : â¨…A âŸ¶ â¨…B
   Ï• = record { _âŸ¨$âŸ©_ = Î» a i â†’ âˆ£ to (AB (lower i)) âˆ£ âŸ¨$âŸ© (a (lower i))
              ; cong = Î» a i â†’ cong âˆ£ to (AB (lower i)) âˆ£ (a (lower i)) }


   Ï•hom : IsHom (â¨… ğ’œ) (â¨… â„¬) Ï•
   Ï•hom = record { compatible = Î» f a i â†’ compatible âˆ¥ to (AB (lower i)) âˆ¥ f (Î» x â†’ a x (lower i))
                 ; preservesâ‰ˆ = Î» xy i â†’ preservesâ‰ˆ âˆ¥ to (AB (lower i)) âˆ¥ (xy (lower i)) }

   Ïˆ : â¨…B âŸ¶ â¨…A
   Ïˆ = record { _âŸ¨$âŸ©_ = Î» b i â†’ âˆ£ from (AB i) âˆ£ âŸ¨$âŸ© (b (lift i))
              ; cong = Î» b i â†’ cong âˆ£ from (AB i) âˆ£ (b (lift i)) }

   Ïˆhom : IsHom (â¨… â„¬) (â¨… ğ’œ) Ïˆ
   Ïˆhom = record { compatible = Î» f b i â†’ compatible âˆ¥ from (AB i) âˆ¥ f Î» x â†’ b x (lift i)
                 ; preservesâ‰ˆ = Î» xy i â†’ preservesâ‰ˆ âˆ¥ from (AB i) âˆ¥ (xy (lift i)) }

   Ï•âˆ¼Ïˆ : âˆ€ b â†’ (Ï• âŸ¨$âŸ© (Ïˆ âŸ¨$âŸ© b)) â‰ˆâ¨…Bâ‰ˆ b
   Ï•âˆ¼Ïˆ b = Î» i â†’ toâˆ¼from (AB (lower i)) (b i)

   Ïˆâˆ¼Ï• : âˆ€ a â†’ (Ïˆ âŸ¨$âŸ© (Ï• âŸ¨$âŸ© a)) â‰ˆâ¨…Aâ‰ˆ a
   Ïˆâˆ¼Ï• a = Î» i â†’ fromâˆ¼to (AB i)(a i)

   Aâ‰…B : â¨… ğ’œ â‰… â¨… â„¬
   Aâ‰…B = mkiso (Ï• , Ï•hom) (Ïˆ , Ïˆhom) Ï•âˆ¼Ïˆ Ïˆâˆ¼Ï•

   Goal : Lift-Alg (â¨… ğ’œ) Î³ â‰… â¨… â„¬
   Goal = â‰…-trans (â‰…-sym Lift-â‰…) Aâ‰…B

\end{code}

------------------------------

<span style="float:left;">[â† Homomorphisms.Setoid.Noether](Homomorphisms.Setoid.Noether.html)</span>
<span style="float:right;">[Homomorphisms.Setoid.HomomorphicImages â†’](Homomorphisms.Setoid.HomomorphicImages.html)</span>

{% include UALib.Links.md %}
