---
layout: default
title : Homomorphisms.Setoid.Noether module (The Agda Universal Algebra Library)
date : 2021-07-17
author: [agda-algebras development team][]
---

### <a id="homomorphism-theorems-for-setoid-algebras">Homomorphism Theorems for Setoid Algebras</a>

This is the [Homomorphisms.Setoid.Noether][] module of the [Agda Universal Algebra Library][].

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Algebras.Basic using (ğ“ ; ğ“¥ ; Signature )

module Homomorphisms.Setoid.Noether {ğ‘† : Signature ğ“ ğ“¥} where

-- Imports from Agda and the Agda Standard Library -------------------------------------------------
open import Data.Product   using ( _,_ ; Î£-syntax ; _Ã—_ ) renaming ( projâ‚ to fst ; projâ‚‚ to snd )
open import Function       using ( _âˆ˜_ ; id )
open import Level          using ( Level )
open import Relation.Binary.PropositionalEquality using ( _â‰¡_ ; refl ; sym ; cong ; module â‰¡-Reasoning )
open import Relation.Unary using ( _âŠ†_ )

-- Imports from the Agda Universal Algebra Library ------------------------------------------------
open import Overture.Preliminaries             using ( âˆ£_âˆ£ ; âˆ¥_âˆ¥ ; _â‰ˆ_)
open import Overture.Inverses                  using ( IsInjective ; IsSurjective ; SurjInv
                                                     ; SurjInvIsRightInv ; epic-factor )
open import Foundations.Welldefined            using ( swelldef )
open import Foundations.Extensionality         using ( block-ext|uip ; pred-ext )
open import Relations.Discrete                 using ( kernel )
open import Algebras.Setoid.Basic      {ğ‘† = ğ‘†} using ( ğ•Œ[_] ; SetoidAlgebra ; _Ì‚_ )
open import Homomorphisms.Setoid.Basic {ğ‘† = ğ‘†} using ( hom ; kercon ; ker[_â‡’_]_â†¾_ ; âˆ˜-hom ; ğ’¾ğ’¹ ; epi
                                                     ; ğ“ğ’¾ğ’»ğ“‰ ; ğ“â„´ğ“Œâ„¯ğ“‡ ; is-homomorphism ; âˆ˜-is-hom )
\end{code}


#### <a id="homomorphism-decomposition-for-setoidalgebras">Homomorphism Decomposition for SetoidAlgebras</a>


If `Ï„ : hom ğ‘¨ ğ‘©`, `Î½ : hom ğ‘¨ ğ‘ª`, `Î½` is surjective, and `ker Î½ âŠ† ker Ï„`, then there exists `Ï† : hom ğ‘ª ğ‘©` such that `Ï„ = Ï† âˆ˜ Î½` so the following diagram commutes:

```
ğ‘¨ --- Î½ ->> ğ‘ª
 \         .
  \       .
   Ï„     Ï†
    \   .
     \ .
      V
      ğ‘©
```

\begin{code}

module _ {Î± Ïáµƒ : Level} {ğ‘¨ : SetoidAlgebra Î± Ïáµƒ}
         {Î² Ïáµ‡ : Level} (ğ‘© : SetoidAlgebra Î² Ïáµ‡)
         {Î³ Ïá¶œ : Level} {ğ‘ª : SetoidAlgebra Î³ Ïá¶œ} where

 private
  A = ğ•Œ[ ğ‘¨ ]
  B = ğ•Œ[ ğ‘© ]
  C = ğ•Œ[ ğ‘ª ]

 open import Axiom.Extensionality.Propositional    using    ()
                                                  renaming (Extensionality to funext)

 HomFactor : swelldef ğ“¥ Î³
  â†’          (Ï„ : hom ğ‘¨ ğ‘©)(Î½ : hom ğ‘¨ ğ‘ª)
  â†’          kernel âˆ£ Î½ âˆ£ âŠ† kernel âˆ£ Ï„ âˆ£ â†’ IsSurjective âˆ£ Î½ âˆ£
             --------------------------------------------------
  â†’          Î£[ Ï† âˆˆ (hom ğ‘ª ğ‘©)] (âˆ£ Ï„ âˆ£ â‰ˆ âˆ£ Ï† âˆ£ âˆ˜ âˆ£ Î½ âˆ£)

 HomFactor wd Ï„ Î½ KÎ½Ï„ Î½E = (Ï† , Ï†IsHomCB)  , Ï„Ï†Î½
  where
  Î½Inv : C â†’ A
  Î½Inv = SurjInv âˆ£ Î½ âˆ£ Î½E

  Î· : âˆ€ c â†’ c â‰¡ âˆ£ Î½ âˆ£ (Î½Inv c)
  Î· c = sym (SurjInvIsRightInv âˆ£ Î½ âˆ£ Î½E c)

  Ï† : C â†’ B
  Ï† = âˆ£ Ï„ âˆ£ âˆ˜ Î½Inv

  Î¾ : âˆ€ a â†’ kernel âˆ£ Î½ âˆ£ (a , Î½Inv (âˆ£ Î½ âˆ£ a))
  Î¾ a = Î· (âˆ£ Î½ âˆ£ a)

  Ï„Ï†Î½ : âˆ£ Ï„ âˆ£ â‰ˆ Ï† âˆ˜ âˆ£ Î½ âˆ£
  Ï„Ï†Î½ x = KÎ½Ï„ (Î¾ x)

  Ï†IsHomCB : âˆ€ ğ‘“ c â†’ Ï† ((ğ‘“ Ì‚ ğ‘ª) c) â‰¡ ((ğ‘“ Ì‚ ğ‘©)(Ï† âˆ˜ c))
  Ï†IsHomCB ğ‘“ c =
   Ï† ((ğ‘“ Ì‚ ğ‘ª) c)                    â‰¡âŸ¨ cong Ï† (wd (ğ‘“ Ì‚ ğ‘ª) c (âˆ£ Î½ âˆ£ âˆ˜ (Î½Inv âˆ˜ c)) Î» i â†’ Î· ((c i)))âŸ©
   Ï† ((ğ‘“ Ì‚ ğ‘ª)(âˆ£ Î½ âˆ£ âˆ˜(Î½Inv âˆ˜ c)))   â‰¡âŸ¨ cong Ï† (sym (âˆ¥ Î½ âˆ¥ ğ‘“ (Î½Inv âˆ˜ c))) âŸ©
   Ï† (âˆ£ Î½ âˆ£((ğ‘“ Ì‚ ğ‘¨)(Î½Inv âˆ˜ c)))     â‰¡âŸ¨ sym (Ï„Ï†Î½ ((ğ‘“ Ì‚ ğ‘¨)(Î½Inv âˆ˜ c))) âŸ©
   âˆ£ Ï„ âˆ£((ğ‘“ Ì‚ ğ‘¨)(Î½Inv âˆ˜ c))         â‰¡âŸ¨ âˆ¥ Ï„ âˆ¥ ğ‘“ (Î½Inv âˆ˜ c) âŸ©
   (ğ‘“ Ì‚ ğ‘©)(Î» x â†’ âˆ£ Ï„ âˆ£(Î½Inv (c x))) âˆ where open â‰¡-Reasoning

\end{code}

If, in addition to the hypotheses of the last theorem, we assume Ï„ is epic, then so is Ï†. (Note that the proof also requires an additional local function extensionality postulate, `funext Î² Î²`.)

\begin{code}

 open epi
 HomFactorEpi : swelldef ğ“¥ Î³
  â†’             (Ï„ : hom ğ‘¨ ğ‘©)(Î½ : hom ğ‘¨ ğ‘ª)
  â†’             kernel âˆ£ Î½ âˆ£ âŠ† kernel âˆ£ Ï„ âˆ£
  â†’             IsSurjective âˆ£ Î½ âˆ£ â†’ IsSurjective âˆ£ Ï„ âˆ£
                ---------------------------------------------
  â†’             Î£[ Ï† âˆˆ epi ğ‘ª ğ‘© ] âˆ£ Ï„ âˆ£ â‰ˆ (Ï† .map) âˆ˜ âˆ£ Î½ âˆ£

 HomFactorEpi wd Ï„ Î½ kerincl Î½e Ï„e = record { map = fst âˆ£ Ï†F âˆ£
                                            ; is-epi = (snd âˆ£ Ï†F âˆ£) , Ï†E
                                            } , âˆ¥ Ï†F âˆ¥
  where
   Ï†F : Î£[ Ï† âˆˆ hom ğ‘ª ğ‘© ] âˆ£ Ï„ âˆ£ â‰ˆ âˆ£ Ï† âˆ£ âˆ˜ âˆ£ Î½ âˆ£
   Ï†F = HomFactor wd Ï„ Î½ kerincl Î½e

   Ï† : C â†’ B
   Ï† = âˆ£ Ï„ âˆ£ âˆ˜ (SurjInv âˆ£ Î½ âˆ£ Î½e)

   Ï†E : IsSurjective Ï†
   Ï†E = epic-factor  âˆ£ Ï„ âˆ£ âˆ£ Î½ âˆ£ Ï† âˆ¥ Ï†F âˆ¥ Ï„e

\end{code}

--------------------------------

[â† Homomorphisms.Setoid.Basic](Homomorphisms.Setoid.Basic.html)
<span style="float:right;">[Homomorphisms.Setoid.Isomorphisms â†’](Homomorphisms.Setoid.Isomorphisms.html)</span>

{% include UALib.Links.md %}

[agda-algebras development team]: https://github.com/ualib/agda-algebras#the-agda-algebras-development-team
