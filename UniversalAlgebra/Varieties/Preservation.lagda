---
layout: default
title : Varieties.Preservation (The Agda Universal Algebra Library)
date : 2021-01-14
author: [the ualib/agda-algebras development team][]
---

### <a id="Equation preservation">Equation preservation</a>

This section presents the [Varieties.Preservation][] module of the [Agda Universal Algebra Library][]. In this module we show that identities are preserved by closure operators H, S, and P.  This will establish the easy direction of Birkhoff's HSP Theorem.


\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}


open import Level renaming ( suc to lsuc )
open import Algebras.Basic

module Varieties.Preservation {Î± ğ“ ğ“¥ : Level} (ğ‘† : Signature ğ“ ğ“¥) where


-- Imports from Agda (builtin/primitive) and the Agda Standard Library ---------------------
open import Axiom.Extensionality.Propositional renaming (Extensionality to funext)
open import Agda.Builtin.Equality   using    ( _â‰¡_ ; refl )
open import Agda.Primitive          renaming ( Set to Type )
                                    using    ( _âŠ”_ )
open import Data.Product            using    ( _,_ ; Î£-syntax ; Î£ ; _Ã—_ )
                                    renaming ( projâ‚ to fst
                                             ; projâ‚‚ to snd )
open import Data.Sum.Base           using    ( _âŠ_ )
open import Function.Base           using    ( _âˆ˜_ )
open import Relation.Binary.PropositionalEquality
                                    using    ( cong ; cong-app ; module â‰¡-Reasoning )
open import Relation.Unary          using    ( Pred ; _âˆˆ_ ; _âŠ†_ ; ï½›_ï½ ; _âˆª_ )



-- Imports from agda-algebras --------------------------------------------------------------
open import Overture.Preliminaries       using ( _â»Â¹ ; âˆ£_âˆ£ ; âˆ¥_âˆ¥ ; ğ‘–ğ‘‘ )
open import Overture.Inverses            using ( Inv ; InvIsInv )
open import Algebras.Products          ğ‘† using (ov)
open import Homomorphisms.Isomorphisms ğ‘† using (_â‰…_; â‰…-refl)
open import Terms.Basic                ğ‘† using (Term ; ğ‘» ; lift-hom)
open import Terms.Operations           ğ‘† using (_âŸ¦_âŸ§; comm-hom-term)
open import Varieties.Basic            ğ‘† using ( _âŠ§_â‰‹_ ; _âŠ§_â‰ˆ_ ; âŠ§-Lift-invar
                                               ; âŠ§-lower-invar ; âŠ§-S-class-invar
                                               ; âŠ§-I-invar ; âŠ§-P-lift-invar
                                               ; âŠ§-P-invar ; âŠ§-S-invar ; Th)

open import Varieties.EquationalLogic ğ‘† using (H; S; P; V)

private variable Î² Î³ ğ“§ : Level

open H
open S
open P
open V
open Term
open _âŠ_

ğ“• ğ“•âº : Level
ğ“• = ov Î±
ğ“•âº = lsuc (ov Î±)    -- (this will be the level of the relatively free algebra)

\end{code}



#### <a id="h-preserves-identities">H preserves identities</a>

First we prove that the closure operator H is compatible with identities that hold in the given class.

\begin{code}

module _ {fe : (âˆ€ a b â†’ funext a b)}{X : Type ğ“§} {ğ’¦ : Pred (Algebra Î± ğ‘†)(ov Î±)} where

 open â‰¡-Reasoning

 H-id1 : (p q : Term X) â†’ ğ’¦ âŠ§ p â‰‹ q â†’ H{Î² = Î±} ğ’¦ âŠ§ p â‰‹ q
 H-id1 p q Ïƒ (hbase x) = âŠ§-Lift-invar fe p q (Ïƒ x)

 H-id1 p q Ïƒ (hhimg{ğ‘¨}{ğ‘ª} HA (ğ‘© , ((Ï† , Ï†hom) , Ï†E))) = Goal
  where
  Î½ : ğ‘¨ âŠ§ p â‰ˆ q
  Î½ = (H-id1 p q Ïƒ) HA

  preim : âˆ€ ğ’ƒ x â†’ âˆ£ ğ‘¨ âˆ£
  preim ğ’ƒ x = Inv Ï† (Ï†E (ğ’ƒ x))

  Î¶ : âˆ€ ğ’ƒ â†’ Ï† âˆ˜ (preim ğ’ƒ) â‰¡ ğ’ƒ
  Î¶ ğ’ƒ = (fe ğ“§ Î±) Î» x â†’ InvIsInv Ï† (Ï†E (ğ’ƒ x))

  Goal : ğ‘© âŸ¦ p âŸ§  â‰¡ ğ‘© âŸ¦ q âŸ§
  Goal = (fe (Î± âŠ” ğ“§) Î±) Î» ğ’ƒ
   â†’  (ğ‘© âŸ¦ p âŸ§) ğ’ƒ             â‰¡âŸ¨ (cong (ğ‘© âŸ¦ p âŸ§) (Î¶ ğ’ƒ))â»Â¹ âŸ©
      (ğ‘© âŸ¦ p âŸ§)(Ï† âˆ˜(preim ğ’ƒ)) â‰¡âŸ¨(comm-hom-term (fe ğ“¥ Î±) ğ‘©(Ï† , Ï†hom) p(preim ğ’ƒ))â»Â¹ âŸ©
      Ï†((ğ‘¨ âŸ¦ p âŸ§)(preim ğ’ƒ))   â‰¡âŸ¨ cong Ï† (cong-app Î½ (preim ğ’ƒ)) âŸ©
      Ï†((ğ‘¨ âŸ¦ q âŸ§)(preim ğ’ƒ))   â‰¡âŸ¨ comm-hom-term (fe ğ“¥ Î±) ğ‘© (Ï† , Ï†hom) q (preim ğ’ƒ) âŸ©
      (ğ‘© âŸ¦ q âŸ§)(Ï† âˆ˜(preim ğ’ƒ)) â‰¡âŸ¨ cong (ğ‘© âŸ¦ q âŸ§) (Î¶ ğ’ƒ) âŸ©
      (ğ‘© âŸ¦ q âŸ§) ğ’ƒ             âˆ

\end{code}

The converse of the foregoing result is almost too obvious to bother with. Nonetheless, we formalize it for completeness.

\begin{code}

 H-id2 : âˆ€ {Î²} â†’ (p q : Term X) â†’ H{Î² = Î²} ğ’¦ âŠ§ p â‰‹ q â†’ ğ’¦ âŠ§ p â‰‹ q

 H-id2 p q Hpq KA = âŠ§-lower-invar fe p q (Hpq (hbase KA))

\end{code}


#### <a id="s-preserves-identities">S preserves identities</a>

\begin{code}

 S-id1 : (p q : Term X) â†’ ğ’¦ âŠ§ p â‰‹ q â†’ S{Î² = Î±} ğ’¦ âŠ§ p â‰‹ q

 S-id1 p q Ïƒ (sbase x) = âŠ§-Lift-invar fe p q (Ïƒ x)
 S-id1 p q Ïƒ (slift x) = âŠ§-Lift-invar fe p q ((S-id1 p q Ïƒ) x)

 S-id1 p q Ïƒ (ssub{ğ‘¨}{ğ‘©} sA Bâ‰¤A) =
  âŠ§-S-class-invar fe p q pq (ğ‘© , ğ‘¨ , (ğ‘© , Bâ‰¤A) , _âŠ_.injâ‚‚ refl , â‰…-refl)
   where --Apply S-âŠ§ to the class ğ’¦ âˆª ï½› ğ‘¨ ï½
   Î½ : ğ‘¨ âŠ§ p â‰ˆ q
   Î½ = S-id1 p q Ïƒ sA

   Apq : ï½› ğ‘¨ ï½ âŠ§ p â‰‹ q
   Apq refl = Î½

   pq : (ğ’¦ âˆª ï½› ğ‘¨ ï½) âŠ§ p â‰‹ q
   pq {ğ‘©} (injâ‚ x) = Ïƒ x
   pq {ğ‘©} (injâ‚‚ y) = Apq y

 S-id1 p q Ïƒ (siso{ğ‘¨}{ğ‘©} x xâ‚) = âŠ§-I-invar fe ğ‘© p q (S-id1 p q Ïƒ x) xâ‚

\end{code}

Again, the obvious converse is barely worth the bits needed to formalize it.

\begin{code}

 S-id2 : âˆ€{Î²}(p q : Term X) â†’ S{Î² = Î²}ğ’¦ âŠ§ p â‰‹ q â†’ ğ’¦ âŠ§ p â‰‹ q

 S-id2 p q Spq {ğ‘¨} KA = âŠ§-lower-invar fe p q (Spq (sbase KA))

\end{code}


#### <a id="p-preserves-identities">P preserves identities</a>

\begin{code}

 P-id1 : (p q : Term X) â†’ ğ’¦ âŠ§ p â‰‹ q â†’ P{Î² = Î±} ğ’¦ âŠ§ p â‰‹ q

 P-id1 p q Ïƒ (pbase x) = âŠ§-Lift-invar fe p q (Ïƒ x)
 P-id1 p q Ïƒ (pliftu x) = âŠ§-Lift-invar fe p q ((P-id1 p q Ïƒ) x)
 P-id1 p q Ïƒ (pliftw x) = âŠ§-Lift-invar fe p q ((P-id1 p q Ïƒ) x)

 P-id1 p q Ïƒ (produ{I}{ğ’œ} x) = âŠ§-P-lift-invar ğ’œ  fe {p}{q} IH
  where
  IH : âˆ€ i â†’ (Lift-Alg (ğ’œ i) Î±) âŸ¦ p âŸ§ â‰¡ (Lift-Alg (ğ’œ i) Î±) âŸ¦ q âŸ§
  IH i = âŠ§-Lift-invar fe p q ((P-id1 p q Ïƒ) (x i))

 P-id1 p q Ïƒ (prodw{I}{ğ’œ} x) = âŠ§-P-lift-invar ğ’œ fe {p}{q}IH
  where
  IH : âˆ€ i â†’ Lift-Alg (ğ’œ i) Î± âŸ¦ p âŸ§ â‰¡ Lift-Alg (ğ’œ i) Î± âŸ¦ q âŸ§
  IH i = âŠ§-Lift-invar fe p q ((P-id1 p q Ïƒ) (x i))

 P-id1 p q Ïƒ (pisow{ğ‘¨}{ğ‘©} x xâ‚) = âŠ§-I-invar fe ğ‘© p q (P-id1 p q Ïƒ x) xâ‚

\end{code}

...and conversely...

\begin{code}

 P-id2 : âˆ€ {Î²}(p q : Term X) â†’ P{Î² = Î²} ğ’¦ âŠ§ p â‰‹ q â†’ ğ’¦ âŠ§ p â‰‹ q
 P-id2 p q PKpq KA = âŠ§-lower-invar fe p q (PKpq (pbase KA))

\end{code}


#### <a id="v-preserves-identities">V preserves identities</a>

Finally, we prove the analogous preservation lemmas for the closure operator `V`.

\begin{code}

 V-id1 : (p q : Term X) â†’ ğ’¦ âŠ§ p â‰‹ q â†’ V{Î² = Î±} ğ’¦ âŠ§ p â‰‹ q
 V-id1 p q Ïƒ (vbase x) = âŠ§-Lift-invar fe p q (Ïƒ x)
 V-id1 p q Ïƒ (vlift{ğ‘¨} x) = âŠ§-Lift-invar  fe p q ((V-id1 p q Ïƒ) x)
 V-id1 p q Ïƒ (vliftw{ğ‘¨} x) = âŠ§-Lift-invar fe p q ((V-id1 p q Ïƒ) x)

 V-id1 p q Ïƒ (vhimg{ğ‘¨}{ğ‘ª}VA (ğ‘© , ((Ï† , Ï†h) , Ï†E))) = Goal
  where
  IH : ğ‘¨ âŠ§ p â‰ˆ q
  IH = V-id1 p q Ïƒ VA

  preim : âˆ€ ğ’ƒ (x : X) â†’ âˆ£ ğ‘¨ âˆ£
  preim ğ’ƒ x = (Inv Ï† (Ï†E (ğ’ƒ x)))

  Î¶ : âˆ€ ğ’ƒ â†’ Ï† âˆ˜ (preim ğ’ƒ) â‰¡ ğ’ƒ
  Î¶ ğ’ƒ = (fe ğ“§ Î±) Î» x â†’ InvIsInv Ï† (Ï†E (ğ’ƒ x))

  Goal : (ğ‘© âŸ¦ p âŸ§) â‰¡ (ğ‘© âŸ¦ q âŸ§)
  Goal = (fe (Î± âŠ” ğ“§) Î±) Î» ğ’ƒ â†’ (ğ‘© âŸ¦ p âŸ§) ğ’ƒ      â‰¡âŸ¨ (cong (ğ‘© âŸ¦ p âŸ§) (Î¶ ğ’ƒ))â»Â¹ âŸ©
                (ğ‘© âŸ¦ p âŸ§)(Ï† âˆ˜(preim ğ’ƒ)) â‰¡âŸ¨(comm-hom-term (fe ğ“¥ Î±) ğ‘©(Ï† , Ï†h) p(preim ğ’ƒ))â»Â¹ âŸ©
                Ï† ((ğ‘¨ âŸ¦ p âŸ§)(preim ğ’ƒ))  â‰¡âŸ¨ cong Ï† (cong-app IH (preim ğ’ƒ)) âŸ©
                Ï† ((ğ‘¨ âŸ¦ q âŸ§)(preim ğ’ƒ))  â‰¡âŸ¨ comm-hom-term (fe ğ“¥ Î±) ğ‘© (Ï† , Ï†h) q (preim ğ’ƒ) âŸ©
                (ğ‘© âŸ¦ q âŸ§)(Ï† âˆ˜(preim ğ’ƒ)) â‰¡âŸ¨ cong (ğ‘© âŸ¦ q âŸ§) (Î¶ ğ’ƒ) âŸ©
                (ğ‘© âŸ¦ q âŸ§) ğ’ƒ             âˆ

 V-id1 p q Ïƒ ( vssubw {ğ‘¨}{ğ‘©} VA Bâ‰¤A ) =
  âŠ§-S-class-invar fe p q pq (ğ‘© , ğ‘¨ , (ğ‘© , Bâ‰¤A) , injâ‚‚ refl , â‰…-refl)
   where
   IH : ğ‘¨ âŠ§ p â‰ˆ q
   IH = V-id1 p q Ïƒ VA

   Asinglepq : ï½› ğ‘¨ ï½ âŠ§ p â‰‹ q
   Asinglepq refl = IH

   pq : (ğ’¦ âˆª ï½› ğ‘¨ ï½) âŠ§ p â‰‹ q
   pq {ğ‘©} (injâ‚ x) = Ïƒ x
   pq {ğ‘©} (injâ‚‚ y) = Asinglepq y

 V-id1 p q Ïƒ (vprodu{I}{ğ’œ} Vğ’œ) = âŠ§-P-invar ğ’œ fe {p}{q} Î» i â†’ V-id1 p q Ïƒ (Vğ’œ i)
 V-id1 p q Ïƒ (vprodw{I}{ğ’œ} Vğ’œ) = âŠ§-P-invar ğ’œ fe {p}{q} Î» i â†’ V-id1 p q Ïƒ (Vğ’œ i)
 V-id1 p q Ïƒ (visou{ğ‘¨}{ğ‘©} VA Aâ‰…B) = âŠ§-I-invar fe ğ‘© p q (V-id1 p q Ïƒ VA) Aâ‰…B
 V-id1 p q Ïƒ (visow{ğ‘¨}{ğ‘©} VA Aâ‰…B) = âŠ§-I-invar fe ğ‘© p q (V-id1 p q Ïƒ VA) Aâ‰…B


 V-id1' : (p q : Term X) â†’ ğ’¦ âŠ§ p â‰‹ q â†’ V{Î² = ğ“•âº} ğ’¦ âŠ§ p â‰‹ q
 V-id1' p q Ïƒ (vbase x) = âŠ§-Lift-invar fe p q (Ïƒ x)
 V-id1' p q Ïƒ (vlift{ğ‘¨} x) = âŠ§-Lift-invar fe p q ((V-id1 p q Ïƒ) x)
 V-id1' p q Ïƒ (vliftw{ğ‘¨} x) = âŠ§-Lift-invar fe p q ((V-id1' p q Ïƒ) x)
 V-id1' p q Ïƒ (vhimg{ğ‘¨}{ğ‘ª} VA (ğ‘© , ((Ï† , Ï†h) , Ï†E))) = Goal
  where
  IH : ğ‘¨ âŠ§ p â‰ˆ q
  IH = V-id1' p q Ïƒ VA

  preim : âˆ€ ğ’ƒ x â†’ âˆ£ ğ‘¨ âˆ£
  preim ğ’ƒ x = (Inv Ï† (Ï†E (ğ’ƒ x)))

  Î¶ : âˆ€ ğ’ƒ â†’ Ï† âˆ˜ (preim ğ’ƒ) â‰¡ ğ’ƒ
  Î¶ ğ’ƒ = (fe ğ“§ ğ“•âº) Î» x â†’ InvIsInv Ï† (Ï†E (ğ’ƒ x))

  Goal : ğ‘© âŸ¦ p âŸ§ â‰¡ ğ‘© âŸ¦ q âŸ§
  Goal = (fe (ğ“§ âŠ” ğ“•âº) ğ“•âº) Î» ğ’ƒ
   â†’     (ğ‘© âŸ¦ p âŸ§) ğ’ƒ               â‰¡âŸ¨ (cong (ğ‘© âŸ¦ p âŸ§) (Î¶ ğ’ƒ))â»Â¹  âŸ©
         (ğ‘© âŸ¦ p âŸ§) (Ï† âˆ˜ (preim ğ’ƒ)) â‰¡âŸ¨ (comm-hom-term (fe ğ“¥ ğ“•âº) ğ‘© (Ï† , Ï†h) p (preim ğ’ƒ))â»Â¹ âŸ©
         Ï†((ğ‘¨ âŸ¦ p âŸ§) (preim ğ’ƒ))    â‰¡âŸ¨ cong Ï† (cong-app IH (preim ğ’ƒ))âŸ©
         Ï†((ğ‘¨ âŸ¦ q âŸ§) (preim ğ’ƒ))    â‰¡âŸ¨ comm-hom-term (fe ğ“¥ ğ“•âº) ğ‘© (Ï† , Ï†h) q (preim ğ’ƒ)âŸ©
         (ğ‘© âŸ¦ q âŸ§) (Ï† âˆ˜ (preim ğ’ƒ)) â‰¡âŸ¨ cong (ğ‘© âŸ¦ q âŸ§) (Î¶ ğ’ƒ)âŸ©
         (ğ‘© âŸ¦ q âŸ§) ğ’ƒ               âˆ

 V-id1' p q Ïƒ (vssubw {ğ‘¨}{ğ‘©} VA Bâ‰¤A) = âŠ§-S-invar fe ğ‘© {p}{q}(V-id1' p q Ïƒ VA) Bâ‰¤A
 V-id1' p q Ïƒ (vprodu{I}{ğ’œ} Vğ’œ) = âŠ§-P-invar ğ’œ fe {p}{q} Î» i â†’ V-id1 p q Ïƒ (Vğ’œ i)
 V-id1' p q Ïƒ (vprodw{I}{ğ’œ} Vğ’œ) = âŠ§-P-invar ğ’œ fe {p}{q} Î» i â†’ V-id1' p q Ïƒ (Vğ’œ i)
 V-id1' p q Ïƒ (visou {ğ‘¨}{ğ‘©} VA Aâ‰…B) = âŠ§-I-invar fe ğ‘© p q (V-id1 p q Ïƒ VA) Aâ‰…B
 V-id1' p q Ïƒ (visow{ğ‘¨}{ğ‘©} VA Aâ‰…B) = âŠ§-I-invar fe ğ‘© p q (V-id1' p q Ïƒ VA)Aâ‰…B

\end{code}


#### <a id="class-identities">Class identities</a>

From `V-id1` it follows that if ğ’¦ is a class of structures, then the set of identities modeled by all structures in `ğ’¦` is equivalent to the set of identities modeled by all structures in `V ğ’¦`.  In other terms, `Th (V ğ’¦)` is precisely the set of identities modeled by `ğ’¦`.   We formalize this observation as follows.

\begin{code}

 ğ’± : Pred (Algebra (ğ“•âº) ğ‘†) (lsuc ğ“•âº)
 ğ’± = V{Î² = ğ“•âº} ğ’¦

 class-ids-â‡’ : (p q : âˆ£ ğ‘» X âˆ£) â†’ ğ’¦ âŠ§ p â‰‹ q  â†’  (p , q) âˆˆ Th ğ’±
 class-ids-â‡’ p q pKq VCloA = V-id1' p q pKq VCloA


 class-ids-â‡ : (p q : âˆ£ ğ‘» X âˆ£) â†’ (p , q) âˆˆ Th ğ’± â†’  ğ’¦ âŠ§ p â‰‹ q
 class-ids-â‡ p q Thpq {ğ‘¨} KA = âŠ§-lower-invar fe p q (Thpq (vbase KA))

\end{code}


Once again, and for the last time, completeness dictates that we formalize the coverse of `V-id1`, however obvious it may be.

\begin{code}

module _ {Î² : Level}{X : Type ğ“§}{ğ’¦ : Pred (Algebra Î± ğ‘†)(ov Î±)} where

 V-id2 : (âˆ€ a b â†’ funext a b) â†’ (p q : Term X) â†’ (V{Î² = Î²} ğ’¦ âŠ§ p â‰‹ q) â†’ (ğ’¦ âŠ§ p â‰‹ q)
 V-id2 fe p q Vpq {ğ‘¨} KA = âŠ§-lower-invar fe p q (Vpq (vbase KA))

\end{code}


----------------------------

[â† Varieties.Varieties](Varieties.Varieties.html)
<span style="float:right;">[FreeAlgebras â†’](Varieties.FreeAlgebras.html)</span>

{% include UALib.Links.md %}

--------------------------------------

[the ualib/agda-algebras development team]: https://github.com/ualib/agda-algebras#the-ualib-agda-algebras-development-team








