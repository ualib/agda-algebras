---
layout: default
title : Varieties.Basic module (The Agda Universal Algebra Library)
date : 2021-01-14
author: [the ualib/agda-algebras development team][]
---

### Varieties, Model Theory, and Equational Logic

This section presents the [Varieties.Basic][] module of the [Agda Universal Algebra Library][] where the binary "models" relation âŠ§, relating algebras (or classes of algebras) to the identities that they satisfy, is defined.

Agda supports the definition of infix operations and relations, and we use this to define âŠ§ so that we may write, e.g., `ğ‘¨ âŠ§ p â‰ˆ q` or `ğ’¦ âŠ« p â‰ˆ q`.

We also prove some closure and invariance properties of âŠ§.  In particular, we prove the following facts (which are needed, for example, in the proof the Birkhoff HSP Theorem).

* [Algebraic invariance](#algebraic-invariance). The âŠ§ relation is an *algebraic invariant* (stable under isomorphism).

* [Subalgebraic invariance](#subalgebraic-invariance). Identities modeled by a class of algebras are also modeled by all subalgebras of algebras in the class.

* [Product invariance](#product-invariance). Identities modeled by a class of algebras are also modeled by all products of algebras in the class.



**Notation**. In the [Agda UniversalAlgebra][] library, because a class of structures has a different type than a single structure, we must use a slightly different syntax to avoid overloading the relations âŠ§ and â‰ˆ. As a reasonable alternative to what we would normally express informally as ğ’¦ âŠ§ ğ‘ â‰ˆ ğ‘, we have settled on ğ’¦ âŠ« p â‰ˆ q to denote this relation.  To reiterate, if ğ’¦ is a class of ğ‘†-algebras, we write ğ’¦ âŠ§ ğ‘ â‰‹ ğ‘ if every ğ‘¨ âˆˆ ğ’¦ satisfies ğ‘¨ âŠ§ ğ‘ â‰ˆ ğ‘.

**Unicode Hints**. To produce the symbols â‰ˆ, âŠ§, and â‰‹ in [agda2-mode][], type `\~~`, `\models`, and `\~~~`, respectively.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}


-- Imports from Agda (builtin/primitive) and the Agda Standard Library

open import Level using ( Level )
open import Algebras.Basic

module Varieties.Basic {ğ‘† : Signature ğ“ ğ“¥} where


-- imports from Agda and the Agda Standard Library -------------------------------------------
open import Agda.Builtin.Equality   using    ( _â‰¡_ ;  refl )
open import Agda.Primitive          using    ( _âŠ”_ ;  lsuc )
                                   renaming ( Set to Type
                                            ; lzero to  â„“â‚€       )
open import Axiom.Extensionality.Propositional
                                    renaming ( Extensionality to funext )
open import Data.Product            using    ( _,_ ; Î£-syntax ; Î£ ; _Ã—_ )
                                    renaming ( projâ‚ to fst
                                             ; projâ‚‚ to snd )
open import Function.Base           using    ( _âˆ˜_ )
open import Relation.Binary.PropositionalEquality
                                    using    ( cong ; cong-app
                                             ; module â‰¡-Reasoning)
open import Relation.Unary          using    ( Pred ; _âˆˆ_ ; _âŠ†_ ; â‹‚ )



-- imports from agda-algebras --------------------------------------------------------------
open import Overture.Preliminaries       using ( âˆ£_âˆ£ ; âˆ¥_âˆ¥ ; ğ‘–ğ‘‘ ; _â»Â¹ ; _â‰ˆ_ ; Î  ; Î -syntax)
open import Overture.Inverses            using ( IsInjective ; âˆ˜-injective )
open import Relations.Extensionality using (DFunExt; SwellDef)

open import Algebras.Products          {ğ‘† = ğ‘†} using ( ov ; â¨… )
open import Homomorphisms.Basic        {ğ‘† = ğ‘†} using ( hom; ğ’¾ğ’¹ ; âˆ˜-hom ; is-homomorphism )
open import Homomorphisms.Isomorphisms {ğ‘† = ğ‘†} using ( _â‰…_ ; â‰…-sym ; â‰…-trans ; Lift-â‰… )
open import Terms.Basic                {ğ‘† = ğ‘†} using ( Term ; ğ‘» ; lift-hom )
open import Terms.Operations           {ğ‘† = ğ‘†} using ( _âŸ¦_âŸ§ ; comm-hom-term
                                               ; interp-prod ; term-agreement )
open import Subalgebras.Subalgebras    {ğ‘† = ğ‘†} using ( _â‰¤_ ; SubalgebraOfClass ; isoâ†’injective )
open Term

\end{code}


#### <a id="the-models-relation">The models relation</a>

We define the binary "models" relation âŠ§ using infix syntax so that we may write, e.g., `ğ‘¨ âŠ§ p â‰ˆ q` or `ğ’¦ âŠ« p â‰ˆ q`, relating algebras (or classes of algebras) to the identities that they satisfy. We also prove a coupld of useful facts about âŠ§.  More will be proved about âŠ§ in the next module, [Varieties.EquationalLogic](Varieties.EquationalLogic.html).

\begin{code}


-- curried versions
-- (unicode: use \models and \~~ to get âŠ§ and â‰ˆ)
_âŠ§_â‰ˆ_ : {Ï‡ : Level}{X : Type Ï‡} â†’ {Î± : Level} â†’ Algebra Î± ğ‘† â†’ Term X â†’ Term X â†’ Type _
ğ‘¨ âŠ§ p â‰ˆ q = ğ‘¨ âŸ¦ p âŸ§ â‰ˆ ğ‘¨ âŸ¦ q âŸ§

-- (unicode: use \||= and \~~ to get âŠ« and â‰ˆ)
_âŠ«_â‰ˆ_ : {Ï‡ : Level}{X : Type Ï‡} â†’ {Î± Ï : Level} â†’ Pred(Algebra Î± ğ‘†) Ï â†’ Term X â†’ Term X â†’ Type _
ğ’¦ âŠ« p â‰ˆ q = {ğ‘¨ : Algebra _ ğ‘†} â†’ ğ’¦ ğ‘¨ â†’ ğ‘¨ âŠ§ p â‰ˆ q


\end{code}

##### <a id="semantics-of-âŠ§">Syntax and semantics of âŠ§</a>
The expression `ğ‘¨ âŠ§ p â‰ˆ q` represents the assertion that the identity `p â‰ˆ q` holds when interpreted in the algebra `ğ‘¨`; syntactically, `ğ‘¨ âŸ¦ p âŸ§ â‰¡ ğ‘¨ âŸ¦ q âŸ§`.  It should be emphasized that the expression  `ğ‘¨ âŸ¦ p âŸ§ â‰¡ ğ‘¨ âŸ¦ q âŸ§` interpreted computationally as an *extensional equality*, by which we mean that for each *assignment function*  `ğ’‚ :  X â†’ âˆ£ ğ‘¨ âˆ£`, assigning values in the domain of `ğ‘¨` to the variable symbols in `X`, we have `âŸ¦ p âŸ§ ğ‘¨ ğ’‚ â‰¡ âŸ¦ q âŸ§  ğ‘¨ ğ’‚`.



#### <a id="equational-theories-and-classes">Equational theories and models</a>

Here we define a type `Th` so that, if ğ’¦ denotes a class of algebras, then `Th ğ’¦` represents the set of identities modeled by all members of ğ’¦.

\begin{code}

module _ {Ï‡ : Level}{X : Type Ï‡} where

 Th : {Î± : Level} â†’ Pred (Algebra Î± ğ‘†) (ov Î±) â†’ Pred(Term X Ã— Term X) (Ï‡ âŠ” ov Î±)
 Th ğ’¦ = Î» (p , q) â†’ ğ’¦ âŠ« p â‰ˆ q

\end{code}

If `â„°` denotes a set of identities, then the class of algebras satisfying all identities in â„° is represented by `Mod â„°`, which we define in the following natural way.

\begin{code}

 Mod : {Î± : Level} â†’ Pred(Term X Ã— Term X) (ov Î±) â†’ Pred(Algebra Î± ğ‘†) (ov (Ï‡ âŠ” Î±))
 Mod â„° = Î» ğ‘¨ â†’ âˆ€ p q â†’ (p , q) âˆˆ â„° â†’ ğ‘¨ âŠ§ p â‰ˆ q

 -- tupled version
 Modtup : {Î¹ : Level}{I : Type Î¹} â†’ (I â†’ Term X Ã— Term X) â†’ {Î± : Level} â†’ Pred(Algebra Î± ğ‘†)(Ï‡ âŠ” Î¹ âŠ” Î±)
 Modtup â„° = Î» ğ‘¨ â†’ âˆ€ i â†’ ğ‘¨ âŠ§ (fst (â„° i)) â‰ˆ (snd (â„° i))

\end{code}

The entailment â„° âŠ¢ p â‰ˆ q is valid iff p â‰ˆ q holds in all models that satify all equations in â„°.

\begin{code}

module _ {Ï‡ : Level}{X : Type Ï‡}{Î± : Level} where

 _âŠ¢_â‰ˆ_ : Pred(Term X Ã— Term X) (ov Î±) â†’ Term X â†’ Term X â†’ Type (ov (Ï‡ âŠ” Î±))
 â„° âŠ¢ p â‰ˆ q = Mod {Î± = Î±} â„° âŠ« p â‰ˆ q

\end{code}


#### Derivations in a "context" X

This section on derivations is an adaptation of Andreas Abel's `Sub`, `_[_]`, and `_âŠ¢_â–¹_â‰¡` types.

Quoting Abel, "Equalitional logic allows us to prove entailments via the inference rules for the judgment E âŠ¢ Î“ â–¹ p â‰¡ q. This could be coined as equational theory over a given set of equations $E$. Relation E âŠ¢ Î“ â–¹ _â‰¡_ is the least congruence over the equations E."

\begin{code}

-- Substitutions. A substitution from Y to X holds a term in X for each variable in Y.
Subst : {Ï‡ : Level}(Y X : Type Ï‡) â†’ Type _
Subst Y X = (x : X) â†’ Term Y

-- Application of a substitution.
_[_] : {Ï‡ : Level}{Y X : Type Ï‡}(t : Term Y) (Ïƒ : Subst X Y) â†’ Term X
(â„Š x) [ Ïƒ ] = Ïƒ x
(node ğ‘“ t)  [ Ïƒ ] = node ğ‘“ Î» i â†’ t i [ Ïƒ ]

module _ {Ï‡ : Level}{X Y : Type Ï‡}
         {Î¹ : Level}{I : Type Î¹} where

 variable
  p q r :  Term X
  op : âˆ£ ğ‘† âˆ£
  ts ts' : {X : Type Ï‡}(i : âˆ¥ ğ‘† âˆ¥ op) â†’ Term X

 data _âŠ¢_â–¹_â‰ˆ_
  (â„° : {X : Type Ï‡} â†’ I â†’ Term X Ã— Term X) : (X : Type Ï‡)(p q : Term X) â†’ Type (Î¹ âŠ” ov Ï‡) where
  hyp   :  âˆ€ i                               â†’ â„° âŠ¢ X â–¹ (fst (â„° i)) â‰ˆ (snd (â„° i))
  base  :  âˆ€ (x : X)                         â†’ â„° âŠ¢ X â–¹ â„Š x â‰ˆ â„Š x
  app   :  (âˆ€ i â†’ â„° âŠ¢ X â–¹ ts i â‰ˆ ts' i)      â†’ â„° âŠ¢ X â–¹ (node op ts) â‰ˆ (node op ts')
  sub   :  â„° âŠ¢ X â–¹ p â‰ˆ q â†’ âˆ€ (Ïƒ : Subst Y X) â†’ â„° âŠ¢ Y â–¹ (p [ Ïƒ ]) â‰ˆ (q [ Ïƒ ])
  refl  :  âˆ€ (t : Term X)                    â†’ â„° âŠ¢ X â–¹ t â‰ˆ t
  sym   :  â„° âŠ¢ X â–¹ p â‰ˆ q                     â†’ â„° âŠ¢ X â–¹ q â‰ˆ p
  trans :  â„° âŠ¢ X â–¹ p â‰ˆ q â†’ â„° âŠ¢ XÂ â–¹ q â‰ˆ r     â†’ â„° âŠ¢ X â–¹ p â‰ˆ r

\end{code}



#### <a id="algebraic-invariance-of-models">Algebraic invariance of âŠ§</a>

The binary relation âŠ§ would be practically useless if it were not an *algebraic invariant* (i.e., invariant under isomorphism).

\begin{code}

open â‰¡-Reasoning

module _ (wd : SwellDef){Î± Î² Ï‡ : Level}{X : Type Ï‡}{ğ‘¨ : Algebra Î± ğ‘†}
         (ğ‘© : Algebra Î² ğ‘†)(p q : Term X) where

 âŠ§-I-invar : ğ‘¨ âŠ§ p â‰ˆ q  â†’  ğ‘¨ â‰… ğ‘©  â†’  ğ‘© âŠ§ p â‰ˆ q

 âŠ§-I-invar Apq (f , g , fâˆ¼g , gâˆ¼f) x =
  (ğ‘© âŸ¦ p âŸ§) x                      â‰¡âŸ¨ wd Ï‡ Î² (ğ‘© âŸ¦ p âŸ§) x (âˆ£ f âˆ£ âˆ˜ âˆ£ g âˆ£ âˆ˜ x) (Î» i â†’ ( fâˆ¼g (x i))â»Â¹) âŸ©
  (ğ‘© âŸ¦ p âŸ§) ((âˆ£ f âˆ£ âˆ˜ âˆ£ g âˆ£) âˆ˜ x)  â‰¡âŸ¨ (comm-hom-term (wd ğ“¥ Î²) ğ‘© f p (âˆ£ g âˆ£ âˆ˜ x))â»Â¹ âŸ©
  âˆ£ f âˆ£ ((ğ‘¨ âŸ¦ p âŸ§) (âˆ£ g âˆ£ âˆ˜ x))    â‰¡âŸ¨ cong âˆ£ f âˆ£ (Apq (âˆ£ g âˆ£ âˆ˜ x))  âŸ©
  âˆ£ f âˆ£ ((ğ‘¨ âŸ¦ q âŸ§) (âˆ£ g âˆ£ âˆ˜ x))    â‰¡âŸ¨ comm-hom-term (wd ğ“¥ Î²) ğ‘© f q (âˆ£ g âˆ£ âˆ˜ x) âŸ©
  (ğ‘© âŸ¦ q âŸ§) ((âˆ£ f âˆ£ âˆ˜ âˆ£ g âˆ£) âˆ˜  x) â‰¡âŸ¨ wd Ï‡ Î² (ğ‘© âŸ¦ q âŸ§) (âˆ£ f âˆ£ âˆ˜ âˆ£ g âˆ£ âˆ˜ x) x (Î» i â†’ ( fâˆ¼g (x i))) âŸ©
  (ğ‘© âŸ¦ q âŸ§) x                      âˆ

\end{code}


 As the proof makes clear, we show ğ‘© âŠ§ p â‰ˆ q by showing that `ğ‘© âŸ¦ p âŸ§ â‰¡ ğ‘© âŸ¦ q âŸ§ holds *extensionally*, that is, `âˆ€ x, ğ‘© âŸ¦ p âŸ§ x â‰¡ ğ‘© âŸ¦q âŸ§ x`.

#### <a id="lift-invariance">Lift-invariance of âŠ§</a>
The âŠ§ relation is also invariant under the algebraic lift and lower operations.

\begin{code}

module _ (wd : SwellDef){Î± Î² Ï‡ : Level}{X : Type Ï‡}{ğ‘¨ : Algebra Î± ğ‘†} where

 âŠ§-Lift-invar : (p q : Term X) â†’ ğ‘¨ âŠ§ p â‰ˆ q â†’ Lift-Alg ğ‘¨ Î² âŠ§ p â‰ˆ q
 âŠ§-Lift-invar p q Apq = âŠ§-I-invar wd (Lift-Alg ğ‘¨ _) p q Apq Lift-â‰…

 âŠ§-lower-invar : (p q : Term X) â†’ Lift-Alg ğ‘¨ Î² âŠ§ p â‰ˆ q  â†’  ğ‘¨ âŠ§ p â‰ˆ q
 âŠ§-lower-invar p q lApq = âŠ§-I-invar wd ğ‘¨ p q lApq (â‰…-sym Lift-â‰…)

\end{code}





#### <a id="subalgebraic-invariance">Subalgebraic invariance of âŠ§</a>

Identities modeled by an algebra `ğ‘¨` are also modeled by every subalgebra of `ğ‘¨`, which fact can be formalized as follows.

\begin{code}

module _ (wd : SwellDef){Ï‡ : Level}{ğ“¤ ğ“¦ : Level}{X : Type Ï‡} where

 âŠ§-S-invar : {ğ‘¨ : Algebra ğ“¤ ğ‘†}(ğ‘© : Algebra ğ“¦ ğ‘†){p q : Term X}
  â†’          ğ‘¨ âŠ§ p â‰ˆ q  â†’  ğ‘© â‰¤ ğ‘¨  â†’  ğ‘© âŠ§ p â‰ˆ q
 âŠ§-S-invar {ğ‘¨} ğ‘© {p}{q} Apq Bâ‰¤A b = (âˆ¥ Bâ‰¤A âˆ¥) (Î¾ b)
  where
  h : hom ğ‘© ğ‘¨
  h = âˆ£ Bâ‰¤A âˆ£

  Î¾ : âˆ€ b â†’ âˆ£ h âˆ£ ((ğ‘© âŸ¦ p âŸ§) b) â‰¡ âˆ£ h âˆ£ ((ğ‘© âŸ¦ q âŸ§) b)
  Î¾ b = âˆ£ h âˆ£((ğ‘© âŸ¦ p âŸ§) b)   â‰¡âŸ¨ comm-hom-term (wd ğ“¥ ğ“¤) ğ‘¨ h p b âŸ©
        (ğ‘¨ âŸ¦ p âŸ§)(âˆ£ h âˆ£ âˆ˜ b) â‰¡âŸ¨ Apq (âˆ£ h âˆ£ âˆ˜ b) âŸ©
        (ğ‘¨ âŸ¦ q âŸ§)(âˆ£ h âˆ£ âˆ˜ b) â‰¡âŸ¨ (comm-hom-term (wd ğ“¥ ğ“¤) ğ‘¨ h q b)â»Â¹ âŸ©
        âˆ£ h âˆ£((ğ‘© âŸ¦ q âŸ§) b)   âˆ

\end{code}

 Next, identities modeled by a class of algebras is also modeled by all subalgebras of the class.  In other terms, every term equation `p â‰ˆ q` that is satisfied by all `ğ‘¨ âˆˆ ğ’¦` is also satisfied by every subalgebra of a member of ğ’¦.

 \begin{code}

 âŠ§-S-class-invar : {ğ’¦ : Pred (Algebra ğ“¤ ğ‘†)(ov ğ“¤)}(p q : Term X)
  â†’                ğ’¦ âŠ« p â‰ˆ q â†’ (ğ‘© : SubalgebraOfClass ğ’¦) â†’ âˆ£ ğ‘© âˆ£ âŠ§ p â‰ˆ q
 âŠ§-S-class-invar p q Kpq (ğ‘© , ğ‘¨ , SA , (ka , BisSA)) = âŠ§-S-invar ğ‘© {p}{q}((Kpq ka)) (h , hinj)
  where
  h : hom ğ‘© ğ‘¨
  h = âˆ˜-hom ğ‘© ğ‘¨ (âˆ£ BisSA âˆ£) âˆ£ snd SA âˆ£
  hinj : IsInjective âˆ£ h âˆ£
  hinj = âˆ˜-injective (isoâ†’injective BisSA) âˆ¥ snd SA âˆ¥

\end{code}


 #### <a id="product-invariance">Product invariance of âŠ§</a>

 An identity satisfied by all algebras in an indexed collection is also satisfied by the product of algebras in that collection.

 \begin{code}

module _ (fe : DFunExt)(wd : SwellDef){Î± Î² Ï‡ : Level}{I : Type Î²}(ğ’œ : I â†’ Algebra Î± ğ‘†){X : Type Ï‡} where

 âŠ§-P-invar : (p q : Term X) â†’ (âˆ€ i â†’ ğ’œ i âŠ§ p â‰ˆ q) â†’ â¨… ğ’œ âŠ§ p â‰ˆ q
 âŠ§-P-invar p q ğ’œpq a = goal
  where
  -- This is where function extensionality is used.
  Î¾ : (Î» i â†’ (ğ’œ i âŸ¦ p âŸ§) (Î» x â†’ (a x) i)) â‰¡ (Î» i â†’ (ğ’œ i âŸ¦ q âŸ§)  (Î» x â†’ (a x) i))
  Î¾ = fe Î² Î± Î» i â†’ ğ’œpq i (Î» x â†’ (a x) i)

  goal : (â¨… ğ’œ âŸ¦ p âŸ§) a  â‰¡  (â¨… ğ’œ âŸ¦ q âŸ§) a
  goal = (â¨… ğ’œ âŸ¦ p âŸ§) a                      â‰¡âŸ¨ interp-prod (wd ğ“¥ (Î± âŠ” Î²)) p ğ’œ a âŸ©
      (Î» i â†’ (ğ’œ i âŸ¦ p âŸ§)(Î» x â†’ (a x)i))  â‰¡âŸ¨ Î¾ âŸ©
      (Î» i â†’ (ğ’œ i âŸ¦ q âŸ§)(Î» x â†’ (a x)i))  â‰¡âŸ¨ (interp-prod (wd ğ“¥ (Î± âŠ” Î²)) q ğ’œ a)â»Â¹ âŸ©
      (â¨… ğ’œ âŸ¦ q âŸ§) a                      âˆ

\end{code}

An identity satisfied by all algebras in a class is also satisfied by the product of algebras in the class.

\begin{code}

 âŠ§-P-class-invar : (ğ’¦ : Pred (Algebra Î± ğ‘†)(ov Î±)){p q : Term X}
  â†’                ğ’¦ âŠ« p â‰ˆ q â†’ (âˆ€ i â†’ ğ’œ i âˆˆ ğ’¦) â†’ â¨… ğ’œ âŠ§ p â‰ˆ q

 âŠ§-P-class-invar ğ’¦ {p}{q}Ïƒ Kğ’œ = âŠ§-P-invar p q Î» i â†’ Ïƒ (Kğ’œ i)


\end{code}

Another fact that will turn out to be useful is that a product of a collection of algebras models p â‰ˆ q if the lift of each algebra in the collection models p â‰ˆ q.

\begin{code}

 âŠ§-P-lift-invar : (p q : Term X) â†’ (âˆ€ i â†’ Lift-Alg (ğ’œ i) Î² âŠ§ p â‰ˆ q)  â†’  â¨… ğ’œ âŠ§ p â‰ˆ q
 âŠ§-P-lift-invar p q Î± = âŠ§-P-invar p q Aipq
  where
  Aipq : âˆ€ i â†’ (ğ’œ i) âŠ§ p â‰ˆ q
  Aipq i = âŠ§-lower-invar wd p q (Î± i) --  (â‰…-sym Lift-â‰…)

\end{code}


#### <a id="homomorphisc-invariance">Homomorphic invariance of âŠ§</a>

If an algebra ğ‘¨ models an identity p â‰ˆ q, then the pair (p , q) belongs to the kernel of every homomorphism Ï† : hom (ğ‘» X) ğ‘¨ from the term algebra to ğ‘¨; that is, every homomorphism from ğ‘» X to ğ‘¨ maps p and q to the same element of ğ‘¨.

 \begin{code}

module _ (wd : SwellDef){Î± Ï‡ : Level}{X : Type Ï‡}{ğ‘¨ : Algebra Î± ğ‘†} where

 âŠ§-H-invar : {p q : Term X}(Ï† : hom (ğ‘» X) ğ‘¨) â†’ ğ‘¨ âŠ§ p â‰ˆ q  â†’  âˆ£ Ï† âˆ£ p â‰¡ âˆ£ Ï† âˆ£ q

 âŠ§-H-invar {p}{q}Ï† Î² = âˆ£ Ï† âˆ£ p               â‰¡âŸ¨ cong âˆ£ Ï† âˆ£(term-agreement(wd ğ“¥ (ov Ï‡)) p)âŸ©
                       âˆ£ Ï† âˆ£((ğ‘» X âŸ¦ p âŸ§) â„Š)  â‰¡âŸ¨ comm-hom-term (wd ğ“¥ Î±) ğ‘¨ Ï† p â„Š âŸ©
                       (ğ‘¨ âŸ¦ p âŸ§) (âˆ£ Ï† âˆ£ âˆ˜ â„Š) â‰¡âŸ¨ Î² (âˆ£ Ï† âˆ£ âˆ˜ â„Š ) âŸ©
                       (ğ‘¨ âŸ¦ q âŸ§) (âˆ£ Ï† âˆ£ âˆ˜ â„Š) â‰¡âŸ¨ (comm-hom-term (wd ğ“¥ Î±)  ğ‘¨ Ï† q â„Š )â»Â¹ âŸ©
                       âˆ£ Ï† âˆ£ ((ğ‘» X âŸ¦ q âŸ§) â„Š) â‰¡âŸ¨(cong âˆ£ Ï† âˆ£ (term-agreement (wd ğ“¥ (ov Ï‡)) q))â»Â¹ âŸ©
                       âˆ£ Ï† âˆ£ q               âˆ


\end{code}

More generally, an identity is satisfied by all algebras in a class if and only if that identity is invariant under all homomorphisms from the term algebra `ğ‘» X` into algebras of the class. More precisely, if `ğ’¦` is a class of `ğ‘†`-algebras and `ğ‘`, `ğ‘` terms in the language of `ğ‘†`, then,

```
  ğ’¦ âŠ§ p â‰ˆ q  â‡”  âˆ€ ğ‘¨ âˆˆ ğ’¦,  âˆ€ Ï† : hom (ğ‘» X) ğ‘¨,  Ï† âˆ˜ (ğ‘» X)âŸ¦ p âŸ§ = Ï† âˆ˜ (ğ‘» X)âŸ¦ q âŸ§.
```

\begin{code}

module _ (wd : SwellDef){Î± Ï‡ : Level}{X : Type Ï‡}{ğ’¦ : Pred (Algebra Î± ğ‘†)(ov Î±)}  where

 -- â‡’ (the "only if" direction)
 âŠ§-H-class-invar : {p q : Term X}
  â†’                ğ’¦ âŠ« p â‰ˆ q â†’ âˆ€ ğ‘¨ Ï† â†’ ğ‘¨ âˆˆ ğ’¦ â†’ âˆ€ a â†’ âˆ£ Ï† âˆ£ ((ğ‘» X âŸ¦ p âŸ§) a) â‰¡ âˆ£ Ï† âˆ£ ((ğ‘» X âŸ¦ q âŸ§) a)
 âŠ§-H-class-invar {p = p}{q} Ïƒ ğ‘¨ Ï† ka a = Î¾
  where
   Î¾ : âˆ£ Ï† âˆ£ ((ğ‘» X âŸ¦ p âŸ§) a) â‰¡ âˆ£ Ï† âˆ£ ((ğ‘» X âŸ¦ q âŸ§) a)
   Î¾ = âˆ£ Ï† âˆ£ ((ğ‘» X âŸ¦ p âŸ§) a)  â‰¡âŸ¨ comm-hom-term (wd ğ“¥ Î±) ğ‘¨ Ï† p a âŸ©
         (ğ‘¨ âŸ¦ p âŸ§)(âˆ£ Ï† âˆ£ âˆ˜ a)   â‰¡âŸ¨ (Ïƒ ka) (âˆ£ Ï† âˆ£ âˆ˜ a) âŸ©
         (ğ‘¨ âŸ¦ q âŸ§)(âˆ£ Ï† âˆ£ âˆ˜ a)   â‰¡âŸ¨ (comm-hom-term (wd ğ“¥ Î±) ğ‘¨ Ï† q a)â»Â¹ âŸ©
         âˆ£ Ï† âˆ£ ((ğ‘» X âŸ¦ q âŸ§) a)  âˆ


-- â‡ (the "if" direction)
 âŠ§-H-class-coinvar : {p q : Term X}
  â†’  (âˆ€ ğ‘¨ Ï† â†’ ğ‘¨ âˆˆ ğ’¦ â†’ âˆ€ a â†’ âˆ£ Ï† âˆ£ ((ğ‘» X âŸ¦ p âŸ§) a) â‰¡ âˆ£ Ï† âˆ£ ((ğ‘» X âŸ¦ q âŸ§) a)) â†’ ğ’¦ âŠ« p â‰ˆ q

 âŠ§-H-class-coinvar {p}{q} Î² {ğ‘¨} ka = Î³
  where
  Ï† : (a : X â†’ âˆ£ ğ‘¨ âˆ£) â†’ hom (ğ‘» X) ğ‘¨
  Ï† a = lift-hom ğ‘¨ a

  Î³ : ğ‘¨ âŠ§ p â‰ˆ q
  Î³ a = (ğ‘¨ âŸ¦ p âŸ§)(âˆ£ Ï† a âˆ£ âˆ˜ â„Š)     â‰¡âŸ¨(comm-hom-term (wd ğ“¥ Î±) ğ‘¨ (Ï† a) p â„Š)â»Â¹ âŸ©
               (âˆ£ Ï† a âˆ£ âˆ˜ (ğ‘» X âŸ¦ p âŸ§)) â„Š  â‰¡âŸ¨ Î² ğ‘¨ (Ï† a) ka â„Š âŸ©
               (âˆ£ Ï† a âˆ£ âˆ˜ (ğ‘» X âŸ¦ q âŸ§)) â„Š  â‰¡âŸ¨ (comm-hom-term (wd ğ“¥ Î±) ğ‘¨ (Ï† a) q â„Š) âŸ©
               (ğ‘¨ âŸ¦ q âŸ§)(âˆ£ Ï† a âˆ£ âˆ˜ â„Š)     âˆ


\end{code}





-------------------------------------

[â†‘ Varieties](Varieties.html)
<span style="float:right;">[Varieties.EquationalLogic â†’](Varieties.EquationalLogic.html)</span>

{% include UALib.Links.md %}


--------------------------------------

[the ualib/agda-algebras development team]: https://github.com/ualib/agda-algebras#the-ualib-agda-algebras-development-team














<!--

  -- open import Relation.Binary.Core using (_â‡”_)

  -- âŠ§-H : DFunExt â†’ {p q : Term X} â†’ ğ’¦ âŠ« p â‰ˆ q â‡” (âˆ€ ğ‘¨ Ï† â†’ ğ‘¨ âˆˆ ğ’¦ â†’ âˆ£ Ï† âˆ£ âˆ˜ (ğ‘» X âŸ¦ p âŸ§) â‰¡ âˆ£ Ï† âˆ£ âˆ˜(ğ‘» X âŸ¦ q âŸ§))
  -- âŠ§-H fe {p}{q} = âŠ§-H-class-invar fe {p}{q} , âŠ§-H-class-coinvar fe {p}{q}


-->
Soundness of the inference rules. We assume a model ğ‘¨ that validates all equations in â„°.


\begin{code}

-- module Soundness {Î¹ : Level}{I : Type Î¹}
--                  {Ï‡ : Level}(â„° : {X : Type Ï‡} â†’ I â†’ Term X Ã— Term X)
--                  {Î± : Level}(ğ‘¨ : Algebra Î± ğ‘†)
--                  (Amod : ğ‘¨ âˆˆ Modtup â„°) where

--  -- In any ğ‘¨ âˆˆ Mod â„°, derived equality is actual equality.

--  sound : {X : Type Ï‡}{p q : Term X} â†’ â„° âŠ¢ X â–¹ p â‰ˆ q â†’ ğ‘¨ âŠ§ p â‰ˆ q
--  sound x = {!!}

\end{code}
 -- (hyp i)                        =  V i
 --    sound (app {op = op} es) Ï           =  den .cong (refl , Î» i â†’ sound (es i) Ï)
 --    sound (sub {t = t} {t' = t'} e Ïƒ) Ï  =  begin
 --      â¦… t [ Ïƒ ]   â¦† .apply Ï   â‰ˆâŸ¨ substitution {M = M} t Ïƒ Ï âŸ©
 --      â¦… t         â¦† .apply Ï'  â‰ˆâŸ¨ sound e Ï' âŸ©
 --      â¦… t'        â¦† .apply Ï'  â‰ˆË˜âŸ¨ substitution {M = M} t' Ïƒ Ï âŸ©
 --      â¦… t' [ Ïƒ ]  â¦† .apply Ï   âˆ
 --      where
 --      open SetoidReasoning Den
 --      Ï' = â¦… Ïƒ â¦†s Ï

 --    sound (base x {f} {f'})              =  isEquiv {M = M} .IsEquivalence.refl {var' x Î»()}

 --    sound (refl t)                       =  isEquiv {M = M} .IsEquivalence.refl {t}
 --    sound (sym {t = t} {t' = t'} e)      =  isEquiv {M = M} .IsEquivalence.sym
 --                                            {x = t} {y = t'} (sound e)
 --    sound (trans  {tâ‚ = tâ‚} {tâ‚‚ = tâ‚‚}
 --                  {tâ‚ƒ = tâ‚ƒ} e e')        =  isEquiv {M = M} .IsEquivalence.trans
 --                                            {i = tâ‚} {j = tâ‚‚} {k = tâ‚ƒ} (sound e) (sound e')



