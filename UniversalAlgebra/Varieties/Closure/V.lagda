---
layout: default
title : Varieties.Closure.V module (The Agda Universal Algebra Library)
date : 2021-01-14
author: [the ualib/agda-algebras development team][]
---

### <a id="the-inductive-types-v">The Inductive Types V</a>

A class ğ’¦ of ğ‘†-algebras is called a **variety** if it is closed under each of the closure operators H, S, and P introduced elsewhere; the corresponding closure operator is often denoted ğ•, but we will denote it by `V`.

We now define `V` as an inductive type which is hand-crafted to contain all the parts of H, S and P, under different names.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Level using ( Level ; Lift )
open import Algebras.Basic

module Varieties.Closure.V {ğ‘† : Signature ğ“ ğ“¥} where

open import Axiom.Extensionality.Propositional renaming ( Extensionality to funext )
open import Agda.Primitive   using    ( _âŠ”_ ;  lsuc )
                             renaming ( Set to Type )
open import Data.Product     using    ( _,_ ; Î£-syntax )
open import Relation.Unary   using    ( Pred ; _âˆˆ_ ; _âŠ†_)

open import Overture.Preliminaries            using ( âˆ£_âˆ£ ; âˆ¥_âˆ¥ )
open import Algebras.Products               {ğ‘† = ğ‘†} using ( ov ; â¨… )
open import Homomorphisms.HomomorphicImages {ğ‘† = ğ‘†} using ( HomImages ; _IsHomImageOf_ ; Lift-Alg-hom-image )
open import Homomorphisms.Isomorphisms      {ğ‘† = ğ‘†} using ( _â‰…_ ; Lift-Alg-associative ; Lift-â‰… ; Lift-hom
                                                    ; Lift-Alg-â¨…â‰… ; â‰…-sym ; Lift-Alg-iso )
open import Subalgebras.Subalgebras         {ğ‘† = ğ‘†} using ( _â‰¤_ ; Lift-â‰¤-Lift )


data V {Î± Î² : Level}(ğ’¦ : Pred(Algebra Î± ğ‘†)(ov Î±)) : Pred(Algebra(Î± âŠ” Î²)ğ‘†)(ov(Î± âŠ” Î²))
 where
 vbase  : {ğ‘¨ : Algebra Î± ğ‘†} â†’ ğ‘¨ âˆˆ ğ’¦ â†’ Lift-Alg ğ‘¨ Î² âˆˆ V ğ’¦
 vlift  : {ğ‘¨ : Algebra Î± ğ‘†} â†’ ğ‘¨ âˆˆ V{Î±}{Î±} ğ’¦ â†’ Lift-Alg ğ‘¨ Î² âˆˆ V ğ’¦
 vliftw : {ğ‘¨ : Algebra _ ğ‘†} â†’ ğ‘¨ âˆˆ V{Î±}{Î²} ğ’¦ â†’ Lift-Alg ğ‘¨ (Î± âŠ” Î²) âˆˆ V ğ’¦
 vhimg  : {ğ‘¨ ğ‘© : Algebra _ ğ‘†} â†’ ğ‘¨ âˆˆ V{Î±}{Î²} ğ’¦ â†’ ((ğ‘© , _) : HomImages ğ‘¨) â†’ ğ‘© âˆˆ V ğ’¦
 vssubw : {ğ‘¨ ğ‘© : Algebra _ ğ‘†} â†’ ğ‘¨ âˆˆ V{Î±}{Î²} ğ’¦ â†’ ğ‘© â‰¤ ğ‘¨ â†’ ğ‘© âˆˆ V ğ’¦
 vprodu : {I : Type Î²}{ğ’œ : I â†’ Algebra Î± ğ‘†} â†’ (âˆ€ i â†’ (ğ’œ i) âˆˆ V{Î±}{Î±} ğ’¦) â†’ â¨… ğ’œ âˆˆ V ğ’¦
 vprodw : {I : Type Î²}{ğ’œ : I â†’ Algebra _ ğ‘†} â†’ (âˆ€ i â†’ (ğ’œ i) âˆˆ V{Î±}{Î²} ğ’¦) â†’ â¨… ğ’œ âˆˆ V ğ’¦
 visou  : {ğ‘¨ : Algebra Î± ğ‘†}{ğ‘© : Algebra _ ğ‘†} â†’ ğ‘¨ âˆˆ V{Î±}{Î±} ğ’¦ â†’ ğ‘¨ â‰… ğ‘© â†’ ğ‘© âˆˆ V ğ’¦
 visow  : {ğ‘¨ ğ‘© : Algebra _ ğ‘†} â†’ ğ‘¨ âˆˆ V{Î±}{Î²} ğ’¦ â†’ ğ‘¨ â‰… ğ‘© â†’ ğ‘© âˆˆ V ğ’¦

\end{code}

Thus, if ğ’¦ is a class of ğ‘†-algebras, then the **variety generated by** ğ’¦ is denoted by `V ğ’¦` and defined to be the smallest class that contains ğ’¦ and is closed under `H`, `S`, and `P`.

With the closure operator V representing closure under HSP, we represent formally what it means to be a variety of algebras as follows.

\begin{code}

is-variety : {Î± : Level}(ğ’± : Pred (Algebra Î± ğ‘†)(ov Î±)) â†’ Type(ov Î±)
is-variety{Î±} ğ’± = V{Î±}{Î±} ğ’± âŠ† ğ’±

variety : (Î± : Level) â†’ Type(lsuc (ğ“ âŠ” ğ“¥ âŠ” (lsuc Î±)))
variety Î± = Î£[ ğ’± âˆˆ (Pred (Algebra Î± ğ‘†)(ov Î±)) ] is-variety ğ’±

\end{code}


#### <a id="V-is-closed-under-lift">V is closed under lift</a>

As mentioned earlier, a technical hurdle that must be overcome when formalizing proofs in Agda is the proper handling of universe levels. In particular, in the proof of the Birkhoff's theorem, for example, we will need to know that if an algebra ğ‘¨ belongs to the variety V ğ’¦, then so does the lift of ğ‘¨.  Let us get the tedious proof of this technical lemma out of the way.

\begin{code}

open Level

module Vlift {Î± : Level} {feâ‚€ : funext (ov Î±) Î±}
             {feâ‚ : funext ((ov Î±) âŠ” (lsuc (ov Î±))) (lsuc (ov Î±))}
             {feâ‚‚ : funext (ov Î±) (ov Î±)}
             {ğ’¦ : Pred (Algebra Î± ğ‘†)(ov Î±)} where

 VlA : {ğ‘¨ : Algebra (ov Î±) ğ‘†} â†’ ğ‘¨ âˆˆ V{Î±}{ov Î±} ğ’¦
  â†’    Lift-Alg ğ‘¨ (lsuc (ov Î±)) âˆˆ V{Î±}{lsuc (ov Î±)} ğ’¦
 VlA (vbase{ğ‘¨} x) = visow (vbase x) (Lift-Alg-associative ğ‘¨)
 VlA (vlift{ğ‘¨} x) = visow (vlift x) (Lift-Alg-associative ğ‘¨)
 VlA (vliftw{ğ‘¨} x) = visow (VlA x) (Lift-Alg-associative ğ‘¨)

 VlA (vhimg{ğ‘¨}{ğ‘©} x hB) = vhimg {ğ‘© = Lift-Alg ğ‘© (lsuc (ov Î±))} (VlA x) (lC , lChi)
  where
  lC : Algebra (lsuc (ov(Î±))) ğ‘†
  lC = Lift-Alg âˆ£ hB âˆ£ (lsuc (ov(Î±)))
  lChi : lC IsHomImageOf _
  lChi = (Lift-Alg-hom-image (lsuc (ov(Î±))) {âˆ£ hB âˆ£} (lsuc (ov(Î±))) âˆ¥ hB âˆ¥)

 VlA (vssubw{ğ‘¨}{ğ‘©} x Bâ‰¤A) = vssubw (VlA x) (Lift-â‰¤-Lift  (lsuc (ov(Î±))) {ğ‘¨}  (lsuc (ov(Î±))) Bâ‰¤A)
 VlA (vprodu{I}{ğ’œ} x) = visow (vprodw vlA) (â‰…-sym Bâ‰…A)
  where
  ğ‘° : Type (lsuc (ov Î±))
  ğ‘° = Lift (lsuc (ov Î±)) I

  lA : ğ‘° â†’ Algebra (lsuc (ov Î±)) ğ‘†
  lA i = Lift-Alg (ğ’œ (lower i)) (lsuc (ov Î±))

  vlA : âˆ€ i â†’ (lA i) âˆˆ V{Î±}{lsuc (ov Î±)} ğ’¦
  vlA i = vlift (x (lower i))

  iso-components : âˆ€ i â†’ ğ’œ i â‰… lA (lift i)
  iso-components i = Lift-â‰…

  Bâ‰…A : Lift-Alg (â¨… ğ’œ) (lsuc (ov Î±)) â‰… â¨… lA
  Bâ‰…A = Lift-Alg-â¨…â‰…  {fizw = feâ‚}{fiu = feâ‚€} iso-components


 VlA (vprodw{I}{ğ’œ} x) = visow (vprodw vlA) (â‰…-sym Bâ‰…A)
  where
  ğ‘° : Type (lsuc (ov Î±))
  ğ‘° = Lift (lsuc (ov Î±)) I

  lA : ğ‘° â†’ Algebra (lsuc (ov Î±)) ğ‘†
  lA i = Lift-Alg (ğ’œ (lower i)) (lsuc (ov Î±))

  vlA : âˆ€ i â†’ (lA i) âˆˆ V{Î±}{lsuc (ov Î±)} ğ’¦
  vlA i = VlA (x (lower i))

  iso-components : âˆ€ i â†’ ğ’œ i â‰… lA (lift i)
  iso-components i = Lift-â‰…

  Bâ‰…A : Lift-Alg (â¨… ğ’œ) (lsuc (ov Î±)) â‰… â¨… lA
  Bâ‰…A = Lift-Alg-â¨…â‰… {fizw = feâ‚}{fiu = feâ‚‚} iso-components

 VlA (visou{ğ‘¨}{ğ‘©} x Aâ‰…B) = visow (vlift x) (Lift-Alg-iso Aâ‰…B)
 VlA (visow{ğ‘¨}{ğ‘©} x Aâ‰…B) = visow (VlA x) (Lift-Alg-iso Aâ‰…B)

\end{code}

--------------------------------------

{% include UALib.Links.md %}

--------------------------------------

[the ualib/agda-algebras development team]: https://github.com/ualib/agda-algebras#the-ualib-agda-algebras-development-team
