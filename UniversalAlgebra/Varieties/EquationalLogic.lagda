---
layout: default
title : Varieties.EquationalLogic module (The Agda Universal Algebra Library)
date : 2021-01-14
author: [the ualib/agda-algebras development team][]
---

This is the [Varieties.EquationalLogic][] module of the [Agda Universal Algebra Library][].

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}


open import Level renaming ( suc to lsuc )
open import Algebras.Basic


module Varieties.EquationalLogic {ğ“ ğ“¥ : Level} (ğ‘† : Signature ğ“ ğ“¥) where



-- Imports from Agda (builtin/primitive) and the Agda Standard Library ---------------------
open import Axiom.Extensionality.Propositional renaming (Extensionality to funext)
open import Agda.Primitive          renaming ( Set to Type )
                                    using    ( _âŠ”_ )
open import Agda.Builtin.Equality   using    ( _â‰¡_ ; refl )
open import Data.Product            using    ( _,_ ; Î£-syntax ; Î£ ; _Ã—_ )
                                    renaming ( projâ‚ to fst
                                             ; projâ‚‚ to snd )
open import Relation.Binary.PropositionalEquality
                                    using    ( cong ; cong-app ; module â‰¡-Reasoning )
open import Relation.Unary          using    ( _âˆˆ_ ; Pred ; _âŠ†_ )



-- imports from agda-algebras --------------------------------------------------------------
open import Overture.Preliminaries       using ( âˆ£_âˆ£ ; âˆ¥_âˆ¥ )
open import Overture.Inverses            using (IsInjective)
open import Relations.Truncation         using ( hfunext )

open import Algebras.Products          ğ‘† using ( ov ; â¨… ; ğ”„ ; class-product)
open import Homomorphisms.Basic        ğ‘† using (hom; ğ’¾ğ’¹; âˆ˜-hom; is-homomorphism)
open import Homomorphisms.Isomorphisms ğ‘† using (_â‰…_ ; â‰…-sym ; Lift-â‰… ; â‰…-trans
                                               ; â‰…-refl ; Lift-Alg-iso ; â¨…â‰…
                                               ; Lift-Alg-associative ; Lift-Alg-â¨…â‰… )

open import Subalgebras.Subalgebras    ğ‘† using ( _â‰¤_ ; _IsSubalgebraOfClass_ ; Lift-â‰¤-Lift
                                               ; SubalgebraOfClass ; isoâ†’injective
                                               ; â‰¤-Lift ; _IsSubalgebraOf_ )
private variable Î± Î² Î³ : Level

import Varieties.Closure.H ğ‘† as VC-H
import Varieties.Closure.S ğ‘† as VC-S
import Varieties.Closure.P ğ‘† as VC-P
import Varieties.Closure.V ğ‘† as VC-V

\end{code}


Fix a signature ğ‘†, let ğ’¦ be a class of ğ‘†-algebras, and define

* H ğ’¦ = algebras isomorphic to a homomorphic image of a members of ğ’¦;
* S ğ’¦ = algebras isomorphic to a subalgebra of a member of ğ’¦;
* P ğ’¦ = algebras isomorphic to a product of members of ğ’¦.

A straight-forward verification confirms that H, S, and P are **closure operators** (expansive, monotone, and idempotent).  A class ğ’¦ of ğ‘†-algebras is said to be *closed under the taking of homomorphic images* provided `H ğ’¦ âŠ† ğ’¦`. Similarly, ğ’¦ is *closed under the taking of subalgebras* (resp., *arbitrary products*) provided `S ğ’¦ âŠ† ğ’¦` (resp., `P ğ’¦ âŠ† ğ’¦`). The operators H, S, and P can be composed with one another repeatedly, forming yet more closure operators.

An algebra is a homomorphic image (resp., subalgebra; resp., product) of every algebra to which it is isomorphic. Thus, the class `H ğ’¦` (resp., `S ğ’¦`; resp., `P ğ’¦`) is closed under isomorphism.

A **variety** is a class of algebras, in the same signature, that is closed under the taking of homomorphic images, subalgebras, and arbitrary products.  To represent varieties we define inductive types for the closure operators `H`, `S`, and `P` that are composable.  Separately, we define an inductive type `V` which simultaneously represents closure under all three operators, `H`, `S`, and `P`.


We import some of these things from sub-modules.
\begin{code}
open VC-H using (H) public
open VC-S public
open VC-P public
open VC-V public
\end{code}


#### <a id="closure-properties">Closure properties</a>

The types defined above represent operators with useful closure properties. We now prove a handful of such properties that we need later.

The next lemma would be too obvious to care about were it not for the fact that we'll need it later, so it too must be formalized.

\begin{code}

SâŠ†SP : (ğ’¦ : Pred (Algebra Î± ğ‘†)(ov Î±))
 â†’     S{Î±}{Î²} ğ’¦ âŠ† S{Î± âŠ” Î²}{Î± âŠ” Î²} (P{Î±}{Î²} ğ’¦)

SâŠ†SP {Î±} {Î²} ğ’¦ {.(Lift-Alg ğ‘¨ Î²)}(sbase{ğ‘¨} x) = siso spllA(â‰…-sym Lift-â‰…)
 where
 llA : Algebra (Î± âŠ” Î²) ğ‘†
 llA = Lift-Alg (Lift-Alg ğ‘¨ Î²) (Î± âŠ” Î²)

 spllA : llA âˆˆ S (P ğ’¦)
 spllA = sbase{Î± âŠ” Î²}{Î± âŠ” Î²} (pbase x)

SâŠ†SP {Î±} {Î²} ğ’¦ {.(Lift-Alg ğ‘¨ Î²)}(slift{ğ‘¨} x) = subalgebraâ†’S lAsc
 where
 splAu : ğ‘¨ âˆˆ S(P ğ’¦)
 splAu = SâŠ†SP{Î±}{Î±} ğ’¦ x

 Asc : ğ‘¨ IsSubalgebraOfClass (P ğ’¦)
 Asc = Sâ†’subalgebra{Î±}{P{Î±}{Î±} ğ’¦}{ğ‘¨} splAu

 lAsc : (Lift-Alg ğ‘¨ Î²) IsSubalgebraOfClass (P ğ’¦)
 lAsc = Lift-Alg-subP' Asc
SâŠ†SP {Î±} {Î²} ğ’¦ {ğ‘©}(ssub{ğ‘¨} sA Bâ‰¤A) = ssub (subalgebraâ†’S lAsc)( â‰¤-Lift ğ‘¨ Bâ‰¤A )
 where
  lA : Algebra (Î± âŠ” Î²) ğ‘†
  lA = Lift-Alg ğ‘¨ Î²

  splAu : ğ‘¨ âˆˆ S (P ğ’¦)
  splAu = SâŠ†SP{Î±}{Î±} ğ’¦ sA

  Asc : ğ‘¨ IsSubalgebraOfClass (P ğ’¦)
  Asc = Sâ†’subalgebra{Î±}{P{Î±}{Î±} ğ’¦}{ğ‘¨} splAu

  lAsc : lA IsSubalgebraOfClass (P ğ’¦)
  lAsc = Lift-Alg-subP' Asc

SâŠ†SP {Î± = Î±}{Î²} ğ’¦ {ğ‘©}(siso{ğ‘¨} sA Aâ‰…B) = siso{Î± âŠ” Î²}{Î± âŠ” Î²} lAsp lAâ‰…B
 where
 lA : Algebra (Î± âŠ” Î²) ğ‘†
 lA = Lift-Alg ğ‘¨ Î²

 lAsc : lA IsSubalgebraOfClass (P ğ’¦)
 lAsc = Lift-Alg-subP' (Sâ†’subalgebra{Î±}{P{Î±}{Î±} ğ’¦}{ğ‘¨} (SâŠ†SP ğ’¦ sA))

 lAsp : lA âˆˆ S(P ğ’¦)
 lAsp = subalgebraâ†’S{Î± âŠ” Î²}{Î± âŠ” Î²}{P{Î±}{Î²} ğ’¦}{lA} lAsc

 lAâ‰…B : lA â‰… ğ‘©
 lAâ‰…B = â‰…-trans (â‰…-sym Lift-â‰…) Aâ‰…B

\end{code}



We need to formalize one more lemma before arriving the main objective of this section, which is the proof of the inclusion PSâŠ†SP.

\begin{code}

module _ {Î± Î² : Level} {ğ’¦ : Pred(Algebra Î± ğ‘†)(ov Î±)} where

 lemPSâŠ†SP : hfunext Î² Î± â†’ funext Î² Î± â†’ {I : Type Î²}{â„¬ : I â†’ Algebra Î± ğ‘†}
  â†’         (âˆ€ i â†’ (â„¬ i) IsSubalgebraOfClass ğ’¦)
  â†’         â¨… â„¬ IsSubalgebraOfClass (P{Î±}{Î²} ğ’¦)

 lemPSâŠ†SP hwu fwu {I}{â„¬} Bâ‰¤K = â¨… ğ’œ , (â¨… SA , â¨…SAâ‰¤â¨…ğ’œ) , Î¾ , (â¨…â‰… {fiu = fwu}{fiw = fwu} Bâ‰…SA)
  where
  ğ’œ : I â†’ Algebra Î± ğ‘†
  ğ’œ = Î» i â†’ âˆ£ Bâ‰¤K i âˆ£

  SA : I â†’ Algebra Î± ğ‘†
  SA = Î» i â†’ âˆ£ fst âˆ¥ Bâ‰¤K i âˆ¥ âˆ£

  Bâ‰…SA : âˆ€ i â†’ â„¬ i â‰… SA i
  Bâ‰…SA = Î» i â†’ âˆ¥ snd âˆ¥ Bâ‰¤K i âˆ¥ âˆ¥

  SAâ‰¤ğ’œ : âˆ€ i â†’ (SA i) IsSubalgebraOf (ğ’œ i)
  SAâ‰¤ğ’œ = Î» i â†’ snd âˆ£ âˆ¥ Bâ‰¤K i âˆ¥ âˆ£

  h : âˆ€ i â†’ âˆ£ SA i âˆ£ â†’ âˆ£ ğ’œ i âˆ£
  h = Î» i â†’ fst âˆ£ SAâ‰¤ğ’œ i âˆ£

  hinj : âˆ€ i â†’ IsInjective (h i)
  hinj = Î» i â†’ snd (snd âˆ£ âˆ¥ Bâ‰¤K i âˆ¥ âˆ£)

  Ïƒ : âˆ£ â¨… SA âˆ£ â†’ âˆ£ â¨… ğ’œ âˆ£
  Ïƒ = Î» x i â†’ (h i) (x i)
  Î½ : is-homomorphism (â¨… SA) (â¨… ğ’œ) Ïƒ
  Î½ = Î» ğ‘“ ğ’‚ â†’ fwu Î» i â†’ (snd âˆ£ SAâ‰¤ğ’œ i âˆ£) ğ‘“ (Î» x â†’ ğ’‚ x i)

  Ïƒinj : IsInjective Ïƒ
  Ïƒinj ÏƒxÏƒy = fwu Î» i â†’ (hinj i)(cong-app ÏƒxÏƒy i)

  â¨…SAâ‰¤â¨…ğ’œ : â¨… SA â‰¤ â¨… ğ’œ
  â¨…SAâ‰¤â¨…ğ’œ = (Ïƒ , Î½) , Ïƒinj

  Î¾ : â¨… ğ’œ âˆˆ P ğ’¦
  Î¾ = produ (Î» i â†’ P-expa (âˆ£ snd âˆ¥ Bâ‰¤K i âˆ¥ âˆ£))


\end{code}



#### <a id="PS-in-SP">PS(ğ’¦) âŠ† SP(ğ’¦)</a>

Finally, we are in a position to prove that a product of subalgebras of algebras in a class ğ’¦ is a subalgebra of a product of algebras in ğ’¦.

\begin{code}

module _ {Î± : Level} {fovu : funext (ov Î±) (ov Î±)}{ğ’¦ : Pred (Algebra Î± ğ‘†)(ov Î±)} where

 PSâŠ†SP : -- extensionality assumptions:
            hfunext (ov Î±)(ov Î±)

  â†’      P{ov Î±}{ov Î±} (S{Î±}{ov Î±} ğ’¦) âŠ† S{ov Î±}{ov Î±} (P{Î±}{ov Î±} ğ’¦)

 PSâŠ†SP _ (pbase (sbase x)) = sbase (pbase x)
 PSâŠ†SP _ (pbase (slift{ğ‘¨} x)) = slift (SâŠ†SP{Î±}{ov Î±} ğ’¦ (slift x))
 PSâŠ†SP _ (pbase{ğ‘©}(ssub{ğ‘¨} sA Bâ‰¤A)) = siso(ssub(SâŠ†SP ğ’¦ (slift sA))(Lift-â‰¤-Lift (ov(Î±)){ğ‘¨}(ov(Î±))Bâ‰¤A)) â‰…-refl
 PSâŠ†SP _ (pbase (siso{ğ‘¨}{ğ‘©} x Aâ‰…B)) = siso (SâŠ†SP ğ’¦ (slift x)) ( Lift-Alg-iso Aâ‰…B )
 PSâŠ†SP hfe (pliftu x) = slift (PSâŠ†SP hfe x)
 PSâŠ†SP hfe (pliftw x) = slift (PSâŠ†SP hfe x)

 PSâŠ†SP hfe (produ{I}{ğ’œ} x) = (S-mono (P-idemp)) (subalgebraâ†’S Î·)
  where
   Î¾ : (i : I) â†’ (ğ’œ i) IsSubalgebraOfClass (P{Î±}{ov Î±} ğ’¦)
   Î¾ i = Sâ†’subalgebra (PSâŠ†SP hfe (x i))

   Î· : â¨… ğ’œ IsSubalgebraOfClass (P{ov Î±}{ov Î±} (P{Î±}{ov Î±} ğ’¦))
   Î· = lemPSâŠ†SP hfe fovu {I} {ğ’œ} Î¾

 PSâŠ†SP hfe (prodw{I}{ğ’œ} x) = (S-mono (P-idemp)) (subalgebraâ†’S Î·)
  where
   Î¾ : (i : I) â†’ (ğ’œ i) IsSubalgebraOfClass (P{Î±}{ov Î±} ğ’¦)
   Î¾ i = Sâ†’subalgebra (PSâŠ†SP hfe (x i))

   Î· : â¨… ğ’œ IsSubalgebraOfClass (P{ov Î±}{ov Î±} (P{Î±}{ov Î±} ğ’¦))
   Î· = lemPSâŠ†SP hfe fovu  {I} {ğ’œ} Î¾

 PSâŠ†SP hfe (pisow{ğ‘¨}{ğ‘©} pA Aâ‰…B) = siso (PSâŠ†SP hfe pA) Aâ‰…B

\end{code}



#### <a id="more-class-inclusions">More class inclusions</a>

We conclude this subsection with three more inclusion relations that will have bit parts to play later (e.g., in the formal proof of Birkhoff's Theorem).

\begin{code}

PâŠ†V : {Î± Î² : Level}{ğ’¦ : Pred (Algebra Î± ğ‘†)(ov Î±)} â†’ P{Î±}{Î²} ğ’¦ âŠ† V{Î±}{Î²} ğ’¦

PâŠ†V (pbase x) = vbase x
PâŠ†V{Î±} (pliftu x) = vlift (PâŠ†V{Î±}{Î±} x)
PâŠ†V{Î±}{Î²} (pliftw x) = vliftw (PâŠ†V{Î±}{Î²} x)
PâŠ†V (produ x) = vprodu (Î» i â†’ PâŠ†V (x i))
PâŠ†V (prodw x) = vprodw (Î» i â†’ PâŠ†V (x i))
PâŠ†V (pisow x xâ‚) = visow (PâŠ†V x) xâ‚


SPâŠ†V : {Î± Î² : Level}{ğ’¦ : Pred (Algebra Î± ğ‘†)(ov Î±)}
 â†’     S{Î± âŠ” Î²}{Î± âŠ” Î²} (P{Î±}{Î²} ğ’¦) âŠ† V ğ’¦

SPâŠ†V (sbase{ğ‘¨} PCloA) = PâŠ†V (pisow PCloA Lift-â‰…)
SPâŠ†V (slift{ğ‘¨} x) = vliftw (SPâŠ†V x)
SPâŠ†V (ssub{ğ‘¨}{ğ‘©} spA Bâ‰¤A) = vssubw (SPâŠ†V spA) Bâ‰¤A
SPâŠ†V (siso x xâ‚) = visow (SPâŠ†V x) xâ‚

\end{code}
#### <a id="V-is-closed-under-lift">V is closed under lift</a>

As mentioned earlier, a technical hurdle that must be overcome when formalizing proofs in Agda is the proper handling of universe levels. In particular, in the proof of the Birkhoff's theorem, for example, we will need to know that if an algebra ğ‘¨ belongs to the variety V ğ’¦, then so does the lift of ğ‘¨.  Let us get the tedious proof of this technical lemma out of the way.

Above we proved that `SP(ğ’¦) âŠ† V(ğ’¦)`, and we did so under fairly general assumptions about the universe level parameters.  Unfortunately, this is sometimes not quite general enough, so we now prove the inclusion again for the specific universe parameters that align with subsequent applications of this result.


\begin{code}

module _ {Î± : Level}  {feâ‚€ : funext (ov Î±) Î±}
         {feâ‚ : funext ((ov Î±) âŠ” (lsuc (ov Î±))) (lsuc (ov Î±))}
         {feâ‚‚ : funext (ov Î±) (ov Î±)}
         {ğ’¦ : Pred (Algebra Î± ğ‘†)(ov Î±)} where

 open Vlift {Î±}{feâ‚€}{feâ‚}{feâ‚‚}{ğ’¦}

 SPâŠ†V' : S{ov Î±}{lsuc (ov Î±)} (P{Î±}{ov Î±} ğ’¦) âŠ† V ğ’¦

 SPâŠ†V' (sbase{ğ‘¨} x) = visow (VlA (SPâŠ†V (sbase x))) (â‰…-sym (Lift-Alg-associative ğ‘¨))
 SPâŠ†V' (slift x) = VlA (SPâŠ†V x)

 SPâŠ†V' (ssub{ğ‘¨}{ğ‘©} spA Bâ‰¤A) = vssubw (VlA (SPâŠ†V spA)) Bâ‰¤lA
  where
   Bâ‰¤lA : ğ‘© â‰¤ Lift-Alg ğ‘¨ (lsuc (ov Î±))
   Bâ‰¤lA = â‰¤-Lift ğ‘¨ Bâ‰¤A

 SPâŠ†V' (siso{ğ‘¨}{ğ‘©} x Aâ‰…B) = visow (VlA (SPâŠ†V x)) Goal
  where
   Goal : Lift-Alg ğ‘¨ (lsuc (ov Î±)) â‰… ğ‘©
   Goal = â‰…-trans (â‰…-sym Lift-â‰…) Aâ‰…B

\end{code}


#### <a id="S-in-SP">â¨… S(ğ’¦) âˆˆ SP(ğ’¦)</a>

Finally, we prove a result that plays an important role, e.g., in the formal proof of Birkhoff's Theorem. As we saw in [Algebras.Products][], the (informal) product `â¨… S(ğ’¦)` of all subalgebras of algebras in ğ’¦ is implemented (formally) in the [UniversalAlgebra][] library as `â¨… ğ”„ S(ğ’¦)`. Our goal is to prove that this product belongs to `SP(ğ’¦)`. We do so by first proving that the product belongs to `PS(ğ’¦)` and then applying the `PSâŠ†SP` lemma.

Before doing so, we need to redefine the class product so that each factor comes with a map from the type `X` of variable symbols into that factor.  We will explain the reason for this below.

\begin{code}

module class-products-with-maps {Î± : Level}
 {X : Type Î±}
 {feğ“•Î± : funext (ov Î±) Î±}
 {feâ‚ : funext ((ov Î±) âŠ” (lsuc (ov Î±))) (lsuc (ov Î±))}
 {feâ‚‚ : funext (ov Î±) (ov Î±)}
 (ğ’¦ : Pred (Algebra Î± ğ‘†)(ov Î±))
 where

 â„‘' : Type (ov Î±)
 â„‘' = Î£[ ğ‘¨ âˆˆ (Algebra Î± ğ‘†) ] ((ğ‘¨ âˆˆ S{Î±}{Î±} ğ’¦) Ã— (X â†’ âˆ£ ğ‘¨ âˆ£))

\end{code}
Notice that the second component of this dependent pair type is  `(ğ‘¨ âˆˆ ğ’¦) Ã— (X â†’ âˆ£ ğ‘¨ âˆ£)`. In previous versions of the [UALib][] this second component was simply `ğ‘¨ âˆˆ ğ’¦`, until we realized that adding the type `X â†’ âˆ£ ğ‘¨ âˆ£` is quite useful. Later we will see exactly why, but for now suffice it to say that a map of type `X â†’ âˆ£ ğ‘¨ âˆ£` may be viewed abstractly as an *ambient context*, or more concretely, as an assignment of *values* in `âˆ£ ğ‘¨ âˆ£` to *variable symbols* in `X`.  When computing with or reasoning about products, while we don't want to rigidly impose a context in advance, want do want to lay our hands on whatever context is ultimately assumed.  Including the "context map" inside the index type `â„‘` of the product turns out to be a convenient way to achieve this flexibility.


Taking the product over the index type `â„‘` requires a function that maps an index `i : â„‘` to the corresponding algebra.  Each `i : â„‘` is a triple, say, `(ğ‘¨ , p , h)`, where `ğ‘¨ : Algebra Î± ğ‘†`, `p : ğ‘¨ âˆˆ ğ’¦`, and `h : X â†’ âˆ£ ğ‘¨ âˆ£`, so the function mapping an index to the corresponding algebra is simply the first projection.

\begin{code}

 ğ”„' : â„‘' â†’ Algebra Î± ğ‘†
 ğ”„' = Î» (i : â„‘') â†’ âˆ£ i âˆ£

\end{code}

Finally, we define `class-product` which represents the product of all members of ğ’¦.

\begin{code}

 class-product' : Algebra (ov Î±) ğ‘†
 class-product' = â¨… ğ”„'

\end{code}

If `p : ğ‘¨ âˆˆ ğ’¦` and `h : X â†’ âˆ£ ğ‘¨ âˆ£`, we view the triple `(ğ‘¨ , p , h) âˆˆ â„‘` as an index over the class, and so we can think of `ğ”„ (ğ‘¨ , p , h)` (which is simply `ğ‘¨`) as the projection of the product `â¨… ğ”„` onto the `(ğ‘¨ , p, h)`-th component.

\begin{code}

 class-prod-s-âˆˆ-ps : class-product' âˆˆ P{ov Î±}{ov Î±}(S ğ’¦)
 class-prod-s-âˆˆ-ps = pisow psPllA (â¨…â‰… {fiu = feâ‚‚}{fiw = feğ“•Î±} llAâ‰…A)

  where
  lA llA : â„‘' â†’ Algebra (ov Î±) ğ‘†
  lA i =  Lift-Alg (ğ”„ i) (ov Î±)
  llA i = Lift-Alg (lA i) (ov Î±)

  slA : âˆ€ i â†’ (lA i) âˆˆ S ğ’¦
  slA i = siso (fst âˆ¥ i âˆ¥) Lift-â‰…

  psllA : âˆ€ i â†’ (llA i) âˆˆ P (S ğ’¦)
  psllA i = pbase (slA i)

  psPllA : â¨… llA âˆˆ P (S ğ’¦)
  psPllA = produ psllA

  llAâ‰…A : âˆ€ i â†’ (llA i) â‰… (ğ”„' i)
  llAâ‰…A i = â‰…-trans (â‰…-sym Lift-â‰…)(â‰…-sym Lift-â‰…)

\end{code}


So, since `PSâŠ†SP`, we see that that the product of all subalgebras of a class `ğ’¦` belongs to `SP(ğ’¦)`.

\begin{code}

 class-prod-s-âˆˆ-sp : hfunext (ov Î±) (ov Î±) â†’ class-product âˆˆ S(P ğ’¦)
 class-prod-s-âˆˆ-sp hfe = PSâŠ†SP {fovu = feâ‚‚} hfe class-prod-s-âˆˆ-ps

\end{code}

----------------------------

[â† Varieties.EquationalLogic](Varieties.EquationalLogic.html)
<span style="float:right;">[Varieties.Preservation â†’](Varieties.Preservation.html)</span>

{% include UALib.Links.md %}


--------------------------------------

[the ualib/agda-algebras development team]: https://github.com/ualib/agda-algebras#the-ualib-agda-algebras-development-team
