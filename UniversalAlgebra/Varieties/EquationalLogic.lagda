---
layout: default
title : Varieties.EquationalLogic module (The Agda Universal Algebra Library)
date : 2021-01-14
author: [the ualib/agda-algebras development team][]
---

This is the [Varieties.EquationalLogic][] module of the [Agda Universal Algebra Library][].

### Entailment, derivation rules, soundness and completeness

This module is based on Andreas Abel's Agda formalization of Birkhoff's completeness theorem.
(See: http://www.cse.chalmers.se/~abela/agda/MultiSortedAlgebra.pdf)


\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}


open import Level using ( Level )
open import Algebras.Basic

module Varieties.EquationalLogic {ğ‘† : Signature ğ“ ğ“¥} where


-- imports from Agda and the Agda Standard Library -------------------------------------------
open import Agda.Builtin.Equality  using    ( _â‰¡_                      )
                                   renaming ( refl    to â‰¡-refl        )
open import Agda.Primitive         using    ( _âŠ”_     ;  lsuc          )
                                   renaming ( Set     to Type          )
open import Data.Product           using    ( _,_     ;  Î£-syntax      )
open import Function.Base          using    ( _âˆ˜_     ;  flip          )
open import Function.Bundles       using    ( Func                     )
open import Relation.Binary        using    ( Setoid  ;  IsEquivalence )
open import Relation.Unary          using    ( Pred ; _âˆˆ_ )
import Relation.Binary.Reasoning.Setoid as SetoidReasoning

open Setoid        using    ( Carrier ; _â‰ˆ_ ; isEquivalence )
open Func          renaming ( f     to  _<$>_ )
open IsEquivalence renaming ( refl  to  reflE
                            ; sym   to  symmE
                            ; trans to  tranE )


-- imports from agda-algebras --------------------------------------------------------------
open import Overture.Preliminaries       using ( âˆ£_âˆ£ )
open import Algebras.Setoid      {ğ‘† = ğ‘†} using ( SetoidAlgebra ; âŸ¦_âŸ§s )
open import Algebras.Products    {ğ‘† = ğ‘†} using ( ov )
open import Terms.Basic          {ğ‘† = ğ‘†} using ( Term )
open import Terms.Setoid         {ğ‘† = ğ‘†} using ( module Environment ; Ops ; Sub ; _[_] )

open Term

private variable
 Ï‡ Î± Ï â„“ : Level
 X Î“ Î” : Type Ï‡
 f     : âˆ£ ğ‘† âˆ£
 op    : âˆ£ Ops Î“ âˆ£

-- Equations
-- An equation is a pair (s , t) of terms in the same context.
record Eq : Type (ov Ï‡) where
 constructor _â‰ˆÌ‡_
 field
  {cxt} : Type Ï‡
  lhs   : Term cxt
  rhs   : Term cxt

open Eq public


-- Equation p â‰ˆÌ‡ q holding in algebra M. (type \~~\^. to get â‰ˆÌ‡) (type \|= to get âŠ¨)
_âŠ¨_ : (M : SetoidAlgebra Î± â„“)(term-identity : Eq{Ï‡}) â†’ Type _
M âŠ¨ (p â‰ˆÌ‡ q) = Equal p q  where open Environment M

module _ {Î¹ : Level}{I : Type Î¹} where

 -- An I-indexed set of equations inhabits the type I â†’ Eq.
 -- For such `E : I â†’ Eq`...

 -- ...`ğ‘¨ âŠ§ E` is the assertion that algebra ğ‘¨ models all equations in a set E.
 _âŠ§_ : (ğ‘¨ : SetoidAlgebra Î± Ï)(E : I â†’ Eq{Ï‡}) â†’ Type _
 ğ‘¨ âŠ§ E = âˆ€ i â†’ Equal (lhs (E i))(rhs (E i))       -- (type \models to get âŠ§)
  where open Environment ğ‘¨

 -- ...`Mod E` is the class of algebras that model all term equations in E.
 Mod : (I â†’ Eq{Ï‡}) â†’ Pred(SetoidAlgebra Î± Ï) (Ï‡ âŠ” Î¹ âŠ” Î± âŠ” Ï)
 Mod E = _âŠ§ E


_âŠ«_ : Pred (SetoidAlgebra Î± Ï) â„“ â†’ Eq{Ï‡} â†’ Type _
ğ’¦ âŠ« eq = âˆ€ ğ‘¨ â†’ ğ’¦ ğ‘¨ â†’ ğ‘¨ âŠ¨ eq                        -- (type \||= to get âŠ«)


module _ {Î±}{Ï}{Î¹}{I : Type Î¹} where
 -- An entailment E âŠƒ eq holds iff it holds in all models of E.
 _âŠƒ_ : (E : I â†’ Eq{Ï‡}) (eq : Eq{Ï‡}) â†’ Type _
 E âŠƒ eq = (M : SetoidAlgebra Î± Ï) â†’ M âŠ§ E â†’ M âŠ¨ eq

\end{code}


#### Derivations in a context

(Based on Andreas Abel's Agda formalization of Birkhoff's completeness theorem;
see: http://www.cse.chalmers.se/~abela/agda/MultiSortedAlgebra.pdf)

\begin{code}

module _ {Ï‡ Î¹ : Level} where

 data _âŠ¢_â–¹_â‰ˆ_ {I : Type Î¹}
  (E : I â†’ Eq) : (Î“ : Type Ï‡) (p q : Term Î“) â†’ Type ((ov Ï‡) âŠ” Î¹) where

  hyp : âˆ€ i â†’ let p â‰ˆÌ‡ q = E i in E âŠ¢ _ â–¹ p â‰ˆ q

  app : âˆ€ {ps qs} â†’ (âˆ€ i â†’ E âŠ¢ Î“ â–¹ ps i â‰ˆ qs i) â†’ E âŠ¢ Î“ â–¹ (node f ps) â‰ˆ (node f qs)

  sub : âˆ€ {p q} â†’ E âŠ¢ Î” â–¹ p â‰ˆ q â†’ âˆ€ (Ïƒ : Sub Î“ Î”) â†’ E âŠ¢ Î“ â–¹ (p [ Ïƒ ]) â‰ˆ (q [ Ïƒ ])

  refl : âˆ€ {p} â†’ E âŠ¢ Î“ â–¹ p â‰ˆ p

  sym : âˆ€ {p q : Term Î“} â†’ E âŠ¢ Î“ â–¹ p â‰ˆ q â†’ E âŠ¢ Î“ â–¹ q â‰ˆ p

  trans : âˆ€ {p q r : Term Î“} â†’ E âŠ¢ Î“ â–¹ p â‰ˆ q â†’ E âŠ¢ Î“ â–¹ q â‰ˆ r â†’ E âŠ¢ Î“ â–¹ p â‰ˆ r

\end{code}



#### Soundness of the inference rules

(Based on Andreas Abel's Agda formalization of Birkhoff's completeness theorem;
see: http://www.cse.chalmers.se/~abela/agda/MultiSortedAlgebra.pdf)


\begin{code}

module Soundness {Ï‡ Î± Ï Î¹ : Level}{I : Type Î¹} (E : I â†’ Eq{Ï‡})
                 (M : SetoidAlgebra Î± Ï)     -- We assume an algebra M
                 (V : M âŠ§ E)         -- that models all equations in E.
                 where
 open SetoidAlgebra M

 -- In any model M that satisfies the equations E, derived equality is actual equality.
 open SetoidReasoning Domain

 open Environment M
 sound : âˆ€ {p q} â†’ E âŠ¢ Î“ â–¹ p â‰ˆ q â†’ M âŠ¨ (p â‰ˆÌ‡ q)

 sound (hyp i)                      =  V i
 sound (app {f = f} es) Ï           =  Interp .cong (â‰¡-refl , Î» i â†’ sound (es i) Ï)
 sound (sub {p = p} {q} Epq Ïƒ) Ï    =  begin
                                       â¦… p [ Ïƒ ] â¦† <$> Ï          â‰ˆâŸ¨ substitution p Ïƒ Ï âŸ©
                                       â¦… p       â¦† <$> (â¦… Ïƒ â¦†s Ï) â‰ˆâŸ¨ sound Epq (â¦… Ïƒ â¦†s Ï)  âŸ©
                                       â¦… q       â¦† <$> (â¦… Ïƒ â¦†s Ï) â‰ˆË˜âŸ¨ substitution  q Ïƒ Ï âŸ©
                                       â¦… q [ Ïƒ ] â¦† <$> Ï          âˆ

 sound (refl {p = p})               = isEquiv .reflE {x = p}
 sound (sym {p = p} {q} Epq)        = isEquiv .symmE {x = p}{q}   (sound Epq)
 sound (trans{p = p}{q}{r} Epq Eqr) = isEquiv .tranE {i = p}{q}{r}(sound Epq)(sound Eqr)


\end{code}

The deductive closure of a set E is the set of equations modeled by all models of E; that is, Th Mod E.

The soundness proof above shows âˆ€ X â†’ E âŠ¢ X â–¹ p â‰ˆ q â†’ (p , q) âˆˆ Th Mod â„°.
That is,  âˆ€ X â†’ E âŠ¢ X â–¹ p â‰ˆ q â†’ Mod E âŠ« p â‰ˆ q.

The converse is Birkhoff's completeness theorem: if Mod E âŠ« p â‰ˆ q, then E âŠ¢ X â–¹ p â‰ˆ q.

We will prove that result next.

#### Birkhoff's completeness theorem

The proof proceeds by constructing a relatively free algebra consisting of term
quotiented by derivable equality E âŠ¢ Î“ â–¹ _â‰ˆ_.  It then suffices to prove
that this model satisfies all equations in $E$.

(Based on Andreas Abel's Agda formalization of Birkhoff's completeness theorem;
see: http://www.cse.chalmers.se/~abela/agda/MultiSortedAlgebra.pdf)

\begin{code}

-- Universal model
-- A term model for E and Î“ is Term Î“ modulo E âŠ¢ Î“ â–¹ _â‰ˆ_.
module TermModel {Ï‡ : Level}{Î“ : Type Ï‡}{Î¹ : Level}{I : Type Î¹} (E : I â†’ Eq) where
 open SetoidAlgebra

 -- Term Î“ modulo E.
 TermSetoid : Type Ï‡ â†’ Setoid _ _

 TermSetoid Î“ = record { Carrier       = Term Î“
                       ; _â‰ˆ_           = E âŠ¢ Î“ â–¹_â‰ˆ_
                       ; isEquivalence = record { refl = refl ; sym = sym ; trans = trans }
                       }

 -- The interpretation of an operation is simply the operation itself.
 -- This works since E âŠ¢ Î“ â–¹_â‰ˆ_ is a congruence.
 TermInterp : âˆ€ {Î“} â†’ Func (âŸ¦ ğ‘† âŸ§s (TermSetoid Î“)) (TermSetoid Î“)
 TermInterp <$> (f , ts) = node f ts
 cong TermInterp (â‰¡-refl , h) = app h

 -- The term model per context Î“.
 M : Type Ï‡ â†’ SetoidAlgebra _ _
 Domain (M Î“) = TermSetoid Î“
 Interp (M Î“) = TermInterp

 open Environment (M Î“)

 -- The identity substitution Ïƒâ‚€ maps variables to themselves.
 Ïƒâ‚€ : {Î“ : Type Ï‡} â†’ Sub Î“ Î“
 Ïƒâ‚€ x = â„Š x

 -- Ïƒâ‚€ acts indeed as identity.
 identity : (t : Term Î“) â†’ E âŠ¢ Î“ â–¹ t [ Ïƒâ‚€ ] â‰ˆ t
 identity (â„Š x) = refl
 identity (node f ts) = app (identity âˆ˜ ts)

 -- Evaluation in the term model is substitution $E âŠ¢ Î“ â–¹ â¦…tâ¦†Ïƒ â‰¡ t[Ïƒ]$.
 -- This would even hold "up to the nose" if we had function extensionality.

 evaluation : (t : Term Î”) (Ïƒ : Sub Î“ Î”) â†’ E âŠ¢ Î“ â–¹ (â¦… t â¦† <$> Ïƒ) â‰ˆ (t [ Ïƒ ])
 evaluation (â„Š x)    Ïƒ = refl
--  evaluation (node f ts)  Ïƒ = app (Î» i â†’ evaluation (ts i) Ïƒ)
 evaluation (node f ts)  Ïƒ = app (flip (evaluation âˆ˜ ts) Ïƒ)

 -- The term model satisfies all the equations it started out with.
 satisfies : âˆ€ i â†’ M Î“ âŠ¨ E i
 satisfies i Ïƒ = begin
                 â¦… p â¦† <$> Ïƒ  â‰ˆâŸ¨ evaluation p Ïƒ âŸ©
                 p [ Ïƒ ]        â‰ˆâŸ¨ sub (hyp i) Ïƒ âŸ©
                 q [ Ïƒ ]        â‰ˆË˜âŸ¨ evaluation q Ïƒ âŸ©
                 â¦… q â¦† <$> Ïƒ  âˆ
                 where
                  open SetoidReasoning (TermSetoid _)
                  p = lhs (E i)
                  q = rhs (E i)

\end{code}

#### Birkhoff's Completeness Theorem

(Based on Andreas Abel's Agda formalization of Birkhoff's completeness theorem;
see: http://www.cse.chalmers.se/~abela/agda/MultiSortedAlgebra.pdf)


\begin{code}

module Completeness {Ï‡ Î¹ : Level}{I : Type Î¹} (E : I â†’ Eq{Ï‡}) {Î“} where
 open TermModel {Î“ = Î“} E
 open Environment (M Î“)

 -- Birkhoff's completeness theorem.
 -- Any valid consequence is derivable.
 completeness : âˆ€ p q â†’ Mod E âŠ« (p â‰ˆÌ‡ q) â†’ E âŠ¢ Î“ â–¹ p â‰ˆ q
 completeness p q V = begin
                  p              â‰ˆË˜âŸ¨ identity p âŸ©
                  p [ Ïƒâ‚€ ]       â‰ˆË˜âŸ¨ evaluation p Ïƒâ‚€ âŸ©
                  â¦… p â¦† <$> Ïƒâ‚€  â‰ˆâŸ¨ V (M Î“) satisfies Ïƒâ‚€ âŸ©
                  â¦… q â¦† <$> Ïƒâ‚€  â‰ˆâŸ¨ evaluation q Ïƒâ‚€ âŸ©
                  q [ Ïƒâ‚€ ]       â‰ˆâŸ¨ identity q âŸ©
                  q              âˆ
                  where open SetoidReasoning (TermSetoid Î“)


\end{code}






--------------------------------------

[the agda-algebras development team]: https://github.com/ualib/agda-algebras#the-agda-algebras-development-team






-- â„° âŠ¢ p â‰ˆ q is valid iff p â‰ˆ q holds in all models that satify all equations in â„°.
-- _âŠ¢_â‰ˆ_ : Pred(Term X Ã— Term X) (ov Î±) â†’ Term X â†’ Term X â†’ Type (ov (Ï‡ âŠ” Î±))
-- â„° âŠ¢ p â‰ˆ q = Mod â„° âŠ« p â‰ˆ q


