---
layout: default
title : Overture.Inverses module (Agda Universal Algebra Library)
date : 2021-01-12
author: William DeMeo
---

### <a id="inverses">Inverses</a>

This is the [Overture.Inverses][] module of the [Agda Universal Algebra Library][].

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

-- Imports from the Agda (Builtin) and the Agda Standard Library
open import Agda.Builtin.Equality using (_â‰¡_; refl)
open import Agda.Primitive using (_âŠ”_; lzero; lsuc; Level; SetÏ‰)
open import Data.Product using (_,_; Î£; Î£-syntax; _Ã—_)
open import Function.Base  using (_âˆ˜_; id)
open import Relation.Binary.PropositionalEquality.Core using (subst; cong-app)

-- Imports from the Agda Universal Algebra Library
open import Overture.Preliminaries using (Type; ğ“¤; ğ“¥; ğ“¦; ğ“©; _â»Â¹; Î ; -Î ; -Î£; _âˆ™_; ğ‘–ğ‘‘; _âˆ¼_)

module Overture.Inverses where

\end{code}

We begin by defining an data type that represents the semantic concept of *inverse image* of a function.

\begin{code}

module _ {A : Type ğ“¤ }{B : Type ğ“¦ } where

 data Image_âˆ‹_ (f : A â†’ B) : B â†’ Type (ğ“¤ âŠ” ğ“¦)
  where
  eq : {b : B} â†’ (a : A) â†’ b â‰¡ f a â†’ Image f âˆ‹ b

\end{code}

An inhabitant of `Image f âˆ‹ b` is a dependent pair `(a , p)`, where `a : A` and `p : b â‰¡ f a` is a proof that `f` maps `a` to `b`.  Since the proof that `b` belongs to the image of `f` is always accompanied by a witness `a : A`, we can actually *compute* a (pseudo)inverse of `f`. For convenience, we define this inverse function, which we call `Inv`, and which takes an arbitrary `b : B` and a (*witness*, *proof*)-pair, `(a , p) : Image f âˆ‹ b`, and returns the witness `a`.

\begin{code}

 Inv : (f : A â†’ B){b : B} â†’ Image f âˆ‹ b  â†’  A
 Inv f (eq a _) = a

\end{code}

We can prove that `Inv f` is the *right-inverse* of `f`, as follows.

\begin{code}

 InvIsInv : (f : A â†’ B){b : B}(q : Image f âˆ‹ b) â†’ f(Inv f q) â‰¡ b
 InvIsInv f (eq _ p) = p â»Â¹

\end{code}


#### <a id="injective-functions">Injective functions</a>

We say that a function `f : A â†’ B` is *injective* (or *monic*) if it does not map two distinct elements of the domain to the same point in the codomain. The following types manifest this property.

\begin{code}

module _ {A : Type ğ“¤}{B : Type ğ“¦} where

 IsInjective : (A â†’ B) â†’ Type (ğ“¤ âŠ” ğ“¦)
 IsInjective f = âˆ€ {x y} â†’ f x â‰¡ f y â†’ x â‰¡ y

 Injective : Type (ğ“¤ âŠ” ğ“¦)
 Injective = Î£[ f ê‰ (A â†’ B) ] IsInjective f

\end{code}


Before moving on to discuss surjective functions, let us prove (the obvious facts) that the identity map is injective and that the composition of injectives is injective.


\begin{code}

id-is-injective : {A : Type ğ“¤} â†’ IsInjective{A = A}{B = A} (Î» x â†’ x)
id-is-injective = Î» z â†’ z

âˆ˜-injective : {A : Type ğ“¤}{B : Type ğ“¦}{C : Type ğ“©}{f : A â†’ B}{g : B â†’ C}
 â†’            IsInjective f â†’ IsInjective g â†’ IsInjective (g âˆ˜ f)
âˆ˜-injective finj ginj = Î» z â†’ finj (ginj z)

\end{code}


#### <a id="epics">Surjective functions</a>

A *surjective function* from `A` to `B` is a function `f : A â†’ B` such that for all `b : B` there exists `a : A` such that `f a â‰¡ b`.  In other words, the range and codomain of `f` agree.  The following types manifest this notion.

\begin{code}

module _ {A : Type ğ“¤}{B : Type ğ“¦} where
 IsSurjective : (A â†’ B) â†’  Type (ğ“¤ âŠ” ğ“¦)
 IsSurjective f = âˆ€ y â†’ Image f âˆ‹ y

 Surjective : Type (ğ“¤ âŠ” ğ“¦)
 Surjective = Î£[ f ê‰ (A â†’ B) ] IsSurjective f

\end{code}

With the next definition, we can represent a *right-inverse* of a surjective function.

\begin{code}

 SurjInv : (f : A â†’ B) â†’ IsSurjective f â†’ B â†’ A
 SurjInv f fE b = Inv f (fE b)

\end{code}

Thus, a right-inverse of `f` is obtained by applying `SurjInv` to `f` and a proof of `IsSurjective f`.  Later, we will prove that this does indeed give the right-inverse, but we postpone the proof since it requires function extensionality, a concept we take up in the [Relations.Extensionality][] module.


-------------------------------------

<p></p>

[â† Overture.Preliminaries](Overture.Preliminaries.html)
<span style="float:right;">[Relations â†’](Relations.html)</span>


{% include UALib.Links.md %}
