---
layout: default
title : Overture.Preliminaries module (The Agda Universal Algebra Library)
date : 2021-01-13
author: William DeMeo
---

### <a id="preliminaries">Preliminaries</a>

This is the [Overture.Preliminaries][] module of the [Agda Universal Algebra Library][].

#### <a id="logical-foundations">Logical foundations</a>

The [Agda Universal Algebra Library](https://github.com/ualib/agda-algebras) (or
[agda-algebras](https://github.com/ualib/agda-algebras) for short) is based on
a version of [Martin-LoÌˆf type theory (MLTT)](https://ncatlab.org/nlab/show/Martin-L%C3%B6f+dependent+type+theory),
similar to the one on which on which MartÃ­n EscardÃ³'s [Type Topology Agda
library](https://github.com/martinescardo/TypeTopology) is based. We don't
discuss [MLTT](https://ncatlab.org/nlab/show/Martin-L%C3%B6f+dependent+type+theory) in
great detail here because there are already good and freely available resources covering the theory.
(See, for example, the section of
[EscardÃ³'s notes](https://www.cs.bham.ac.uk/~mhe/HoTT-UF-in-Agda-Lecture-Notes) on
[A spartan Martin-LÃ¶f type theory](https://www.cs.bham.ac.uk/~mhe/HoTT-UF-in-Agda-Lecture-Notes/HoTT-UF-Agda.html\#mlttinagda),
or the
[ncatlab entry on Martin-LoÌˆf dependent type theory](https://ncatlab.org/nlab/show/Martin-L\%C3\%B6f+dependent+type+theory),
or the
[HoTT book](https://homotopytypetheory.org/book/).)


The objects and assumptions that form the foundation of
[MLTT](https://ncatlab.org/nlab/show/Martin-L%C3%B6f+dependent+type+theory) are few.
There are the *primitive types* (`ğŸ˜`, `ğŸ™`, and `â„•`, denoting the empty type,
one-element type, and natural numbers), the *type formers* (`+`, `Î `, `Î£`, `Id`,
denoting *binary sum*, *product*, *sum*, and the *identity* type). Each of these
type formers is defined by a *type forming rule* which specifies how that type
is constructed. Lastly, we have an infinite collection of *type universes*
(types of types) and *universe variables* to denote them. Following EscardÃ³,
[agda-algebras][] denotes universe levels by upper-case calligraphic letters
from the second half of the English alphabet; to be precise, these are `ğ“`, `ğ“ `,
`ğ“¡`, â€¦, `ğ“§`, `ğ“¨`, `ğ“©`.<sup>[0](Overture.Preliminaries.html#fn0)</sup>

That's all. There are no further axioms or logical deduction (proof derivation)
rules needed for the foundations of
[MLTT](https://ncatlab.org/nlab/show/Martin-L%C3%B6f+dependent+type+theory)
that we take as the starting point of [agda-algebras][].  The logical semantics
come from the [propositions-as-types correspondence](https://ncatlab.org/nlab/show/propositions+as+types):
propositions and predicates are represented by types and the inhabitants of
these types are the proofs of the propositions and predicates.  As such, proofs
are constructed using the type forming rules. In other words, the type forming
rules *are* the proof derivation rules.

To this foundation, we add certain *extensionality principles* when and were we
need them.  These will be developed as we progress.  However, classical axioms
such as the [*Axiom of Choice*](https://ncatlab.org/nlab/show/axiom+of+choice)
or the [*Law of the Excluded Middle*](https://ncatlab.org/nlab/show/excluded+middle) are not needed and are
not assumed anywhere in the library.  In this sense, all theorems and proofs in
[agda-algebras][] are [*constructive*](https://ncatlab.org/nlab/show/constructive+mathematics)
(according to [nlab's definition](https://ncatlab.org/nlab/show/constructive+mathematics)). 

A few specific instances (e.g., the proof of the Noether isomorphism theorems
and Birkhoff's HSP theorem) require certain *truncation* assumptions. In such
cases, the theory is not
[predicative](https://ncatlab.org/nlab/show/predicative+mathematics) (according 
to [nlab's definition](https://ncatlab.org/nlab/show/predicative+mathematics)).
These instances are always clearly identified.



#### <a id="specifying-logical-foundations">Specifying logical foundations in Agda</a>

An Agda program typically begins by setting some options and by importing types
from existing Agda libraries. Options are specified with the `OPTIONS` *pragma*
and control the way Agda behaves by, for example, specifying the logical axioms
and deduction rules we wish to assume when the program is type-checked to verify
its correctness. Every Agda program in [agda-algebras][] begins with the following line.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

\end{code}

These options control certain foundational assumptions that Agda makes when type-checking the program to verify its correctness.

* `--without-K` disables [Streicher's K axiom](https://ncatlab.org/nlab/show/axiom+K+%28type+theory%29) ; see also the [section on axiom K](https://agda.readthedocs.io/en/v2.6.1/language/without-k.html) in the [Agda Language Reference][] manual.

* `--exact-split` makes Agda accept only those definitions that behave like so-called *judgmental* equalities.  [EscardÃ³][] explains this by saying it "makes sure that pattern matching corresponds to Martin-LÃ¶f eliminators;" see also the [Pattern matching and equality section](https://agda.readthedocs.io/en/v2.6.1/tools/command-line-options.html#pattern-matching-and-equality) of the [Agda Tools][] documentation.

* `safe` ensures that nothing is postulated outright---every non-MLTT axiom has to be an explicit assumption (e.g., an argument to a function or module); see also [this section](https://agda.readthedocs.io/en/v2.6.1/tools/command-line-options.html#cmdoption-safe) of the [Agda Tools][] documentation and the [Safe Agda section](https://agda.readthedocs.io/en/v2.6.1/language/safe-agda.html#safe-agda) of the [Agda Language Reference][].

Note that if we wish to type-check a file that imports another file that still has some unmet proof obligations, we must replace the `--safe` flag with `--allow-unsolved-metas`, but this is never done in (publicly released versions of) [UniversalAlgebra][].



#### <a id="agda-modules">Agda Modules</a>

The `OPTIONS` pragma is usually followed by the start of a module.  For example, the [Overture.Preliminaries][] module begins with the following line.

\begin{code}

module Overture.Preliminaries where

\end{code}


#### <a id="agda-universes">Agda Universes</a>

Here we import the basic primitive operations we need for working with Agda's type universes. For the very small amount of background about *type universes* we require, we refer the reader to the brief [section on universe-levels](https://agda.readthedocs.io/en/v2.6.1.3/language/universe-levels.html) in the [Agda documentation](https://agda.readthedocs.io/en/v2.6.1.3/language/universe-levels.html).

\begin{code}

open import Agda.Primitive using (_âŠ”_; lzero; lsuc; Level; SetÏ‰) public
open import Data.Empty public

{-
open import Agda.Builtin.Equality renaming (_â‰¡_ to infix 0 _â‰¡_)
open import Axiom.Extensionality.Propositional renaming (Extensionality to funext)
open import Data.Product renaming (_,_ to infixr 50 _,_) using (Î£; Î£-syntax; _Ã—_)
open import Data.Sum.Base using (_âŠ_) public
open import Function.Related
open import Relation.Binary.PropositionalEquality.Core using (sym; trans; subst; cong; cong-app)
open import Relation.Unary using (Pred; âˆ…; _âˆª_; _âˆˆ_; _âŠ†_) public


-}

\end{code}

We prefer to use `Type` in place of Agda's `Set` since for us *set* will mean a very special kind of (truncated) type. (See [Relatoins.Truncation][]).

\begin{code}

Type : (ğ“¤ : Level) â†’ Set (lsuc ğ“¤)
Type ğ“¤ = Set ğ“¤

\end{code}

We adopt EscardÃ³'s convention of denoting universe levels by capital calligraphic letters.

\begin{code}

variable
 ğ“ ğ“  ğ“¡ ğ“¢ ğ“£ ğ“¤ ğ“¥ ğ“¦ ğ“§ ğ“¨ ğ“© : Level

\end{code}


#### <a id="dependent-pair-type">Sigma types (dependent pairs)</a>

Given universes ğ“¤ and ğ“¥, a type `A : Type ğ“¤`, and a type family `B : A â†’ Type ğ“¥`, the *Sigma type* (or *dependent pair type*), denoted by `Î£[x âˆˆ A] B x`, generalizes the Cartesian product `A Ã— B` by allowing the type `B x` of the second argument of the ordered pair `(x , y)` to depend on the value `x` of the first.  That is, an inhabitant of the type `Î£[x âˆˆ A] B x` is a pair `(x , y)` such that `x : A` and `y : B x`.

For pedagogical reasons, we given the definition of the dependent product type here, inside a hidden module, but it is already defined in the `Data.Product` module of the [Agda Standard Library][], so ultimately we import it from there.

\begin{code}

module hide-sigma where

 record Î£ {ğ“¤ ğ“¥} {A : Type ğ“¤ } (B : A â†’ Type ğ“¥ ) : Type(ğ“¤ âŠ” ğ“¥)  where
  constructor _,_
  field
   prâ‚ : A
   prâ‚‚ : B prâ‚

 infixr 50 _,_

open import Data.Product renaming (_,_ to infixr 50 _,_) using (Î£; Î£-syntax) public

\end{code}


Agda's default syntax for this type is `Î£ A (Î» x â†’ B)`, but we prefer the notation `Î£ x ê‰ A , B`, which is closer to the syntax in the preceding paragraph and the notation used in the [HoTT book][], for example. Fortunately, we can make the preferred syntax available; this is done in the [Type Topology][] library with the following type definition and `syntax` declaration.

\begin{code}

-Î£ : {ğ“¤ ğ“¥ : Level} (A : Type ğ“¤ ) (B : A â†’ Type ğ“¥ ) â†’ Type(ğ“¤ âŠ” ğ“¥)
-Î£ = Î£

syntax -Î£ A (Î» x â†’ B) = Î£ x ê‰ A , B

infixr -1 -Î£

\end{code}

Also, the standard library made an alternative notation for the dependent pair type available which allows us to write `Î£[ x âˆˆ A ] B x` in place of `Î£ A (Î» x â†’ B)`.  In the [agda-algebras][] library we may use any one of the three alternative notations,

+ `Î£ A (Î» x â†’ B)` (standard Agda notation)
+ `Î£[ x âˆˆ A ] B x` ([Agda Standard Library][] notation)
+ `Î£ x ê‰ A , B` ([Type Topology][] notation)

**Warning!** The symbol ê‰ is not the same as : despite how similar they may appear. The correct colon in the expression `Î£ x ê‰ A , B` above is obtained by typing `\:4` in [agda2-mode][].

A special case of the Sigma type is the one in which the type `B` doesn't depend on `A`. This is the usual Cartesian product, defined in Agda as follows.

\begin{code}

module hode-Ã— where

 _Ã—_ : Type ğ“¤ â†’ Type ğ“¥ â†’ Type (ğ“¤ âŠ” ğ“¥)
 A Ã— B = Î£ x ê‰ A , B

open import Data.Product using (_Ã—_)

\end{code}


#### <a id="dependent-function-type">Pi types (dependent functions)</a>

Given universes `ğ“¤` and `ğ“¥`, a type `X : Type ğ“¤`, and a type family `Y : X â†’ Type ğ“¥`, the *Pi type* (aka *dependent function type*) is denoted by `Î (x : X), Y x` and generalizes the function type `X â†’ Y` by letting the type `Y x` of the codomain depend on the value `x` of the domain type. The dependent function type is defined in the [Type Topology][] in a standard way, but for the reader's benefit we repeat the definition here.

\begin{code}

Î  : {A : Type ğ“¤ } (B : A â†’ Type ğ“¦ ) â†’ Type (ğ“¤ âŠ” ğ“¦)
Î  {A = A} B = (x : A) â†’ B x

\end{code}

To make the syntax for `Î ` conform to the standard notation for *Pi types* (or dependent function type), [EscardÃ³][] uses the same trick as the one used above for *Sigma types*.

\begin{code}

-Î  : (A : Type ğ“¤ )(B : A â†’ Type ğ“¦ ) â†’ Type(ğ“¤ âŠ” ğ“¦)
-Î  A B = Î  B

infixr -1 -Î 
syntax -Î  A (Î» x â†’ B) = Î  x ê‰ A , B

\end{code}

**Warning!** The symbol ê‰ is not the same as :. Type the colon in `Î  x ê‰ A , B` as `\:4` in [agda2-mode][].



#### <a id="projection notation">Projection notation</a>

The definition of `Î£` (and thus, of `Ã—`) includes the fields `prâ‚` and `prâ‚‚` representing the first and second projections out of the product.  Sometimes we prefer to denote these projections by `âˆ£_âˆ£` and `âˆ¥_âˆ¥` respectively. However, for emphasis or readability we alternate between these and the following standard notations: `prâ‚` and `fst` for the first projection, `prâ‚‚` and `snd` for the second.  We define these alternative notations for projections out of pairs as follows.

\begin{code}

module _ {A : Type ğ“¤ }{B : A â†’ Type ğ“¥} where

 âˆ£_âˆ£ fst : Î£[ x âˆˆ A ] B x â†’ A
 âˆ£ x , y âˆ£ = x
 fst (x , y) = x

 âˆ¥_âˆ¥ snd : (z : Î£[ a âˆˆ A ] B a) â†’ B âˆ£ z âˆ£
 âˆ¥ x , y âˆ¥ = y
 snd (x , y) = y

\end{code}

Here we put the definitions inside an *anonymous module*, which starts with the `module` keyword followed by an underscore (instead of a module name). The purpose is simply to move the postulated typing judgments---the "parameters" of the module (e.g., `A : Type ğ“¤`)---out of the way so they don't obfuscate the definitions inside the module.

Also note that multiple inhabitants of a single type (e.g., `âˆ£_âˆ£` and `fst`) may be declared on the same line.

----------------------------------------

<sup>0</sup><span class="footnote" id="fn0"> We avoid using `ğ“Ÿ` as a universe
variable because in some libraries `ğ“Ÿ` denotes a powerset type.</span>

<sup>1</sup><span class="footnote" id="fn1"> [MartÃ­n EscardÃ³][] has written an
outstanding set of notes entitled
[Introduction to Univalent Foundations of Mathematics with Agda](https://www.cs.bham.ac.uk/~mhe/HoTT-UF-in-Agda-Lecture-Notes/index.html)
which we highly recommend to anyone wanting more details than we provide here
about [MLTT][] and Univalent Foundations/HoTT in Agda.</span>

<sup>2</sup><span class="footnote" id="fn2"> We won't discuss every line of the `Universes.lagda` file; instead we merely highlight the few lines of code from the `Universes` module that define the notational devices adopted throughout [UniversalAlgebra][]. For more details we refer readers to [Martin Escardo's notes](https://www.cs.bham.ac.uk/~mhe/HoTT-UF-in-Agda-Lecture-Notes).</span>

<br>
<br>

[â†‘ Overture](Overture.html)
<span style="float:right;">[Overture.Equality â†’](Overture.Equality.html)</span>

[agda-algebras]: https://github.com/ualib/agda-algebras

{% include UALib.Links.md %}


