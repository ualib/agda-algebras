---
layout: default
title : Overture.Preliminaries module
date : 2021-01-13
author: [the ualib/agda-algebras development team][]
---

### <a id="preliminaries">Preliminaries</a>

This is the [Overture.Preliminaries][] module of the [agda-algebras][].

#### <a id="logical-foundations">Logical foundations</a>

The [Agda Universal Algebra Library](https://github.com/ualib/agda-algebras) (or [agda-algebras](https://github.com/ualib/agda-algebras) for short) is based on a version of [Martin-LoÃàf type theory (MLTT)](https://ncatlab.org/nlab/show/Martin-L%C3%B6f+dependent+type+theory). We don't discuss [MLTT](https://ncatlab.org/nlab/show/Martin-L%C3%B6f+dependent+type+theory) in great detail here because there are already good and freely available resources covering the theory. (See, for example, the section of [Escard√≥'s notes](https://www.cs.bham.ac.uk/~mhe/HoTT-UF-in-Agda-Lecture-Notes) on [A spartan Martin-L√∂f type theory](https://www.cs.bham.ac.uk/~mhe/HoTT-UF-in-Agda-Lecture-Notes/HoTT-UF-Agda.html\#mlttinagda), or the [ncatlab entry on Martin-LoÃàf dependent type theory](https://ncatlab.org/nlab/show/Martin-L\%C3\%B6f+dependent+type+theory), or the [HoTT book](https://homotopytypetheory.org/book/).)

The objects and assumptions that form the foundation of [MLTT](https://ncatlab.org/nlab/show/Martin-L%C3%B6f+dependent+type+theory) are few. There are the *primitive types* (`ùüò`, `ùüô`, and `‚Ñï`, denoting the empty type, one-element type, and natural numbers), the *type formers* (`+`, `Œ†`, `Œ£`, `Id`, denoting *binary sum*, *product*, *sum*, and the *identity* type). Each of these type formers is defined by a *type forming rule* which specifies how that type is constructed. Lastly, we have an infinite collection of *type universes* (types of types) and *universe variables* to denote them.

There are no further axioms or logical deduction (proof derivation) rules needed for the foundations of
[MLTT](https://ncatlab.org/nlab/show/Martin-L%C3%B6f+dependent+type+theory) that we take as the starting point of [agda-algebras][].  The logical semantics come from the [propositions-as-types correspondence](https://ncatlab.org/nlab/show/propositions+as+types): propositions and predicates are represented by types and the inhabitants of these types are the proofs of the propositions and predicates.  As such, proofs are constructed using the type forming rules. In other words, the type forming rules *are* the proof derivation rules.

To this foundation, we add certain *extensionality principles* when and were we need them.  These will be developed as we progress.  However, classical axioms such as the [*Axiom of Choice*](https://ncatlab.org/nlab/show/axiom+of+choice) or the [*Law of the Excluded Middle*](https://ncatlab.org/nlab/show/excluded+middle) are not needed and are not assumed anywhere in the library.  In this sense, all theorems and proofs in [agda-algebras][] are [*constructive*](https://ncatlab.org/nlab/show/constructive+mathematics) (according to [nlab's definition](https://ncatlab.org/nlab/show/constructive+mathematics)).

A few specific instances (e.g., the proof of the Noether isomorphism theorems and Birkhoff's HSP theorem) require certain *truncation* assumptions. In such cases, the theory is not [predicative](https://ncatlab.org/nlab/show/predicative+mathematics) (according to [nlab's definition](https://ncatlab.org/nlab/show/predicative+mathematics)). These instances are always clearly identified.

#### <a id="specifying-logical-foundations">Specifying logical foundations in Agda</a>

An Agda program typically begins by setting some options and by importing types from existing Agda libraries. Options are specified with the `OPTIONS` *pragma* and control the way Agda behaves by, for example, specifying the logical axioms and deduction rules we wish to assume when the program is type-checked to verify its correctness. Every Agda program in [agda-algebras][] begins with the following line.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

\end{code}

These options control certain foundational assumptions that Agda makes when type-checking the program to verify its correctness.

* `--without-K` disables [Streicher's K axiom](https://ncatlab.org/nlab/show/axiom+K+%28type+theory%29) ; see also the [section on axiom K](https://agda.readthedocs.io/en/v2.6.1/language/without-k.html) in the [Agda Language Reference][] manual.

* `--exact-split` makes Agda accept only those definitions that behave like so-called *judgmental* equalities.  [Escard√≥][] explains this by saying it "makes sure that pattern matching corresponds to Martin-L√∂f eliminators;" see also the [Pattern matching and equality section](https://agda.readthedocs.io/en/v2.6.1/tools/command-line-options.html#pattern-matching-and-equality) of the [Agda Tools][] documentation.

* `safe` ensures that nothing is postulated outright---every non-MLTT axiom has to be an explicit assumption (e.g., an argument to a function or module); see also [this section](https://agda.readthedocs.io/en/v2.6.1/tools/command-line-options.html#cmdoption-safe) of the [Agda Tools][] documentation and the [Safe Agda section](https://agda.readthedocs.io/en/v2.6.1/language/safe-agda.html#safe-agda) of the [Agda Language Reference][].

Note that if we wish to type-check a file that imports another file that still has some unmet proof obligations, we must replace the `--safe` flag with `--allow-unsolved-metas`, but this is never done in (publicly released versions of) the [agda-algebras][] library.

#### <a id="agda-modules">Agda Modules</a>

The `OPTIONS` pragma is usually followed by the start of a module.  For example, the [Overture.Preliminaries][] module begins with the following line, and then a list of imports of things used in the module.

\begin{code}
module Overture.Preliminaries where

-- Imports from the Agda (Builtin) and the Agda Standard Library
open import Agda.Builtin.Equality                 using    ( _‚â°_      ;  refl   )
open import Function.Base                         using    ( _‚àò_      ;  id     )
open import Relation.Binary.PropositionalEquality using    ( sym      ;  trans  )
open import Data.Product                          using    ( _,_      ;   Œ£
                                                           ; Œ£-syntax ;   _√ó_   )
                                                  renaming ( proj‚ÇÅ    to  fst
                                                           ; proj‚ÇÇ    to  snd   )
open import Agda.Primitive                        using    ( _‚äî_                )
                                                  renaming ( Set      to  Type  )
open import Level                                 using    ( Level    ;   Lift
                                                           ; lift     ;   lower )
                                                  renaming ( suc      to  lsuc  )

\end{code}

We prefer to use `Type` in place of Agda's `Set` since for us *set* will mean a very special kind of (truncated) type. (See [Relations.Truncation][]). We also use `‚Ñì‚ÇÄ` in place of level `zero`, and `‚Ñì‚ÇÅ` for the successor of `‚Ñì‚ÇÄ`.


#### <a id="agda-universes">Agda Universes</a>

Here we import the basic primitive operations we need for working with Agda's type universes. For the very small amount of background about *type universes* we require, we refer the reader to the brief [section on universe-levels](https://agda.readthedocs.io/en/v2.6.1.3/language/universe-levels.html) in the Agda documentation.

\begin{code}

private variable Œ± Œ≤ : Level

\end{code}

#### <a id="projection notation">Projection notation</a>

The definition of `Œ£` (and thus, of `√ó`) includes the fields `proj‚ÇÅ` and `proj‚ÇÇ` representing the first and second projections out of the product.  However, we prefer the shorter names `fst` and `snd`.  Sometimes we prefer to denote these projections by `‚à£_‚à£` and `‚à•_‚à•`, respectively. We define these alternative notations for projections out of pairs as follows.

\begin{code}

module _ {A : Type Œ± }{B : A ‚Üí Type Œ≤} where

 ‚à£_‚à£ : Œ£[ x ‚àà A ] B x ‚Üí A
 ‚à£_‚à£ = fst

 ‚à•_‚à• : (z : Œ£[ a ‚àà A ] B a) ‚Üí B ‚à£ z ‚à£
 ‚à•_‚à• = snd

 infix  40 ‚à£_‚à£
\end{code}

Here we put the definitions inside an *anonymous module*, which starts with the `module` keyword followed by an underscore (instead of a module name). The purpose is simply to move the postulated typing judgments---the "parameters" of the module (e.g., `A : Type Œ±`)---out of the way so they don't obfuscate the definitions inside the module.

Also note that multiple inhabitants of a single type (e.g., `‚à£_‚à£` and `fst`) may be declared on the same line.

We prove that `‚â°` obeys the substitution rule (subst) in the next subsection (see the definition of `ap` below), but first we define some syntactic sugar that will make it easier to apply symmetry and transitivity of `‚â°` in proofs.<sup>[2](Overture.Equality.html#fn3)</sup>

\begin{code}

_‚Åª¬π : {A : Type Œ±} {x y : A} ‚Üí x ‚â° y ‚Üí y ‚â° x
p ‚Åª¬π = sym p

infix  40 _‚Åª¬π

\end{code}

If we have a proof `p : x ‚â° y`, and we need a proof of `y ‚â° x`, then instead of `sym p` we can use the more intuitive `p ‚Åª¬π`. Similarly, the following syntactic sugar makes abundant appeals to transitivity easier to stomach.

\begin{code}

_‚àô_ : {A : Type Œ±}{x y z : A} ‚Üí x ‚â° y ‚Üí y ‚â° z ‚Üí x ‚â° z
p ‚àô q = trans p q

ùëñùëë : (A : Type Œ± ) ‚Üí A ‚Üí A
ùëñùëë A = Œª x ‚Üí x

infixl 30 _‚àô_
\end{code}


#### <a id="agdas-universe-hierarchy">Agda's universe hierarchy</a>

The hierarchy of universes in Agda is structured as follows:<sup>[1](Overture.Lifts.html#fn1)</sup>

```agda
Type Œ± : Type (lsuc Œ±),   Type(lsuc Œ±) : Type (lsuc (lsuc Œ±)),  etc.
```

This means that the universe `Type Œ±` has type `Type(lsuc Œ±)`, and  `Type(lsuc Œ±)` has type `Type(lsuc (lsuc Œ±))`, and so on.  It is important to note, however, this does *not* imply that  `Type Œ± : Type(lsuc(lsuc Œ±))`. In other words, Agda's universe hierarchy is *non-cumulative*. This makes it possible to treat universe levels more precisely, which is nice. On the other hand, a non-cumulative hierarchy can sometimes make for a non-fun proof assistant. Specifically, in certain situations, the non-cumulativity makes it unduly difficult to convince Agda that a program or proof is correct.




#### <a id="lifting-and-lowering">Lifting and lowering</a>

Here we describe a general `Lift` type that help us overcome the technical issue described in the previous subsection.  In the [Lifts of algebras section](Algebras.Algebras.html#lifts-of-algebras) of the [Algebras.Algebras][] module we will define a couple domain-specific lifting types which have certain properties that make them useful for resolving universe level problems when working with algebra types.

Let us be more concrete about what is at issue here by considering a typical example. Agda will often complain with errors like the following:

<samp>
Birkhoff.lagda:498,20-23 <br>
Œ± != ùìû ‚äî ùì• ‚äî (lsuc Œ±) when checking that the expression... has type...
</samp>

This error message means that Agda encountered the universe level `lsuc Œ±`, on line 498 (columns 20--23) of the file `Birkhoff.lagda`, but was expecting a type at level `ùìû ‚äî ùì• ‚äî lsuc Œ±` instead.

The general `Lift` record type that we now describe makes these situations easier to deal with. It takes a type inhabiting some universe and embeds it into a higher universe and, apart from syntax and notation, it is equivalent to the `Lift` type one finds in the `Level` module of the [Agda Standard Library][].

```agda
 record Lift {ùì¶ Œ± : Level} (A : Type Œ±) : Type (Œ± ‚äî ùì¶) where
  constructor lift
  field lower : A
```

The point of having a ramified hierarchy of universes is to avoid Russell's paradox, and this would be subverted if we were to lower the universe of a type that wasn't previously lifted.  However, we can prove that if an application of `lower` is immediately followed by an application of `lift`, then the result is the identity transformation. Similarly, `lift` followed by `lower` is the identity.

\begin{code}

lift‚àºlower : {A : Type Œ±} ‚Üí lift ‚àò lower ‚â° ùëñùëë (Lift Œ≤ A)
lift‚àºlower = refl

lower‚àºlift : {A : Type Œ±} ‚Üí lower {Œ±}{Œ≤}(lift {Œ±}{Œ≤}(Œª x ‚Üí x)) ‚â° ùëñùëë A
lower‚àºlift = refl

\end{code}

The proofs are trivial. Nonetheless, we'll come across some holes these lemmas can fill.

#### <a id="pointwise-equality-of-dependent-functions">Pointwise equality of dependent functions</a>

We conclude this module with a definition that conveniently represents te assertion that two functions are (extensionally) the same in the sense that they produce the same output when given the same input.  (We will have more to say about this notion of equality in the [Relations.Extensionality][] module.)

\begin{code}

_‚àº_ : {A : Type Œ± } {B : A ‚Üí Type Œ≤ } ‚Üí (f g : (a : A) ‚Üí B a) ‚Üí Type (Œ± ‚äî Œ≤)
f ‚àº g = ‚àÄ x ‚Üí f x ‚â° g x

infix 8 _‚àº_

\end{code}

The following is convenient for proving two pairs of a product type are equal using the fact that their
respective components are equal.

\begin{code}

‚â°-by-parts : {A : Type Œ±}{B : Type Œ≤}{u v : A √ó B} ‚Üí fst u ‚â° fst v ‚Üí snd u ‚â° snd v ‚Üí u ‚â° v
‚â°-by-parts refl refl = refl

\end{code}

Lastly, we will use the following type (instead of `subst`) to transport equality proofs.

\begin{code}

transport : {A : Type Œ± } (B : A ‚Üí Type Œ≤) {x y : A} ‚Üí x ‚â° y ‚Üí B x ‚Üí B y
transport B refl = id

\end{code}

---------------

<sup>1</sup><span class="footnote" id="fn0"> We avoid using `ùìü` as a universe
variable because in some libraries `ùìü` denotes a powerset type.</span>

<sup>2</sup> <span class="footnote" id="fn2"> Most of these types are already defined by in the [Type Topology][] library or the [Agda Standard Library][], so we often imports the definitions; occasionally, however, we repeat the definitions here for pedagogical reasons and to keep the presentation somewhat self-contained.


<sup>4</sup> <span class="footnote" id="fn4"> Moreover, if one assumes the [univalence axiom][] of [Homotopy Type Theory][], then point-wise equality of functions is equivalent to definitional equality of functions. (See [Function extensionality from univalence](https://www.cs.bham.ac.uk/~mhe/HoTT-UF-in-Agda-Lecture-Notes/HoTT-UF-Agda.html#funextfromua).)</span>

<sup>5</sup><span class="footnote" id="fn5">Recall, from the [Overture.Preliminaries][] module, the special notation we use to denote Agda's *levels* and *universes*.</span>

<br>
<br>

[‚Üë Overture](Overture.html)
<span style="float:right;">[Overture.Inverses ‚Üí](Overture.Inverses.html)</span>

{% include UALib.Links.md %}

------------------------------

[the ualib/agda-algebras development team]: https://github.com/ualib/agda-algebras#the-ualib-agda-algebras-development-team
[agda-algebras]: https://github.com/ualib/agda-algebras


