---
layout: default
title : Relations.Extensionality module (The Agda Universal Algebra Library)
date : 2021-02-23
author: William DeMeo
---

### <a id="relation-extensionality">Relation Extensionality</a>

This section presents the [Relations.Extensionality][] module of the [Agda Universal Algebra Library][].

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

module Relations.Extensionality where

open import Relations.Truncation public
open import MGS-MLTT using (ğŸ™) public

\end{code}

The principle of *proposition extensionality* asserts that logically equivalent propositions are equivalent.  That is, if `P` and `Q` are propositions and if `P âŠ† Q` and `Q âŠ† P`, then `P â‰¡ Q`. For our purposes, it will suffice to formalize this notion for general predicates, rather than for propositions (i.e., truncated predicates).

\begin{code}

pred-ext : (ğ“¤ ğ“¦ : Level) â†’ Type (lsuc (ğ“¤ âŠ” ğ“¦))
pred-ext ğ“¤ ğ“¦ = âˆ€ {A : Type ğ“¤}{P Q : Pred A ğ“¦ } â†’ P âŠ† Q â†’ Q âŠ† P â†’ P â‰¡ Q

\end{code}

Note that `pred-ext` merely defines an extensionality principle. It does not postulate that the principle holds.  If we wish to postulate `pred-ext`, then we do so by assuming that type is inhabited (see `block-ext` below, for example).



#### <a id="quotient-extensionality">Quotient extensionality</a>

We need an identity type for congruence classes (blocks) over sets so that two different presentations of the same block (e.g., using different representatives) may be identified.  This requires two postulates: (1) *predicate extensionality*, manifested by the `pred-ext` type; (2) *equivalence class truncation* or "uniqueness of block identity proofs", manifested by the `blk-uip` type defined in the [Relations.Truncation][] module. We now use `pred-ext` and `blk-uip` to define a type called `block-ext|uip` which we require for the proof of the First Homomorphism Theorem presented in [Homomorphisms.Noether][].

\begin{code}

module _ {A : Type ğ“¤}{R : Rel A ğ“¦} where

 block-ext : pred-ext ğ“¤ ğ“¦ â†’ IsEquivalence R â†’ {u v : A} â†’ R u v â†’ [ u ]{R} â‰¡ [ v ]{R}
 block-ext pe Req {u}{v} Ruv = pe (/-subset Req Ruv) (/-supset Req Ruv)


 to-subtype|uip : blk-uip A R â†’ {C D : Pred A ğ“¦}{c : IsBlock C {R}}{d : IsBlock D {R}}
  â†’               C â‰¡ D â†’ (C , c) â‰¡ (D , d)

 to-subtype|uip buip {C}{D}{c}{d}CD = to-Î£-â‰¡(CD , buip D(transport(Î» B â†’ IsBlock B)CD c)d)


 block-ext|uip : pred-ext ğ“¤ ğ“¦ â†’ blk-uip A R â†’ IsEquivalence R â†’ âˆ€{u}{v} â†’ R u v â†’ âŸª u âŸ« â‰¡ âŸª v âŸ«
 block-ext|uip pe buip Req Ruv = to-subtype|uip buip (block-ext pe Req Ruv)

\end{code}



#### <a id="strongly-well-defined-operations">Strongly well-defined operations</a>

We now describe an extensionality principle that seems weaker than function extensionality, but still (probably) not provable in [MLTT][]. (We address this and other questions  below.)  We call this the principle *strong well-definedness of operations*. We will encounter situations in which this weaker extensionality principle suffices as a substitute for function extensionality.

Suppose we have a function whose domain is a function type, say, `I â†’ A`.  For example, inhabitants of the type `Op` defined above are such functions.  (Recall, the domain of inhabitants of type `Op I A := (I â†’ A) â†’ A` is `I â†’ A`.)

Of course, operations of type `Op I A` are well-defined in the sense that equal inputs result in equal outputs.

\begin{code}

welldef : {A : Type ğ“¤}{I : Type ğ“¥}(f : Op I A) â†’ âˆ€ u v â†’ u â‰¡ v â†’ f u â‰¡ f v
welldef f u v refl = refl

\end{code}

A stronger form of well-definedness of operations is to suppose that point-wise equal inputs lead to the same output.  In other terms, we could suppose that  for all `f : Op I A`, we have `f u â‰¡ f v` whenever `âˆ€ i â†’ u i â‰¡ v i` holds.  We call this formalize this notation in the following type.

\begin{code}

swelldef : (ğ“¥ ğ“¤ : Level) â†’ Type (lsuc (ğ“¤ âŠ” ğ“¥))
swelldef ğ“¥ ğ“¤ = âˆ€ {A : Type ğ“¤}{I : Type ğ“¥}(f : Op I A)(u v : I â†’ A) â†’ (âˆ€ i â†’ u i â‰¡ v i) â†’ f u â‰¡ f v

funextâ†’swelldef : {ğ“¤ ğ“¥ : Level} â†’ dfunext ğ“¥ ğ“¤ â†’ swelldef ğ“¥ ğ“¤
funextâ†’swelldef fe f u v ptweq = Î³
 where
 uv : u â‰¡ v
 uv = fe ptweq
 Î³ : f u â‰¡ f v
 Î³ = welldef f u v uv

\end{code}


-----------------------------

#### <a id="general-relation-extensionality">General relation extensionality*</a>

We define the following *relation extensionality* principles which generalize the predicate extensionality principle (`pred-ext`) defined above.

\begin{code}

cont-relext : (ğ“¤ ğ“¥ ğ“¦ : Level) â†’ Type (lsuc (ğ“¤ âŠ” ğ“¥ âŠ” ğ“¦))
cont-relext ğ“¤ ğ“¥ ğ“¦ = âˆ€ {I : Type ğ“¥}{A : Type ğ“¤}{P Q : ContRel I A ğ“¦ } â†’ P âŠ† Q â†’ Q âŠ† P â†’ P â‰¡ Q

dep-relext : (ğ“¤ ğ“¥ ğ“¦ : Level) â†’ Type (lsuc (ğ“¤ âŠ” ğ“¥ âŠ” ğ“¦))
dep-relext ğ“¤ ğ“¥ ğ“¦ = âˆ€ {I : Type ğ“¥}{ğ’œ : I â†’ Type ğ“¤}{P Q : DepRel I ğ’œ ğ“¦ } â†’ P âŠ† Q â†’ Q âŠ† P â†’ P â‰¡ Q

\end{code}

These types are not used in other modules of the [UniversalAlgebra][] library.


-------

#### <a id="open-questions-about-extensionality">Open questions about strength and provability of extensionality principles</a>

Here are some questions about extensionaity for future consideration.

**Questions about strong vs weak well-definedness**.

1. Is strong well-definedness of operations (`swelldef`) provable in [MLTT][]?

2. Is strong well-definedness of operations strictly weaker than function extensionality?


**Questions about prop vs pred extensionality**.

1. Is predicate extensionality (`pred-ext`) at least as strong as proposition extensionality?  That is, does `pred-ext ğ“¤ ğ“¦` imply

   `âˆ€(A : Type ğ“¤)(P : Pred A ğ“¦)(x : A)(p q : P x) â†’ p â‰¡ q` ?

2. Are the relation extensionality principles `cont-relext` and `dep-relext` at least as strong as proposition extensionality?

---------------------------------------

[â† Relations.Truncation](Relations.Truncation.html)
<span style="float:right;">[Algebras â†’](Algebras.html)</span>


{% include UALib.Links.md %}


