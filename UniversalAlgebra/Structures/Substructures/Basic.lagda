---
layout: default
title : Structures.Substructures.Basic module (The Agda Universal Algebra Library)
date : 2021-07-23
author: [agda-algebras development team][]
---

### Subuniverses of Structures

This is the [Structures.Substructures.Basic][] module of the [Agda Universal Algebra Library][].

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

module Structures.Substructures.Basic where

-- imports from Agda and the Agda Standard Library
open import Agda.Builtin.Equality using ( _â‰¡_ ; refl )
open import Agda.Primitive        using ( _âŠ”_ ; lsuc ; Level ) renaming ( Set to Type )
open import Data.Product          using ( _,_ ; Î£-syntax ; _Ã—_ ) renaming ( projâ‚ to fst ; projâ‚‚ to snd )
open import Function.Base         using ( _âˆ˜_ )
open import Relation.Unary        using ( Pred ; _âˆˆ_ ; _âŠ†_ ; â‹‚ )
import Relation.Binary.PropositionalEquality as PE

-- -- -- imports from agda-algebras ------------------------------------------------------
open import Overture.Preliminaries   using ( âˆ£_âˆ£ ; âˆ¥_âˆ¥ ; _â»Â¹ )
open import Relations.Discrete       using ( Im_âŠ†_ )
open import Relations.Extensionality using ( swelldef )
open import Structures.Basic         using ( signature ; structure ; _áµ’_ ; sigl ; siglË¡ ; siglÊ³ )
open import Structures.Homs          using ( hom )
open import Structures.Terms

open structure
open signature

private variable
 ğ“â‚€ ğ“¥â‚€ ğ“â‚ ğ“¥â‚ Ï Î± Ïáµƒ Î² Ïáµ‡ Î³ Ïá¶œ Ï‡ Î¹ : Level
 ğ¹ : signature ğ“â‚€ ğ“¥â‚€
 ğ‘… : signature ğ“â‚ ğ“¥â‚

module _ {ğ‘¨ : structure ğ¹ ğ‘… {Î±}{Ïáµƒ}}
         {X : Type Ï‡} where

 Subuniverses : Pred (Pred (carrier ğ‘¨) Ï) (sigl ğ¹ âŠ” Î± âŠ” Ï)
 Subuniverses B = âˆ€ f a â†’ Im a âŠ† B â†’ (f áµ’ ğ‘¨) a âˆˆ B

 -- Subuniverses as a record type
 record Subuniverse : Type (sigl ğ¹ âŠ” Î± âŠ” lsuc Ï) where
  constructor mksub
  field       sset  : Pred (carrier ğ‘¨) Ï
              isSub : sset âˆˆ Subuniverses


 -- Subuniverse Generation
 data Sg (G : Pred (carrier ğ‘¨) Ï) : Pred (carrier ğ‘¨) (sigl ğ¹ âŠ” Î± âŠ” Ï) where
  var : âˆ€ {v} â†’ v âˆˆ G â†’ v âˆˆ Sg G
  app : âˆ€ f a â†’ Im a âŠ† Sg G â†’ (f áµ’ ğ‘¨) a âˆˆ Sg G

\end{code}

(The inferred types in the `app` constructor are `f : âˆ£ ğ‘† âˆ£` and `a : âˆ¥ ğ‘† âˆ¥ ğ‘“ â†’ âˆ£ ğ‘¨ âˆ£`.)

Given an arbitrary subset `X` of the domain `âˆ£ ğ‘¨ âˆ£` of an `ğ‘†`-algebra `ğ‘¨`, the type `Sg X` does indeed represent a subuniverse of `ğ‘¨`. Proving this using the inductive type `Sg` is trivial, as we see here.

\begin{code}

 sgIsSub : {G : Pred (carrier ğ‘¨) Ï} â†’ Sg G âˆˆ Subuniverses
 sgIsSub = app

\end{code}

Next we prove by structural induction that `Sg X` is the smallest subuniverse of `ğ‘¨` containing `X`.

\begin{code}

 sgIsSmallest : {G : Pred (carrier ğ‘¨) Ï}(B : Pred (carrier ğ‘¨) Ïáµ‡)
  â†’             B âˆˆ Subuniverses  â†’  G âŠ† B  â†’  Sg G âŠ† B

 sgIsSmallest _ _ GâŠ†B (var Gx) = GâŠ†B Gx
 sgIsSmallest B Bâ‰¤A GâŠ†B {.((f áµ’ ğ‘¨) a)} (app f a SgGa) = Goal
  where
  IH : Im a âŠ† B
  IH i = sgIsSmallest B Bâ‰¤A GâŠ†B (SgGa i)

  Goal : (f áµ’ ğ‘¨) a âˆˆ B
  Goal = Bâ‰¤A f a IH

\end{code}

When the element of `Sg G` is constructed as `app f a SgGa`, we may assume (the induction hypothesis) that the arguments in the tuple `a` belong to `B`. Then the result of applying `f` to `a` also belongs to `B` since `B` is a subuniverse.

\begin{code}

 â‹‚s : (I : Type Î¹){ğ’œ : I â†’ Pred (carrier ğ‘¨) Ï}
  â†’   (âˆ€ i â†’ ğ’œ i âˆˆ Subuniverses) â†’ â‹‚ I ğ’œ âˆˆ Subuniverses

 â‹‚s I Ïƒ f a Î½ = Î» i â†’ Ïƒ i f a (Î» x â†’ Î½ x i)

\end{code}

In the proof above, we assume the following typing judgments:

```
Î½    : Im a âŠ† â‹‚ I ğ’œ
a    : arity ğ¹ f â†’ carrier ğ‘¨
f    : symbol ğ¹
Ïƒ    : (i : I) â†’ ğ’œ i âˆˆ Subuniverses
ğ’œ    : I â†’ Pred (carrier ğ‘¨) Ï   (not in scope)
```
and we must prove `(f áµ’ ğ‘¨) a âˆˆ â‹‚ I ğ’œ`.   Agda can fill in the proof term
`Î» i â†’ Ïƒ i f a (Î» x â†’ Î½ x i)` automatically using `C-c C-a`.

\begin{code}


 -- subuniverses are closed under the action of term operations
 sub-term-closed : (B : Pred (carrier ğ‘¨) Ï) â†’ (B âˆˆ Subuniverses)
  â†’                (t : Term X)(b : X â†’ (carrier ğ‘¨))
  â†’                (Im b âŠ† B) â†’ (ğ‘¨ âŸ¦ t âŸ§) b âˆˆ B

 sub-term-closed _ _ (â„Š x) b Bb = Bb x

 sub-term-closed B Bâ‰¤A (node f t) b Î½ =
  Bâ‰¤A f (Î» z â†’ (ğ‘¨ âŸ¦ t z âŸ§) b) (Î» x â†’ sub-term-closed B Bâ‰¤A (t x) b Î½)

\end{code}

In the induction step of the foregoing proof, the typing judgments of the premise are the following:

```
Î½    : Im b âŠ† B
b    : X â†’ carrier ğ‘¨
t    : arity ğ¹ f â†’ Term X
f    : symbol ğ¹
Bâ‰¤A  : B âˆˆ Subuniverses
B    : Pred (carrier ğ‘¨) Ï
ğ‘¨    : structure ğ¹ ğ‘…
```
and the given proof term establishes the goal `op ğ‘¨ f (Î» i â†’ (ğ‘¨ âŸ¦ t i âŸ§) b) âˆˆ B`

Alternatively, we could express the preceeding fact using an inductive type representing images of terms.

\begin{code}

 data TermImage (B : Pred (carrier ğ‘¨) Ï) : Pred (carrier ğ‘¨) (sigl ğ¹ âŠ” Î± âŠ” Ï)
  where
  var : âˆ€ {b : carrier ğ‘¨} â†’ b âˆˆ B â†’ b âˆˆ TermImage B
  app : âˆ€ f ts â†’  ((i : (arity ğ¹) f) â†’ ts i âˆˆ TermImage B)  â†’ (f áµ’ ğ‘¨) ts âˆˆ TermImage B

 -- `TermImage B` is a subuniverse of ğ‘¨ that contains B.
 TermImageIsSub : {B : Pred (carrier ğ‘¨) Ï} â†’ TermImage B âˆˆ Subuniverses
 TermImageIsSub = app

 B-onlyif-TermImageB : {B : Pred (carrier ğ‘¨) Ï} â†’ B âŠ† TermImage B
 B-onlyif-TermImageB Ba = var Ba

 -- Since `Sg B` is the smallest subuniverse containing B, we obtain the following inclusion.
 SgB-onlyif-TermImageB : (B : Pred (carrier ğ‘¨) Ï) â†’ Sg B âŠ† TermImage B
 SgB-onlyif-TermImageB B = sgIsSmallest (TermImage B) TermImageIsSub B-onlyif-TermImageB


 module _ {ğ‘© : structure ğ¹ ğ‘… {Î²}{Ïáµ‡}} where

  private
   A = carrier ğ‘¨
   B = carrier ğ‘©

  -- Homomorphisms are uniquely determined by their values on a generating set.
  hom-unique : swelldef (siglÊ³ ğ¹) Î² â†’ (G : Pred A Ï)  (g h : hom ğ‘¨ ğ‘©)
   â†’           ((x : A) â†’ (x âˆˆ G â†’ âˆ£ g âˆ£ x â‰¡ âˆ£ h âˆ£ x))
               -------------------------------------------------
   â†’           (a : A) â†’ (a âˆˆ Sg G â†’ âˆ£ g âˆ£ a â‰¡ âˆ£ h âˆ£ a)

  hom-unique _ G g h Ïƒ a (var Ga) = Ïƒ a Ga
  hom-unique wd G g h Ïƒ .((f áµ’ ğ‘¨) a) (app f a SgGa) = Goal
   where
   IH : âˆ€ x â†’ âˆ£ g âˆ£ (a x) â‰¡ âˆ£ h âˆ£ (a x)
   IH x = hom-unique wd G g h Ïƒ (a x) (SgGa x)
   open PE.â‰¡-Reasoning
   Goal : âˆ£ g âˆ£ ((f áµ’ ğ‘¨) a) â‰¡ âˆ£ h âˆ£ ((f áµ’ ğ‘¨) a)
   Goal = âˆ£ g âˆ£ ((f áµ’ ğ‘¨) a) â‰¡âŸ¨ snd âˆ¥ g âˆ¥ f a âŸ©
          (f áµ’ ğ‘©)(âˆ£ g âˆ£ âˆ˜ a ) â‰¡âŸ¨ wd (f áµ’ ğ‘©) (âˆ£ g âˆ£ âˆ˜ a) (âˆ£ h âˆ£ âˆ˜ a) IH âŸ©
          (f áµ’ ğ‘©)(âˆ£ h âˆ£ âˆ˜ a)  â‰¡âŸ¨ (snd âˆ¥ h âˆ¥ f a)â»Â¹ âŸ©
          âˆ£ h âˆ£ ((f áµ’ ğ‘¨) a )  âˆ

\end{code}

In the induction step, the following typing judgments are assumed:

```
SgGa : Im a âŠ† Sg G
a    : arity ğ¹ f â†’ carrier ğ‘¨
f    : symbol ğ¹
Ïƒ    : (x : A) â†’ x âˆˆ G â†’ âˆ£ g âˆ£ x â‰¡ âˆ£ h âˆ£ x
h    : hom ğ‘¨ ğ‘©
g    : hom ğ‘¨ ğ‘©
G    : Pred A Ï
wd   : swelldef (siglÊ³ ğ¹) Î²
ğ‘©    : structure ğ¹ ğ‘…
```

and, under these assumptions, we proved `âˆ£ g âˆ£ ((f áµ’ ğ‘¨) a) â‰¡ âˆ£ h âˆ£ ((f áµ’ ğ‘¨) a)`.

------------------------------

[agda-algebras development team]: https://github.com/ualib/agda-algebras#the-agda-algebras-development-team


