---
layout: default
title : Homomorphisms.Basic module (The Agda Universal Algebra Library)
date : 2021-01-13
author: William DeMeo
---

### <a id="basic-definitions">Basic Definitions</a>

This section describes the [Homomorphisms.Basic] module of the [Agda Universal Algebra Library][].

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

-- Imports from the Agda (Builtin) and the Agda Standard Library
open import Agda.Builtin.Equality using (_â‰¡_; refl)
open import Axiom.Extensionality.Propositional renaming (Extensionality to funext)
open import Data.Product using (_,_; Î£; _Ã—_)
open import Function.Base  using (_âˆ˜_; id)
open import Level renaming (suc to lsuc; zero to lzero)
open import Relation.Binary using (Rel; IsEquivalence)
open import Relation.Binary.PropositionalEquality.Core using (sym; trans; cong)

-- Imports from the Agda Universal Algebra Library
open import Algebras.Basic
open import Overture.Preliminaries using (Type; ğ“; ğ“¤; ğ“¥; ğ“¦; ğ“§; ğ“¨; ğ“©; Î ; -Î ; -Î£; _â‰¡âŸ¨_âŸ©_; _âˆ; _â»Â¹; âˆ£_âˆ£; âˆ¥_âˆ¥; fst)
open import Overture.Inverses using (IsInjective; IsSurjective; Image_âˆ‹_)
open import Relations.Discrete using (ker) -- ğŸ; _|:_)
open import Relations.Extensionality using (swelldef)
open import Relations.Quotients using (ker-IsEquivalence; _/_; âŸª_âŸ«)

module Homomorphisms.Basic {ğ‘† : Signature ğ“ ğ“¥} where

open import Algebras.Congruences{ğ‘† = ğ‘†} using (Con; IsCongruence; mkcon; _â•±_; /-â‰¡)
open import Algebras.Products{ğ‘† = ğ‘†} using (â¨…)
open IsCongruence

\end{code}

#### <a id="homomorphisms">Homomorphisms</a>

If `ğ‘¨` and `ğ‘©` are `ğ‘†`-algebras, then a *homomorphism* from `ğ‘¨` to `ğ‘©` is a function `h : âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£` from the domain of `ğ‘¨` to the domain of `ğ‘©` that is *compatible* (or *commutes*) with all of the basic operations of the signature; that is, for all operation symbols `ğ‘“ : âˆ£ ğ‘† âˆ£` and tuples `a : âˆ¥ ğ‘† âˆ¥ ğ‘“ â†’ âˆ£ ğ‘¨ âˆ£` of `ğ‘¨`, the following holds:<sup>[1](Homomorphisms.Basic.html#fn1)</sup>

`h ((ğ‘“ Ì‚ ğ‘¨) a) â‰¡ (ğ‘“ Ì‚ ğ‘©) (h âˆ˜ a)`.

To formalize this concept, we first define a type representing the assertion that a function `h : âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£` commutes with a single basic operation `ğ‘“`.  With Agda's extremely flexible syntax the defining equation above can be expressed unadulterated.

\begin{code}

module _ (ğ‘¨ : Algebra ğ“¤ ğ‘†)(ğ‘© : Algebra ğ“¦ ğ‘†) where

 compatible-op-map : âˆ£ ğ‘† âˆ£ â†’ (âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£) â†’ Type(ğ“¤ âŠ” ğ“¥ âŠ” ğ“¦)
 compatible-op-map ğ‘“ h = âˆ€ ğ‘ â†’ h ((ğ‘“ Ì‚ ğ‘¨) ğ‘) â‰¡ (ğ‘“ Ì‚ ğ‘©) (h âˆ˜ ğ‘)

\end{code}

Agda infers from the shorthand `âˆ€ ğ‘` that `ğ‘` has type `âˆ¥ ğ‘† âˆ¥ ğ‘“ â†’ âˆ£ ğ‘¨ âˆ£` since it must be a tuple on `âˆ£ ğ‘¨ âˆ£` of "length" `âˆ¥ ğ‘† âˆ¥ ğ‘“` (the arity of `ğ‘“`).

We now define the type `hom ğ‘¨ ğ‘©` of homomorphisms from `ğ‘¨` to `ğ‘©` by first defining the type `is-homomorphism` which represents the property of being a homomorphism.

\begin{code}

 is-homomorphism : (âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£) â†’ Type(ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âŠ” ğ“¦)
 is-homomorphism g = âˆ€ ğ‘“  â†’  compatible-op-map ğ‘“ g

 hom : Type(ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âŠ” ğ“¦)
 hom = Î£[ g ê‰ (âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£) ] is-homomorphism g

\end{code}


#### <a id="homomorphism-composition">Homomorphism composition</a>

The composition of homomorphisms is again a homomorphism.  We formalize this in a number of alternative ways.

\begin{code}

module _ (ğ‘¨ : Algebra ğ“§ ğ‘†){ğ‘© : Algebra ğ“¨ ğ‘†}(ğ‘ª : Algebra ğ“© ğ‘†) where

  âˆ˜-hom : hom ğ‘¨ ğ‘©  â†’  hom ğ‘© ğ‘ª  â†’  hom ğ‘¨ ğ‘ª
  âˆ˜-hom (g , ghom) (h , hhom) = h âˆ˜ g , Î³ where

   Î³ : âˆ€ ğ‘“ a â†’ (h âˆ˜ g)((ğ‘“ Ì‚ ğ‘¨) a) â‰¡ (ğ‘“ Ì‚ ğ‘ª)(h âˆ˜ g âˆ˜ a)
   Î³ ğ‘“ a = (h âˆ˜ g)((ğ‘“ Ì‚ ğ‘¨) a)     â‰¡âŸ¨ cong h ( ghom ğ‘“ a ) âŸ©
           h ((ğ‘“ Ì‚ ğ‘©)(g âˆ˜ a))     â‰¡âŸ¨ hhom ğ‘“ ( g âˆ˜ a ) âŸ©
           (ğ‘“ Ì‚ ğ‘ª)(h âˆ˜ g âˆ˜ a)     âˆ


  âˆ˜-is-hom : {f : âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£}{g : âˆ£ ğ‘© âˆ£ â†’ âˆ£ ğ‘ª âˆ£}
   â†’         is-homomorphism ğ‘¨ ğ‘© f â†’ is-homomorphism ğ‘© ğ‘ª g â†’ is-homomorphism ğ‘¨ ğ‘ª (g âˆ˜ f)
  âˆ˜-is-hom {f} {g} fhom ghom = âˆ¥ âˆ˜-hom (f , fhom) (g , ghom) âˆ¥

\end{code}



#### <a id="exmples-of-homomorphisms">Examples of homomorphisms</a>

Let's look at a few examples of homomorphisms. These examples are actually quite special in that the function in question commutes with the basic operations of *all* algebras and so, no matter the algebras involved, is always a homomorphism (trivially). We begin with the identity map, which is proved to be (the underlying map of) a homomorphism as follows.

\begin{code}

ğ’¾ğ’¹ : (ğ‘¨ : Algebra ğ“¤ ğ‘†) â†’ hom ğ‘¨ ğ‘¨
ğ’¾ğ’¹ _ = id , Î» ğ‘“ ğ‘ â†’ refl

\end{code}

Next, `lift` and `lower`, defined in the [Overture.Lifts][] module, are (the maps of) homomorphisms.  Again, the proofs are trivial.

\begin{code}

open Lift

ğ“ğ’¾ğ’»ğ“‰ : {ğ“¦ : Level}{ğ‘¨ : Algebra ğ“¤ ğ‘†} â†’ hom ğ‘¨ (Lift-alg ğ‘¨ ğ“¦)
ğ“ğ’¾ğ’»ğ“‰ = lift , Î» ğ‘“ ğ‘ â†’ refl

ğ“â„´ğ“Œâ„¯ğ“‡ : {ğ“¦ : Level}{ğ‘¨ : Algebra ğ“¤ ğ‘†} â†’ hom (Lift-alg ğ‘¨ ğ“¦) ğ‘¨
ğ“â„´ğ“Œâ„¯ğ“‡ = lower , Î» ğ‘“ ğ‘ â†’ refl

\end{code}




#### <a id="monomorphisms-and-epimorphisms">Monomorphisms and epimorphisms</a>

A *monomorphism* is an injective homomorphism and an *epimorphism* is a surjective homomorphism. These are represented in the [UniversalAlgebra][] library by the following types.

\begin{code}

is-monomorphism : (ğ‘¨ : Algebra ğ“¤ ğ‘†)(ğ‘© : Algebra ğ“¦ ğ‘†) â†’ (âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£) â†’ Type(ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âŠ” ğ“¦)
is-monomorphism ğ‘¨ ğ‘© g = is-homomorphism ğ‘¨ ğ‘© g Ã— IsInjective g

mon : Algebra ğ“¤ ğ‘† â†’ Algebra ğ“¦ ğ‘†  â†’ Type(ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âŠ” ğ“¦)
mon ğ‘¨ ğ‘© = Î£[ g ê‰ (âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£) ] is-monomorphism ğ‘¨ ğ‘© g

is-epimorphism : (ğ‘¨ : Algebra ğ“¤ ğ‘†)(ğ‘© : Algebra ğ“¦ ğ‘†) â†’ (âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£) â†’ Type(ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âŠ” ğ“¦)
is-epimorphism ğ‘¨ ğ‘© g = is-homomorphism ğ‘¨ ğ‘© g Ã— IsSurjective g

epi : Algebra ğ“¤ ğ‘† â†’ Algebra ğ“¦ ğ‘†  â†’ Type(ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âŠ” ğ“¦)
epi ğ‘¨ ğ‘© = Î£[ g ê‰ (âˆ£ ğ‘¨ âˆ£ â†’ âˆ£ ğ‘© âˆ£) ] is-epimorphism ğ‘¨ ğ‘© g

\end{code}

It will be convenient to have a function that takes an inhabitant of `mon` (or `epi`) and extracts the homomorphism part, or *hom reduct* (that is, the pair consisting of the map and a proof that the map is a homomorphism).

\begin{code}

mon-to-hom : (ğ‘¨ : Algebra ğ“¤ ğ‘†){ğ‘© : Algebra ğ“¦ ğ‘†} â†’ mon ğ‘¨ ğ‘© â†’ hom ğ‘¨ ğ‘©
mon-to-hom ğ‘¨ Ï• = âˆ£ Ï• âˆ£ , fst âˆ¥ Ï• âˆ¥

epi-to-hom : {ğ‘¨ : Algebra ğ“¤ ğ‘†}(ğ‘© : Algebra ğ“¦ ğ‘†) â†’ epi ğ‘¨ ğ‘© â†’ hom ğ‘¨ ğ‘©
epi-to-hom _ Ï• = âˆ£ Ï• âˆ£ , fst âˆ¥ Ï• âˆ¥

\end{code}






#### <a id="kernels-of-homomorphisms">Kernels of homomorphisms</a>

The kernel of a homomorphism is a congruence relation and conversely for every congruence relation Î¸, there exists a homomorphism with kernel Î¸ (namely, that canonical projection onto the quotient modulo Î¸).

\begin{code}

module _ {ğ“¤ ğ“¦ : Level}{ğ‘¨ : Algebra ğ“¤ ğ‘†} where

 homker-comp : swelldef ğ“¥ ğ“¦ â†’ {ğ‘© : Algebra ğ“¦ ğ‘†}(h : hom ğ‘¨ ğ‘©) â†’ compatible ğ‘¨ (ker âˆ£ h âˆ£)
 homker-comp wd {ğ‘©} h f {u}{v} kuv = âˆ£ h âˆ£((f Ì‚ ğ‘¨) u)   â‰¡âŸ¨ âˆ¥ h âˆ¥ f u âŸ©
                                     (f Ì‚ ğ‘©)(âˆ£ h âˆ£ âˆ˜ u) â‰¡âŸ¨ wd(f Ì‚ ğ‘©)(âˆ£ h âˆ£ âˆ˜ u)(âˆ£ h âˆ£ âˆ˜ v)kuv âŸ©
                                     (f Ì‚ ğ‘©)(âˆ£ h âˆ£ âˆ˜ v) â‰¡âŸ¨ (âˆ¥ h âˆ¥ f v)â»Â¹ âŸ©
                                     âˆ£ h âˆ£((f Ì‚ ğ‘¨) v)   âˆ


\end{code}

(Notice, it is here that the `swelldef` postulate comes into play, and because it is needed to prove `homker-comp`, it is postulated by all the lemmas below that depend upon `homker-comp`.)

It is convenient to define a function that takes a homomorphism and constructs a congruence from its kernel.  We call this function `kercon`.

\begin{code}

 kercon : swelldef ğ“¥ ğ“¦ â†’ {ğ‘© : Algebra ğ“¦ ğ‘†} â†’ hom ğ‘¨ ğ‘© â†’ Con{ğ“¤}{ğ“¦} ğ‘¨
 kercon wd {ğ‘©} h = ker âˆ£ h âˆ£ , mkcon (ker-IsEquivalence âˆ£ h âˆ£)(homker-comp wd {ğ‘©} h)

\end{code}

With this congruence we construct the corresponding quotient, along with some syntactic sugar to denote it.

\begin{code}

 kerquo : swelldef ğ“¥ ğ“¦ â†’ {ğ‘© : Algebra ğ“¦ ğ‘†} â†’ hom ğ‘¨ ğ‘© â†’ Algebra (ğ“¤ âŠ” lsuc ğ“¦) ğ‘†
 kerquo wd {ğ‘©} h = ğ‘¨ â•± (kercon wd {ğ‘©} h)


ker[_â‡’_]_â†¾_ : (ğ‘¨ : Algebra ğ“¤ ğ‘†)(ğ‘© : Algebra ğ“¦ ğ‘†) â†’ hom ğ‘¨ ğ‘© â†’ swelldef ğ“¥ ğ“¦ â†’ Algebra (ğ“¤ âŠ” lsuc ğ“¦) ğ‘†
ker[ ğ‘¨ â‡’ ğ‘© ] h â†¾ wd = kerquo wd {ğ‘©} h

\end{code}

Thus, given `h : hom ğ‘¨ ğ‘©`, we can construct the quotient of `ğ‘¨` modulo the kernel of `h`, and the syntax for this quotient in the [UniversalAlgebra][] library is `ğ‘¨ [ ğ‘© ]/ker h â†¾ fe`.



#### <a id="the-canonical-projection">The canonical projection</a>

Given an algebra `ğ‘¨` and a congruence `Î¸`, the *canonical projection* is a map from `ğ‘¨` onto `ğ‘¨ â•± Î¸` that is constructed, and proved epimorphic, as follows.

\begin{code}

module _ {ğ“¤ ğ“¦ : Level}{ğ‘¨ : Algebra ğ“¤ ğ‘†} where
 Ï€epi : (Î¸ : Con{ğ“¤}{ğ“¦} ğ‘¨) â†’ epi ğ‘¨ (ğ‘¨ â•± Î¸)
 Ï€epi Î¸ = (Î» a â†’ âŸª a âŸ«) , (Î» _ _ â†’ refl) , cÏ€-is-epic  where
  cÏ€-is-epic : IsSurjective (Î» a â†’ âŸª a âŸ«)
  cÏ€-is-epic (C , (a , refl)) =  Image_âˆ‹_.im a

\end{code}

In may happen that we don't care about the surjectivity of `Ï€epi`, in which case would might prefer to work with the *homomorphic reduct* of `Ï€epi`. This is obtained by applying `epi-to-hom`, like so.

\begin{code}

 Ï€hom : (Î¸ : Con{ğ“¤}{ğ“¦} ğ‘¨) â†’ hom ğ‘¨ (ğ‘¨ â•± Î¸)
 Ï€hom Î¸ = epi-to-hom (ğ‘¨ â•± Î¸) (Ï€epi Î¸)

\end{code}


We combine the foregoing to define a function that takes ğ‘†-algebras `ğ‘¨` and `ğ‘©`, and a homomorphism `h : hom ğ‘¨ ğ‘©` and returns the canonical epimorphism from `ğ‘¨` onto `ğ‘¨ [ ğ‘© ]/ker h`. (Recall, the latter is the special notation we defined above for the quotient of `ğ‘¨` modulo the kernel of `h`.)

\begin{code}

 Ï€ker : (wd : swelldef ğ“¥ ğ“¦){ğ‘© : Algebra ğ“¦ ğ‘†}(h : hom ğ‘¨ ğ‘©) â†’ epi ğ‘¨ (ker[ ğ‘¨ â‡’ ğ‘© ] h â†¾ wd)
 Ï€ker wd {ğ‘©} h = Ï€epi (kercon wd {ğ‘©} h)

\end{code}

The kernel of the canonical projection of `ğ‘¨` onto `ğ‘¨ / Î¸` is equal to `Î¸`, but since equality of inhabitants of certain types (like `Congruence` or `Rel`) can be a tricky business, we settle for proving the containment `ğ‘¨ / Î¸ âŠ† Î¸`. Of the two containments, this is the easier one to prove; luckily it is also the one we need later.

\begin{code}

 open IsCongruence

 ker-in-con : {wd : swelldef ğ“¥ (ğ“¤ âŠ” lsuc ğ“¦)}(Î¸ : Con ğ‘¨)
  â†’           âˆ€ {x}{y} â†’ âˆ£ kercon wd {ğ‘¨ â•± Î¸} (Ï€hom Î¸) âˆ£ x y â†’  âˆ£ Î¸ âˆ£ x y

 ker-in-con Î¸ hyp = /-â‰¡ Î¸ hyp

\end{code}



#### <a id="product-homomorphisms">Product homomorphisms</a>

Suppose we have an algebra `ğ‘¨`, a type `I : Type ğ“˜`, and a family `â„¬ : I â†’ Algebra ğ“¦ ğ‘†` of algebras.  We sometimes refer to the inhabitants of `I` as *indices*, and call `â„¬` an *indexed family of algebras*.

If in addition we have a family `ğ’½ : (i : I) â†’ hom ğ‘¨ (â„¬ i)` of homomorphisms, then we can construct a homomorphism from `ğ‘¨` to the product `â¨… â„¬` in the natural way.

\begin{code}

module _ {ğ“˜ ğ“¦ : Level}{I : Type ğ“˜}(â„¬ : I â†’ Algebra ğ“¦ ğ‘†) where

 â¨…-hom-co : funext ğ“˜ ğ“¦ â†’ {ğ“¤ : Level}(ğ‘¨ : Algebra ğ“¤ ğ‘†) â†’ (âˆ€(i : I) â†’ hom ğ‘¨ (â„¬ i)) â†’ hom ğ‘¨ (â¨… â„¬)
 â¨…-hom-co fe ğ‘¨ ğ’½ = (Î» a i â†’ âˆ£ ğ’½ i âˆ£ a) , (Î» ğ‘“ ğ’¶ â†’ fe Î» i â†’ âˆ¥ ğ’½ i âˆ¥ ğ‘“ ğ’¶)

\end{code}

The family `ğ’½` of homomorphisms inhabits the dependent type `Î  i ê‰ I , hom ğ‘¨ (â„¬ i)`.  The syntax we use to represent this type is available to us because of the way `-Î ` is defined in the [Type Topology][] library.  We like this syntax because it is very close to the notation one finds in the standard type theory literature.  However,
we could equally well have used one of the following alternatives, which may be closer to "standard Agda" syntax:

`Î  Î» i â†’ hom ğ‘¨ (â„¬ i)` &nbsp; or &nbsp; `(i : I) â†’ hom ğ‘¨ (â„¬ i)` &nbsp; or &nbsp; `âˆ€ i â†’ hom ğ‘¨ (â„¬ i)`.

The foregoing generalizes easily to the case in which the domain is also a product of a family of algebras. That is, if we are given `ğ’œ : I â†’ Algebra ğ“¤ ğ‘† and â„¬ : I â†’ Algebra ğ“¦ ğ‘†` (two families of `ğ‘†`-algebras), and `ğ’½ :  Î  i ê‰ I , hom (ğ’œ i)(â„¬ i)` (a family of homomorphisms), then we can construct a homomorphism from `â¨… ğ’œ` to `â¨… â„¬` in the following natural way.

\begin{code}

 â¨…-hom : funext ğ“˜ ğ“¦ â†’ {ğ“¤ : Level}(ğ’œ : I â†’ Algebra ğ“¤ ğ‘†) â†’ Î [ i ê‰ I ] hom (ğ’œ i)(â„¬ i) â†’ hom (â¨… ğ’œ)(â¨… â„¬)
 â¨…-hom fe ğ’œ ğ’½ = (Î» x i â†’ âˆ£ ğ’½ i âˆ£ (x i)) , (Î» ğ‘“ ğ’¶ â†’ fe Î» i â†’ âˆ¥ ğ’½ i âˆ¥ ğ‘“ (Î» x â†’ ğ’¶ x i))

\end{code}



#### <a id="projections-out-of-products">Projection out of products</a>

Later we will need a proof of the fact that projecting out of a product algebra onto one of its factors is a homomorphism.

\begin{code}

 â¨…-projection-hom : Î [ i ê‰ I ] hom (â¨… â„¬) (â„¬ i)
 â¨…-projection-hom = Î» x â†’ (Î» z â†’ z x) , Î» _ _ â†’ refl

\end{code}

We could prove a more general result involving projections onto multiple factors, but so far the single-factor result has sufficed.



--------------------------------------

<sup>1</sup><span class="footnote" id="fn1">
Recall, `h âˆ˜ ğ’‚` is the tuple whose i-th component is `h (ğ’‚ i)`.</span>

<span class="footnote">Instead of "homomorphism," we sometimes use the nickname "hom" to refer to such a map.</span>


<br>

[â†‘ Homomorphisms](Homomorphisms.html)
<span style="float:right;">[Homomorphisms.Noether â†’](Homomorphisms.Noether.html)</span>

{% include UALib.Links.md %}









