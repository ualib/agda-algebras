---
layout: default
title : Homomorphisms.Noether module (The Agda Universal Algebra Library)
date : 2021-01-13
author: [the ualib/agda-algebras development team][]
---

### <a id="homomorphism-theorems">Homomorphism Theorems</a>

This chapter presents the [Homomorphisms.Noether][] module of the [Agda Universal Algebra Library][].

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Level using ( Level ; Lift )
open import Algebras.Basic

module Homomorphisms.Noether {ğ“ ğ“¥ : Level} (ğ‘† : Signature ğ“ ğ“¥) where


-- Imports from Agda (builtin/primitive) and the Agda Standard Library ---------------------
open import Axiom.Extensionality.Propositional    using    ()
                                                  renaming (Extensionality to funext)
open import Agda.Primitive                        using    ( _âŠ”_      ;   lsuc  )
                                                  renaming ( Set      to  Type  )
open import Agda.Builtin.Equality                 using    ( _â‰¡_      ;   refl  )
open import Data.Product                          using    ( _,_      ;   Î£
                                                           ; Î£-syntax ;   _Ã—_   )
                                                  renaming ( projâ‚    to  fst
                                                           ; projâ‚‚    to  snd   )
open import Function.Base                         using    ( _âˆ˜_      ;   id    )
open import Relation.Binary                       using    ( IsEquivalence   )
open import Relation.Binary.PropositionalEquality using    ( trans    ;   cong
                                                           ; cong-app
                                                           ; module â‰¡-Reasoning )
open import Relation.Unary                        using    ( _âŠ†_ )


-- Imports from agda-algebras --------------------------------------------------------------
open import Overture.Preliminaries   using ( âˆ£_âˆ£ ; âˆ¥_âˆ¥ ; ğ‘–ğ‘‘ ; _â»Â¹ )
open import Overture.Inverses        using ( IsInjective ; IsSurjective ; Image_âˆ‹_ ; SurjInv )
open import Relations.Discrete       using ( ker ; kernel )
open import Relations.Quotients      using ( ker-IsEquivalence ; _/_ ; âŸª_âŸ« ; âŒ_âŒŸ ; R-block)
open import Relations.Truncation     using ( is-set ; blk-uip ; is-embedding
                                           ; monic-is-embedding|Set )
open import Relations.Extensionality using ( swelldef ; block-ext|uip ; pred-ext
                                           ; SurjInvIsRightInv ; epic-factor )
open import Algebras.Congruences   ğ‘† using ( Con ; IsCongruence )
open import Homomorphisms.Basic    ğ‘† using ( hom ; kercon ; ker[_â‡’_]_â†¾_ ; Ï€ker
                                           ; is-homomorphism ; epi ; epi-to-hom )

private variable Î± Î² Î³ : Level

\end{code}


#### <a id="the-first-homomorphism-theorem">The First Homomorphism Theorem</a>

Here we formalize a version of the *first homomorphism theorem*, sometimes called *Noether's first homomorphism theorem*, after Emmy Noether who was among the first proponents of the abstract approach to the subject that we now call "modern algebra").

Informally, the theorem states that every homomorphism from `ğ‘¨` to `ğ‘©` (`ğ‘†`-algebras) factors through the quotient algebra `ğ‘¨ â•± ker h` (`ğ‘¨` modulo the kernel of the given homomorphism).  In other terms, given `h : hom ğ‘¨ ğ‘©` there exists `Ï† : hom (ğ‘¨ â•± ker h) ğ‘©` which, when composed with the canonical projection `Ï€ker : ğ‘¨ â†  ğ‘¨ â•± ker h`, is equal to `h`; that is, `h = Ï† âˆ˜ Ï€ker`.  Moreover, `Ï†` is a *monomorphism* (injective homomorphism) and is unique.

Our formal proof of this theorem will require function extensionality, proposition extensionality, and a couple of truncation assumptions.  The extensionality assumptions are postulated using `dfunext` and `pred-ext` from [Overture.FunExtensionality][] and [Relations.Extensionality][] (resp.). As for truncation, to prove that `Ï†` is injective we require<sup>[1](Homomorphisms.Noether.html#fn1)</sup>

+ `buip`: *uniqueness of (block) identity proofs*; given two blocks of the kernel there is at most one proof that the blocks are equal;

To prove that `Ï†` is an embedding we require

+ `Bset`: *uniqueness of identity proofs* in the codomain; that is, the codomain `âˆ£ ğ‘© âˆ£` is assumed to be a *set*.

Note that the classical, informal statement of the first homomorphism theorem does not demand that `Ï†` be an embedding (in our sense of having subsingleton fibers), and if we left this out of the consequent of our formal theorem statement, then we could omit from the antecedent the assumption that `âˆ£ ğ‘© âˆ£` is a set.

Without further ado, we present our formalization of the first homomorphism theorem.<sup>[2](Homomorphisms.Noether.html#fn2)</sup>

\begin{code}
open â‰¡-Reasoning

FirstHomTheorem|Set :

    (ğ‘¨ : Algebra Î± ğ‘†)(ğ‘© : Algebra Î² ğ‘†)(h : hom ğ‘¨ ğ‘©)
    (pe : pred-ext Î± Î²)(fe : swelldef ğ“¥ Î²)                          -- extensionality assumptions
    (Bset : is-set âˆ£ ğ‘© âˆ£)(buip : blk-uip âˆ£ ğ‘¨ âˆ£ âˆ£ kercon fe {ğ‘©} h âˆ£) -- truncation assumptions
    ----------------------------------------------------------------
 â†’  Î£[ Ï† âˆˆ hom (ker[ ğ‘¨ â‡’ ğ‘© ] h â†¾ fe) ğ‘©  ]
                            ( ( âˆ£ h âˆ£ â‰¡ âˆ£ Ï† âˆ£ âˆ˜ âˆ£ Ï€ker fe{ğ‘©}h âˆ£ )
                              Ã— IsInjective âˆ£ Ï† âˆ£  Ã—  is-embedding âˆ£ Ï† âˆ£  )

FirstHomTheorem|Set ğ‘¨ ğ‘© h pe fe Bset buip = (Ï† , Ï†hom) , refl , Ï†mon , Ï†emb
 where
  Î¸ : Con ğ‘¨
  Î¸ = kercon fe{ğ‘©} h
  Î¾ : IsEquivalence âˆ£ Î¸ âˆ£
  Î¾ = IsCongruence.is-equivalence âˆ¥ Î¸ âˆ¥

  Ï† : âˆ£ (ker[ ğ‘¨ â‡’ ğ‘© ] h â†¾ fe) âˆ£ â†’ âˆ£ ğ‘© âˆ£
  Ï† a = âˆ£ h âˆ£ âŒ a âŒŸ

  Ï†hom : is-homomorphism (ker[ ğ‘¨ â‡’ ğ‘© ] h â†¾ fe) ğ‘© Ï†
  Ï†hom ğ‘“ a = âˆ£ h âˆ£ ( (ğ‘“ Ì‚ ğ‘¨) (Î» x â†’ âŒ a x âŒŸ) ) â‰¡âŸ¨ âˆ¥ h âˆ¥ ğ‘“ (Î» x â†’ âŒ a x âŒŸ)  âŸ©
             (ğ‘“ Ì‚ ğ‘©) (âˆ£ h âˆ£ âˆ˜ (Î» x â†’ âŒ a x âŒŸ))  â‰¡âŸ¨ cong (ğ‘“ Ì‚ ğ‘©) refl âŸ©
             (ğ‘“ Ì‚ ğ‘©) (Î» x â†’ Ï† (a x))            âˆ

  Ï†mon : IsInjective Ï†
  Ï†mon {_ , R-block u refl} {_ , R-block v refl} Ï†uv = block-ext|uip pe buip Î¾ Ï†uv

  Ï†emb : is-embedding Ï†
  Ï†emb = monic-is-embedding|Set Ï† Bset Ï†mon

\end{code}

Below we will prove that the homomorphism `Ï†`, whose existence we just proved, is unique (see `NoetherHomUnique`), but first we show that if we add to the hypotheses of the first homomorphism theorem the assumption that `h` is surjective, then we obtain the so-called *first isomorphism theorem*.  Naturally, we let `FirstHomTheorem|Set` do most of the work. (Note that the proof also requires an additional local function extensionality postulate.)

\begin{code}

FirstIsoTheorem|Set :

     (ğ‘¨ : Algebra Î± ğ‘†) (ğ‘© : Algebra Î² ğ‘†) (h : hom ğ‘¨ ğ‘©)
     (pe : pred-ext Î± Î²) (fe : swelldef ğ“¥ Î²)                        -- extensionality assumptions
     (Bset : is-set âˆ£ ğ‘© âˆ£)(buip : blk-uip âˆ£ ğ‘¨ âˆ£ âˆ£ kercon fe{ğ‘©}h âˆ£)  -- truncation assumptions
 â†’   IsSurjective âˆ£ h âˆ£
     ---------------------------------------------------------------
 â†’   Î£[ f âˆˆ (epi (ker[ ğ‘¨ â‡’ ğ‘© ] h â†¾ fe) ğ‘©)]
                          ( âˆ£ h âˆ£ â‰¡ âˆ£ f âˆ£ âˆ˜ âˆ£ Ï€ker fe{ğ‘©}h âˆ£ )
                            Ã— IsInjective âˆ£ f âˆ£ Ã— is-embedding âˆ£ f âˆ£

FirstIsoTheorem|Set ğ‘¨ ğ‘© h pe fe Bset buip hE =
 (fmap , fhom , fepic) , refl , (snd âˆ¥ FHT âˆ¥)
  where
  FHT = FirstHomTheorem|Set ğ‘¨ ğ‘© h pe fe Bset buip

  fmap : âˆ£ ker[ ğ‘¨ â‡’ ğ‘© ] h â†¾ fe âˆ£ â†’ âˆ£ ğ‘© âˆ£
  fmap = fst âˆ£ FHT âˆ£

  fhom : is-homomorphism (ker[ ğ‘¨ â‡’ ğ‘© ] h â†¾ fe) ğ‘© fmap
  fhom = snd âˆ£ FHT âˆ£

  fepic : IsSurjective fmap
  fepic b = Goal where
   a : âˆ£ ğ‘¨ âˆ£
   a = SurjInv âˆ£ h âˆ£ hE b

   bfa : b â‰¡ fmap âŸª a âŸ«
   bfa = ((SurjInvIsRightInv âˆ£ h âˆ£ hE) b)â»Â¹

   Goal : Image fmap âˆ‹ b
   Goal = Image_âˆ‹_.eq âŸª a âŸ« bfa

\end{code}

Now we prove that the homomorphism `Ï†`, whose existence is guaranteed by `FirstHomTheorem|Set`, is unique.

\begin{code}

module _ {fe : swelldef ğ“¥ Î²}(ğ‘¨ : Algebra Î± ğ‘†)(ğ‘© : Algebra Î² ğ‘†)(h : hom ğ‘¨ ğ‘©) where

 NoetherHomUnique : (f g : hom (ker[ ğ‘¨ â‡’ ğ‘© ] h â†¾ fe) ğ‘©)
  â†’                 âˆ£ h âˆ£ â‰¡ âˆ£ f âˆ£ âˆ˜ âˆ£ Ï€ker fe{ğ‘©}h âˆ£
  â†’                 âˆ£ h âˆ£ â‰¡ âˆ£ g âˆ£ âˆ˜ âˆ£ Ï€ker fe{ğ‘©}h âˆ£
  â†’                 âˆ€ a  â†’  âˆ£ f âˆ£ a â‰¡ âˆ£ g âˆ£ a

 NoetherHomUnique f g hfk hgk (_ , R-block a refl) =
  âˆ£ f âˆ£ (_ , R-block a refl) â‰¡âŸ¨ cong-app(hfk â»Â¹)a âŸ©
  âˆ£ h âˆ£ a                    â‰¡âŸ¨ cong-app(hgk)a âŸ©
  âˆ£ g âˆ£ (_ , R-block a refl) âˆ

\end{code}

If, in addition, we postulate extensionality of functions defined on the domain `ker[ ğ‘¨ â‡’ ğ‘© ] h`, then we obtain the following variation of the last result.<sup>[1](Homomorphisms.Noether.html#fn1)</sup>

\begin{code}

 fe-NoetherHomUnique : {fuww : funext (Î± âŠ” lsuc Î²) Î²}(f g : hom (ker[ ğ‘¨ â‡’ ğ‘© ] h â†¾ fe) ğ‘©)
  â†’                    âˆ£ h âˆ£ â‰¡ âˆ£ f âˆ£ âˆ˜ âˆ£ Ï€ker fe{ğ‘©}h âˆ£
  â†’                    âˆ£ h âˆ£ â‰¡ âˆ£ g âˆ£ âˆ˜ âˆ£ Ï€ker fe{ğ‘©}h âˆ£
  â†’                    âˆ£ f âˆ£ â‰¡ âˆ£ g âˆ£

 fe-NoetherHomUnique {fuww} f g hfk hgk = fuww (NoetherHomUnique f g hfk hgk)

\end{code}

The proof of `NoetherHomUnique` goes through for the special case of epimorphisms, as we now verify.

\begin{code}

 NoetherIsoUnique : (f g : epi (ker[ ğ‘¨ â‡’ ğ‘© ] h â†¾ fe) ğ‘©)
  â†’                 âˆ£ h âˆ£ â‰¡ âˆ£ f âˆ£ âˆ˜ âˆ£ Ï€ker fe{ğ‘©}h âˆ£
  â†’                 âˆ£ h âˆ£ â‰¡ âˆ£ g âˆ£ âˆ˜ âˆ£ Ï€ker fe{ğ‘©}h âˆ£
  â†’                 âˆ€ a â†’ âˆ£ f âˆ£ a â‰¡ âˆ£ g âˆ£ a

 NoetherIsoUnique f g hfk hgk = NoetherHomUnique (epi-to-hom ğ‘© f) (epi-to-hom ğ‘© g) hfk hgk

\end{code}







#### <a id="homomorphism-decomposition">Homomorphism decomposition</a>

If `Ï„ : hom ğ‘¨ ğ‘©`, `Î½ : hom ğ‘¨ ğ‘ª`, `Î½` is surjective, and `ker Î½ âŠ† ker Ï„`, then there exists `Ï† : hom ğ‘ª ğ‘©` such that `Ï„ = Ï† âˆ˜ Î½` so the following diagram commutes:

```
ğ‘¨ --- Î½ ->> ğ‘ª
 \         .
  \       .
   Ï„     Ï†
    \   .
     \ .
      V
      ğ‘©
```

\begin{code}

module _ {ğ‘¨ : Algebra Î± ğ‘†}{ğ‘ª : Algebra Î³ ğ‘†} where

 HomFactor : funext Î± Î² â†’ swelldef ğ“¥ Î³
  â†’          (ğ‘© : Algebra Î² ğ‘†)(Ï„ : hom ğ‘¨ ğ‘©)(Î½ : hom ğ‘¨ ğ‘ª)
  â†’          kernel âˆ£ Î½ âˆ£ âŠ† kernel âˆ£ Ï„ âˆ£ â†’ IsSurjective âˆ£ Î½ âˆ£
             --------------------------------------------------
  â†’          Î£[ Ï† âˆˆ (hom ğ‘ª ğ‘©)] âˆ£ Ï„ âˆ£ â‰¡ âˆ£ Ï† âˆ£ âˆ˜ âˆ£ Î½ âˆ£

 HomFactor fxy wd ğ‘© Ï„ Î½ KÎ½Ï„ Î½E = (Ï† , Ï†IsHomCB) , Ï„Ï†Î½
  where
   Î½Inv : âˆ£ ğ‘ª âˆ£ â†’ âˆ£ ğ‘¨ âˆ£
   Î½Inv = SurjInv âˆ£ Î½ âˆ£ Î½E

   Î· : âˆ€ c â†’ âˆ£ Î½ âˆ£ (Î½Inv c) â‰¡ c
   Î· c = SurjInvIsRightInv âˆ£ Î½ âˆ£ Î½E c

   Ï† : âˆ£ ğ‘ª âˆ£ â†’ âˆ£ ğ‘© âˆ£
   Ï† = âˆ£ Ï„ âˆ£ âˆ˜ Î½Inv

   Î¾ : âˆ€ a â†’ kernel âˆ£ Î½ âˆ£ (a , Î½Inv (âˆ£ Î½ âˆ£ a))
   Î¾ a = (Î· (âˆ£ Î½ âˆ£ a))â»Â¹

   Ï„Ï†Î½ : âˆ£ Ï„ âˆ£ â‰¡ Ï† âˆ˜ âˆ£ Î½ âˆ£
   Ï„Ï†Î½ = fxy Î» x â†’ KÎ½Ï„ (Î¾ x)

   Ï†IsHomCB : âˆ€ ğ‘“ c â†’ Ï† ((ğ‘“ Ì‚ ğ‘ª) c) â‰¡ ((ğ‘“ Ì‚ ğ‘©)(Ï† âˆ˜ c))
   Ï†IsHomCB ğ‘“ c = Ï† ((ğ‘“ Ì‚ ğ‘ª) c)     â‰¡âŸ¨ cong Ï† (wd (ğ‘“ Ì‚ ğ‘ª) c (âˆ£ Î½ âˆ£ âˆ˜ (Î½Inv âˆ˜ c)) (Î» i â†’ (Î· (c i))â»Â¹)) âŸ©
                  Ï† ((ğ‘“ Ì‚ ğ‘ª)(âˆ£ Î½ âˆ£ âˆ˜(Î½Inv âˆ˜ c)))   â‰¡âŸ¨ cong Ï† (âˆ¥ Î½ âˆ¥ ğ‘“ (Î½Inv âˆ˜ c))â»Â¹ âŸ©
                  Ï† (âˆ£ Î½ âˆ£((ğ‘“ Ì‚ ğ‘¨)(Î½Inv âˆ˜ c)))     â‰¡âŸ¨ cong-app(Ï„Ï†Î½ â»Â¹)((ğ‘“ Ì‚ ğ‘¨)(Î½Inv âˆ˜ c))âŸ©
                  âˆ£ Ï„ âˆ£((ğ‘“ Ì‚ ğ‘¨)(Î½Inv âˆ˜ c))         â‰¡âŸ¨ âˆ¥ Ï„ âˆ¥ ğ‘“ (Î½Inv âˆ˜ c) âŸ©
                  (ğ‘“ Ì‚ ğ‘©)(Î» x â†’ âˆ£ Ï„ âˆ£(Î½Inv (c x))) âˆ

\end{code}

If, in addition to the hypotheses of the last theorem, we assume Ï„ is epic, then so is Ï†. (Note that the proof also requires an additional local function extensionality postulate, `funext Î² Î²`.)

\begin{code}

 HomFactorEpi : funext Î± Î² â†’ swelldef ğ“¥ Î³
  â†’             (ğ‘© : Algebra Î² ğ‘†)(Ï„ : hom ğ‘¨ ğ‘©)(Î½ : hom ğ‘¨ ğ‘ª)
  â†’             kernel âˆ£ Î½ âˆ£ âŠ† kernel âˆ£ Ï„ âˆ£
  â†’             IsSurjective âˆ£ Î½ âˆ£ â†’ IsSurjective âˆ£ Ï„ âˆ£
                ---------------------------------------------
  â†’             Î£[ Ï† âˆˆ epi ğ‘ª ğ‘© ] âˆ£ Ï„ âˆ£ â‰¡ âˆ£ Ï† âˆ£ âˆ˜ âˆ£ Î½ âˆ£

 HomFactorEpi fxy wd ğ‘© Ï„ Î½ kerincl Î½e Ï„e = (fst âˆ£ Ï†F âˆ£ ,(snd âˆ£ Ï†F âˆ£ , Ï†E)), âˆ¥ Ï†F âˆ¥
  where
   Ï†F : Î£[ Ï† âˆˆ hom ğ‘ª ğ‘© ] âˆ£ Ï„ âˆ£ â‰¡ âˆ£ Ï† âˆ£ âˆ˜ âˆ£ Î½ âˆ£
   Ï†F = HomFactor fxy wd ğ‘© Ï„ Î½ kerincl Î½e

   Ï† : âˆ£ ğ‘ª âˆ£ â†’ âˆ£ ğ‘© âˆ£
   Ï† = âˆ£ Ï„ âˆ£ âˆ˜ (SurjInv âˆ£ Î½ âˆ£ Î½e)

   Ï†E : IsSurjective Ï†
   Ï†E = epic-factor  âˆ£ Ï„ âˆ£ âˆ£ Î½ âˆ£ Ï† âˆ¥ Ï†F âˆ¥ Ï„e

\end{code}


--------------------------------------

<sup>1</sup><span class="footnote" id="fn1"> See [Relations.Truncation][] for a discussion of *truncation*, *sets*, and *uniqueness of identity proofs*.</span>

<sup>2</sup><span class="footnote" id="fn2"> In this module we are already assuming *global* function extensionality (`gfe`), and we could just appeal to `gfe` (e.g., in the proof of `FirstHomomorphismTheorem`) instead of adding local function extensionality (\ab{fe}) to the list of assumptions.  However, we sometimes add an extra extensionality postulate in order to highlight where and how the principle is applied.}</span>

<br>
<br>

[â† Homomorphisms.Basic](Homomorphisms.Basic.html)
<span style="float:right;">[Homomorphisms.Isomorphisms â†’](Homomorphisms.Isomorphisms.html)</span>

{% include UALib.Links.md %}


------------------------------

[the ualib/agda-algebras development team]: https://github.com/ualib/agda-algebras#the-ualib-agda-algebras-development-team
