---
layout: default
title : Foundations.Extensionality module (The Agda Universal Algebra Library)
date : 2021-02-23
author: [the ualib/agda-algebras development team][]
---

### Extensionality

This is the [Foundations.Extensionality][] module of the [Agda Universal Algebra Library][].

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

module Foundations.Extensionality where

-- imports from Agda and the Agda Standard Library ------------------------------------
open import Agda.Builtin.Equality  using (_â‰¡_ ; refl )
open import Agda.Primitive         using ( _âŠ”_ ; lsuc ; Level ) renaming ( Set to Type ; SetÏ‰ to TypeÏ‰ )
open import Data.Product           using ( _,_ ;  _Ã—_ )
open import Function.Base          using ( _âˆ˜_ ; id )
open import Relation.Binary        using ( IsEquivalence ) renaming ( Rel to BinRel )
open import Relation.Unary         using ( Pred ; _âŠ†_ )
open import Axiom.Extensionality.Propositional using () renaming ( Extensionality to funext )
import Relation.Binary.PropositionalEquality as PE


-- imports from agda-algebras --------------------------------------------------------------
open import Overture.Preliminaries using ( _â‰ˆ_; _â»Â¹ ; _âˆ™_ ; transport )
open import Overture.Inverses      using ( IsSurjective ; SurjInv ; InvIsInv ; Image_âˆ‹_ ; eq )
open import Relations.Discrete     using ( Op )
open import Relations.Quotients    using ( [_] ; []-âŠ† ; []-âŠ‡ ; IsBlock ; âŸª_âŸ« )
open import Foundations.Truncation   using ( blk-uip ; to-Î£-â‰¡ )


private variable Î± Î² Î³ Ï ğ“¥ : Level

\end{code}

#### Function Extensionality


Previous versions of [UniversalAlgebra][] made heavy use of a *global function extensionality principle*. This asserts that function extensionality holds at all universe levels.
However, we decided to remove all instances of global function extensionality from the latest version of the library and limit ourselves to local applications of the principle. This has the advantage of making transparent precisely how and where the library depends on function extensionality. The price we pay for this precision is a library that is littered with extensionality postulates. Eventually we will probably remove these postulates in favor of an alternative approach to extensionality, or even remove the need for it altogether.

The following definition is useful for postulating function extensionality when and where needed.

\begin{code}

DFunExt : TypeÏ‰
DFunExt = (ğ“¤ ğ“¥ : Level) â†’ funext ğ“¤ ğ“¥


module _ {A : Type Î±}{B : Type Î²} where

 SurjInvIsRightInv : (f : A â†’ B)(fE : IsSurjective f) â†’ âˆ€ b â†’ f ((SurjInv f fE) b) â‰¡ b
 SurjInvIsRightInv f fE b = InvIsInv f (fE b)

 open PE.â‰¡-Reasoning

 -- composition law for epics
 epic-factor : {C : Type Î³}(f : A â†’ B)(g : A â†’ C)(h : C â†’ B)
  â†’            f â‰ˆ h âˆ˜ g â†’ IsSurjective f â†’ IsSurjective h

 epic-factor f g h compId fe y = Goal
  where
   finv : B â†’ A
   finv = SurjInv f fe

   Î¶ : y â‰¡ f (finv y)
   Î¶ = (SurjInvIsRightInv f fe y)â»Â¹

   Î· : y â‰¡ (h âˆ˜ g) (finv y)
   Î· = Î¶ âˆ™ compId (finv y)

   Goal : Image h âˆ‹ y
   Goal = eq (g (finv y)) Î·

 epic-factor-intensional : {C : Type Î³}(f : A â†’ B)(g : A â†’ C)(h : C â†’ B)
  â†’                        f â‰¡ h âˆ˜ g â†’ IsSurjective f â†’ IsSurjective h

 epic-factor-intensional f g h compId fe y = Goal
  where
   finv : B â†’ A
   finv = SurjInv f fe

   Î¶ : f (finv y) â‰¡ y
   Î¶ = SurjInvIsRightInv f fe y

   Î· : (h âˆ˜ g) (finv y) â‰¡ y
   Î· = (PE.cong-app (compId â»Â¹)(finv y)) âˆ™ Î¶

   Goal : Image h âˆ‹ y
   Goal = eq (g (finv y)) (Î· â»Â¹)

\end{code}


#### An alternative way to express function extensionality

A useful alternative for expressing dependent function extensionality, which is essentially equivalent to `dfunext`, is to assert that the `happly` function is actually an *equivalence*.


The principle of *proposition extensionality* asserts that logically equivalent propositions are equivalent.  That is, if `P` and `Q` are propositions and if `P âŠ† Q` and `Q âŠ† P`, then `P â‰¡ Q`. For our purposes, it will suffice to formalize this notion for general predicates, rather than for propositions (i.e., truncated predicates).

\begin{code}

_â‰_ : {Î± Î² : Level}{A : Type Î±}(P Q : Pred A Î² ) â†’ Type _
P â‰ Q = (P âŠ† Q) Ã— (Q âŠ† P)

pred-ext : (Î± Î² : Level) â†’ Type (lsuc (Î± âŠ” Î²))
pred-ext Î± Î² = âˆ€ {A : Type Î±}{P Q : Pred A Î² } â†’ P âŠ† Q â†’ Q âŠ† P â†’ P â‰¡ Q

\end{code}

Note that `pred-ext` merely defines an extensionality principle. It does not postulate that the principle holds.  If we wish to postulate `pred-ext`, then we do so by assuming that type is inhabited (see `block-ext` below, for example).


#### Quotient extensionality

We need an identity type for congruence classes (blocks) over sets so that two different presentations of the same block (e.g., using different representatives) may be identified.  This requires two postulates: (1) *predicate extensionality*, manifested by the `pred-ext` type; (2) *equivalence class truncation* or "uniqueness of block identity proofs", manifested by the `blk-uip` type defined in the [Relations.Truncation][] module. We now use `pred-ext` and `blk-uip` to define a type called `block-ext|uip` which we require for the proof of the First Homomorphism Theorem presented in [Homomorphisms.Noether][].

\begin{code}

module _ {A : Type Î±}{R : BinRel A Ï} where

 block-ext : pred-ext Î± Ï â†’ IsEquivalence{a = Î±}{â„“ = Ï} R
  â†’          {u v : A} â†’ R u v â†’ [ u ] R â‰¡ [ v ] R

 block-ext pe Req {u}{v} Ruv = pe ([]-âŠ† {R = (R , Req)} u v Ruv)
                                  ([]-âŠ‡ {R = (R , Req)} u v Ruv)


 private
   to-subtype|uip : blk-uip A R
    â†’               {C D : Pred A Ï}{c : IsBlock C {R}}{d : IsBlock D {R}}
    â†’               C â‰¡ D â†’ (C , c) â‰¡ (D , d)

   to-subtype|uip buip {C}{D}{c}{d} CD =
    to-Î£-â‰¡ (CD , buip D (transport (Î» B â†’ IsBlock B) CD c) d)

 block-ext|uip : pred-ext Î± Ï â†’ blk-uip A R
  â†’              IsEquivalence R â†’ âˆ€{u}{v} â†’ R u v â†’ âŸª u âŸ« â‰¡ âŸª v âŸ«

 block-ext|uip pe buip Req Ruv = to-subtype|uip buip (block-ext pe Req Ruv)

\end{code}





---------------------------------------

{% include UALib.Links.md %}


[the ualib/agda-algebras development team]: https://github.com/ualib/agda-algebras#the-ualib-agda-algebras-development-team

