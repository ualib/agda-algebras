---
layout: default
title : Products.Basic module (Agda Universal Algebra Library)
date : 2021-01-12
author: [agda-algebras development team][]
---


### Products of Algebras and Product Algebras

This is the [Products.Basic][] module of the [Agda Universal Algebra Library][].

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}


open import Algebras.Basic

module Products.Basic {ğ‘† : Signature ğ“ ğ“¥} where

open import Agda.Primitive                        using    ( lsuc  ; _âŠ”_ ; Level     )
                                                  renaming ( Set   to Type    )
open import Data.Product                          using    ( _,_ ; Î£ ; Î£-syntax )
open import Relation.Unary                        using    ( Pred  ; _âŠ†_ ; _âˆˆ_  )

open import Overture.Preliminaries using (_â»Â¹; ğ‘–ğ‘‘; âˆ£_âˆ£; âˆ¥_âˆ¥)

private variable Î± Î² Ï ğ“˜ : Level
\end{code}

From now on, the modules of the [UniversalAlgebra][] library will assume a fixed signature `ğ‘† : Signature ğ“ ğ“¥`.

Recall the informal definition of a *product* of `ğ‘†`-algebras. Given a type `I : Type ğ“˜` and a family `ğ’œ : I â†’ Algebra Î± ğ‘†`, the *product* `â¨… ğ’œ` is the algebra whose domain is the Cartesian product `Î  ğ‘– ê‰ I , âˆ£ ğ’œ ğ‘– âˆ£` of the domains of the algebras in `ğ’œ`, and whose operations are defined as follows: if `ğ‘“` is a `J`-ary operation symbol and if `ğ‘ : Î  ğ‘– ê‰ I , J â†’ ğ’œ ğ‘–` is an `I`-tuple of `J`-tuples such that `ğ‘ ğ‘–` is a `J`-tuple of elements of `ğ’œ ğ‘–` (for each `ğ‘–`), then `(ğ‘“ Ì‚ â¨… ğ’œ) ğ‘ := (i : I) â†’ (ğ‘“ Ì‚ ğ’œ i)(ğ‘ i)`.

In [UniversalAlgebra][] a *product of* `ğ‘†`-*algebras* is represented by the following type.

\begin{code}

â¨… : {I : Type ğ“˜ }(ğ’œ : I â†’ Algebra Î± ğ‘† ) â†’ Algebra (ğ“˜ âŠ” Î±) ğ‘†

â¨… {I = I} ğ’œ = ( âˆ€ (i : I) â†’ âˆ£ ğ’œ i âˆ£ ) ,           -- domain of the product algebra
               Î» ğ‘“ ğ‘ i â†’ (ğ‘“ Ì‚ ğ’œ i) Î» x â†’ ğ‘ x i   -- basic operations of the product algebra

\end{code}

(Alternative acceptable notation for the domain of the product is `âˆ€ i â†’ âˆ£ ğ’œ i âˆ£`.)

The type just defined is the one that will be used throughout the [UniversalAlgebra][] library whenever the product of an indexed collection of algebras (of type `Algebra`) is required.  However, for the sake of completeness, here is how one could define a type representing the product of algebras inhabiting the record type `algebra`.

\begin{code}

open algebra

â¨…' : {I : Type ğ“˜ }(ğ’œ : I â†’ algebra Î± ğ‘†) â†’ algebra (ğ“˜ âŠ” Î±) ğ‘†

â¨…' {I} ğ’œ = record { carrier = âˆ€ i â†’ carrier (ğ’œ i) ;                 -- domain
                     opsymbol = Î» ğ‘“ ğ‘ i â†’ (opsymbol (ğ’œ i)) ğ‘“ Î» x â†’ ğ‘ x i } -- basic operations

\end{code}



**Notation**. Given a signature `ğ‘† : Signature ğ“ ğ“¥`, the type `Algebra Î± ğ‘†` has type `Type(ğ“ âŠ” ğ“¥ âŠ” lsuc Î±)`.  Such types occur so often in the [UniversalAlgebra][] library that we define the following shorthand for their universes.

\begin{code}

ov : Level â†’ Level
ov Î± = ğ“ âŠ” ğ“¥ âŠ” lsuc Î±

\end{code}



#### <a id="products-of-classes-of-algebras">Products of classes of algebras</a>

An arbitrary class `ğ’¦` of algebras is represented as a predicate over the type `Algebra Î± ğ‘†`, for some universe level `Î±` and signature `ğ‘†`. That is, `ğ’¦ : Pred (Algebra Î± ğ‘†) Î²`, for some type `Î²`. Later we will formally state and prove that the product of all subalgebras of algebras in `ğ’¦` belongs to the class `SP(ğ’¦)` of subalgebras of products of algebras in `ğ’¦`. That is, `â¨… S(ğ’¦) âˆˆ SP(ğ’¦ )`. This turns out to be a nontrivial exercise.

To begin, we need to define types that represent products over arbitrary (nonindexed) families such as `ğ’¦` or `S(ğ’¦)`. Observe that `Î  ğ’¦` is certainly not what we want.  For recall that `Pred (Algebra Î± ğ‘†) Î²` is just an alias for the function type `Algebra Î± ğ‘† â†’ Type Î²`, and the semantics of the latter takes `ğ’¦ ğ‘¨` (and `ğ‘¨ âˆˆ ğ’¦`) to mean that `ğ‘¨` belongs to the class `ğ’¦`. Thus, by definition,

```agda
 Î  ğ’¦   :=   Î  ğ‘¨ ê‰ (Algebra Î± ğ‘†) , ğ’¦ ğ‘¨   :=   âˆ€ (ğ‘¨ : Algebra Î± ğ‘†) â†’ ğ‘¨ âˆˆ ğ’¦,
```

which asserts that every inhabitant of the type `Algebra Î± ğ‘†` belongs to `ğ’¦`.  Evidently this is not the product algebra that we seek.

What we need is a type that serves to index the class `ğ’¦`, and a function `ğ”„` that maps an index to the inhabitant of `ğ’¦` at that index. But `ğ’¦` is a predicate (of type `(Algebra Î± ğ‘†) â†’ Type Î²`) and the type `Algebra Î± ğ‘†` seems rather nebulous in that there is no natural indexing class with which to "enumerate" all inhabitants of `Algebra Î± ğ‘†` belonging to `ğ’¦`.<sup>[1](Algebras.Product.html#fn1)</sup>

The solution is to essentially take `ğ’¦` itself to be the indexing type, at least heuristically that is how one can view the type `â„‘` that we now define.<sup>[2](Algebras.Product.html#fn2)</sup>

\begin{code}

module _ {ğ’¦ : Pred (Algebra Î± ğ‘†)(ov Î±)} where
 â„‘ : Type (ov Î±)
 â„‘ = Î£[ ğ‘¨ âˆˆ Algebra Î± ğ‘† ] ğ‘¨ âˆˆ ğ’¦

\end{code}

Taking the product over the index type `â„‘` requires a function that maps an index `i : â„‘` to the corresponding algebra.  Each `i : â„‘` is a pair, `(ğ‘¨ , p)`, where `ğ‘¨` is an algebra and `p` is a proof that `ğ‘¨` belongs to `ğ’¦`, so the function mapping an index to the corresponding algebra is simply the first projection.

\begin{code}

 ğ”„ : â„‘ â†’ Algebra Î± ğ‘†
 ğ”„ i = âˆ£ i âˆ£

\end{code}

Finally, we define `class-product` which represents the product of all members of ğ’¦.

\begin{code}

 class-product : Algebra (ov Î±) ğ‘†
 class-product = â¨… ğ”„

\end{code}

If `p : ğ‘¨ âˆˆ ğ’¦`, we view the pair `(ğ‘¨ , p) âˆˆ â„‘` as an *index* over the class, so we can think of `ğ”„ (ğ‘¨ , p)` (which is simply `ğ‘¨`) as the projection of the product `â¨… ğ”„` onto the `(ğ‘¨ , p)`-th component.



-----------------------

<sup>1</sup><span class="footnote" id="fn1"> If you haven't seen this before, give it some thought and see if the correct type comes to you organically.</span>

<sup>2</sup><span class="footnote" id="fn2"> **Unicode Hints**. Some of our types are denoted with with Gothic ("mathfrak") symbols. To produce them in [agda2-mode][], type `\Mf` followed by a letter. For example, `\MfI` â† `â„‘`.</span>

[â† Algebras.Basic](Algebras.Basic.html)
<span style="float:right;">[Algebras.Congruences â†’](Algebras.Congruences.html)</span>

{% include UALib.Links.md %}

-----------------------------------------------

[agda-algebras development team]: https://github.com/ualib/agda-algebras#the-agda-algebras-development-team
