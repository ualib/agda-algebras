---
layout: default
title : Subalgebras.Subuniverses module (The Agda Universal Algebra Library)
date : 2021-01-14
author: William DeMeo
---

### <a id="subuniverses">Subuniverses</a>

This section presents the [Subalgebras.Subuniverses][] module of the [Agda Universal Algebra Library][].

We start by defining a type that represents the important concept of **subuniverse**. Suppose ğ‘¨ is an algebra.  A subset B âŠ† âˆ£ ğ‘¨ âˆ£ is said to be **closed under the operations of** ğ‘¨ if for each ğ‘“ âˆˆ âˆ£ ğ‘† âˆ£ and all tuples ğ’ƒ : âˆ¥ ğ‘† âˆ¥ ğ‘“ â†’ ğµ the element (ğ‘“ Ì‚ ğ‘¨) ğ’ƒ belongs to B. If a subset B âŠ† ğ´ is closed under the operations of ğ‘¨, then we call B a **subuniverse** of ğ‘¨.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

module Subalgebras.Subuniverses  where

open import Terms.Operations public
open import Relation.Unary using (â‹‚) public

module subuniverses {ğ‘† : Signature ğ“ ğ“¥} where
 open operations {ğ‘† = ğ‘†} public

\end{code}

We first show how to represent in [Agda][] the collection of subuniverses of an algebra `ğ‘¨`.  Since a subuniverse is viewed as a subset of the domain of `ğ‘¨`, we define it as a predicate on `âˆ£ ğ‘¨ âˆ£`.  Thus, the collection of subuniverses is a predicate on predicates on `âˆ£ ğ‘¨ âˆ£`.

\begin{code}

 module _ {ğ“¤ ğ“¦ : Level} where

  Subuniverses : (ğ‘¨ : Algebra ğ“¤ ğ‘†) â†’ Pred (Pred âˆ£ ğ‘¨ âˆ£ ğ“¦)(ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âŠ” ğ“¦)
  Subuniverses ğ‘¨ B = (ğ‘“ : âˆ£ ğ‘† âˆ£)(ğ‘ : âˆ¥ ğ‘† âˆ¥ ğ‘“ â†’ âˆ£ ğ‘¨ âˆ£) â†’ Im ğ‘ âŠ† B â†’ (ğ‘“ Ì‚ ğ‘¨) ğ‘ âˆˆ B

\end{code}

Here's one way to construct an algebra out of a subuniverse.

\begin{code}

  SubunivAlg : (ğ‘¨ : Algebra ğ“¤ ğ‘†)(B : Pred âˆ£ ğ‘¨ âˆ£ ğ“¦) â†’ B âˆˆ Subuniverses ğ‘¨ â†’ Algebra (ğ“¤ âŠ” ğ“¦) ğ‘†
  SubunivAlg ğ‘¨ B BâˆˆSubA = Î£ B , Î» ğ‘“ ğ‘ â†’ (ğ‘“ Ì‚ ğ‘¨)(fst âˆ˜ ğ‘) , BâˆˆSubA ğ‘“ (fst âˆ˜ ğ‘)(snd âˆ˜ ğ‘)

\end{code}



#### <a id="subuniverses-as-records">Subuniverses as records</a>

Next we define a type to represent a single subuniverse of an algebra. If `ğ‘¨` is the algebra in question, then a subuniverse of `ğ‘¨` is a subset of (i.e., predicate over) the domain `âˆ£ ğ‘¨ âˆ£` that belongs to `Subuniverses ğ‘¨`.

\begin{code}

  record Subuniverse {ğ‘¨ : Algebra ğ“¤ ğ‘†} : Set(ov (ğ“¤ âŠ” ğ“¦)) where
   constructor mksub
   field       sset : Pred âˆ£ ğ‘¨ âˆ£ ğ“¦
               isSub : sset âˆˆ Subuniverses ğ‘¨

\end{code}


#### <a id="subuniverse-generation">Subuniverse Generation</a>

If `ğ‘¨` is an algebra and `X âŠ† âˆ£ ğ‘¨ âˆ£` a subset of the domain of `ğ‘¨`, then the **subuniverse of** `ğ‘¨` **generated by** `X` is typically denoted by `Sg`<sup>`ğ‘¨`</sup>`(X)` and defined to be the smallest subuniverse of `ğ‘¨` containing `X`.  Equivalently,

`Sg`<sup>`ğ‘¨`</sup>`(X)`  =  `â‹‚` { `U` : `U` is a subuniverse of `ğ‘¨` and  `B âŠ† U` }.

We define an inductive type, denoted by `Sg`, that represents the subuniverse generated by a given subset of the domain of a given algebra, as follows.

\begin{code}

  data Sg (ğ‘¨ : Algebra ğ“¤ ğ‘†)(X : Pred âˆ£ ğ‘¨ âˆ£ ğ“¦) : Pred âˆ£ ğ‘¨ âˆ£ (ğ“ âŠ” ğ“¥ âŠ” ğ“¦ âŠ” ğ“¤) where
   var : âˆ€ {v} â†’ v âˆˆ X â†’ v âˆˆ Sg ğ‘¨ X
   app : (ğ‘“ : âˆ£ ğ‘† âˆ£)(ğ‘ : âˆ¥ ğ‘† âˆ¥ ğ‘“ â†’ âˆ£ ğ‘¨ âˆ£) â†’ Im ğ‘ âŠ† Sg ğ‘¨ X â†’ (ğ‘“ Ì‚ ğ‘¨) ğ‘ âˆˆ Sg ğ‘¨ X

\end{code}

Given an arbitrary subset `X` of the domain `âˆ£ ğ‘¨ âˆ£` of an `ğ‘†`-algebra `ğ‘¨`, the type `Sg X` does indeed represent a subuniverse of `ğ‘¨`. Proving this using the inductive type `Sg` is trivial, as we see here.

\begin{code}

 module _ {ğ“¤ ğ“¦ : Level} where

  sgIsSub : {ğ‘¨ : Algebra ğ“¤ ğ‘†}{X : Pred âˆ£ ğ‘¨ âˆ£ ğ“¦} â†’ Sg ğ‘¨ X âˆˆ Subuniverses ğ‘¨
  sgIsSub = app

\end{code}

Next we prove by structural induction that `Sg X` is the smallest subuniverse of `ğ‘¨` containing `X`.

\begin{code}

  sgIsSmallest : {ğ“¡ : Level}(ğ‘¨ : Algebra ğ“¤ ğ‘†){X : Pred âˆ£ ğ‘¨ âˆ£ ğ“¦}(Y : Pred âˆ£ ğ‘¨ âˆ£ ğ“¡)
   â†’             Y âˆˆ Subuniverses ğ‘¨  â†’  X âŠ† Y  â†’  Sg ğ‘¨ X âŠ† Y

  sgIsSmallest _ _ _ XinY (var Xv) = XinY Xv

  sgIsSmallest ğ‘¨ Y YsubA XinY (app ğ‘“ ğ‘ SgXa) = faâˆˆY
   where
   IH : Im ğ‘ âŠ† Y
   IH i = sgIsSmallest ğ‘¨ Y YsubA XinY (SgXa i)

   faâˆˆY : (ğ‘“ Ì‚ ğ‘¨) ğ‘ âˆˆ Y
   faâˆˆY = YsubA ğ‘“ ğ‘ IH

\end{code}

When the element of `Sg X` is constructed as `app ğ‘“ ğ‘ SgXa`, we may assume (the induction hypothesis) that the arguments in the tuple `ğ‘` belong to `Y`. Then the result of applying `ğ‘“` to `ğ‘` also belongs to `Y` since `Y` is a subuniverse.



#### <a id="subuniverse-lemmas">Subuniverse Lemmas</a>

Here we formalize a few basic properties of subuniverses. First, the intersection of subuniverses is again a subuniverse.

\begin{code}

 module _ {ğ“˜ ğ“¤ ğ“¦ : Level} where

  sub-intersection : {ğ‘¨ : Algebra ğ“¤ ğ‘†}{I : Set ğ“˜}{ğ’œ : I â†’ Pred âˆ£ ğ‘¨ âˆ£ ğ“¦}
   â†’                 Î  i ê‰ I , ğ’œ i âˆˆ Subuniverses ğ‘¨
                     ----------------------------------
   â†’                 â‹‚ I ğ’œ âˆˆ Subuniverses ğ‘¨

  sub-intersection Î± ğ‘“ ğ‘ Î² = Î» i â†’ Î± i ğ‘“ ğ‘ (Î» x â†’ Î² x i)

\end{code}

In the proof above, we assume the following typing judgments:

```
 Î± : âˆ€ i â†’ ğ’œ i âˆˆ Subuniverses ğ‘¨
 ğ‘“ : âˆ£ ğ‘† âˆ£
 ğ‘ : âˆ¥ ğ‘† âˆ¥ ğ‘“ â†’ âˆ£ ğ‘¨ âˆ£
 Î² : Im ğ‘ âŠ† â‹‚ I ğ’œ
```
and we must prove `(ğ‘“ Ì‚ ğ‘¨) ğ‘ âˆˆ â‹‚ I ğ’œ`. In this case, Agda will fill in the proof term `Î» i â†’ Î± i ğ‘“ ğ‘ (Î» x â†’ Î² x i)` automatically with the command `C-c C-a`.

Next, subuniverses are closed under the action of term operations.

\begin{code}

 module _ {ğ“¤ ğ“¦ : Level} where

  sub-term-closed : {ğ“§ : Level}{X : Set ğ“§}(ğ‘¨ : Algebra ğ“¤ ğ‘†){B : Pred âˆ£ ğ‘¨ âˆ£ ğ“¦}
   â†’                (B âˆˆ Subuniverses ğ‘¨) â†’ (t : Term X)(b : X â†’ âˆ£ ğ‘¨ âˆ£)
   â†’                Î  x ê‰ X , (b x âˆˆ B)  â†’  (ğ‘¨ âŸ¦ t âŸ§)b âˆˆ B

  sub-term-closed ğ‘¨ AB (â„Š x) b Bb = Bb x
  sub-term-closed ğ‘¨{B}Î±(node ğ‘“ ğ‘¡)b Î² = Î± ğ‘“(Î» z â†’ (ğ‘¨ âŸ¦ ğ‘¡ z âŸ§)b) Î» x â†’ sub-term-closed ğ‘¨{B}Î±(ğ‘¡ x)b Î²

\end{code}

In the induction step of the foregoing proof, the typing judgments of the premise are the following:

```
ğ‘¨   : Algebra ğ“¤ ğ‘†
B   : Pred âˆ£ ğ‘¨ âˆ£ ğ“¦
Î±   : B âˆˆ Subuniverses ğ‘¨
ğ‘“   : âˆ£ ğ‘† âˆ£
ğ‘¡   : âˆ¥ ğ‘† âˆ¥ ğ‘“ â†’ Term X
b   : X â†’ âˆ£ ğ‘¨ âˆ£
Î²   : âˆ€ x â†’ b x âˆˆ B
```
and the given proof term establishes the goal `(node ğ‘“ ğ‘¡ Ì‡ ğ‘¨) b âˆˆ B`.

Alternatively, we could express the preceeding fact using an inductive type representing images of terms.

\begin{code}

  data TermImage (ğ‘¨ : Algebra ğ“¤ ğ‘†)(Y : Pred âˆ£ ğ‘¨ âˆ£ ğ“¦) : Pred âˆ£ ğ‘¨ âˆ£ (ğ“ âŠ” ğ“¥ âŠ” ğ“¤ âŠ” ğ“¦)
   where
   var : âˆ€ {y : âˆ£ ğ‘¨ âˆ£} â†’ y âˆˆ Y â†’ y âˆˆ TermImage ğ‘¨ Y
   app : âˆ€ ğ‘“ ğ‘¡ â†’  Î  x ê‰ âˆ¥ ğ‘† âˆ¥ ğ‘“ , ğ‘¡ x âˆˆ TermImage ğ‘¨ Y  â†’ (ğ‘“ Ì‚ ğ‘¨) ğ‘¡ âˆˆ TermImage ğ‘¨ Y

\end{code}

By what we proved above, it should come as no surprise that `TermImage ğ‘¨ Y` is a subuniverse of ğ‘¨ that contains Y.

\begin{code}

  TermImageIsSub : {ğ‘¨ : Algebra ğ“¤ ğ‘†}{Y : Pred âˆ£ ğ‘¨ âˆ£ ğ“¦} â†’ TermImage ğ‘¨ Y âˆˆ Subuniverses ğ‘¨
  TermImageIsSub = app

  Y-onlyif-TermImageY : {ğ‘¨ : Algebra ğ“¤ ğ‘†}{Y : Pred âˆ£ ğ‘¨ âˆ£ ğ“¦} â†’ Y âŠ† TermImage ğ‘¨ Y
  Y-onlyif-TermImageY {a} Ya = var Ya

\end{code}

Since `Sg ğ‘¨ Y` is the smallest subuniverse containing Y, we obtain the following inclusion.

\begin{code}

  SgY-onlyif-TermImageY : (ğ‘¨ : Algebra ğ“¤ ğ‘†)(Y : Pred âˆ£ ğ‘¨ âˆ£ ğ“¦) â†’ Sg ğ‘¨ Y âŠ† TermImage ğ‘¨ Y
  SgY-onlyif-TermImageY ğ‘¨ Y = sgIsSmallest ğ‘¨ (TermImage ğ‘¨ Y) TermImageIsSub Y-onlyif-TermImageY

\end{code}



#### <a id="properties-of-homomorphisms">Homomorphic images are subuniverses</a>

Now that we have developed the machinery of subuniverse generation, we can prove two basic facts that play an important role in many theorems about algebraic structures. First, the image of a homomorphism is a subuniverse of its codomain.

\begin{code}

  hom-image-is-sub : dfunext ğ“¥ ğ“¦ â†’ {ğ‘¨ : Algebra ğ“¤ ğ‘†}{ğ‘© : Algebra ğ“¦ ğ‘†}
                     (Ï• : hom ğ‘¨ ğ‘©) â†’ (HomImage ğ‘© Ï•) âˆˆ Subuniverses ğ‘©

  hom-image-is-sub fe {ğ‘¨}{ğ‘©} Ï• ğ‘“ b Imfb = eq ((ğ‘“ Ì‚ ğ‘©) b) ((ğ‘“ Ì‚ ğ‘¨) ar) Î³
   where
   ar : âˆ¥ ğ‘† âˆ¥ ğ‘“ â†’ âˆ£ ğ‘¨ âˆ£
   ar = Î» x â†’ Inv âˆ£ Ï• âˆ£ (Imfb x)

   Î¶ : âˆ£ Ï• âˆ£ âˆ˜ ar â‰¡ b
   Î¶ = fe (Î» x â†’ InvIsInv âˆ£ Ï• âˆ£ (Imfb x))

   Î³ : (ğ‘“ Ì‚ ğ‘©) b â‰¡ âˆ£ Ï• âˆ£ ((ğ‘“ Ì‚ ğ‘¨) ar)
   Î³ = (ğ‘“ Ì‚ ğ‘©) b            â‰¡âŸ¨ ap (ğ‘“ Ì‚ ğ‘©)(Î¶ â»Â¹) âŸ©
       (ğ‘“ Ì‚ ğ‘©) (âˆ£ Ï• âˆ£ âˆ˜ ar) â‰¡âŸ¨(âˆ¥ Ï• âˆ¥ ğ‘“ ar)â»Â¹ âŸ©
       âˆ£ Ï• âˆ£ ((ğ‘“ Ì‚ ğ‘¨) ar)   âˆ

\end{code}


Next we prove the important fact that homomorphisms are uniquely determined by their values on a generating set.

\begin{code}

  hom-unique : funext ğ“¥ ğ“¦ â†’ {ğ‘¨ : Algebra ğ“¤ ğ‘†}{ğ‘© : Algebra ğ“¦ ğ‘†}
               (X : Pred âˆ£ ğ‘¨ âˆ£ ğ“¤)  (g h : hom ğ‘¨ ğ‘©)
   â†’           Î  x ê‰ âˆ£ ğ‘¨ âˆ£ , (x âˆˆ X â†’ âˆ£ g âˆ£ x â‰¡ âˆ£ h âˆ£ x)
               -------------------------------------------------
   â†’           Î  a ê‰ âˆ£ ğ‘¨ âˆ£ , (a âˆˆ Sg ğ‘¨ X â†’ âˆ£ g âˆ£ a â‰¡ âˆ£ h âˆ£ a)

  hom-unique _ _ _ _ Î± a (var x) = Î± a x

  hom-unique fe {ğ‘¨}{ğ‘©} X g h Î± fa (app ğ‘“ ğ’‚ Î²) = âˆ£ g âˆ£ ((ğ‘“ Ì‚ ğ‘¨) ğ’‚)   â‰¡âŸ¨ âˆ¥ g âˆ¥ ğ‘“ ğ’‚ âŸ©
                                                (ğ‘“ Ì‚ ğ‘©)(âˆ£ g âˆ£ âˆ˜ ğ’‚ ) â‰¡âŸ¨ ap (ğ‘“ Ì‚ ğ‘©)(fe IH) âŸ©
                                                (ğ‘“ Ì‚ ğ‘©)(âˆ£ h âˆ£ âˆ˜ ğ’‚)  â‰¡âŸ¨ ( âˆ¥ h âˆ¥ ğ‘“ ğ’‚ )â»Â¹ âŸ©
                                                âˆ£ h âˆ£ ((ğ‘“ Ì‚ ğ‘¨) ğ’‚ )  âˆ
   where IH = Î» x â†’ hom-unique fe {ğ‘¨}{ğ‘©} X g h Î± (ğ’‚ x) (Î² x)

\end{code}

In the induction step, we have the following typing judgments in the premise:

```
fe  : funext ğ“¥ ğ“¦
ğ‘¨   : Algebra ğ“¤ ğ‘†
ğ‘©   : Algebra ğ“¦ ğ‘†
X   : Pred âˆ£ ğ‘¨ âˆ£ ğ“¤
g h  : hom ğ‘¨ ğ‘©
Î±   : Î  x ê‰ âˆ£ ğ‘¨ âˆ£ , (x âˆˆ X â†’ âˆ£ g âˆ£ x â‰¡ âˆ£ h âˆ£ x)
fa  : âˆ£ ğ‘¨ âˆ£
fa  = (ğ‘“ Ì‚ ğ‘¨) ğ’‚
ğ‘“   : âˆ£ ğ‘† âˆ£
ğ’‚   : âˆ¥ ğ‘† âˆ¥ ğ‘“ â†’ âˆ£ ğ‘¨ âˆ£
Î²   : Im ğ’‚ âŠ† Sg ğ‘¨ X
```
and, under these assumptions, we prove `âˆ£ g âˆ£ ((ğ‘“ Ì‚ ğ‘¨) ğ’‚) â‰¡ âˆ£ h âˆ£ ((ğ‘“ Ì‚ ğ‘¨) ğ’‚)`.

---------------------------------

[â†‘ Subalgebras](Subalgebras.html)
<span style="float:right;">[Subalgebras.Subalgebras â†’](Subalgebras.Subalgebras.html)</span>


{% include UALib.Links.md %}


<!--

As an example application, here is a formal proof that the equalizer of two
homomorphisms with domain `ğ‘¨` is a subuniverse of `ğ‘¨`.


 ğ¸hom-is-subuniverse : dfunext ğ“¥ ğ“¦ â†’ {ğ‘¨ : Algebra ğ“¤ ğ‘†}(ğ‘© : Algebra ğ“¦ ğ‘†)(g h : hom ğ‘¨ ğ‘©)
  â†’                    Subuniverse {ğ‘¨ = ğ‘¨}

 ğ¸hom-is-subuniverse fe ğ‘© g h = mksub (ğ¸hom ğ‘© g h) Î» ğ‘“ a x â†’ ğ¸hom-closed fe ğ‘© g h ğ‘“ a x

-->
