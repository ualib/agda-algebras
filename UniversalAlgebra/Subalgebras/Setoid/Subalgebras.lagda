---
layout: default
title : Subalgebras.Setoid.Subalgebras module (The Agda Universal Algebra Library)
date : 2021-07-17
author: [agda-algebras development team][]
---

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import Algebras.Basic using ( ğ“ ; ğ“¥ ; Signature )

module Subalgebras.Setoid.Subalgebras {ğ‘† : Signature ğ“ ğ“¥} where

-- imports from Agda and the Agda Standard Library
open import Agda.Primitive         using ( _âŠ”_ ; lsuc ; Level ) renaming ( Set to Type )
open import Agda.Builtin.Equality   using    ( _â‰¡_ ; refl )
open import Data.Product            using    ( _,_ ; Î£-syntax ; _Ã—_ )
open import Function.Base           using    ( id )
open import Function.Bundles        using    ( Injection )
open import Relation.Binary         using    ( Setoid ; REL )
open import Relation.Unary          using    ( Pred ; _âˆˆ_ ; _âŠ†_ )

-- -- -- -- imports from agda-algebras ------------------------------------------------------
open import Overture.Preliminaries                    using ( âˆ£_âˆ£ ; âˆ¥_âˆ¥ )
open import Overture.Inverses                         using ( IsInjective ; id-is-injective ; âˆ˜-injective )
open import Algebras.Setoid.Basic             {ğ‘† = ğ‘†} using ( SetoidAlgebra ; Lift-SetoidAlg )
open import Algebras.Products                 {ğ‘† = ğ‘†} using ( ov )
open import Homomorphisms.Setoid.Basic        {ğ‘† = ğ‘†} using ( hom ; âˆ˜-hom )
open import Homomorphisms.Setoid.Isomorphisms {ğ‘† = ğ‘†} using ( _â‰…_ ; â‰…toInjective ; â‰…fromInjective
                                                            ;  â‰…-sym ; â‰…-refl ; â‰…-trans ; Lift-â‰… )

private variable Ï : Level

\end{code}


#### Subalgebras of SetoidAlgebras

\begin{code}
module _ where

 private variable
  Î± Ïáµƒ Î² Ïáµ‡ : Level

 _â‰¥s_  -- (alias for supalgebra (aka overalgebra))
  _IsSupalgebraOf_ : SetoidAlgebra Î± Ïáµƒ â†’ SetoidAlgebra Î² Ïáµ‡ â†’ Type _
 ğ‘¨ IsSupalgebraOf ğ‘© = Î£[ h âˆˆ hom ğ‘© ğ‘¨ ] IsInjective âˆ£ h âˆ£

 _â‰¤s_  -- (alias for subalgebra relation))
  _IsSubalgebraOf_ : SetoidAlgebra Î± Ïáµƒ â†’ SetoidAlgebra Î² Ïáµ‡ â†’ Type _
 ğ‘¨ IsSubalgebraOf ğ‘© = Î£[ h âˆˆ hom ğ‘¨ ğ‘© ] IsInjective âˆ£ h âˆ£

 -- Syntactic sugar for sup/sub-algebra relations.
 ğ‘¨ â‰¥s ğ‘© = ğ‘¨ IsSupalgebraOf ğ‘©
 ğ‘¨ â‰¤s ğ‘© = ğ‘¨ IsSubalgebraOf ğ‘©

 open _â‰…_
 â‰…â†’â‰¤s : {ğ‘¨ : SetoidAlgebra Î± Ïáµƒ}{ğ‘© : SetoidAlgebra Î² Ïáµ‡} â†’ ğ‘¨ â‰… ğ‘© â†’ ğ‘¨ â‰¤s ğ‘©
 â‰…â†’â‰¤s Ï† = (to Ï†) , â‰…toInjective Ï†

 â‰…â†’â‰¥s : {ğ‘¨ : SetoidAlgebra Î± Ïáµƒ}{ğ‘© : SetoidAlgebra Î² Ïáµ‡} â†’ ğ‘¨ â‰… ğ‘© â†’ ğ‘¨ â‰¥s ğ‘©
 â‰…â†’â‰¥s Ï† = (from Ï†) , â‰…fromInjective Ï†

 record SubalgebraOf : Type (ov (Î± âŠ” Î² âŠ” Ï âŠ” Ïáµƒ âŠ” Ïáµ‡)) where
  field
   algebra : SetoidAlgebra Î± Ïáµƒ
   subalgebra : SetoidAlgebra Î² Ïáµ‡
   issubalgebra : subalgebra â‰¤s algebra


 Subalgebra : SetoidAlgebra Î± Ïáµƒ â†’ {Î² Ïáµ‡ : Level} â†’ Type _
 Subalgebra ğ‘¨ {Î²}{Ïáµ‡} = Î£[ ğ‘© âˆˆ (SetoidAlgebra Î² Ïáµ‡) ] ğ‘© â‰¤s ğ‘¨

 -- usage note: for ğ‘¨ : SetoidAlgebra Î± Ïáµƒ, inhabitant of `Subalgebra ğ‘¨` is a pair
 --             `(ğ‘© , p) : Subalgebra ğ‘¨`  providing
 --                                       - `ğ‘© : SetoidAlgebra Î² Ïáµ‡` and
 --                                       - `p : ğ‘© â‰¤s ğ‘¨`, a proof that ğ‘© is a subalgebra of ğ´.


 IsSubalgebraREL : {Î± Ïáµƒ Î² Ïáµ‡ : Level} â†’ REL (SetoidAlgebra Î± Ïáµƒ)(SetoidAlgebra Î² Ïáµ‡) Ï â†’ Type _
 IsSubalgebraREL {Î±}{Ïáµƒ}{Î²}{Ïáµ‡} R = âˆ€ {ğ‘¨ : SetoidAlgebra Î± Ïáµƒ}{ğ‘© : SetoidAlgebra Î² Ïáµ‡} â†’ ğ‘¨ â‰¤s ğ‘©

 record SubalgebraREL(R : REL (SetoidAlgebra Î² Ïáµ‡)(SetoidAlgebra Î± Ïáµƒ) Ï) : Type (ov (Î± âŠ” Î² âŠ” Ï âŠ” Ïáµƒ âŠ” Ïáµ‡))
  where
  field isSubalgebraREL : IsSubalgebraREL R


\end{code}

From now on we will use `ğ‘© â‰¤s ğ‘¨` to express the assertion that `ğ‘©` is a subalgebra of `ğ‘¨`.


#### Subalgebras of a class of algebras

Suppose `ğ’¦ : Pred (Algebra Î± ğ‘†) Î³` denotes a class of `ğ‘†`-algebras and `ğ‘© : SetoidAlgebra Î² Ïáµ‡` denotes an arbitrary `ğ‘†`-algebra. Then we might wish to consider the assertion that `ğ‘©` is a subalgebra of an algebra in the class `ğ’¦`.  The next type we define allows us to express this assertion as `ğ‘© IsSubalgebraOfClass ğ’¦`.

\begin{code}

module _ where

 private variable
  Î± Ïáµƒ Î² Ïáµ‡ : Level

 _â‰¤c_
  _IsSubalgebraOfClass_ : SetoidAlgebra Î² Ïáµ‡ â†’ Pred (SetoidAlgebra Î± Ïáµƒ) Ï â†’ Type _
 ğ‘© IsSubalgebraOfClass ğ’¦ = Î£[ ğ‘¨ âˆˆ SetoidAlgebra _ _ ] ((ğ‘¨ âˆˆ ğ’¦) Ã— (ğ‘© â‰¤s ğ‘¨))

 ğ‘© â‰¤c ğ’¦ = ğ‘© IsSubalgebraOfClass ğ’¦

 record SubalgebraOfClass : Type (ov (Î± âŠ” Î² âŠ” Ï âŠ” Ïáµƒ âŠ” Ïáµ‡))
  where
  field
   class : Pred (SetoidAlgebra Î± Ïáµƒ) Ï
   subalgebra : SetoidAlgebra Î² Ïáµ‡
   issubalgebraofclass : subalgebra â‰¤c class


 record SubalgebraOfClass' : Type (ov (Î± âŠ” Î² âŠ” Ï âŠ” Ïáµƒ âŠ” Ïáµ‡))
  where
  field
   class : Pred (SetoidAlgebra Î± Ïáµƒ) Ï
   classalgebra : SetoidAlgebra Î± Ïáµƒ
   isclassalgebra : classalgebra âˆˆ class
   subalgebra : SetoidAlgebra Î² Ïáµ‡
   issubalgebra : subalgebra â‰¤s classalgebra

 -- The collection of subalgebras of algebras in class ğ’¦.
 SubalgebrasOfClass : Pred (SetoidAlgebra Î± Ïáµƒ) Ï â†’ {Î² Ïáµ‡ : Level} â†’ Type _
 SubalgebrasOfClass ğ’¦ {Î²}{Ïáµ‡} = Î£[ ğ‘© âˆˆ SetoidAlgebra Î² Ïáµ‡ ] ğ‘© â‰¤c ğ’¦

\end{code}


#### Subalgebra lemmas

The subalgebra relation is a *preorder*; i.e., it is a reflexive, transitive binary relation.

\begin{code}


private variable Î± Ïáµƒ Î² Ïáµ‡ Î³ Ïá¶œ : Level

â‰¤s-refl : {ğ‘¨ ğ‘© : SetoidAlgebra Î± Ïáµƒ} â†’ ğ‘¨ â‰… ğ‘© â†’ ğ‘¨ â‰¤s ğ‘©
â‰¤s-refl {ğ‘¨ = ğ‘¨}{ğ‘©} Aâ‰…B = â‰…â†’â‰¤s Aâ‰…B

â‰¥s-refl : {ğ‘¨ ğ‘© : SetoidAlgebra Î± Ïáµƒ} â†’ ğ‘¨ â‰… ğ‘© â†’ ğ‘¨ â‰¥s ğ‘©
â‰¥s-refl {ğ‘¨ = ğ‘¨}{ğ‘©} Aâ‰…B = â‰…â†’â‰¤s (â‰…-sym Aâ‰…B)

â‰¤s-refl' : {ğ‘¨ : SetoidAlgebra Î± Ïáµƒ} â†’ ğ‘¨ â‰¤s ğ‘¨
â‰¤s-refl' {ğ‘¨ = ğ‘¨} = (id , Î» f a â†’ refl) , Injection.injective id-is-injective


â‰¤s-trans : (ğ‘¨ : SetoidAlgebra Î± Ïáµƒ){ğ‘© : SetoidAlgebra Î² Ïáµ‡}(ğ‘ª : SetoidAlgebra Î³ Ïá¶œ)
  â†’        ğ‘¨ â‰¤s ğ‘© â†’ ğ‘© â‰¤s ğ‘ª â†’ ğ‘¨ â‰¤s ğ‘ª

â‰¤s-trans ğ‘¨ {ğ‘©} ğ‘ª Aâ‰¤B Bâ‰¤C = (âˆ˜-hom ğ‘¨ ğ‘© ğ‘ª âˆ£ Aâ‰¤B âˆ£ âˆ£ Bâ‰¤C âˆ£ ) , Goal
 where
 Goal : IsInjective âˆ£ (âˆ˜-hom ğ‘¨ ğ‘© ğ‘ª âˆ£ Aâ‰¤B âˆ£ âˆ£ Bâ‰¤C âˆ£) âˆ£
 Goal = âˆ˜-injective âˆ¥ Aâ‰¤B âˆ¥ âˆ¥ Bâ‰¤C âˆ¥

â‰¥s-trans : (ğ‘¨ : SetoidAlgebra Î± Ïáµƒ){ğ‘© : SetoidAlgebra Î² Ïáµ‡}(ğ‘ª : SetoidAlgebra Î³ Ïá¶œ)
  â†’        ğ‘¨ â‰¥s ğ‘© â†’ ğ‘© â‰¥s ğ‘ª â†’ ğ‘¨ â‰¥s ğ‘ª
â‰¥s-trans ğ‘¨ {ğ‘©} ğ‘ª Aâ‰¥B Bâ‰¥C = â‰¤s-trans ğ‘ª {ğ‘©} ğ‘¨ Bâ‰¥C Aâ‰¥B


module _ {Î± Ïáµƒ Ï : Level} where

 open import Relation.Binary.Structures {a = ov(Î± âŠ” Ïáµƒ)}{â„“ = (ğ“ âŠ” ğ“¥ âŠ” Î±)} (_â‰…_ {Î±}{Ïáµƒ}{Î±}{Ïáµƒ})

 open IsPreorder
 â‰¤s-preorder : IsPreorder _â‰¤s_
 isEquivalence â‰¤s-preorder = record { refl = â‰…-refl ; sym = â‰…-sym ; trans = â‰…-trans }
 reflexive â‰¤s-preorder = â‰¤s-refl
 trans â‰¤s-preorder {ğ‘¨}{ğ‘©}{ğ‘ª} Aâ‰¤B Bâ‰¤C = â‰¤s-trans ğ‘¨ {ğ‘©} ğ‘ª Aâ‰¤B Bâ‰¤C



open _â‰…_

module _ (ğ‘¨ : SetoidAlgebra Î± Ïáµƒ){ğ‘© : SetoidAlgebra Î² Ïáµ‡}(ğ‘ª : SetoidAlgebra Î³ Ïá¶œ) where

 Aâ‰¥Bâ†’Bâ‰…Câ†’Aâ‰¥C : ğ‘¨ â‰¥s ğ‘© â†’ ğ‘© â‰… ğ‘ª â†’ ğ‘¨ â‰¥s ğ‘ª
 Aâ‰¥Bâ†’Bâ‰…Câ†’Aâ‰¥C Aâ‰¥B Bâ‰…C  = â‰¥s-trans ğ‘¨ {ğ‘©} ğ‘ª Aâ‰¥B (â‰…â†’â‰¥s Bâ‰…C)

 Aâ‰¤Bâ†’Bâ‰…Câ†’Aâ‰¤C : ğ‘¨ â‰¤s ğ‘© â†’ ğ‘© â‰… ğ‘ª â†’ ğ‘¨ â‰¤s ğ‘ª
 Aâ‰¤Bâ†’Bâ‰…Câ†’Aâ‰¤C Aâ‰¤B Bâ‰…C = â‰¤s-trans ğ‘¨{ğ‘©} ğ‘ª Aâ‰¤B (â‰…â†’â‰¤s Bâ‰…C)

 Aâ‰…Bâ†’Bâ‰¥Câ†’Aâ‰¥C : ğ‘¨ â‰… ğ‘© â†’ ğ‘© â‰¥s ğ‘ª â†’ ğ‘¨ â‰¥s ğ‘ª

 Aâ‰…Bâ†’Bâ‰¥Câ†’Aâ‰¥C Aâ‰…B Bâ‰¥C = â‰¥s-trans ğ‘¨{ğ‘©}ğ‘ª (â‰…â†’â‰¥s Aâ‰…B) Bâ‰¥C

 Aâ‰…Bâ†’Bâ‰¤Câ†’Aâ‰¤C : ğ‘¨ â‰… ğ‘© â†’ ğ‘© â‰¤s ğ‘ª â†’ ğ‘¨ â‰¤s ğ‘ª
 Aâ‰…Bâ†’Bâ‰¤Câ†’Aâ‰¤C Aâ‰…B Bâ‰¤C = â‰¤s-trans ğ‘¨{ğ‘©}ğ‘ª (â‰…â†’â‰¤s Aâ‰…B) Bâ‰¤C


â‰¤s-TRANS-â‰… : (ğ‘¨ : SetoidAlgebra Î± Ïáµƒ){ğ‘© : SetoidAlgebra Î² Ïáµ‡}(ğ‘ª : SetoidAlgebra Î³ Ïá¶œ)
 â†’          ğ‘¨ â‰¤s ğ‘© â†’ ğ‘© â‰… ğ‘ª â†’ ğ‘¨ â‰¤s ğ‘ª
â‰¤s-TRANS-â‰… ğ‘¨ {ğ‘©} ğ‘ª (h , hinj) Bâ‰…C = (âˆ˜-hom ğ‘¨ ğ‘© ğ‘ª h (to Bâ‰…C)) , âˆ˜-injective hinj (â‰…toInjective Bâ‰…C)

â‰¤s-mono : (ğ‘© : SetoidAlgebra Î² Ïáµ‡){ğ’¦ ğ’¦' : Pred (SetoidAlgebra Î± Ïáµƒ) Î³}
 â†’        ğ’¦ âŠ† ğ’¦' â†’ ğ‘© IsSubalgebraOfClass ğ’¦ â†’ ğ‘© IsSubalgebraOfClass ğ’¦'

â‰¤s-mono ğ‘© KK' (ğ‘¨ , (KA , Bâ‰¤A)) = ğ‘¨ , ((KK' KA) , Bâ‰¤A)

\end{code}


#### Lifts of subalgebras

\begin{code}

module _ {ğ’¦ : Pred (SetoidAlgebra Î± Ïáµƒ)(ov Î±)}{ğ‘© : SetoidAlgebra Î² Ïáµ‡}{â„“ : Level} where

 Lift-is-sub : ğ‘© IsSubalgebraOfClass ğ’¦ â†’ (Lift-SetoidAlg ğ‘© â„“) IsSubalgebraOfClass ğ’¦
 Lift-is-sub (ğ‘¨ , (KA , Bâ‰¤A)) = ğ‘¨ , (KA , Aâ‰¥Bâ†’Bâ‰…Câ†’Aâ‰¥C ğ‘¨ (Lift-SetoidAlg ğ‘© â„“) Bâ‰¤A Lift-â‰…)

â‰¤s-Lift : (ğ‘¨ : SetoidAlgebra Î± Ïáµƒ){ğ‘© : SetoidAlgebra Î² Ïáµ‡}{â„“ : Level} â†’ ğ‘¨ â‰¤s ğ‘© â†’ ğ‘¨ â‰¤s Lift-SetoidAlg ğ‘© â„“
â‰¤s-Lift ğ‘¨ {ğ‘©}{â„“} Aâ‰¤sB = Aâ‰¤Bâ†’Bâ‰…Câ†’Aâ‰¤C ğ‘¨ (Lift-SetoidAlg ğ‘© â„“) Aâ‰¤sB Lift-â‰…

â‰¥s-Lift : (ğ‘¨ : SetoidAlgebra Î± Ïáµƒ){ğ‘© : SetoidAlgebra Î² Ïáµ‡}{â„“ : Level} â†’ ğ‘¨ â‰¥s ğ‘© â†’ ğ‘¨ â‰¥s Lift-SetoidAlg ğ‘© â„“
â‰¥s-Lift ğ‘¨ {ğ‘©}{â„“} Aâ‰¥sB = Aâ‰¥Bâ†’Bâ‰…Câ†’Aâ‰¥C ğ‘¨ (Lift-SetoidAlg ğ‘© â„“) Aâ‰¥sB Lift-â‰…

Lift-â‰¤s-Lift : {ğ‘¨ : SetoidAlgebra Î± Ïáµƒ}(â„“áµƒ : Level){ğ‘© : SetoidAlgebra Î² Ïáµ‡}(â„“áµ‡ : Level)
 â†’             ğ‘¨ â‰¤s ğ‘© â†’ Lift-SetoidAlg ğ‘¨ â„“áµƒ â‰¤s Lift-SetoidAlg ğ‘© â„“áµ‡
Lift-â‰¤s-Lift {ğ‘¨ = ğ‘¨} â„“áµƒ {ğ‘©} â„“áµ‡ Aâ‰¤sB = â‰¥s-Lift (Lift-SetoidAlg ğ‘© â„“áµ‡){ğ‘¨} (â‰¤s-Lift ğ‘¨{ğ‘©} Aâ‰¤sB)

\end{code}

------------------------------

[agda-algebras development team]: https://github.com/ualib/agda-algebras#the-agda-algebras-development-team

