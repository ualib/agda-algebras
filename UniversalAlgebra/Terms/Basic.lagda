---
layout: default
title : Terms.Basic module (The Agda Universal Algebra Library)
date : 2021-01-14
author: William DeMeo
---

### <a id="basic-definitions">Basic Definitions</a>

This section presents the [Terms.Basic][] module of the [Agda Universal Algebra Library][].

The theoretical background that begins each subsection below is based on Cliff Bergman's textbook [Bergman (2012)][], specifically, Section 4.3.  Apart from notation, our presentation is similar to Bergman's, but we will be more concise, omitting some details and all examples, in order to more quickly arrive at our objective, which is to use type theory to express the concepts and formalize them in the [Agda][] language.  We refer the reader to [Bergman (2012)][] for a more complete exposition of classical (informal) universal algebra.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

-- Imports from Agda (builtin/primitive) and the Agda Standard Library
open import Agda.Builtin.Equality using (_â‰¡_; refl)
open import Axiom.Extensionality.Propositional renaming (Extensionality to funext)
open import Data.Product using (_,_; Î£; _Ã—_)
open import Function.Base  using (_âˆ˜_)
open import Level renaming (suc to lsuc; zero to lzero)
open import Relation.Binary.PropositionalEquality.Core using (cong; module â‰¡-Reasoning)
open â‰¡-Reasoning
open import Relation.Unary using (Pred)

-- Imports from the Agda Universal Algebra Library
open import Algebras.Basic
open import Overture.Preliminaries
 using (Type; ğ“; ğ“¤; ğ“¥; ğ“¦; ğ“§; ğ“¨; Î ; -Î ; -Î£; _â»Â¹; ğ‘–ğ‘‘; âˆ£_âˆ£; âˆ¥_âˆ¥)
open import Overture.Inverses using (IsSurjective; Image_âˆ‹_; Inv; InvIsInv; eq)


module Terms.Basic {ğ‘† : Signature ğ“ ğ“¥} where

open import Algebras.Products{ğ‘† = ğ‘†} using (ov)
open import Homomorphisms.Basic {ğ‘† = ğ‘†} using (hom) -- ; ğ“ğ’¾ğ’»ğ“‰; ğ“â„´ğ“Œâ„¯ğ“‡)

\end{code}

#### <a id="the-type-of-terms">The type of terms</a>

Fix a signature `ğ‘†` and let `X` denote an arbitrary nonempty collection of variable symbols. Assume the symbols in `X` are distinct from the operation symbols of `ğ‘†`, that is `X âˆ© âˆ£ ğ‘† âˆ£ = âˆ…`.

By a *word* in the language of `ğ‘†`, we mean a nonempty, finite sequence of members of `X âˆª âˆ£ ğ‘† âˆ£`. We denote the concatenation of such sequences by simple juxtaposition.

Let `Sâ‚€` denote the set of nullary operation symbols of `ğ‘†`. We define by induction on `n` the sets `ğ‘‡â‚™` of *words* over `X âˆª âˆ£ ğ‘† âˆ£` as follows (cf. [Bergman (2012)][] Def. 4.19):

`ğ‘‡â‚€ := X âˆª Sâ‚€` and `ğ‘‡â‚™â‚Šâ‚ := ğ‘‡â‚™ âˆª ğ’¯â‚™`

where `ğ’¯â‚™` is the collection of all `ğ‘“ ğ‘¡` such that `ğ‘“ : âˆ£ ğ‘† âˆ£` and `ğ‘¡ : âˆ¥ ğ‘† âˆ¥ ğ‘“ â†’ ğ‘‡â‚™`. (Recall, `âˆ¥ ğ‘† âˆ¥ ğ‘“` is the arity of the operation symbol ğ‘“.)

We define the collection of *terms* in the signature `ğ‘†` over `X` by `Term X := â‹ƒâ‚™ ğ‘‡â‚™`. By an ğ‘†-*term* we mean a term in the language of `ğ‘†`.

The definition of `Term X` is recursive, indicating that an inductive type could be used to represent the semantic notion of terms in type theory. Indeed, such a representation is given by the following inductive type.

\begin{code}

data Term (X : Type ğ“§ ) : Type(ov ğ“§)  where
 â„Š : X â†’ Term X    -- (â„Š for "generator")
 node : (f : âˆ£ ğ‘† âˆ£)(ğ‘¡ : âˆ¥ ğ‘† âˆ¥ f â†’ Term X) â†’ Term X

open Term public

\end{code}

This is a very basic inductive type that represents each term as a tree with an operation symbol at each `node` and a variable symbol at each leaf (`generator`).


**Notation**. As usual, the type `X` represents an arbitrary collection of variable symbols. Recall, `ov ğ“§` is our shorthand notation for the universe level `ğ“ âŠ” ğ“¥ âŠ” lsuc ğ“§`.


#### <a id="the-term-algebra">The term algebra</a>

For a given signature `ğ‘†`, if the type `Term X` is nonempty (equivalently, if `X` or `âˆ£ ğ‘† âˆ£` is nonempty), then we can define an algebraic structure, denoted by `ğ‘» X` and called the *term algebra in the signature* `ğ‘†` *over* `X`.  Terms are viewed as acting on other terms, so both the domain and basic operations of the algebra are the terms themselves.


+ For each operation symbol `ğ‘“ : âˆ£ ğ‘† âˆ£`, denote by `ğ‘“ Ì‚ (ğ‘» X)` the operation on `Term X` that maps a tuple `ğ‘¡ : âˆ¥ ğ‘† âˆ¥ ğ‘“ â†’ âˆ£ ğ‘» X âˆ£` to the formal term `ğ‘“ ğ‘¡`.
+ Define `ğ‘» X` to be the algebra with universe `âˆ£ ğ‘» X âˆ£ := Term X` and operations `ğ‘“ Ì‚ (ğ‘» X)`, one for each symbol `ğ‘“` in `âˆ£ ğ‘† âˆ£`.

In [Agda][] the term algebra can be defined as simply as one could hope.

\begin{code}

ğ‘» : (X : Type ğ“§ ) â†’ Algebra (ov ğ“§) ğ‘†
ğ‘» X = Term X , node

\end{code}



#### <a id="the-universal-property">The universal property</a>

The term algebra `ğ‘» X` is *absolutely free* (or *universal*, or *initial*) for algebras in the signature `ğ‘†`. That is, for every ğ‘†-algebra `ğ‘¨`, the following hold.

1. Every function from `ğ‘‹` to `âˆ£ ğ‘¨ âˆ£` lifts to a homomorphism from `ğ‘» X` to `ğ‘¨`.
2. The homomorphism that exists by item 1 is unique.

We now prove this in [Agda][], starting with the fact that every map from `X` to `âˆ£ ğ‘¨ âˆ£` lifts to a map from `âˆ£ ğ‘» X âˆ£` to `âˆ£ ğ‘¨ âˆ£` in a natural way, by induction on the structure of the given term.

\begin{code}

private variable X : Type ğ“§

free-lift : (ğ‘¨ : Algebra ğ“¤ ğ‘†)(h : X â†’ âˆ£ ğ‘¨ âˆ£) â†’ âˆ£ ğ‘» X âˆ£ â†’ âˆ£ ğ‘¨ âˆ£
free-lift _ h (â„Š x) = h x
free-lift ğ‘¨ h (node f ğ‘¡) = (f Ì‚ ğ‘¨) (Î» i â†’ free-lift ğ‘¨ h (ğ‘¡ i))

\end{code}

Naturally, at the base step of the induction, when the term has the form `generator`
x, the free lift of `h` agrees with `h`.  For the inductive step, when the
given term has the form `node f ğ‘¡`, the free lift is defined as
follows: Assuming (the induction hypothesis) that we know the image of each
subterm `ğ‘¡ i` under the free lift of `h`, define the free lift at the
full term by applying `f Ì‚ ğ‘¨` to the images of the subterms.

The free lift so defined is a homomorphism by construction. Indeed, here is the trivial proof.

\begin{code}

lift-hom : (ğ‘¨ : Algebra ğ“¤ ğ‘†) â†’ (X â†’ âˆ£ ğ‘¨ âˆ£) â†’ hom (ğ‘» X) ğ‘¨
lift-hom ğ‘¨ h = free-lift ğ‘¨ h , Î» f a â†’ cong (f Ì‚ ğ‘¨) refl

\end{code}

Finally, we prove that the homomorphism is unique.  This requires `funext ğ“¥ ğ“¤` (i.e., *function extensionality* at universe levels `ğ“¥` and `ğ“¤`) which we postulate by making it part of the premise in the following function type definition.

\begin{code}

free-unique : funext ğ“¥ ğ“¤ â†’ (ğ‘¨ : Algebra ğ“¤ ğ‘†)(g h : hom (ğ‘» X) ğ‘¨)
 â†’            (âˆ€ x â†’ âˆ£ g âˆ£ (â„Š x) â‰¡ âˆ£ h âˆ£ (â„Š x))
              ----------------------------------------
 â†’            âˆ€ (t : Term X) â†’  âˆ£ g âˆ£ t â‰¡ âˆ£ h âˆ£ t

free-unique _ _ _ _ p (â„Š x) = p x
free-unique fe ğ‘¨ g h p (node ğ‘“ ğ‘¡) = âˆ£ g âˆ£ (node ğ‘“ ğ‘¡)  â‰¡âŸ¨ âˆ¥ g âˆ¥ ğ‘“ ğ‘¡ âŸ©
                                   (ğ‘“ Ì‚ ğ‘¨)(âˆ£ g âˆ£ âˆ˜ ğ‘¡)  â‰¡âŸ¨ Î± âŸ©
                                   (ğ‘“ Ì‚ ğ‘¨)(âˆ£ h âˆ£ âˆ˜ ğ‘¡)  â‰¡âŸ¨ (âˆ¥ h âˆ¥ ğ‘“ ğ‘¡)â»Â¹ âŸ©
                                   âˆ£ h âˆ£ (node ğ‘“ ğ‘¡)   âˆ
 where
 Î± : (ğ‘“ Ì‚ ğ‘¨) (âˆ£ g âˆ£ âˆ˜ ğ‘¡) â‰¡ (ğ‘“ Ì‚ ğ‘¨) (âˆ£ h âˆ£ âˆ˜ ğ‘¡)
 Î± = cong (ğ‘“ Ì‚ ğ‘¨) (fe Î» i â†’ free-unique fe ğ‘¨ g h p (ğ‘¡ i))

\end{code}

Let's account for what we have proved thus far about the term algebra.  If we postulate a type `X : Type ğ“§` (representing an arbitrary collection of variable symbols) such that for each `ğ‘†`-algebra `ğ‘¨` there is a map from `X` to the domain of `ğ‘¨`, then it follows that for every `ğ‘†`-algebra `ğ‘¨` there is a homomorphism from `ğ‘» X` to `âˆ£ ğ‘¨ âˆ£` that "agrees with the original map on `X`," by which we mean that for all `x : X` the lift evaluated at `â„Š x` is equal to the original function evaluated at `x`.

If we further assume that each of the mappings from `X` to `âˆ£ ğ‘¨ âˆ£` is *surjective*, then the homomorphisms constructed with `free-lift` and `lift-hom` are *epimorphisms*, as we now prove.

\begin{code}

lift-of-epi-is-epi : (ğ‘¨ : Algebra ğ“¤ ğ‘†){hâ‚€ : X â†’ âˆ£ ğ‘¨ âˆ£}
 â†’                   IsSurjective hâ‚€ â†’ IsSurjective âˆ£ lift-hom ğ‘¨ hâ‚€ âˆ£
lift-of-epi-is-epi ğ‘¨ {hâ‚€} hE y = Î³
 where
 hâ‚€â»Â¹y = Inv hâ‚€ (hE y)

 Î· : y â‰¡ âˆ£ lift-hom ğ‘¨ hâ‚€ âˆ£ (â„Š hâ‚€â»Â¹y)
 Î· = (InvIsInv hâ‚€ (hE y))â»Â¹

 Î³ : Image âˆ£ lift-hom ğ‘¨ hâ‚€ âˆ£ âˆ‹ y
 Î³ = eq (â„Š hâ‚€â»Â¹y) Î·

\end{code}

The `lift-hom` and `lift-of-epi-is-epi` types will be called to action when such epimorphisms are needed later (e.g., in the [Varieties][] module).


--------------------------------------

<p></p>

[â†‘ Terms](Terms.html)
<span style="float:right;">[Terms.Operations â†’](Terms.Operations.html)</span>

{% include UALib.Links.md %}
